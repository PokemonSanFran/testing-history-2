//gText_Employedeverafter_
//Quest_Employedeverafter_

const PLAYER = OBJ_EVENT_ID_PLAYER
const GUEST_A = 1
const GUEST_B = 2
const OFFICIANT = 3
const GUEST_C = 5

const BRIDE_A = 4
const BRIDE_MISSING = 6

const BRIDE_A_GLDNGTEWILDS = 2
const BRIDE_MISSING_GLDNGTEWILDS = 3

const FLAG_HIDE_BRIDE_A = FLAG_TEMP_1
const FLAG_HIDE_BRIDE_MISSING = FLAG_TEMP_3
const FLAG_HIDE_WEDDING_PARTY = FLAG_TEMP_2

const FLAG_HIDE_BRIDE_A_GLDNGTEWILDS = FLAG_TEMP_2
const FLAG_HIDE_BRIDE_MISSING_GLDNGTEWILDS = FLAG_TEMP_3


script Quest_Employedeverafter_CleanUpWeddingParty_Script{
    setflag(FLAG_HIDE_WEDDING_PARTY)
    setobjectxy(BRIDE_A,37,17)
    setobjectxyperm(BRIDE_A,37,17)
    setobjectmovementtype(BRIDE_A,MOVEMENT_TYPE_FACE_LEFT)
    return
}

script Quest_Employedeverafter_MovePlayerIntoPosition_Script{
    getplayerxy(VAR_TEMP_0,VAR_TEMP_1)

        if (var(VAR_TEMP_0) != 19 || (var(VAR_TEMP_1) != 28)){
            fadescreen(FADE_TO_BLACK)
                setobjectxy(PLAYER,19,28)
                TeleportCamera(19,28)
                applymovement(PLAYER,Common_Movement_FaceUp)
                waitmovement(PLAYER)
                fadescreen(FADE_FROM_BLACK)
        }
    goto(Quest_Employedeverafter_StartQuest)
}

script Quest_Employedeverafter_StartQuest{
    lock
        msgbox(gText_Employedeverafter_WhyWouldShe,MSGBOX_AUTOCLOSE,SPEAKER_KATHARINA,TAIL_SHOUT,EMOTE_SAD)
        closemessage
        applymovement(BRIDE_A,Quest_Employedeverafter_LeaveAltar_Movement)
        applymovement(PLAYER,Quest_Employedeverafter_WatchBridgeLeave_Movement)
        waitmovement(PLAYER)
        addobject(OFFICIANT)
        applymovement(OFFICIANT,Quest_Employedeverafter_WalkAfterBride_Movement)
        waitmovement(OFFICIANT)
        msgbox(gText_Employedeverafter_WhatAMess,,SPEAKER_OFFICIANT,,EMOTE_SWEAT)
        closemessage
        applymovement(PLAYER,Common_Movement_FaceUp)
        waitmovement(PLAYER)
        msgbox(gText_Employedeverafter_TheBrideGotCold,,SPEAKER_OFFICIANT)
        msgbox(gText_Employedeverafter_EverybodyIsOff,,SPEAKER_OFFICIANT)
        msgbox(gText_Employedeverafter_IHopeSheTurns,,SPEAKER_OFFICIANT)
        closemessage
        startquest(QUEST_EMPLOYEDEVERAFTER)
        call(Quest_Employedeverafter_LockBride_Script)
        release
        end
}

script Quest_Employedeverafter_LockBride_Script{
        setobjectxy(BRIDE_A,17,34)
        setobjectxyperm(BRIDE_A,17,34)

        if (flag(FLAG_QUEST_EMPLOYEDEVERAFTER_FOUND_BRIDE)){
           setflag(FLAG_HIDE_BRIDE_A) 
        }
    return
}

script Quest_Employedeverafter_Palacefienarts_HideBrideMissing_Script{
    setflag(FLAG_HIDE_BRIDE_MISSING)
        return
}


script Quest_Employedeverafter_Gldngtewilds_HideBrideMissing_Script{
    setflag(FLAG_HIDE_BRIDE_MISSING_GLDNGTEWILDS)
    return
}

script Quest_Employedeverafter_Gldngtewilds_HideBrideA_Script{
    questmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_EMPLOYEDEVERAFTER)
    if (var(VAR_RESULT) || !(flag(FLAG_QUEST_EMPLOYEDEVERAFTER_FOUND_BRIDE))){
        setflag(FLAG_HIDE_BRIDE_A_GLDNGTEWILDS)
    }
    return
}

script Quest_Employedeverafter_MoveBrideA_Gldngtewilds_Script{
    if (flag(FLAG_QUEST_EMPLOYEDEVERAFTER_FOUND_BRIDE)){
        setobjectxy(BRIDE_A_GLDNGTEWILDS,44,6)
            setobjectxyperm(BRIDE_A_GLDNGTEWILDS,44,6)
    }
    return
}

script Quest_Employedeverafter_BrideMissing_Interact_Script{

    if (flag(FLAG_QUEST_EMPLOYEDEVERAFTER_FOUND_BRIDE)){
        goto(Quest_Employedeverafter_WonderAboutRing_Dialogue)
    }else{
        goto(Quest_Employedeverafter_PostBattle_Dialogue)
    }
}

script Quest_Employedeverafter_BrideA_Interact_Script{
    msgbox(gText_Employedeverafter_ICantBelieve,MSGBOX_NPC,SPEAKER_KATHARINA)
        applymovement(BRIDE_A_GLDNGTEWILDS,Common_Movement_FaceOriginalDirection)
        waitmovement(BRIDE_A_GLDNGTEWILDS)
        end
}

script Quest_Employedeverafter_MovePlayerIntoSecondPosition_Script{
    getplayerxy(VAR_TEMP_0,VAR_TEMP_1)

        if (var(VAR_TEMP_0) != 45 || (var(VAR_TEMP_1) != 7)){
            fadescreen(FADE_TO_BLACK)
                setobjectxy(PLAYER,45,7)
                TeleportCamera(45,7)
                applymovement(PLAYER,Common_Movement_FaceUp)
                waitmovement(PLAYER)
                fadescreen(FADE_FROM_BLACK)
        }
    return
}

script Quest_Employedeverafter_CheckQuestStatus_Script{
    goto_if_quest_inactive(QUEST_EMPLOYEDEVERAFTER,Quest_Employedeverafter_MovePlayerIntoPosition_Script)
    release
    end
}

script Quest_Employedeverafter_PostBattle_Dialogue{
    call(Quest_Employedeverafter_MovePlayerIntoSecondPosition_Script)
        setflag(FLAG_QUEST_EMPLOYEDEVERAFTER_FOUND_BRIDE)
        msgbox(gText_Gldngtewilds_ImSupposedToBeGetting,MSGBOX_NPC,SPEAKER_HELEN)
        msgbox(gText_Employedeverafter_AreYouGettingMarriedAt,,SPEAKER_ZOEY,,EMOTE_CONFUSE)
        msgbox(gText_Employedeverafter_HowDid,,SPEAKER_HELEN)
        msgbox(gText_Employedeverafter_IMetYourFiance,,SPEAKER_ZOEY)
        closemessage
        applymovement(BRIDE_MISSING_GLDNGTEWILDS,Common_Movement_FaceAwayPlayer)
        waitmovement(BRIDE_MISSING_GLDNGTEWILDS)
        msgbox(gText_Employedeverafter_IKnowBut,,SPEAKER_HELEN)
        applymovement(BRIDE_MISSING_GLDNGTEWILDS,Common_Movement_FacePlayer)
        waitmovement(BRIDE_MISSING_GLDNGTEWILDS)
        msgbox(gText_Employedeverafter_IGotLaidOff,,SPEAKER_HELEN,EMOTE_SAD)
        msgbox(gText_Employedeverafter_AndImSoScared,,SPEAKER_HELEN,EMOTE_SAD)
        msgbox(gText_Employedeverafter_ButIKnowSheWouldNever,,SPEAKER_HELEN)
        msgbox(gText_Employedeverafter_AndSheSaid,,SPEAKER_HELEN,,EMOTE_LOVE)
        msgbox(gText_Employedeverafter_WowThatMakesALot,,SPEAKER_HELEN)
        msgbox(gText_Employedeverafter_IDidntSay,,SPEAKER_ZOEY,TAIL_THOUGHT,EMOTE_SHOCK)
        closemessage

        msgbox(gText_Employedeverafter_HowCouldYouBe,,SPEAKER_KATHARINA,TAIL_SHOUT,EMOTE_ANGRY)
        msgbox(gText_Employedeverafter_Kath,,SPEAKER_HELEN,,EMOTE_SHOCK)
        closemessage

        addobject(BRIDE_A_GLDNGTEWILDS)
        applymovement(BRIDE_MISSING_GLDNGTEWILDS,Common_Movement_FaceLeft)
        applymovement(BRIDE_A_GLDNGTEWILDS,Quest_Employedeverafter_WalkToBride_Movement)
        waitmovement(BRIDE_A_GLDNGTEWILDS)

        msgbox(gText_Employedeverafter_IfIWasntSoRelieved,,SPEAKER_KATHARINA,,EMOTE_ANGRY)
        msgbox(gText_Employedeverafter_IReallyThought,,SPEAKER_KATHARINA,,EMOTE_SAD)
        msgbox(gText_Employedeverafter_YeahButPlayer,,SPEAKER_HELEN)
        msgbox(gText_Employedeverafter_ImReady,,SPEAKER_HELEN)
        msgbox(gText_Employedeverafter_OkayTheresOneProblem,,SPEAKER_KATHARINA)
        closemessage
        msgbox(gText_Employedeverafter_IOnlyFoundYou,,SPEAKER_HELEN)
        msgbox(gText_Employedeverafter_AndOnTheWayUp,,SPEAKER_HELEN)
        msgbox(gText_Employedeverafter_YouDidWhat,,SPEAKER_KATHARINA,,EMOTE_SHOCK)
        msgbox(gText_Employedeverafter_ThesePeople,,SPEAKER_ZOEY,TAIL_THOUGHT,EMOTE_SHOCK)
        msgbox(gText_Employedeverafter_WhatAreWeGoing,,SPEAKER_HELEN)
        call(Quest_Employedeverafter_ProgressQuestWithRing_Script)
        goto_if_quest_reward(QUEST_EMPLOYEDEVERAFTER,Quest_Employedeverafter_FoundRing_Dialogue)
        closemessage
        end
}

script Quest_Employedeverafter_ProgressQuestWithRing_Script{
    checkitem(ITEM_ENGAGEMENT_RING,1)
    if (var(VAR_RESULT)){
        questmenu(QUEST_MENU_SET_REWARD,QUEST_EMPLOYEDEVERAFTER)
    }
    return
}

script Quest_Employedeverafter_WonderAboutRing_Dialogue{
        goto_if_quest_reward(QUEST_EMPLOYEDEVERAFTER,Quest_Employedeverafter_FoundRing_Dialogue)
        applymovement(BRIDE_A_GLDNGTEWILDS,Common_Movement_FaceRight)
        applymovement(BRIDE_MISSING_GLDNGTEWILDS,Common_Movement_FaceLeft)
        waitmovement(BRIDE_MISSING_GLDNGTEWILDS)
        msgbox(gText_Employedeverafter_UghWhyDid,,SPEAKER_HELEN) 
        msgbox(gText_Employedeverafter_YoureAskingMe,,SPEAKER_KATHARINA)
        closemessage
        end
}

script Quest_Employedeverafter_FoundRing_Dialogue{
    call(Quest_Employedeverafter_MovePlayerIntoSecondPosition_Script)
        applymovement(BRIDE_A_GLDNGTEWILDS,Common_Movement_FaceDown)
        applymovement(BRIDE_MISSING_GLDNGTEWILDS,Common_Movement_FaceDown)
        waitmovement(BRIDE_MISSING_GLDNGTEWILDS)
        msgbox(gText_Employedeverafter_IThinkIGot,,SPEAKER_ZOEY)
        msgbox(gText_Employedeverafter_WoahBaby,,SPEAKER_HELEN)
        msgbox(gText_Employedeverafter_AwwYouShouldnt,,SPEAKER_KATHARINA)
        closemessage
        applymovement(BRIDE_A_GLDNGTEWILDS,Common_Movement_FaceRight)
        applymovement(BRIDE_MISSING_GLDNGTEWILDS,Common_Movement_FaceLeft)
        waitmovement(BRIDE_MISSING_GLDNGTEWILDS)
        msgbox(gText_Employedeverafter_DoYouStillWant,,SPEAKER_KATHARINA)
        msgbox(gText_Employedeverafter_MoreThanAnything,,SPEAKER_HELEN,,EMOTE_LOVE)
        closemessage
        applymovement(BRIDE_A_GLDNGTEWILDS,Common_Movement_FaceDown)
        waitmovement(BRIDE_A_GLDNGTEWILDS)
        msgbox(gText_Employedeverafter_MyAuntGave,,SPEAKER_KATHARINA)
        giveitem(ITEM_TM31)
        removeitem(ITEM_ENGAGEMENT_RING)
        applymovement(BRIDE_A_GLDNGTEWILDS,Quest_Employedeverafter_WalkAway_Movement)
        applymovement(BRIDE_MISSING_GLDNGTEWILDS,Quest_Employedeverafter_WalkAway_Movement)
        completequest(QUEST_EMPLOYEDEVERAFTER) 
        fadescreen(FADE_TO_BLACK)
        removeobject(BRIDE_A_GLDNGTEWILDS)
        removeobject(BRIDE_MISSING_GLDNGTEWILDS)
        fadescreen(FADE_FROM_BLACK)
        release
        end
}

movement Quest_Employedeverafter_LeaveAltar_Movement{
    walk_faster_down * 13
        step_end
}
movement Quest_Employedeverafter_WatchBridgeLeave_Movement{
    delay_16
        face_right
        face_down
        step_end
}

movement Quest_Employedeverafter_WalkAfterBride_Movement{
    walk_down * 8
        step_end
}

movement Quest_Employedeverafter_WalkToBride_Movement{
    walk_right * 6
        walk_down
        walk_right * 2
        step_end
}

movement Quest_Employedeverafter_WalkAway_Movement{
    walk_left * 2
        walk_up
        walk_left * 6
        step_end
}

text gText_Employedeverafter_WhyWouldShe{
    format("Why would she just leave me here?")
}
text gText_Employedeverafter_WhatAMess{
    format("What a mess...")
}
text gText_Employedeverafter_TheBrideGotCold{
    format("I think the bride got cold feet. She was supposed to be here an hour ago... but nothing.")
}
text gText_Employedeverafter_EverybodyIsOff{
    format("Everybody is off looking for her, but we haven't had any luck.")
}
text gText_Employedeverafter_IHopeSheTurns{
    format("I hope she turns up!")
}
text gText_Employedeverafter_ICantBelieve{
    format("I can't believe what a day it's been...")
}
text gText_Employedeverafter_AreYouGettingMarriedAt{
    format("Are you getting married at Palacefinearts?")
}
text gText_Employedeverafter_HowDid{
    format("How did you know that?")
}
text gText_Employedeverafter_IMetYourFiance{
    format("I met your finance. The whole wedding party, actually. Its super messed up for you to just leave!")
}
text gText_Employedeverafter_IKnowBut{
    format("I know I should be there, but...")
}
text gText_Employedeverafter_IGotLaidOff{
    format("I got laid off a few months ago. Its really hard to find work, so Katharina has been supporting both of us.")
}
text gText_Employedeverafter_AndImSoScared{
    format("And I'm so scared that she's going to fall out of love with me if I can't hold up my end of the deal. Like, will she think I'm useless?")
}
text gText_Employedeverafter_ButIKnowSheWouldNever{
    format("But I know she would never do that!")
}
text gText_Employedeverafter_AndSheSaid{
    format("And she's even told me a million times that she's happy to support us, that we're a team....")
}
text gText_Employedeverafter_WowThatMakesALot{
    format("Wow, that makes a lot of sense. Thank you, {PLAYER}!")
}
text gText_Employedeverafter_IDidntSay{
    format("...I didn't say anything.")
}
text gText_Employedeverafter_HowCouldYouBe{
    format("How could you be so stupid?!?!?!")
}
text gText_Employedeverafter_Kath{
    format("Kath?")
}
text gText_Employedeverafter_IfIWasntSoRelieved{
    format("If I wasn't so relieved, I'd be so angry!")
}
text gText_Employedeverafter_IReallyThought{
    format("I really thought you were just going to leave me! At the altar!")
}
text gText_Employedeverafter_YeahButPlayer{
    format("Yeah, but {PLAYER} helped talk me out it.")
}
text gText_Employedeverafter_ImReady{
    format("I'm sorry. But I'm ready now. Let's go get married.")
}
text gText_Employedeverafter_OkayTheresOneProblem{
    format("...there's one problem.")
}
text gText_Employedeverafter_IOnlyFoundYou{
    format("I only found you because I was standing on top of some hill...and I saw you from there.")
}
text gText_Employedeverafter_AndOnTheWayUp{
    format("And on the way up the hill, I was so angry... I threw your ring into a lake.")
}
text gText_Employedeverafter_YouDidWhat{
    format("You did what?!?!?")
}
text gText_Employedeverafter_ThesePeople{
    format("These people are insane. And perfect for each other.")
}
text gText_Employedeverafter_WhatAreWeGoing{
    format("What are we going to do?")
}
text gText_Employedeverafter_UghWhyDid{
    format("Ugh, why are we like this?!?")
}
text gText_Employedeverafter_YoureAskingMe{
    format("You're asking me?")
}
text gText_Employedeverafter_IThinkIGot{
    format("I think I got your ring from the lake.")
}
text gText_Employedeverafter_WoahBaby{
    format("Woah baby, you went down and got it for us?")
}
text gText_Employedeverafter_AwwYouShouldnt{
    format("Aww, you shouldn't have!")
}
text gText_Employedeverafter_DoYouStillWant{
    format("Do you still want to do this?")
}
text gText_Employedeverafter_MoreThanAnything{
    format("More than anything.")
}
text gText_Employedeverafter_MyAuntGave{
    format("My aunt gave us this for our wedding... but you can have it instead.")
}
