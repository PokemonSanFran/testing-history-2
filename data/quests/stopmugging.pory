const PLAYER = OBJ_EVENT_ID_PLAYER
const ROSE = 1
const VICTIM = 2
const MUGGERA = 3
const MUGGERB = 4

script Quest_StopMugging_HideObject_Script{
    if (defeated(TRAINER_MUGGERA_MUGGERB)){
        call(Quest_StopMugging_HideVictim_Script)

            questmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_STOPMUGGING)

            if (var(VAR_RESULT)){
                call(Quest_StopMugging_HideMuggers_Script)
            }
    }
}

script Quest_StopMugging_HideVictim_Script{
        removeobject(VICTIM)
        setflag(FLAG_TEMP_2)
}

script Quest_StopMugging_HideMuggers_Script{
    setflag(FLAG_TEMP_3)
}

script Quest_StopMugging_Trigger_Script {
    lock
        questmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_STOPMUGGING)
        if (var(VAR_RESULT)) {
            release
                end
        }

    questmenu(QUEST_MENU_CHECK_ACTIVE, QUEST_STOPMUGGING)
        if (var(VAR_RESULT)){
            call(Quest_StopMugging_ResumeMugging_Dialogue)
        } else {
            call(Quest_StopMugging_Main_Dialogue)
        }
}

script Quest_StopMugging_MoveIntoPlace_Script {
    getplayerxy(VAR_TEMP_0,VAR_TEMP_1)

        if (var(VAR_TEMP_0) == 15){
            applymovement(PLAYER,Common_Movement_WalkRight)
        }
    waitmovement(0)
    applymovement(PLAYER,Common_Movement_WalkUp)
    waitmovement(0)

        msgbox(gText_StopMugging_Hey,MSGBOX_DEFAULT,SPEAKER_ZOEY)
        
        closemessage

        playse(SE_PIN)
        applymovement(MUGGERA,Common_Movement_ExclamationMark)
        applymovement(MUGGERB,Common_Movement_ExclamationMark)
        waitmovement(MUGGERA)
        waitmovement(MUGGERB)

        applymovement(MUGGERA,Quest_StopMugging_WalkToPlayerA_Movement)
        applymovement(MUGGERB,Quest_StopMugging_WalkToPlayerB_Movement)
        waitmovement(0)
}

script Quest_StopMugging_Main_Dialogue {
    startquest(QUEST_STOPMUGGING)
        msgbox(gText_StopMugging_HurryItUp,MSGBOX_DEFAULT,SPEAKER_DEFAULT)
        msgbox(gText_StopMugging_OhArceusOkay,MSGBOX_DEFAULT,SPEAKER_DEFAULT)
        msgbox(gText_StopMugging_IfYouDontHurry,MSGBOX_DEFAULT,SPEAKER_DEFAULT)
        closemessage

        call(Quest_StopMugging_MoveIntoPlace_Script)

        msgbox(gText_StopMugging_TheHellDo,MSGBOX_DEFAULT,SPEAKER_DEFAULT)
        msgbox(gText_StopMugging_GetOutOfHere,MSGBOX_DEFAULT,SPEAKER_ZOEY)
        
        goto(Quest_StopMugging_StartBattle_Script)
}

script Quest_StopMugging_StartBattle_Script {
    lock
        msgbox(gText_StopMugging_GuessWereGetting,MSGBOX_DEFAULT,SPEAKER_DEFAULT)
        closemessage
        trainerbattle_no_intro(TRAINER_MUGGERA_MUGGERB,gText_StopMugging_Shit)
        goto(Quest_StopMugging_PostBattle_Dialogue)
}

script Quest_StopMugging_ResumeMugging_Dialogue {
    call(Quest_StopMugging_MoveIntoPlace_Script)
        msgbox(gText_StopMugging_YouAgain,MSGBOX_DEFAULT,SPEAKER_DEFAULT)
        goto(Quest_StopMugging_StartBattle_Script)
}

script Quest_StopMugging_PostBattle_Dialogue {
    call(Quest_StopMugging_HideObject_Script)
        lock
        msgbox(gText_StopMugging_DamnYoureScary,MSGBOX_DEFAULT,SPEAKER_DEFAULT)
        msgbox(gText_StopMugging_LeaveNow,MSGBOX_DEFAULT,SPEAKER_ZOEY)
        
        closemessage

        applymovement(MUGGERA,Quest_StopMugging_WalkAwayA_Movement)
        applymovement(MUGGERB,Quest_StopMugging_WalkAwayB_Movement)
        waitmovement(0)
        removeobject(MUGGERA)
        removeobject(MUGGERB)
        completequest(QUEST_STOPMUGGING)
        end
}

movement Quest_StopMugging_WalkToPlayerA_Movement {
        walk_down
        face_right
        step_end
}

movement Quest_StopMugging_WalkToPlayerB_Movement {
    walk_down
        step_end
}

movement Quest_StopMugging_WalkAwayA_Movement {
    walk_down * 6
        step_end
}

movement Quest_StopMugging_WalkAwayB_Movement {
    walk_left
        walk_down * 6
        step_end
}

text gText_StopMugging_HurryItUp{
    format("Hurry it up, your phone and your wallet!")
}

text gText_StopMugging_OhArceusOkay{
    format("*sob* Oh Arceus okay, please don't hurt me...")
}

text gText_StopMugging_IfYouDontHurry{
    format("If you don't hurry up, we're gonna have a problem.")
}

text gText_StopMugging_Hey{
    format("Hey!")
}

text gText_StopMugging_TheHellDo{
    format("The hell do you want?")
}

text gText_StopMugging_GetOutOfHere{
    format("Get out of here! Leave them alone!")
}

text gText_StopMugging_GuessWereGetting{
    format("Guess we're getting two wallets in one alley!")
}

text gText_StopMugging_Shit{
    format("Shit!")
}

text gText_StopMugging_DamnYoureScary{
    format("Damn, you're scary as all hell, we don't want no trouble...")
}

text gText_StopMugging_LeaveNow{
    format("Leave. Now.")
}

text gText_StopMugging_YouAgain{
    format("You again? Didn't we already kick your ass once?")
}
