//gText_Restaurantexpansion_
//Quest_Restaurantexpansion_
const FLAG_HIDE_FALKNER = FLAG_TEMP_4
const FLAG_HIDE_JORIS = FLAG_TEMP_5
const PLAYER = OBJ_EVENT_ID_PLAYER
const FALKNER = 8
const FALKNER_MISSION = 7
const JORIS = 7
const JORIS_KID = 9
const NURSE = 1

const MARIN_NURSE= 0
const HAIGHTASHBURY_NURSE= 1
const SOMA_NURSE= 2
const BERKELEY_NURSE= 3
const ALL_NURSE = 4


script Quest_Restaurantexpansion_HideObjects_Script{
    setflag(FLAG_HIDE_JORIS)

    if(var(VAR_FALKNER_STATE) != BATTLE_1_COMPLETE){
        setflag(FLAG_HIDE_FALKNER)
    }
    return
}

script Quest_Restaurantexpansion_Mission_Leader_Visibilty_Script{
    switch(var(VAR_FALKNER_STATE)){
        case NO_BADGE:
        case QUEST_2_COMPLETE:
        case BATTLE_2_COMPLETE: setflag(FLAG_HIDE_FALKNER)
        default: return
    }
    return
}

script Quest_Restaurantexpansion_MovePlayer_Script{
    fadescreen(FADE_TO_BLACK)
    setobjectxy(PLAYER,31,19)
    applymovement(PLAYER,Common_Movement_FaceLeft)
    TeleportCamera(31,19)
    fadescreen(FADE_FROM_BLACK)
}

script Quest_Restaurantexpansion_Startcutscene_Script{
    specialvar(VAR_RESULT,GetPlayerFacingDirection)

    if (var(VAR_RESULT) != DIR_WEST){
        call(Quest_Restaurantexpansion_MovePlayer_Script)    
    }
    return
}

script Quest_Restaurantexpansion_Meetup_Dialogue{
    setvar(VAR_FALKNER_STATE,CUTSCENE_COMPLETE)
    call(Quest_Restaurantexpansion_Startcutscene_Script)
    lock
    msgbox(gText_Restaurantexpansion_HelloPlayer,MSGBOX_NPC,SPEAKER_FALKNER,,EMOTE_HAPPY)
    msgbox(gText_Restaurantexpansion_AreYouHere,,SPEAKER_FALKNER)
    msgbox(gText_Restaurantexpansion_IDidntEven,,SPEAKER_ZOEY)
    msgbox(gText_Restaurantexpansion_OhCome,,SPEAKER_FALKNER)
    closemessage
    call(Quest_Restaurantexpansion_StartWalk_Dialogue)
    msgbox(gText_Restaurantexpansion_IfMyRestaurant,,SPEAKER_FALKNER)
    msgbox(gText_Restaurantexpansion_ITry,,SPEAKER_FALKNER)
    msgbox(gText_Restaurantexpansion_WhatDoMeanByElevate,,SPEAKER_ZOEY)
    msgbox(gText_Restaurantexpansion_TheBestCooksAndFreshest,,SPEAKER_FALKNER)
    closemessage
    waitmovement(FALKNER)
    applymovement(FALKNER,Common_Movement_FaceRight)
    call(Quest_Restaurantexpansion_SpawnJoris_Script)
    call(Quest_Restaurantexpansion_JorisWalkToPlayer_Script)
    waitmovement(JORIS)
    applymovement(FALKNER,Common_Movement_FaceDown)
    applymovement(PLAYER,Common_Movement_FaceDown)
    msgbox(gText_Restaurantexpansion_HeyManYouGot,,SPEAKER_JORIS)
    msgbox(gText_Restaurantexpansion_SorryIDont,,SPEAKER_FALKNER)
    msgbox(gText_Restaurantexpansion_ItsCool,,SPEAKER_JORIS)
    closemessage
    applymovement(JORIS,Quest_Restaurantexpansion_WalkAway_Movement)
    applymovement(JORIS_KID,Quest_Restaurantexpansion_WalkAway_Movement)
    waitmovement(JORIS)
    msgbox(gText_Restaurantexpansion_WaitHoldOn,,SPEAKER_FALKNER,TAIL_SHOUT)
    closemessage
    playse(SE_PIN)
    applymovement(JORIS,Common_Movement_ExclamationMark)
    applymovement(FALKNER,Quest_Restaurantexpansion_WalkAway_Movement)
    waitmovement(FALKNER)
    msgbox(gText_Restaurantexpansion_CanIActually,,SPEAKER_FALKNER)
    applymovement(JORIS_KID,Quest_Restaurantexpansion_Jump_Movement)
    msgbox(gText_Restaurantexpansion_WaitReally,,SPEAKER_JORIS)
    msgbox(gText_Restaurantexpansion_YeahThisPlaceJust,,SPEAKER_FALKNER)
    closemessage
    call(Quest_Restaurantexpansion_GroupWalkDoor_Script)
    waitmovement(FALKNER)
    applymovement(PLAYER,Common_Movement_FaceLeft)
    msgbox(gText_Restaurantexpansion_PlayerComeTo,,SPEAKER_FALKNER)
    closemessage
    call(Quest_Restaurantexpansion_GoIntoShop_Script)
    release
    end
}

script Quest_Restaurantexpansion_StartWalk_Dialogue{
    applymovement(FALKNER,Quest_Restaurantexpansion_WalkToShop_Movement)
    applymovement(PLAYER,Quest_Restaurantexpansion_WalkToShop_Movement)
    return
}

script Quest_Restaurantexpansion_SpawnJoris_Script{
    clearflag(FLAG_HIDE_JORIS)
    addobject(JORIS)
    addobject(JORIS_KID)
    return
}

script Quest_Restaurantexpansion_JorisWalkToPlayer_Script{
    applymovement(JORIS,Quest_Restaurantexpansion_WalkToLeader_Movement)
    applymovement(JORIS_KID,Quest_Restaurantexpansion_WalkToLeader_Movement)
    return
}

script Quest_Restaurantexpansion_GroupWalkDoor_Script{
    applymovement(FALKNER,Quest_Restaurantexpansion_ReturnToDoorAndLook_Movement)
    applymovement(JORIS,Quest_Restaurantexpansion_ReturnToDoor_Movement)
    applymovement(JORIS_KID,Quest_Restaurantexpansion_ReturnToDoor_Movement)
    return
}

script Quest_Restaurantexpansion_GoIntoShop_Script{
    opendoor(2,18)
    waitdooranim
    applymovement(FALKNER,Quest_Restaurantexpansion_GoInside_A_Movement)
    applymovement(JORIS,Quest_Restaurantexpansion_GoInside_B_Movement)
    applymovement(JORIS_KID,Quest_Restaurantexpansion_GoInside_C_Movement)
    waitmovement(FALKNER)
    waitmovement(JORIS)
    waitmovement(JORIS_KID)
    removeobject(FALKNER)
    playse(SE_EXIT)
    removeobject(JORIS)
    playse(SE_EXIT)
    removeobject(JORIS_KID)
    playse(SE_EXIT)
    closedoor(2,18)
    return
}

script Quest_Restaurantexpansion_Leader_Interact_Script{
    lock
    goto_if_quest_complete(QUEST_RESTAURANTEXPANSION1,Quest_Restaurantexpansion2_Leader_Interact_Script)

    goto_if_quest_reward(QUEST_RESTAURANTEXPANSION1,Quest_Restaurantexpansion_Reward_Dialogue)
    goto_if_quest_active(QUEST_RESTAURANTEXPANSION1,Quest_Restaurantexpansion_Active_Dialogue)
    goto(Quest_Restaurantexpansion_StartQuest)
}

script Quest_Restaurantexpansion_StartQuest{
    msgbox(gText_Restaurantexpansion_PlayerSorryAbout,MSGBOX_NPC,SPEAKER_FALKNER)
    msgbox(gText_Restaurantexpansion_NoProblemThat,,SPEAKER_ZOEY,,EMOTE_HAPPY)
    msgbox(gText_Restaurantexpansion_TheyReallyOpened,,SPEAKER_FALKNER)
    msgbox(gText_Restaurantexpansion_HowCanI,,SPEAKER_FALKNER)
    msgbox(gText_Restaurantexpansion_ThatsWhy,,SPEAKER_FALKNER)

    msgbox(gText_Restaurantexpansion_Wow,,SPEAKER_ZOEY,,EMOTE_SHOCK)

    msgbox(gText_Restaurantexpansion_IveBeenHardAtWork,,SPEAKER_FALKNER)
    msgbox(gText_Restaurantexpansion_MeICant,,SPEAKER_ZOEY,,EMOTE_CONFUSE)
    msgbox(gText_Restaurantexpansion_NotCookingOutreach,,SPEAKER_FALKNER)
    msgbox(gText_Restaurantexpansion_IWantToWorkWith,,SPEAKER_FALKNER)

    msgbox(gText_Restaurantexpansion_KindaWeird,,SPEAKER_ZOEY,TAIL_THOUGHT)
    msgbox(gText_Restaurantexpansion_SureICan,,SPEAKER_ZOEY)
    closemessage

    startquest(QUEST_RESTAURANTEXPANSION1)
    goto(Quest_Restaurantexpansion_GiveFood_Marin_Dialogue)
}

script Quest_Restaurantexpansion_GiveFood_Marin_Dialogue{
    lock
    bufferstring(STR_VAR_1,gText_Quest_RestaurantExpansion1_Sub1_Map)
    call(Quest_Restaurantexpansion_FoodCold_Dialogue)
    msgbox(gText_Restaurantexpansion_FirstYoureGoing,MSGBOX_NPC,SPEAKER_FALKNER)
    lock
    giveitem(ITEM_TEA,1)
    msgbox(gText_Restaurantexpansion_WaitOnlyOne,,SPEAKER_ZOEY)
    msgbox(gText_Restaurantexpansion_OfCourse,,SPEAKER_FALKNER)
    msgbox(gText_Restaurantexpansion_IRegret,,SPEAKER_ZOEY,TAIL_THOUGHT,EMOTE_ANGRY)
    closemessage
    release
    end
}
script Quest_Restaurantexpansion_GiveFood_Haightashbury_Dialogue{
    lock
    bufferstring(STR_VAR_1,gText_Quest_RestaurantExpansion1_Sub2_Map)
    call(Quest_Restaurantexpansion_FoodCold_Dialogue)
    msgbox(gText_Restaurantexpansion_NextIs,MSGBOX_NPC,SPEAKER_FALKNER)
    giveitem(ITEM_TEA,1)
    msgbox(gText_Restaurantexpansion_HowAreYouPaying,,SPEAKER_ZOEY)
    msgbox(gText_Restaurantexpansion_WellMyClients,,SPEAKER_FALKNER)
    closemessage
    release
    end
}
script Quest_Restaurantexpansion_GiveFood_SOMA_Dialogue{
    lock
    bufferstring(STR_VAR_1,gText_Quest_RestaurantExpansion1_Sub3_Map)
    call(Quest_Restaurantexpansion_FoodCold_Dialogue)
    msgbox(gText_Restaurantexpansion_NowGoTo,MSGBOX_NPC,SPEAKER_FALKNER)
    giveitem(ITEM_TEA,1)
    msgbox(gText_Restaurantexpansion_HowManyOf,,SPEAKER_ZOEY)
    msgbox(gText_Restaurantexpansion_JustOneMore,,SPEAKER_FALKNER)
    closemessage
    release
    end
}
script Quest_Restaurantexpansion_GiveFood_Berkeley_Dialogue{
    lock
    bufferstring(STR_VAR_1,gText_Quest_RestaurantExpansion1_Sub4_Map)
    call(Quest_Restaurantexpansion_FoodCold_Dialogue)
    msgbox(gText_Restaurantexpansion_LastOne,MSGBOX_NPC,SPEAKER_FALKNER)
    giveitem(ITEM_TEA,1)
    msgbox(gText_Restaurantexpansion_ThisIsExhausting,,SPEAKER_ZOEY,TAIL_THOUGHT,EMOTE_SWEAT)
    closemessage
    release
    end
}

script Quest_Restaurantexpansion_FoodCold_Dialogue{
    checkitem(ITEM_TEA,1)
        if (var(VAR_RESULT)){
            msgbox(gText_Restaurantexpansion_GottaGoBeforeIt,MSGBOX_NPC,SPEAKER_FALKNER,TAIL_SHOUT,EMOTE_ANGRY)
            closemessage
            release
            end
        }
    return
}


script Quest_Restaurantexpansion_Active_Dialogue{
    lock
    call(Quest_Restaurantexpansion_CountProgress_Script)

    switch(var(VAR_TEMP_0)){
        case MARIN_NURSE: goto(Quest_Restaurantexpansion_GiveFood_Marin_Dialogue)
        case HAIGHTASHBURY_NURSE: goto(Quest_Restaurantexpansion_GiveFood_Haightashbury_Dialogue)
        case SOMA_NURSE: goto(Quest_Restaurantexpansion_GiveFood_SOMA_Dialogue)
        case BERKELEY_NURSE: goto(Quest_Restaurantexpansion_GiveFood_Berkeley_Dialogue)
    }
}

script Quest_Restaurantexpansion_Reward_Dialogue{
    msgbox(gText_Restaurantexpansion_PlayerThisIs,MSGBOX_NPC,SPEAKER_FALKNER,,EMOTE_HAPPY)
    msgbox(gText_Restaurantexpansion_Itwasgood,,SPEAKER_ZOEY)
    msgbox(gText_Restaurantexpansion_NowToWorkOnHiring,,SPEAKER_FALKNER)
    closemessage
    applymovement(FALKNER_MISSION,Quest_Restaurantexpansion_WalkInside_Movement)
    setvar(VAR_FALKNER_STATE,QUEST_1_COMPLETE)
    completequest(QUEST_RESTAURANTEXPANSION1)
    fadescreen(FADE_TO_BLACK)
    removeobject(FALKNER_MISSION)
    fadescreen(FADE_FROM_BLACK)
    release
    end
}

script Quest_Restaurantexpansion_Nurse_Question_Script{
    special(GetCurrentMap)

        switch(var(VAR_RESULT)){
            case MAP_MARIN_POKEMONCENTER_1F: call(Quest_Restaurantexpansion_Marin_Nurse_Dialogue)
            case MAP_HAIGHTASHBURY_POKEMONCENTER_1F: call(Quest_Restaurantexpansion_Haightashbury_NurseDialogue)
            case MAP_SOMA_POKEMONCENTER_1F: call(Quest_Restaurantexpansion_SOMA_Nurse_Dialogue)
            case MAP_BERKELEY_POKEMONCENTER_1F: call(Quest_Restaurantexpansion_Berkeley_Nurse_Dialogue)
            default: return
        }
    return
}

script Quest_Restaurantexpansion_Marin_Nurse_Dialogue{
    setvar(VAR_TEMP_0,0)

    call_if_quest_active(QUEST_RESTAURANTEXPANSION1,Quest_Restaurantexpansion_CountProgress_Script)

    checkitem(ITEM_TEA,1)
    if (!var(VAR_RESULT) || var(VAR_TEMP_0) != MARIN_NURSE){
        return
    }
subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_RESTAURANTEXPANSION1,SUB_QUEST_1)
    removeitem(ITEM_TEA,1)

    msgbox(gText_Restaurantexpansion_ActuallyQuestion,,SPEAKER_ZOEY)
    msgbox(gText_Restaurantexpansion_YesILove,,SPEAKER_NURSE)
    msgbox(gText_Restaurantexpansion_TheyreLaunchingANew,,SPEAKER_ZOEY)
    closemessage
    call(Quest_Restaurantexpansion_HangPoster_Script)
    msgbox(gText_Restaurantexpansion_ThisIsDelicious,,SPEAKER_NURSE,,EMOTE_LOVE)
    closemessage
    applymovement(NURSE,Common_Movement_FaceDown)
    waitmovement(NURSE)
    msgbox(gText_Restaurantexpansion_ThatWentWell,,SPEAKER_ZOEY,TAIL_THOUGHT)
    closemessage
    return
}

script Quest_Restaurantexpansion_Haightashbury_NurseDialogue{
    setvar(VAR_TEMP_0,0)

    call_if_quest_active(QUEST_RESTAURANTEXPANSION1,Quest_Restaurantexpansion_CountProgress_Script)

    checkitem(ITEM_TEA,1)
    if (!var(VAR_RESULT) || var(VAR_TEMP_0) != HAIGHTASHBURY_NURSE){
        return
    }
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_RESTAURANTEXPANSION1,SUB_QUEST_2)
    removeitem(ITEM_TEA,1)

    msgbox(gText_Restaurantexpansion_ActuallyQuestion,,SPEAKER_ZOEY)
    msgbox(gText_Restaurantexpansion_NahThatsSomeRich,,SPEAKER_NURSE)
    msgbox(gText_Restaurantexpansion_TheOwnerIsMoving,,SPEAKER_ZOEY)
    closemessage
    call(Quest_Restaurantexpansion_HangPoster_Script)
    msgbox(gText_Restaurantexpansion_OkayThisIs,,SPEAKER_NURSE,,EMOTE_LOVE)
    msgbox(gText_Restaurantexpansion_TwoDown,,SPEAKER_ZOEY,TAIL_THOUGHT)
    applymovement(NURSE,Common_Movement_FaceDown)
    closemessage
    return
}

script Quest_Restaurantexpansion_SOMA_Nurse_Dialogue{
    setvar(VAR_TEMP_0,0)

    call_if_quest_active(QUEST_RESTAURANTEXPANSION1,Quest_Restaurantexpansion_CountProgress_Script)

    checkitem(ITEM_TEA,1)
    if (!var(VAR_RESULT) || var(VAR_TEMP_0) != SOMA_NURSE){
        return
    }
subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_RESTAURANTEXPANSION1,SUB_QUEST_3)
    removeitem(ITEM_TEA,1)

    msgbox(gText_Restaurantexpansion_ActuallyQuestion,,SPEAKER_ZOEY)
    msgbox(gText_Restaurantexpansion_NoIDontReally,,SPEAKER_NURSE)
    msgbox(gText_Restaurantexpansion_TheyreChangingTheir,,SPEAKER_ZOEY)
    closemessage
    call(Quest_Restaurantexpansion_HangPoster_Script)
    msgbox(gText_Restaurantexpansion_FancyOrNot,,SPEAKER_NURSE,,EMOTE_LOVE)
    msgbox(gText_Restaurantexpansion_IDontThinkLeader,,SPEAKER_ZOEY,TAIL_THOUGHT)
    applymovement(NURSE,Common_Movement_FaceDown)
    closemessage
    return
}

script Quest_Restaurantexpansion_Berkeley_Nurse_Dialogue{
    setvar(VAR_TEMP_0,0)

    call_if_quest_active(QUEST_RESTAURANTEXPANSION1,Quest_Restaurantexpansion_CountProgress_Script)

    checkitem(ITEM_TEA,1)
    if (!var(VAR_RESULT) || var(VAR_TEMP_0) != BERKELEY_NURSE){
        return
    }
subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_RESTAURANTEXPANSION1,SUB_QUEST_4)
    removeitem(ITEM_TEA,1)

    msgbox(gText_Restaurantexpansion_ActuallyQuestion,,SPEAKER_ZOEY)
    msgbox(gText_Restaurantexpansion_TheyDontHaveMy,,SPEAKER_NURSE)
    msgbox(gText_Restaurantexpansion_TheyreAddingNew,,SPEAKER_ZOEY)
    closemessage
    call(Quest_Restaurantexpansion_HangPoster_Script)
    msgbox(gText_Restaurantexpansion_ChillVibes,,SPEAKER_NURSE)
    bufferstring(STR_VAR_1,gText_Quest_RestaurantExpansion1_Map)
    msgbox(gText_Restaurantexpansion_TheLastOne,,SPEAKER_ZOEY,TAIL_THOUGHT)
    applymovement(NURSE,Common_Movement_FaceDown)
    closemessage
    return
}

script Quest_Restaurantexpansion_ProgressToReward_Script{
    call(Quest_Restaurantexpansion_CountProgress_Script)
    if(var(VAR_TEMP_0) == ALL_NURSE){
        questmenu(QUEST_MENU_SET_REWARD,QUEST_RESTAURANTEXPANSION1)
    }
    return
}

script Quest_Restaurantexpansion_HangPoster_Script{
    call(Quest_Restaurantexpansion_ProgressToReward_Script)
    fadescreen(FADE_TO_BLACK)
    setobjectxy(PLAYER,12,2)
    TeleportCamera(12,2)
    applymovement(PLAYER,Common_Movement_FaceLeft)
    applymovement(NURSE,Common_Movement_FaceRight)

    special(GetCurrentMap)

    switch(var(VAR_RESULT)){
        case MAP_SOMA_POKEMONCENTER_1F: call(Quest_Restaurantexpansion_SOMA_HangPosterTile_Script)
        case MAP_BERKELEY_POKEMONCENTER_1F: call(Quest_Restaurantexpansion_Berkeley_HangPosterTile_Script)
        case MAP_HAIGHTASHBURY_POKEMONCENTER_1F: call(Quest_Restaurantexpansion_Haightashbury_HangPosterTile_Script)
        case MAP_MARIN_POKEMONCENTER_1F: call(Quest_Restaurantexpansion_Marin_HangPosterTile_Script)
        default: return
    }
    delay(8)
    fadescreen(FADE_FROM_BLACK)
    return
}

script Quest_Restaurantexpansion_Haightashbury_HangPosterTile_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_RESTAURANTEXPANSION1,SUB_QUEST_2)
    if (var(VAR_RESULT)){
    setmetatile(12,0,0x24D,TRUE)
    setmetatile(12,1,0x255,TRUE)
    special(DrawWholeMapView)
    }
    return
}

script Quest_Restaurantexpansion_Marin_HangPosterTile_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_RESTAURANTEXPANSION1,SUB_QUEST_1)
    if (var(VAR_RESULT)){
    setmetatile(12,0,0x24D,TRUE)
    setmetatile(12,1,0x255,TRUE)
    special(DrawWholeMapView)
    }
    return
}

script Quest_Restaurantexpansion_Berkeley_HangPosterTile_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_RESTAURANTEXPANSION1,SUB_QUEST_4)
    if (var(VAR_RESULT)){
    setmetatile(12,0,0x24D,TRUE)
    setmetatile(12,1,0x255,TRUE)
    special(DrawWholeMapView)
    }
    return
}

script Quest_Restaurantexpansion_SOMA_HangPosterTile_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_RESTAURANTEXPANSION1,SUB_QUEST_3)
    if (var(VAR_RESULT)){
    setmetatile(12,0,0x24D,TRUE)
    setmetatile(12,1,0x255,TRUE)
    special(DrawWholeMapView)
    }
    return
}

script Quest_Restaurantexpansion_CountProgress_Script{
    setvar(VAR_TEMP_0,0)

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_RESTAURANTEXPANSION1,SUB_QUEST_1)
        if(var(VAR_RESULT)){
            addvar(VAR_TEMP_0,1)
        }
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_RESTAURANTEXPANSION1,SUB_QUEST_2)
        if(var(VAR_RESULT)){
            addvar(VAR_TEMP_0,1)
        }
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_RESTAURANTEXPANSION1,SUB_QUEST_3)
        if(var(VAR_RESULT)){
            addvar(VAR_TEMP_0,1)
        }
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_RESTAURANTEXPANSION1,SUB_QUEST_4)
        if(var(VAR_RESULT)){
            addvar(VAR_TEMP_0,1)
        }
        return
}

movement Quest_Restaurantexpansion_WalkToShop_Movement{
    walk_left * 28
    step_end
}

movement Quest_Restaurantexpansion_WalkToLeader_Movement{
    walk_up * 5
    step_end
}
movement Quest_Restaurantexpansion_WalkAway_Movement{
    walk_down * 2
    step_end
}
movement Quest_Restaurantexpansion_Jump_Movement{
    jump_in_place_up
    step_end
}
movement Quest_Restaurantexpansion_ReturnToDoorAndLook_Movement{
    walk_up * 2
    face_player
    step_end
}
movement Quest_Restaurantexpansion_ReturnToDoor_Movement{
    walk_up * 2
    step_end
}
movement Quest_Restaurantexpansion_GoInside_A_Movement{
    walk_up
    step_end
}
movement Quest_Restaurantexpansion_GoInside_B_Movement{
    walk_up * 2
    step_end
}
movement Quest_Restaurantexpansion_GoInside_C_Movement{
    walk_left
    walk_up * 2
    step_end
}
movement Quest_Restaurantexpansion_WalkInside_Movement{
    walk_left * 8
    step_end
}

text gText_Restaurantexpansion_HelloPlayer{
    format("Hello {PLAYER}. What a delicious surprise!")
}
text gText_Restaurantexpansion_AreYouHere{
    format("Are you here for the grand re-opening of the updated menu for Millenium Bakery?")
}
text gText_Restaurantexpansion_IDidntEven{
    format("I didn't even know that was a thing...")
}
text gText_Restaurantexpansion_OhCome{
    format("Oh! Come on, we'll go together.")
}
text gText_Restaurantexpansion_IfMyRestaurant{
    format("If my restaurant is going to be at the peak of Sanfran culinary culture, there's a lot of work happening in the front of house to make that happen. I have to make sure I understand region's preferred palettes, while also making sure I can continually elevate the plates that leave my line.")
}
text gText_Restaurantexpansion_ITry{
    format("I try to stay ahead of the tickets, one resturant opening at a time.")
}
text gText_Restaurantexpansion_WhatDoMeanByElevate{
    format("What do you mean by elevate?")
}
text gText_Restaurantexpansion_TheBestCooksAndFreshest{
    format("I'm not just trying to plate up Sanfran cuisine, I'm serving the best cuts of Sanfran cuisine. Freshest ingredients from the ground, finest Kalosian chefs, with no expense spared. I want my food to be the definitive dining experience of Sanfran!")
}
text gText_Restaurantexpansion_HeyManYouGot{
    format("Hey man, you got a couple dollars for food.")
}
text gText_Restaurantexpansion_SorryIDont{
    format("Sorry, I don't have anything.")
}
text gText_Restaurantexpansion_ItsCool{
    format("Its cool. Come on, little man.")
}
text gText_Restaurantexpansion_WaitHoldOn{
    format("Wait! Hold on.")
}
text gText_Restaurantexpansion_CanIActually{
    format("Can I actually buy you dinner?")
}
text gText_Restaurantexpansion_WaitReally{
    format("Wait, really? That's awful nice man.")
}
text gText_Restaurantexpansion_YeahThisPlaceJust{
    format("This restaurant is experimenting with a blend of flavors tonight, and I want - hold on.")
}
text gText_Restaurantexpansion_PlayerComeTo{
    format("{PLAYER}, we'll have to take a raincheck. Come see me in Mission soon!")
}
text gText_Restaurantexpansion_PlayerSorryAbout{
	format("{PLAYER}, sorry about the restaurant in Oakland!")
}
text gText_Restaurantexpansion_NoProblemThat{
	format("No problem! That was a really nice thing you did.")
}
text gText_Restaurantexpansion_TheyReallyOpened{
	format("They really expanded my palette.")
}
text gText_Restaurantexpansion_HowCanI{
	format("How can I serve the people of this region if the majority of the people can't afford our menu? That kid had never even heard of Cioppino. A good Cioppino? With Cloyster and Seaking? To die for!")
}
text gText_Restaurantexpansion_ThatsWhy{
	format("That's why I'm making some changes. I'm reopening my restaurant, with a Pay What You Can model. Anybody can eat here and experience the best cuisine Sanfran has to offer.")
}
text gText_Restaurantexpansion_Wow{
	format("Wow! That's a huge change, congratulations!")
}
text gText_Restaurantexpansion_IveBeenHardAtWork{
	format("I've been hard at work adjusting the menu and suppliers to make sure it'll all work, but I'm ready for the next step in the recipe. That's where you come in!")
}
text gText_Restaurantexpansion_MeICant{
	format("Me? But I can't really cook...")
}
text gText_Restaurantexpansion_NotCookingOutreach{
	format("Not cooking. Outreach! You're a Trainer, and you've traveled around the region - you've met all the people that I wasn't serving before.")
}
text gText_Restaurantexpansion_IWantToWorkWith{
	format("Pok√©mon Centers are great places where people can gather, and the front of house there are all seen as members of their local communities. I want you to deliver meals to a few key nurses, and convince them to hang some flyers in their lobbies. What'd you say?")
}
text gText_Restaurantexpansion_KindaWeird{
	format("Kinda weird to be advertising food I've never eaten... but its for a good cause.")
}
text gText_Restaurantexpansion_SureICan{
	format("Sure, I'm happy to help.")
}
text gText_Restaurantexpansion_FirstYoureGoing{
	format("First, you're going to {STR_VAR_1}. A lot of my existing clients are there, and I want to whet their appetite for the grand re-opening.")
}
text gText_Restaurantexpansion_WaitOnlyOne{
	format("Wait, I thought you wanted to do a couple different places?")
}
text gText_Restaurantexpansion_OfCourse{
	format("Yeah, but I'm cooking the meals one at a time! I can't have you dragging soggy food around the region like Doordash! Deliver that one, and then come back to me for the next one.")
}
text gText_Restaurantexpansion_IRegret{
	format("...I regret this now.")
}
text gText_Restaurantexpansion_NextIs{
	format("Next on the menu is {STR_VAR_1}! Here you go.")
}
text gText_Restaurantexpansion_HowAreYouPaying{
	format("Hey Falkner, how are you paying for this new model?")
}
text gText_Restaurantexpansion_WellMyClients{
    format("Well, my clients were in a top shelf price range - money was never an object. A lot of them won't notice if I double prices! I'll keep up the same service and excellent food... well, you get it.")
}
text gText_Restaurantexpansion_NowGoTo{
	format("Okay, now go to {STR_VAR_1}. Gotta get those techies that work downtown, you know?")
}
text gText_Restaurantexpansion_HowManyOf{
	format("How many of these are left?")
}
text gText_Restaurantexpansion_JustOneMore{
	format("Just one more after this!")
}
text gText_Restaurantexpansion_LastOne{
	format("Last order for the night... {STR_VAR_1}! Students have to eat too, you know?")
}

text gText_Restaurantexpansion_ThisIsExhausting{
	format("This is exhausting... I feel like I'm working in their kitchen!")
}
text gText_Restaurantexpansion_GottaGoBeforeIt{
	format("Okay, head to {STR_VAR_1}! The food is dying in the window, and I need delivery on the fly!")
}
text gText_Restaurantexpansion_PlayerThisIs{
	format("{PLAYER}, this is excellent work, three stars!")
}
text gText_Restaurantexpansion_Itwasgood{
	format("It was good to help out.")
}
text gText_Restaurantexpansion_NowToWorkOnHiring{
	format("Now to work on hiring...")
}
text gText_Restaurantexpansion_ActuallyQuestion{
	format("Actually, I have a question. Have you eaten at CafeVenetia?")
}
text gText_Restaurantexpansion_YesILove{
	format("Yes, I absolutely love their Tauros Burrito!")
}
text gText_Restaurantexpansion_TheyreLaunchingANew{
	format("They're launching a new menu, and sent over a meal for you to try. And if you like it...")
}
text gText_Restaurantexpansion_ThisIsDelicious{
	format("This is delicious wow! I'll absolutely tell people about this. And yeah, you can hang that right there.")
}
text gText_Restaurantexpansion_ThatWentWell{
	format("That went well!")
}
text gText_Restaurantexpansion_NahThatsSomeRich{
	format("Nah, that's some rich people food, you know?.")
}
text gText_Restaurantexpansion_TheOwnerIsMoving{
	format("The owner is moving to a Pay What You Can model, which is why I'm here! They want you to try some of their new menu items and see if you like...")
}
text gText_Restaurantexpansion_OkayThisIs{
	format("Okay, this is pretty tight. And you said its cheaper now? Okay, I'll have to check it out soon. And yeah, that flyer right there is perfect.")
}
text gText_Restaurantexpansion_TwoDown{
	format("Two down!")
}
text gText_Restaurantexpansion_NoIDontReally{
	format("No, they never had salads and that's really what I'm into right now!")
}
text gText_Restaurantexpansion_TheyreChangingTheir{
	format("They're changing up their menu and expanding their options. I brought some, free of charge for you to try...")
}
text gText_Restaurantexpansion_FancyOrNot{
	format("I don't know if its better than a salad, but its tasty for sure. I'll mention it to hungry Trainers. Put the flyers wherever you want!")
}
text gText_Restaurantexpansion_IDontThinkLeader{
	format("I don't think Falkner realizes what he's dealing with out here...")
}
text gText_Restaurantexpansion_TheyDontHaveMy{
	format("That place is too up tight, I just want somewhere casual I can eat between classes, you know?")
}
text gText_Restaurantexpansion_TheyreAddingNew{
	format("They're doing a total makeover to be more relaxed, and I brought over a free meal! And I wanted to hand some posters...")
}
text gText_Restaurantexpansion_ChillVibes{
    format("Chill vibes and good food? That's my kind of spot. I can talk this up all day. And yeah, right by the PC, good spot for that poster.")
}
text gText_Restaurantexpansion_TheLastOne{
    format("The last one! Time to head back to {STR_VAR_1}, finally.")
}
