const PLAYER = OBJ_EVENT_ID_PLAYER
const BUGSY = 1
//script Quest_HybridCulture
//text gText_Quest_HybridCulture

script Quest_HybridCulture_HideLeader_Script{

    if (var(VAR_BUGSY_STATE) != QUEST_1_COMPLETE){
        setflag(FLAG_TEMP_1) //Make Bugsy invisible in Japantown
    }
}

script Quest_HybridCulture_CheckSubquests_Script{

    questmenu(QUEST_MENU_CHECK_ACTIVE,QUEST_HYBRIDCULTURE)

    if (!var(VAR_RESULT)){
        return
    }

    setvar(VAR_TEMP_0,0)

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_HYBRIDCULTURE, SUB_QUEST_1)
    if (var(VAR_RESULT)){
        addvar(VAR_TEMP_0,1)
    }

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_HYBRIDCULTURE, SUB_QUEST_2)
    if (var(VAR_RESULT)){
        addvar(VAR_TEMP_0,1)
    }

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_HYBRIDCULTURE, SUB_QUEST_3)
    if (var(VAR_RESULT)){
        addvar(VAR_TEMP_0,1)
    }

    if (var(VAR_TEMP_0) == 3){
        questmenu(QUEST_MENU_SET_REWARD,QUEST_HYBRIDCULTURE)
    }

    return
}

script Quest_HybridCulture_Event_Script{
    lock
        call(Quest_HybridCulture_CheckSubquests_Script)
        returnqueststate(QUEST_HYBRIDCULTURE)

        switch(var(VAR_RESULT)){
            case QUEST_MENU_SET_ACTIVE: 
                goto(Quest_HybridCulture_Active_Dialogue)
            case QUEST_MENU_SET_REWARD: goto(Quest_HybridCulture_Reward_Dialogue)
            case QUEST_MENU_COMPLETE_QUEST: goto(Quest_HybridCulture_Complete_Dialogue)
            default: goto(Quest_HybridCulture_StartQuest_Dialogue)
        }
}

script Quest_HybridCulture_StartQuest_Dialogue{
    setvar(VAR_MUGSHOT,MUGSHOT_BUGSY)
    callnative(DrawMugshot)
    msgbox(gText_Quest_HybridCulture_HeyThanksFor,MSGBOX_NPC)
    msgbox(gText_Quest_HybridCulture_IThinkINeedToLearn,MSGBOX_NPC)
    callnative(ClearMugshot)

    setvar(VAR_MUGSHOT,MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_Quest_HybridCulture_HaveYouStarted,MSGBOX_NPC)
    callnative(ClearMugshot)

    setvar(VAR_MUGSHOT,MUGSHOT_BUGSY)
    callnative(DrawMugshot)
    msgbox(gText_Quest_HybridCulture_IHaventYet,MSGBOX_NPC)
    msgbox(gText_Quest_HybridCulture_CanYouHelp,MSGBOX_NPC)
    callnative(ClearMugshot)
    startquest(QUEST_HYBRIDCULTURE)
    end
}

script Quest_HybridCulture_Active_Dialogue{
    setvar(VAR_MUGSHOT,MUGSHOT_BUGSY)
    callnative(DrawMugshot)
    msgbox(gText_Quest_HybridCulture_HaveYouHad,MSGBOX_NPC)
    callnative(ClearMugshot)
    end
}

script Quest_HybridCulture_Reward_Dialogue{
    setvar(VAR_MUGSHOT,MUGSHOT_BUGSY)
    callnative(DrawMugshot)
    msgbox(gText_Quest_HybridCulture_OhWhatDidYouLearn,MSGBOX_NPC)
    closemessage
    callnative(ClearMugshot)

    fadescreen(FADE_TO_BLACK)
    msgbox(gText_Quest_HybridCulture_PlayerExplained)
    closemessage
    fadescreen(FADE_FROM_BLACK)

    setvar(VAR_MUGSHOT,MUGSHOT_BUGSY)
    callnative(DrawMugshot)
    msgbox(gText_Quest_HybridCulture_ThankYou,MSGBOX_NPC)
    closemessage
    callnative(ClearMugshot)

    call(Quest_HybridCulture_GiveReward_Script)

    completequest(QUEST_HYBRIDCULTURE)
    setvar(VAR_BUGSY_STATE,QUEST_2_COMPLETE)
    end
}

script Quest_HybridCulture_GiveReward_Script{
    giveitem(ITEM_MASTER_BALL,1)
    return
}

script Quest_HybridCulture_Complete_Dialogue{
    setvar(VAR_MUGSHOT,MUGSHOT_BUGSY)
    callnative(DrawMugshot)
    msgbox(gText_Quest_HybridCulture_IHaveSoMuch,MSGBOX_NPC)
    closemessage
    callnative(ClearMugshot)

    applymovement(BUGSY,Quest_HybridCulture_WalkBackIntoGym_Movement)
    waitmovement(BUGSY)
    opendoor(17,6)
    waitdooranim
    applymovement(BUGSY,Common_Movement_WalkUp2)
    waitmovement(BUGSY)
    //delay(16)
    playse(SE_EXIT)
    removeobject(BUGSY)
    waitse
    closedoor(17,6)
    end
}

text gText_Quest_HybridCulture_HeyThanksFor{
    format("Hey, thanks again for your help in Chinatown. That was a rough situation, but was a wake up call. That's not anything like what I want to be.")
}
text gText_Quest_HybridCulture_IThinkINeedToLearn{
    format("I think I need to learn more about the culture of SanFran.")
}
text gText_Quest_HybridCulture_HaveYouStarted{
    format("Have you started with just Japantown? Maybe find the best parts of your culture and see how they're represented here.")
}
text gText_Quest_HybridCulture_IHaventYet{
    format("I haven't yet. But perhaps that's a good idea.")
}
text gText_Quest_HybridCulture_CanYouHelp{
    format("Can you help with that? You're a traveling Trainer, so you'll see more of Japantown than I will. What are the great parts of Japantown culture?")
}
text gText_Quest_HybridCulture_HaveYouHad{
    format("Have you had any luck learning about Japantown culture?")
}
text gText_Quest_HybridCulture_OhWhatDidYouLearn{
    format("Oh! What did you learn?")
}
text gText_Quest_HybridCulture_PlayerExplained{
    format("{PLAYER} explained what they learned about Japantown's food, traditions, and fashion.")
}
text gText_Quest_HybridCulture_ThankYou{
    format("Thank you. *sniff* In a way, it is beautiful to see my country appreciated in this new way. I'm glad the people here love my culture as much I do. Please, take this!")
}
text gText_Quest_HybridCulture_IHaveSoMuch{
    format("I think for now, I'll go back to my Gym. Now that you're stronger, I'd love to battle you again!")
}

movement Quest_HybridCulture_WalkBackIntoGym_Movement{
    walk_down
    walk_right * 2
    face_up
    step_end
}
