//Quest_ContractorPorygon_
//gText_ContractorPorygon_

script Quest_ContractorPorygon_Debug_Script{
    addvar(VAR_TEMP_0,1)
    additem(ITEM_LAVA_COOKIE,1)
    additem(ITEM_RAGE_CANDY_BAR,1)
    additem(ITEM_PEWTER_CRUNCHIES,1)
    end
}

script Quest_ContractorPorygon_StartQuest_Dialogue{
    lock
    faceplayer
    msgbox(gText_ContractorPorygon_HeyWereAStartup)    
    msgbox(gText_ContractorPorygon_WeHaventEatenOrSlept)    
    msgbox(gText_ContractorPorygon_WhenWeDoGetHungry)
    closemessage
    startquest(QUEST_CONTRACTORPORYGON)
    release
    end
}
script Quest_ContractorPorygon_Subquest1_Ineligible_Dialogue{
    msgbox(gText_ContractorPorygon_WantToGetOutThisUpgrade,MSGBOX_NPC)
    applymovement(VAR_LAST_TALKED,Common_Movement_FaceOriginalDirection)
    waitmovement(VAR_LAST_TALKED)
    
    end
}
script Quest_ContractorPorygon_Subquest2_Ineligible_Dialogue{
    msgbox(gText_ContractorPorygon_TheyWantMeToReplicate,MSGBOX_NPC) 
    applymovement(VAR_LAST_TALKED,Common_Movement_FaceOriginalDirection)
    waitmovement(VAR_LAST_TALKED)
    
    end
}
script Quest_ContractorPorygon_Subquest3_Ineligible_Dialogue{
    msgbox(gText_ContractorPorygon_NotReallyHungryYet,MSGBOX_NPC) 
    applymovement(VAR_LAST_TALKED,Common_Movement_FaceOriginalDirection)
    waitmovement(VAR_LAST_TALKED)
    
    end
}
script Quest_ContractorPorygon_Subquest1_Request_Dialogue{
    lock
    faceplayer
    msgbox(gText_ContractorPorygon_OkayGoodStoppingPoint)
    msgbox(gText_ContractorPorygon_CouldIGetDumplings)

    checkitem(ITEM_PEWTER_CRUNCHIES,1)
    if (var(VAR_RESULT)){
        goto(Quest_ContractorPorygon_Subquest1_Thanks_Dialogue)
    }
    closemessage

    release
    end
}
script Quest_ContractorPorygon_Subquest2_Request_Dialogue{
    lock
    faceplayer
    msgbox(gText_ContractorPorygon_IThinkTheDisc)
    msgbox(gText_ContractorPorygon_AreYouStillGetting)

    checkitem(ITEM_LAVA_COOKIE,1)
    if (var(VAR_RESULT)){
        goto(Quest_ContractorPorygon_Subquest2_Thanks_Dialogue)
    }
    closemessage

    release
    end 
}
script Quest_ContractorPorygon_Subquest3_Request_Dialogue{
    lock
    faceplayer
    msgbox(gText_ContractorPorygon_TheProgressTheyreMaking) 
    msgbox(gText_ContractorPorygon_IDidntRealize)

    checkitem(ITEM_RAGE_CANDY_BAR,1)
    if (var(VAR_RESULT)){
        goto(Quest_ContractorPorygon_Reward_Dialogue)
    }
    closemessage

    release
    end
}

script Quest_ContractorPorygon_GivePorygon_Script{

    switch(var(VAR_LAST_TALKED)){
        case 2:
            bufferspeciesname(STR_VAR_2,SPECIES_PORYGON)
            givecustommon(SPECIES_PORYGON,1,ITEM_EVIOLITE ,ITEM_POKE_BALL ,NATURE_MODEST,1,0,0,0,0,0,0,,,,,,,MOVE_TRI_ATTACK,MOVE_NONE,MOVE_NONE,MOVE_NONE)
        case 1:
            bufferspeciesname(STR_VAR_2,SPECIES_PORYGON_Z)
            givecustommon(SPECIES_PORYGON_Z,47,ITEM_DUBIOUS_DISC ,ITEM_POKE_BALL ,NATURE_BOLD,0,132,0,252,72,52,0,31,31,31,31,31,31,MOVE_NASTY_PLOT,MOVE_HYPER_BEAM,MOVE_EERIE_IMPULSE,MOVE_RECOVER)
        case 3:
            bufferspeciesname(STR_VAR_2,SPECIES_PORYGON2)
            givecustommon(SPECIES_PORYGON2,26,ITEM_UPGRADE ,ITEM_POKE_BALL ,NATURE_QUIET,1,252,0,160,0,16,80,,,,,,,MOVE_TRICK_ROOM,MOVE_NONE,MOVE_NONE,MOVE_NONE)
    }

        playfanfare(MUS_OBTAIN_ITEM)
        msgbox(gText_ContractorPorygon_YouRecievedA)
        waitmessage
        waitfanfare
}

script Quest_ContractorPorygon_Subquest1_Thanks_Dialogue{
    lock
    faceplayer 
    msgbox(gText_ContractorPorygon_ScoreSalad) 
    closemessage
    call(Quest_ContractorPorygon_GivePorygon_Script)
    applymovement(VAR_LAST_TALKED,Common_Movement_FaceOriginalDirection)
    waitmovement(VAR_LAST_TALKED)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CONTRACTORPORYGON,SUB_QUEST_1)
    release
    end
}
script Quest_ContractorPorygon_Subquest2_Thanks_Dialogue{
    lock
    faceplayer 
    msgbox(gText_ContractorPorygon_YesssDumpling) 
    closemessage
    call(Quest_ContractorPorygon_GivePorygon_Script)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CONTRACTORPORYGON,SUB_QUEST_2)
    applymovement(VAR_LAST_TALKED,Common_Movement_FaceOriginalDirection)
    waitmovement(VAR_LAST_TALKED)
    release
    end
}
script Quest_ContractorPorygon_Subquest1_Script{
    special(GetNumberOfBadges)
    
    if (var(VAR_RESULT) < 3){
        goto(Quest_ContractorPorygon_Subquest1_Ineligible_Dialogue)
    }

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CONTRACTORPORYGON,SUB_QUEST_1)

    if (!var(VAR_RESULT)){
        goto(Quest_ContractorPorygon_Subquest1_Request_Dialogue)
    }else{
        goto(Quest_ContractorPorygon_Focused_Dialogue)
    }
}
script Quest_ContractorPorygon_Subquest2_Script{
    special(GetNumberOfBadges)
    
    if (var(VAR_RESULT) < 6){
        goto(Quest_ContractorPorygon_Subquest2_Ineligible_Dialogue)
    }

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CONTRACTORPORYGON,SUB_QUEST_2)

    if (!var(VAR_RESULT)){
        goto(Quest_ContractorPorygon_Subquest2_Request_Dialogue)
    }else{
        goto(Quest_ContractorPorygon_Focused_Dialogue)
    }
}
script Quest_ContractorPorygon_Subquest3_Script{

    setvar(VAR_TEMP_2,0)

    special(GetNumberOfBadges)
    if (var(VAR_RESULT) > 7){
        addvar(VAR_TEMP_2,1)
    }

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CONTRACTORPORYGON,SUB_QUEST_1)
        if (var(VAR_RESULT)){
            addvar(VAR_TEMP_2,1)
        }
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_CONTRACTORPORYGON,SUB_QUEST_2)
        if (var(VAR_RESULT)){
            addvar(VAR_TEMP_2,1)
        }

    if (var(VAR_TEMP_2) == 3){
        goto(Quest_ContractorPorygon_Subquest3_Request_Dialogue)
    }else{
        goto(Quest_ContractorPorygon_Subquest3_Ineligible_Dialogue)
    }
}

script Quest_ContractorPorygon_Reward_Dialogue{
    msgbox(gText_ContractorPorygon_HellYesCurry)
    closemessage
    call(Quest_ContractorPorygon_GivePorygon_Script)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_CONTRACTORPORYGON,SUB_QUEST_3)
    applymovement(VAR_LAST_TALKED,Common_Movement_FaceOriginalDirection)
    waitmovement(VAR_LAST_TALKED)
    completequest(QUEST_CONTRACTORPORYGON)
    release
    end
}
script Quest_ContractorPorygon_Completed_Dialogue{
    msgbox(gText_ContractorPorygon_IThinkWereGonnaMake,MSGBOX_NPC)
    applymovement(VAR_LAST_TALKED,Common_Movement_FaceOriginalDirection)
    waitmovement(VAR_LAST_TALKED)
    end
}

script Quest_ContractorPorygon_Focused_Dialogue{
    lock
    msgbox(gText_ContractorPorygon_Focused)
    closemessage
    release
    end
}
script Quest_ContractorPorygon_Interact_Shan_Script{
	returnqueststate(QUEST_CONTRACTORPORYGON)

	    switch(var(VAR_RESULT)){
	        case QUEST_MENU_SET_ACTIVE: goto(Quest_ContractorPorygon_Subquest3_Script)
	        case QUEST_MENU_COMPLETE_QUEST: goto(Quest_ContractorPorygon_Completed_Dialogue)
	        default: goto(Quest_ContractorPorygon_StartQuest_Dialogue)
	}
}
script Quest_ContractorPorygon_Interact_Evan_Script{
    	returnqueststate(QUEST_CONTRACTORPORYGON)

	    switch(var(VAR_RESULT)){
	        case QUEST_MENU_SET_ACTIVE: goto(Quest_ContractorPorygon_Subquest1_Script)
	        default: goto(Quest_ContractorPorygon_Focused_Dialogue)
	}
}
script Quest_ContractorPorygon_Interact_Nathan_Script{
    	returnqueststate(QUEST_CONTRACTORPORYGON)

	    switch(var(VAR_RESULT)){
	        case QUEST_MENU_SET_ACTIVE: goto(Quest_ContractorPorygon_Subquest2_Script)
	        default: goto(Quest_ContractorPorygon_Focused_Dialogue)
	}
}
text gText_ContractorPorygon_HeyWereAStartup{
	format("hey we're a startup. the league asked us to replicate the work of Cinnabar Pokemon Lab and create Porygon... and its evolutions... in one work")
}
text gText_ContractorPorygon_WeHaventEatenOrSlept{
	format("its a tough task. we haven't eaten or slept in about 90 hours")
}
text gText_ContractorPorygon_WhenWeDoGetHungry{
	format("when we do get hungry, we're not gonna have time to get food. could you swing by later and grab food for us? we'll tell you what we want.")
}
text gText_ContractorPorygon_WantToGetOutThisUpgrade{
	format("want to get out this up-grade.... and i'm so close! after this block of code, I'll eat...")
}
text gText_ContractorPorygon_TheyWantMeToReplicate{
	format("they want me to replicate the dubious disc? that's why its DUBIOUS!")
}
text gText_ContractorPorygon_NotReallyHungryYet{
	format("not really hungry right now... maybe later though. you're probably wondering why we couldn't just order delievery, right? well, we're testing the Porygon on our phones! can't use them for anything else while the tests are running, haha.")
}
text gText_ContractorPorygon_OkayGoodStoppingPoint{
	format("okay, good stopping point!")
}
text gText_ContractorPorygon_CouldIGetDumplings{
	format("could i get some dumplings? dumpling king in the sunset is the best spot for them.")
}
text gText_ContractorPorygon_IThinkTheDisc{
	format("i think the disc is done....")
}
text gText_ContractorPorygon_AreYouStillGetting{
	format("i hear you were getting us food, is that still a thing? Yeah? can I get a salad from Sweetgreeen? It's in Berkeley.")
}
text gText_ContractorPorygon_TheProgressTheyreMaking{
	format("the progress they're making is fucking insane! are the prototypes working well?")
}
text gText_ContractorPorygon_IDidntRealize{
	format("i didn't realize how hungry i was gonna be")
}
text gText_ContractorPorygon_ScoreSalad{
	format("score! salad. thank you so much! i just finished this, give is a try.")
}
text gText_ContractorPorygon_YesssDumpling{
	format("yesssss dumplings this is gonna be so good. here, take this Pokemon.")
}
text gText_ContractorPorygon_HellYesCurry{
	format("hellllll yes curry! UGH this shit is gonna slap. here, take one of these!")
}
text gText_ContractorPorygon_IThinkWereGonnaMake{
	format("I think we're gonna make the deadline!")
}
text gText_ContractorPorygon_Focused{
	format("Hmm.....")
}
text gText_ContractorPorygon_YouRecievedA{
    format("You recieved a {STR_VAR_2}!")
}
