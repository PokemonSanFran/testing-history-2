const CHUCK = 21
const FLAG_HIDE_CHUCK = FLAG_TEMP_5
const FLAG_UNLOCK_DOOR = FLAG_TEMP_3

//gText_StealTheAxe_
//Quest_StealTheAxe_

script Quest_StealTheAxe_HideLeader_Script{
    special(GetNumberOfBadges)

        if (var(VAR_CHUCK_STATE) != BATTLE_1_COMPLETE){
            setflag(FLAG_HIDE_CHUCK)
        }

    if (var(VAR_RESULT) < 6){
        setflag(FLAG_HIDE_CHUCK)
    }

    switch (var(VAR_ALAMEDA_STATE)){
        case START_SURVIVAL_CHANCE:
        case POST_SURVIVAL_CHANCE:
        case WHY_ARE_YOU_HELPING_THEM:
        case POST_EARTHQUAKE:
        case START_EARTHQUAKE_RESCUE:
        case POST_EARTHQUAKE_RESCUE:
        case MEET_AT_PSFROUTE8:
            setflag(FLAG_HIDE_CHUCK)
    }
}

script Quest_StealTheAxe_Event_Script{
    setvar(VAR_TEMP_0,0)
    bufferitemname(STR_VAR_3,ITEM_COIN_CASE)
    returnqueststate(QUEST_STEALTHEAXE)

        lock
        faceplayer

        switch(var(VAR_RESULT)){
            case QUEST_MENU_SET_REWARD:
                goto(Quest_StealTheAxe_Reward_Dialogue)
            case QUEST_MENU_SET_ACTIVE:
                goto(Quest_StealTheAxe_Active_Dialogue)
            default:
                call(Quest_StealTheAxe_CheckItem_Script)
                goto(Quest_StealTheAxe_StartQuest_Dialogue)
        }
    release
        end
}

script Quest_StealTheAxe_StartQuest_Dialogue{
        msgbox(gText_StealTheAxe_PlayerGetToSee,,SPEAKER_CHUCK)
        msgbox(gText_StealTheAxe_ShouldntYouBe,,SPEAKER_ZOEY,,EMOTE_CONFUSE)
        msgbox(gText_StealTheAxe_ThatsExactlyWhy,,SPEAKER_CHUCK)
        msgbox(gText_StealTheAxe_IJustCameFromThe,,SPEAKER_CHUCK)
        msgbox(gText_StealTheAxe_IKeepHearingAbout,,SPEAKER_CHUCK)
        msgbox(gText_StealTheAxe_JustLikeWhitney,,SPEAKER_CHUCK,,EMOTE_ANGRY)
        msgbox(gText_StealTheAxe_IStillDontKnow,,SPEAKER_CHUCK)
        msgbox(gText_StealTheAxe_SomebodyWithMyCreds,,SPEAKER_CHUCK)
        msgbox(gText_StealTheAxe_ButIfSomebody,,SPEAKER_CHUCK)
        startquest(QUEST_STEALTHEAXE)
        msgbox(gText_StealTheAxe_IllBeHere,,SPEAKER_CHUCK)
        closemessage
        release
        end
}

script Quest_StealTheAxe_Active_Dialogue{
    msgbox(gText_StealTheAxe_WhatsTheSitrep,MSGBOX_NPC,SPEAKER_CHUCK)
    msgbox(gText_StealTheAxe_GainingATactical,,SPEAKER_CHUCK)
    closemessage
    release
    end
}

script Quest_StealTheAxe_Reward_Dialogue{
    playse(SE_PIN)
    applymovement(CHUCK,Common_Movement_ExclamationMark)
    msgbox(gText_StealTheAxe_MissionComplete,,SPEAKER_CHUCK,TAIL_SHOUT,EMOTE_HAPPY)
    goto(Quest_StealTheAxe_Conclusion_Dialogue)
}

script Quest_StealTheAxe_Conclusion_Dialogue{
    applymovement(CHUCK,Common_Movement_FaceAwayPlayer)
    waitmovement(CHUCK)
    bufferspeciesname(STR_VAR_1,SPECIES_TOXAPEX)
    msgbox(gText_StealTheAxe_NowLetsHave,,SPEAKER_CHUCK,TAIL_WHISPER)
    closemessage
    playse(SE_PIN)
    applymovement(CHUCK,Common_Movement_ExclamationMark)
    msgbox(gText_StealTheAxe_ItsNotDoing,,SPEAKER_CHUCK,TAIL_WHISPER,EMOTE_ANGRY)
    msgbox(gText_StealTheAxe_StatsAreHoldingSteady,,SPEAKER_CHUCK,TAIL_WHISPER,EMOTE_ANGRY)
    applymovement(CHUCK,Common_Movement_FacePlayer)
    waitmovement(CHUCK)
    msgbox(gText_StealTheAxe_IGotSomeBadIntel,,SPEAKER_CHUCK)
    closemessage
    call(Quest_StealTheAxe_HandleMoney_Script)
    msgbox(gText_StealTheAxe_SeeYouOnTheBattlefield,,SPEAKER_CHUCK)
    applymovement(CHUCK,Quest_StealTheAxe_LeaveNavalBase_Movement)
    completequest(QUEST_STEALTHEAXE)
    removeobject(CHUCK)
    setvar(VAR_CHUCK_STATE,QUEST_1_COMPLETE)
    setvar(VAR_BERKELEY_AXE_STATE,AXE_THROWN_AWAY)
    release
    end
}

script Quest_StealTheAxe_HandleMoney_Script{
    showmoneybox(0, 0)
    waitbuttonpress
    if (var(VAR_TEMP_0) > 0){
        addmoney(8000)
    }
    else{
            addmoney(4000)
        }
    playse(SE_SHOP)
    updatemoneybox(0)
    waitse
    waitbuttonpress
    hidemoneybox
    closemessage
    return
}

script Quest_StealTheAxe_CheckItem_Script{
    checkitem(ITEM_COIN_CASE,1)

        if (var(VAR_RESULT)){
            goto(Quest_StealTheAxe_SecretComplete_Dialogue)
        }
    return
}

script Quest_StealTheAxe_SecretComplete_Dialogue{
    setvar(VAR_TEMP_0,1)
        msgbox(gText_StealTheAxe_PlayerGetToSee,,SPEAKER_CHUCK)
        msgbox(gText_StealTheAxe_IsThatTheAxe,MSGBOX_YESNO,SPEAKER_CHUCK,,EMOTE_SHOCK)
        if (var(VAR_RESULT)){
            goto(Quest_StealTheAxe_Conclusion_Dialogue)
        }else{
            msgbox(gText_StealTheAxe_ISeeWhen,,SPEAKER_CHUCK)
        }
    release
        end
}

script Quest_StealTheAxe_CheckIfNight_Script{
    gettime
        setvar(VAR_TEMP_2,0)
        if (var(VAR_0x8000) > 20 || (var(VAR_0x8000) < 6)) {
            setvar(VAR_TEMP_2,1)
        }
    return
}

script Quest_StealTheAxe_UnlockDoor_Script{
    call(Quest_StealTheAxe_CheckIfNight_Script)
    if (var(VAR_TEMP_2) == 1){
        setflag(FLAG_UNLOCK_DOOR)
            release
            end
    }
}

script Quest_StealTheAxe_LockedDoor_Dialogue{
    returnqueststate(QUEST_STEALTHEAXE)
        if(var(VAR_RESULT) == QUEST_MENU_SET_ACTIVE){
            msgbox(gText_StealTheAxe_IShould,,SPEAKER_ZOEY,TAIL_THOUGHT)
        }
    return
}

script Quest_StealTheAxe_AxeUntouched_Script{
    msgbox(gText_StealTheAxe_TheAxe)

    call(Quest_StealTheAxe_CheckIfNight_Script)
    if (var(VAR_TEMP_2) == 1){
        msgbox(gText_StealTheAxe_TheDisplayCase,MSGBOX_YESNO)

        if (var(VAR_RESULT) == YES){
            goto(Quest_StealTheAxe_TakeAxe_Script)
        }
    }
        closemessage
        release
        end
}

script Quest_StealTheAxe_AxeStolen_Dialogue{
    msgbox(gText_StealTheAxe_YouTookTheAxe)

    returnqueststate(QUEST_STEALTHEAXE)

    if (var(VAR_RESULT) == QUEST_MENU_COMPLETE_QUEST){
        msgbox(gText_StealTheAxe_IWonderIf,,SPEAKER_ZOEY,TAIL_THOUGHT,EMOTE_CONFUSE)
    }
    closemessage
    release
    end
}

script Quest_StealTheAxe_TurnedOverAxe_Dialogue{
    msgbox(gText_StealTheAxe_TheAxeIsGoneBut,MSGBOX_SIGN)
}

script Quest_StealTheAxe_TakeAxe_Script{
        giveitem(ITEM_COIN_CASE,1)
        setvar(VAR_BERKELEY_AXE_STATE,AXE_STOLEN)

        returnqueststate(QUEST_STEALTHEAXE)
        if (var(VAR_RESULT) == QUEST_MENU_SET_ACTIVE){
            msgbox(gText_StealTheAxe_NowToBring,,SPEAKER_ZOEY,TAIL_THOUGHT)
        }else{
            msgbox(gText_StealTheAxe_HuhWhy,,SPEAKER_ZOEY,TAIL_THOUGHT,EMOTE_CONFUSE)
        }
        closemessage
        release
        end
}

script Quest_StealTheAxe_DiscussStolen_Dialogue{
    call(Berkeley_Leland_Story_Dialogue)
    lock
    msgbox(gText_StealTheAxe_SomebodyStole,,SPEAKER_LELAND,TAIL_SHOUT,EMOTE_SHOCK)
    msgbox(gText_StealTheAxe_IDontThinkI,,SPEAKER_ZOEY,TAIL_THOUGHT)
    closemessage
    release
    end
}

script Quest_StealTheAxe_FirstAsk_Dialogue{
        call(Berkeley_Leland_Story_Dialogue)
        msgbox(gText_StealTheAxe_SomebodyStole,,SPEAKER_LELAND,TAIL_SHOUT,EMOTE_SHOCK)
        msgbox(gText_StealTheAxe_YouHaveIt,,SPEAKER_LELAND,,EMOTE_SHOCK)
        setvar(VAR_BERKELEY_AXE_STATE,AXE_ASKED)
}

script Quest_StealTheAxe_AskAxe_Dialogue{
    applymovement(VAR_LAST_TALKED,Common_Movement_FacePlayer)
    if (var(VAR_BERKELEY_AXE_STATE) == AXE_THROWN_AWAY){
        call(Quest_StealTheAxe_FirstAsk_Dialogue)
    }
    msgbox(gText_StealTheAxe_IllTrade,MSGBOX_YESNO,SPEAKER_LELAND)

    if (var(VAR_RESULT) == YES){
        goto(Quest_StealTheAxe_Trade_Script)
    }else{
        msgbox(gText_StealTheAxe_YouHaveNoIdea,,SPEAKER_LELAND,,EMOTE_SAD)
    }
    closemessage
        release
        end
}

script Quest_StealTheAxe_Trade_Script{
    removeitem(ITEM_COIN_CASE,1)
    setvar(VAR_BERKELEY_AXE_STATE,AXE_TURNED_OVER)

    setflag(FLAG_FORCE_SHINY)
    givemon(SPECIES_STUFFUL,20,ITEM_NONE)
    clearflag(FLAG_FORCE_SHINY)

    setvar(VAR_TEMP_1, SPECIES_STUFFUL)
        if (var(VAR_RESULT) == 0){
            call(Quest_StealTheAxe_SendGiftToParty_Script)
        } else {
            call(Quest_StealTheAxe_SendGiftToPC_Script)
        }
        
        call(Quest_StealTheAxe_HasAxe_Dialogue)
        closemessage
        release
        end
}

script Quest_StealTheAxe_HasAxe_Dialogue{
    msgbox(gText_StealTheAxe_IllCherishThisTakeCare,MSGBOX_NPC,SPEAKER_LELAND,,EMOTE_HAPPY)
}

script Quest_StealTheAxe_SendGiftToParty_Script {
    call(Quest_StealTheAxe_RecievedGiftFanfare)
        msgbox(gText_NicknameThisPokemon,MSGBOX_YESNO)

        if (var(VAR_RESULT) == YES){
            call(Common_EventScript_GetGiftMonPartySlot)
                call(Common_EventScript_NameReceivedPartyMon)
        }
}

script Quest_StealTheAxe_SendGiftToPC_Script {
    call(Quest_StealTheAxe_RecievedGiftFanfare)
        msgbox(gText_NicknameThisPokemon,MSGBOX_YESNO)

        if (var(VAR_RESULT) == YES){
            call(Common_EventScript_NameReceivedBoxMon)
        }
    call(Common_EventScript_TransferredToPC)
}

script Quest_StealTheAxe_RecievedGiftFanfare {
    bufferspeciesname(STR_VAR_2,SPECIES_STUFFUL)
        playfanfare(MUS_OBTAIN_ITEM)
        msgbox(gText_Quest_StealTheAxe_ObtainedGift)
        waitmessage
        waitfanfare
        bufferspeciesname(STR_VAR_1, SPECIES_STUFFUL)
}

movement Quest_StealTheAxe_LeaveNavalBase_Movement{
    walk_up * 6
    step_end
}

text gText_StealTheAxe_IShould{
    format("Hmm... I should come back tonight.")
}

text gText_StealTheAxe_PlayerGetToSee{
    format("{PLAYER}! Good to see you.")
}
text gText_StealTheAxe_ShouldntYouBe{
    format("Shouldn't you be fighting some war or something?")
}
text gText_StealTheAxe_ThatsExactlyWhy{
    format("That's exactly why I'm here!")
}
text gText_StealTheAxe_IJustCameFromThe{
    format("I just came from Berkeleyuniversity. They've got a display for the {STR_VAR_3}, one of the prized possessions of their Athletic department.")
}
text gText_StealTheAxe_IKeepHearingAbout{
    format("I've heard rumors that this axe provides incredible power to the wielder!")
}
text gText_StealTheAxe_JustLikeWhitney{
    format("But just like Whtiney, they've got it sealed up in some glass case! Just the intelligence that had it in our active possesion would be a huge tactical advantage!")
}
text gText_StealTheAxe_IStillDontKnow{
    format("I still don't know what this tactical advantage is for...")
}
text gText_StealTheAxe_SomebodyWithMyCreds{
    format("Somebody with my credentials can't be seen sneaking into Berkeleyuniversity at night, slipping past all the guards, and stealing the {STR_VAR_3} with almost zero security...")
}
text gText_StealTheAxe_ButIfSomebody{
    format("But if somebody else did that, I would be willing to pay hansomely for it.")
}
text gText_StealTheAxe_IllBeHere{
    format("I'll be here.")
}
text gText_StealTheAxe_WhatsTheSitrep{
    format("What's the sitrep?")
}
text gText_StealTheAxe_GainingATactical{
    format("My analysis tells me that the best strategic window to infilitate Berkeleyuniversity and get the {STR_VAR_3} is between 21:00 and 6:00.")
}
text gText_StealTheAxe_MissionComplete{
    format("Mission complete!")
}
text gText_StealTheAxe_NowLetsHave{
    format("Now let's have my {STR_VAR_1} hold it...")
}
text gText_StealTheAxe_ItsNotDoing{
    format("...its not doing anything!")
}
text gText_StealTheAxe_StatsAreHoldingSteady{
    format("Stats are holding steady... mood hasn't changed. This isn't making any difference!")
}
text gText_StealTheAxe_IGotSomeBadIntel{
    format("I got some bad intel out there. I think this thing is a hunk of junk. You can keep it. And as promised...")
}
text gText_StealTheAxe_SeeYouOnTheBattlefield{
    format("See you on the battlefield solider.")
}
text gText_StealTheAxe_IsThatTheAxe{
    format("Is that the {STR_VAR_3}? I would pay very handsomely if I could buy it from you, {PLAYER}.")
}
text gText_StealTheAxe_ISeeWhen{
    format("I see. When you change your mind, I'll be here.")
}
text gText_StealTheAxe_TheAxe{
    format("The {STR_VAR_3} is sitting on a pedastal.")
}
text gText_StealTheAxe_TheDisplayCase{
    format("The glass on the display case isn't locked. Take the {STR_VAR_3}?")
}
text gText_StealTheAxe_NowToBring{
    format("Time to bring this thing back to Chuck for this...tactical advantage.")
}
text gText_StealTheAxe_HuhWhy{
    format("Huh. Why did I take this? No going back now...")
}
text gText_StealTheAxe_YouTookTheAxe{
    format("You took the {STR_VAR_3}. Putting it back would be more trouble than its worth.")
}
text gText_StealTheAxe_IWonderIf{
    format("Is there anybody else that cares about this thing?")
}
text gText_StealTheAxe_TheAxeIsGoneBut{
    format("The {STR_VAR_3} is gone, but somebody else is enjoying it.")
}
text gText_StealTheAxe_SomebodyStole{
    format("Did you hear though? Some stole the {STR_VAR_3}! I bet you it was those rotten kids from Southbay...")
}
text gText_StealTheAxe_YouHaveIt{
    format("You have it!?! Oh, its fantastic to see it so up close... you know, I've been collecting rare Pokemon for many years now, and I can see you're a Trainer.")
}
text gText_StealTheAxe_YouHaveNoIdea{
    format("You have no idea what that means to me...")
}
text gText_StealTheAxe_IllTrade{
    format("I'll trade you a Shiny {STR_VAR_1} in exchange for {STR_VAR_3}, do we have a deal?")
}
text gText_StealTheAxe_IDontThinkI{
    format("I don't think I should say anything...")
}
text gText_Quest_StealTheAxe_ObtainedGift{
    format("{PLAYER} traded the {STR_VAR_3} for a Shiny {STR_VAR_2}.")
}
text gText_StealTheAxe_IllCherishThisTakeCare{
    format("I'll cherish this for many years. Take care of that {STR_VAR_2}.")
}
