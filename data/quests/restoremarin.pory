const LENORA_MARIN = 7
const FLAG_HIDE_LENORA_MARIN = FLAG_TEMP_1

//Quest_Restoremarin_
//gText_Restoremarin_

script Quest_Restoremarin_Debug_Script{
    questmenu(QUEST_MENU_CHECK_ACTIVE,QUEST_RESTOREMARIN)

        if (var(VAR_RESULT)){
            msgbox("you now have everything lenora wants.")
            closemessage
            givecustommon(SPECIES_KRABBY,100,,,,0,,,,,,,,,,,,,MOVE_KNOCK_OFF)
            givecustommon(SPECIES_COPPERAJAH,100,,,,0,,,,,,,,,,,,,MOVE_POWER_WHIP)
        }
    release
        end
}

script Quest_Restoremarin_MoveLenora_Script{
    if (var(VAR_STORYLINE_STATE) > STORY_POST_BATTLE_ARCHER_TREASUREISLAND){
        setobjectxy(LENORA_MARIN,12,18)
        setobjectxyperm(LENORA_MARIN,12,18)
        setobjectmovementtype(LENORA_MARIN,MOVEMENT_TYPE_FACE_DOWN)
    }
    return
}
script Quest_Restoremarin_HideLenora_Script{
    questmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_RESTOREMARIN)

        if (var(VAR_RESULT)){
            setflag(FLAG_HIDE_LENORA_MARIN)
        }
}

script Quest_Restoremarin_BufferStrings_Part1_Script{
    bufferspeciesname(STR_VAR_1,SPECIES_KRABBY)
        buffermovename(STR_VAR_2,MOVE_KNOCK_OFF)
        return
}

script Quest_Restoremarin_BufferStrings_Part2_Script{
    bufferspeciesname(STR_VAR_1,SPECIES_COPPERAJAH)
        buffermovename(STR_VAR_2,MOVE_POWER_WHIP)
        return
}

script Quest_Restoremarin_StartQuest_Dialogue{
        msgbox(gText_Restoremarin_YourePlayer,,SPEAKER_LENORA)

        msgbox(gText_Restoremarin_YeahImHelping,,SPEAKER_ZOEY)

        msgbox(gText_Restoremarin_AreYouHelping,,SPEAKER_LENORA)

        msgbox(gText_Restoremarin_IHearThat,,SPEAKER_ZOEY)

        msgbox(gText_Restoremarin_TheBestWay,,SPEAKER_LENORA)

        msgbox(gText_Restoremarin_RightBut,,SPEAKER_ZOEY)

        msgbox(gText_Restoremarin_HmmI,,SPEAKER_LENORA)
        msgbox(gText_Restoremarin_IHave,,SPEAKER_LENORA)

        msgbox(gText_Restoremarin_LikeTrainer,,SPEAKER_ZOEY)

        msgbox(gText_Restoremarin_OhIDidnt,,SPEAKER_LENORA)
        msgbox(gText_Restoremarin_TheresABattle,,SPEAKER_LENORA)
        msgbox(gText_Restoremarin_CanYouBring,,SPEAKER_LENORA)
        msgbox(gText_Restoremarin_IFIWant,,SPEAKER_LENORA)
        call(Quest_Restoremarin_BufferStrings_Part1_Script)
        msgbox(gText_Restoremarin_WeNeed_A,,SPEAKER_LENORA)
        call(Quest_Restoremarin_BufferStrings_Part2_Script)
        msgbox(gText_Restoremarin_WeNeed_B,,SPEAKER_LENORA)
        msgbox(gText_Restoremarin_ComeBack,,SPEAKER_LENORA)


        startquest(QUEST_RESTOREMARIN)
        release
        end
}

script Quest_Marin_ConstructMenu_Script{
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_RESTOREMARIN,SUB_QUEST_1)
        if(!var(VAR_RESULT)){
            buffermovename(STR_VAR_1,MOVE_KNOCK_OFF)
            bufferspeciesname(STR_VAR_2,SPECIES_KRABBY)
            dynmultipush("{STR_VAR_2} that knows {STR_VAR_1}",0)
        }

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_RESTOREMARIN,SUB_QUEST_2)
        if(!var(VAR_RESULT)){
            buffermovename(STR_VAR_1,MOVE_POWER_WHIP)
            bufferspeciesname(STR_VAR_2,SPECIES_COPPERAJAH)
            dynmultipush("{STR_VAR_2} that knows {STR_VAR_1}",1)
        }

    dynmultipush("Nothing yet.",2)
}

script Quest_Restoremarin_Active_Script{
    msgbox(gText_Restoremarin_WeNeedSomePretty,,SPEAKER_LENORA)

    call(Quest_Marin_ConstructMenu_Script)
    dynmultistack(24,0,TRUE,2,TRUE,0,DYN_MULTICHOICE_CB_NONE)
    closemessage

    switch(var(VAR_RESULT)){
        case 0: goto(Quest_Restoremarin_HandleKrabby_Script)
        case 1: goto(Quest_Restoremarin_HandleCopperajah_Script)
        case 2:
        case MULTI_B_PRESSED:
            goto(Quest_Restoremarin_Cancel_Dialogue)
    }
}

script Quest_Restoremarin_HandleKrabby_Script{
    special(ChoosePartyMon)
        waitstate

        if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN){
            goto(Quest_Restoremarin_Cancel_Dialogue)
        }

        checkspeciesandmove(SPECIES_KRABBY, MOVE_KNOCK_OFF, VAR_RESULT)
        if (!var(VAR_RESULT)){
            goto(Quest_Restoremarin_Cancel_Dialogue)
        }

        special(BufferMonNickname)
        call(Quest_Restoremarin_AskTakeMon_Dialogue)

        special(HasEnoughMonsForDoubleBattle)
        if (var(VAR_RESULT) != PLAYER_HAS_TWO_USABLE_MONS){
            goto(Quest_Restoremarin_NotEnoughMons_Dialogue)
        }

    special(DeleteChosenPartyMon)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_RESTOREMARIN,SUB_QUEST_1)
    goto(Quest_Restoremarin_HandleProgress_Dialogue)
}

script Quest_Restoremarin_HandleCopperajah_Script{
    special(ChoosePartyMon)
        waitstate

        if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN){
            goto(Quest_Restoremarin_Cancel_Dialogue)
        }

        checkspeciesandmove(SPECIES_COPPERAJAH, MOVE_POWER_WHIP, VAR_RESULT)
        if (!var(VAR_RESULT)){
            goto(Quest_Restoremarin_Cancel_Dialogue)
        }

        special(BufferMonNickname)
        call(Quest_Restoremarin_AskTakeMon_Dialogue)

        special(HasEnoughMonsForDoubleBattle)
        if (var(VAR_RESULT) != PLAYER_HAS_TWO_USABLE_MONS){
            goto(Quest_Restoremarin_NotEnoughMons_Dialogue)
        }

    special(DeleteChosenPartyMon)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_RESTOREMARIN,SUB_QUEST_2)
    goto(Quest_Restoremarin_HandleProgress_Dialogue)
}

script Quest_Restoremarin_AskTakeMon_Dialogue{
    msgbox(gText_Restoremarin_AreYouSure,MSGBOX_YESNO,SPEAKER_LENORA)

    if (var(VAR_RESULT) == NO){
        goto(Quest_Restoremarin_Cancel_Dialogue)
    }else{
        return
    }
}

script Quest_Restoremarin_NotEnoughMons_Dialogue{
    msgbox(gText_Restoremarin_IsntThatYour,,SPEAKER_LENORA)
    closemessage
    release
    end
}

script Quest_Restoremarin_HandleProgress_Dialogue{
    msgbox(gText_Restoremarin_Excellent,,SPEAKER_LENORA)

    specialvar(VAR_RESULT,Quest_Restoremarin_CheckRemainingSubquests)

        if (var(VAR_RESULT) == 0){
            goto(Quest_Restoremarin_Reward_Dialogue)
        }else{
            goto(Quest_Restoremarin_Active_Script)
        }
}

script Quest_Restoremarin_Cancel_Dialogue{
        msgbox(gText_Restoremarin_ComeBack,,SPEAKER_LENORA)
        closemessage
release
end
}

script Quest_Restoremarin_Reward_Dialogue{
    if (var(VAR_QUEST_RESTOREMARIN) == SAVE_BEFORE_LENORA_BATTLE){
        goto(Quest_Restoremarin_StartBattle_Script)
    }

    questmenu(QUEST_MENU_SET_REWARD,QUEST_RESTOREMARIN)
    setvar(VAR_QUEST_RESTOREMARIN,SAVE_BEFORE_LENORA_BATTLE)

        msgbox(gText_Quest_Restoremarin_IAskedA,,SPEAKER_LENORA)

        msgbox(gText_Quest_Restoremarin_IAbsolutely,,SPEAKER_ZOEY)

        msgbox(gText_Quest_Restoremarin_HellIKindaWanna,,SPEAKER_LENORA)


        startmenu_savescreen

        goto(Quest_Restoremarin_StartBattle_Script)
}

script Quest_Restoremarin_StartBattle_Script{
    lock
        msgbox(gText_Quest_Restoremarin_OkayChampion,,SPEAKER_LENORA)
        trainerbattle_no_intro(TRAINER_GRETA,gText_Quest_Restoremarin_YoureTougherThan)
        closemessage

        goto(Quest_Restoremarin_PostBattle_Dialogue)
}

script Quest_Restoremarin_PostBattle_Dialogue{
    lock
        setvar(VAR_QUEST_RESTOREMARIN,DEFEATED_LENORA)

        msgbox(gText_Quest_Restoremarin_ManIfTrainersSeeYou,,SPEAKER_LENORA)

        msgbox(gText_Quest_Restoremarin_IThinkYoureGiving,,SPEAKER_ZOEY)

        msgbox(gText_Quest_Restoremarin_AndICantWait,,SPEAKER_LENORA)

        msgbox(gText_Quest_Restoremarin_IsThatYourGoal,,SPEAKER_ZOEY)

        msgbox(gText_Quest_Restoremarin_YeahThingsWere,,SPEAKER_LENORA)

        msgbox(gText_Quest_Restoremarin_YouCantGoBack,,SPEAKER_ZOEY)

        msgbox(gText_Quest_Restoremarin_YouDontGet,,SPEAKER_LENORA)

        msgbox(gText_Quest_Restoremarin_IWasntHere,,SPEAKER_ZOEY)

        msgbox(gText_Quest_Restoremarin_YourHeartFeels,,SPEAKER_LENORA)

        closemessage

        opendoor(12,17)
        waitdooranim
        applymovement(LENORA_MARIN,Common_Movement_WalkUp)
        waitmovement(LENORA_MARIN)
        removeobject(LENORA_MARIN)
        playse(SE_EXIT)
        waitse
        closedoor(12,17)
        waitdooranim

        completequest(QUEST_RESTOREMARIN)
        call(LetsGrabLunch_CheckRestorationProgress_Script)
        release
        end
}

text gText_Restoremarin_YourePlayer{
    format("You're {PLAYER} - Archer mentioned you'd be coming.")
}

text gText_Restoremarin_YeahImHelping{
    format("Yeah, I'm helping them get their project off the ground.")
}
text gText_Restoremarin_AreYouHelping{
    format("I meant what I said to Archer. I'll help get this off the ground, but we still need to figure out the next steps to taking down the whole League.")
}
text gText_Restoremarin_IHearThat{
    format("That's a future goal though, right? We want to focus on helping Trainers in the region now.")
}
text gText_Restoremarin_TheBestWay{
    format("I'm not interested in bandaid fixes. This new League is hurting people now, right?")
}
text gText_Restoremarin_RightBut{
    format("Right! But we have something we can do, right now, to make things better, right now. Archer is gonna make this happen, but it would mean so much to get you onboard.")
}
text gText_Restoremarin_HmmI{
    format("Hmm. That kid does seem driven. Let's see what I can do.")
}
text gText_Restoremarin_IHave{
    format("I want to help Trainers learn to adapt. When everything that Trainers trust are stripped away, how can they make the best of the hand they've been dealt?")
}
text gText_Restoremarin_LikeTrainer{
    format("Like the Trainers of SanFran?")
}
text gText_Restoremarin_OhIDidnt{
    format("Oh! I didn't think of it like that. You're right.")
}
text gText_Restoremarin_TheresABattle{
    format("There's a Battle Facility in Hoenn and Sinnoh that has a similar concept. A trainer's Pokémon is taken away from them, and they are forced to use rental Pokémon. How hard would that be for us to do...?")
}
text gText_Restoremarin_CanYouBring{
    format("Can you bring me two Pokémon? ")
}
text gText_Restoremarin_IFIWant{
    format("If I want to start my own Battle Factory, we'll need to streamline to teaching moves to Pokémon.")
}
text gText_Restoremarin_DidYouFind{
    format("Did you find the Pokémon I mentioned?")
}

text gText_Restoremarin_WeNeed_A{
    format("We need a {STR_VAR_1} that knows {STR_VAR_2},")
}
text gText_Restoremarin_WeNeed_B{
    format("and a {STR_VAR_1} that knows {STR_VAR_2}.")
}
text gText_Restoremarin_ComeBack{
    format("Come back when you've caught one.")
}
text gText_Restoremarin_WeNeed{
    format("We still need a {STR_VAR_1} that knows {STR_VAR_2}, but we're close!")
}
text gText_Restoremarin_Excellent{
    format("Excellent! This is perfect.")
}



text gText_Quest_Restoremarin_IAskedA{
    format("I asked a few others Trainer for similar things. I think after a few tests, we might have something going here! You should stop by and help us test it out.")
}



text gText_Quest_Restoremarin_IAbsolutely{
    format("I absolutely will!")
}




text gText_Quest_Restoremarin_HellIKindaWanna{
    format("Hell, I kinda wanna see it for myself right now. Are you really all that?")
}




text gText_Quest_Restoremarin_OkayChampion{
    format("Okay Champion, bring it on!")
}




text gText_Quest_Restoremarin_YoureTougherThan{
    format("You're tougher than expected!")
}




text gText_Quest_Restoremarin_ManIfTrainersSeeYou{
    format("Man, if Trainers see you backing this and people already know how strong you are... we've got a shot at real legitimacy.")
}



text gText_Quest_Restoremarin_IThinkYoureGiving{
    format("I think you're giving me too much credit! Believe it or not, Ycombinator's Kickstart problem was a huge part of the reason I was able to hone my skills this much.")
}




text gText_Quest_Restoremarin_AndICantWait{
    format("And I can't wait to turn that power against those assholes. They're gonna collapse under their own weight, and then we can reinstate all the old gyms and go back to when times were good and I was running this joint!")
}



text gText_Quest_Restoremarin_IsThatYourGoal{
    format("Is that your goal? Just go to back to the past?")
}




text gText_Quest_Restoremarin_YeahThingsWere{
    format("Yeah! Things were so much better before.")
}



text gText_Quest_Restoremarin_YouCantGoBack{
    format("Lenora, you can't go back to the past. You've gotta stick to the reality we've got right now. I think what you're going to do here is going to make a real difference for Trainers, and that's awesome. But if you want the Trainers of the region to be the best they can be, I think we need to figure out a path forward... in the present. That's what Archer and I want to do.")
}




text gText_Quest_Restoremarin_YouDontGet{
    format("You don't get it, {PLAYER}, you weren't here for before.")
}



text gText_Quest_Restoremarin_IWasntHere{
    format("I wasn't here, but I'm here now. And while I'm new, I'm really starting to love it here and I want to help Trainers, and help people, just like you do. And we can do that, moving forward.")
}




text gText_Quest_Restoremarin_YourHeartFeels{
    format("Your heart feels like its in the right place. I gotta get back to work. Let's at least get this test off the ground.")
}

text gText_Restoremarin_WeNeedSomePretty{
    format("We need some pretty specific Pokémon if I'm going to get this off the ground. What do you have?")
}

text gText_Restoremarin_AreYouSure{
    format("You sure I can have this {STR_VAR_1}? I'm gonna keep it forever.")
}

text gText_Restoremarin_IsntThatYour{
    format("Isn't that your last Pokémon...?")
}
