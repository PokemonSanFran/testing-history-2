//PSF TODO if the player talks to gertrude before the detective ariana quest, she will teach fly to one of your pokemon
//
//if you talk to her during the right phase of the quest, she will just talk about team rocket
//
//if you talk to her after the detective ariana quest, this quest will start

//gText_IBelieveICanFly
//Quest_IBelieveICanFly

const FLAG_HIDE_GERTRUDE = FLAG_TEMP_3
const GERTRUDE = 3

script Quest_IBelieveICanFly_Gertrude_Visibility_Script{
    questmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_DETECTIVEARIANA) 
        if (!var(VAR_RESULT)){
            setflag(FLAG_HIDE_GERTRUDE)
        }

    questmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_IBELIEVEICANFLY)
        if (var(VAR_RESULT)){
            setflag(FLAG_HIDE_GERTRUDE)
        }
    return 
}

script Quest_IBelieveICanFly_Interact_Gertrude_Script{
    goto_if_quest_active(QUEST_IBELIEVEICANFLY,Quest_IBelieveICanFly_Active_Dialogue)
    goto(Quest_IBelieveICanFly_StartQuest_Dialogue)
}
script Quest_IBelieveICanFly_StartQuest_Dialogue{
    msgbox(gText_IBelieveICanFly_OhHelloThere,MSGBOX_NPC,SPEAKER_GERTRUDE)
    msgbox(gText_IBelieveICanFly_ItsDishearteningToSee,,SPEAKER_GERTRUDE)
    startquest(QUEST_IBELIEVEICANFLY)
    goto_if_quest_active(QUEST_IBELIEVEICANFLY,Quest_IBelieveICanFly_Active_Dialogue)
}
script Quest_IBelieveICanFly_Active_Dialogue{
    lock
    faceplayer
    msgbox(gText_IBelieveICanFly_ToEarnMyTeachingsOn,MSGBOX_YESNO,SPEAKER_GERTRUDE)
    if (var(VAR_RESULT) == NO){
        goto(Quest_IBelieveICanFly_DenyBattle_Script)
    }   
    //special(TrySkyBattle)
    //PSF TODO merge Sky Battles
    setvar(VAR_RESULT,TRUE)
    
    if (var(VAR_RESULT)){
        goto(Quest_IBelieveICanFly_StartBattle_Script)
    }
    goto(Quest_IBelieveICanFly_NotEligibleSkyBattle_Script)
}
script Quest_IBelieveICanFly_StartBattle_Script{
    msgbox(gText_IBelieveICanFly_LetsBeginPrepare,,SPEAKER_GERTRUDE)
        closemessage
        trainerbattle_no_intro(TRAINER_GERTRUDE,gText_IBelieveICanFly_WellFoughtYouAre)
        if (defeated(TRAINER_GERTRUDE)){
            goto(Quest_IBelieveICanFly_Reward_Dialogue)
        }    
        msgbox(gText_IBelieveICanFly_WellFoughtYouAre,,SPEAKER_GERTRUDE)
        special(HealPlayerParty)
        closemessage
        release
        end
}

script Quest_IBelieveICanFly_DenyBattle_Script{
    msgbox(gText_IBelieveICanFly_OhISeeYou,,SPEAKER_GERTRUDE)
    closemessage
    release
    end
}

script Quest_IBelieveICanFly_NotEligibleSkyBattle_Script{
    msgbox(gText_IBelieveICanFly_ItLooksLikeYouDon,,SPEAKER_GERTRUDE)
    closemessage
    release
    end
}

script Quest_IBelieveICanFly_Reward_Dialogue{
    msgbox(gText_IBelieveICanFly_CongratulationsOnYourVictory,,SPEAKER_GERTRUDE)
    closemessage
    call(Quest_IBelieveICanFly_Gertrude_LeaveOutside_Script)
    completequest(QUEST_IBELIEVEICANFLY)
    release
    end
}

script Quest_IBelieveICanFly_Gertrude_LeaveOutside_Script{
    opendoor(37,31)
    waitdooranim
    applymovement(GERTRUDE,Quest_IBelieveICanFly_WalkInside_Movement)
    waitmovement(GERTRUDE)
    playse(SE_EXIT)
    removeobject(GERTRUDE)
    waitse
    closedoor(37,31)
    waitdooranim
    return
}

movement Quest_IBelieveICanFly_WalkInside_Movement{
    walk_up
    step_end
}

text gText_IBelieveICanFly_OhHelloThere{
    format("Oh, hello there! I remember you as a friend of Ariana.")
}

text gText_IBelieveICanFly_ItsDishearteningToSee{
    format("It's disheartening to see so many Trainers relying on Uber instead of using their Pokémon. That's why I want to teach more Trainers how to use Fly.")
}

text gText_IBelieveICanFly_ToEarnMyTeachingsOn{
    format("To earn my teachings on Fly, you must first win a Sky Battle. Only Flying-type Pokémon or those with the Levitate ability can participate. Are you ready?")
}

text gText_IBelieveICanFly_LetsBeginPrepare{
    format("Let's begin! Prepare yourself for a true Sky Battle!")
}

text gText_IBelieveICanFly_WellFoughtYouAre{
    format("Well fought! You are a worthy opponent.")
}

text gText_IBelieveICanFly_YouPutUpAGood{
    format("You put up a good fight. Let me heal your Pokémon for you. It's always important to take care of them.")
}

text gText_IBelieveICanFly_OhISeeYou{
    format("Oh, I see you're not up for a battle. That's a shame. I'll be waiting here for someone who is.")
}

text gText_IBelieveICanFly_ItLooksLikeYouDon{
    format("It looks like you don't have any eligible Pokémon for a Sky Battle. Come back when you have some!")
}

text gText_IBelieveICanFly_CongratulationsOnYourVictory{
    format("Congratulations on your victory! You are an impressive Trainer. Please come inside, and I'll teach you everything you need to know about using the move Fly.")
}
