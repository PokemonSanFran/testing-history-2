const PLAYER = OBJ_EVENT_ID_PLAYER
const FLAG_HIDE_DRUMMERS = FLAG_TEMP_6
const DRUMMER_A = 23
const DRUMMER_B = 24
const DRUMMER_C = 25
const DRUMMER_D = 26

//Quest_DrumCircle_
//gText_DrumCircle_
script Quest_DrumCircle_Interact_Event_Script{
    lock
    faceplayer
    returnqueststate(QUEST_DRUMCIRCLE)

    switch(var(VAR_RESULT)){
        case QUEST_MENU_SET_ACTIVE: goto(Quest_DrumCircle_ActiveDialogue)
        case QUEST_MENU_SET_REWARD: goto(Quest_DrumCircle_RewardCutscene)
        case QUEST_MENU_COMPLETE_QUEST: goto(Quest_DrumCircle_CheckVenueCompletion_Script)
        default: goto(Quest_DrumCircle_ExplainQuest_Dialogue)
    }
}

script Quest_DrumCircle_ExplainQuest_Dialogue{
    msgbox(gText_DrumCircle_YoureATrainerWantAChallenge,,SPEAKER_TONALLI)
    msgbox(gText_DrumCircle_BattleAllOfUs,,SPEAKER_DRUMMER_B)
    msgbox(gText_DrumCircle_IfYouWin,,SPEAKER_DRUMMER_C)
    msgbox(gText_DrumCircle_IfYouLose,,SPEAKER_DRUMMER_D)
    msgbox(gText_DrumCircle_WhatDoYouSay,MSGBOX_YESNO,SPEAKER_TONALLI)

    if (var(VAR_RESULT) == YES){
        goto(Quest_DrumCircle_StartQuest_Dialogue)
    }else{
        goto(Quest_DrumCircle_DeclineQuest_Dialogue)
    }
}

script Quest_DrumCircle_DeclineQuest_Dialogue{
    msgbox(gText_DrumCircle_HehNotReady,,SPEAKER_TONALLI)
    release
    end
}

script Quest_DrumCircle_StartQuest_Dialogue{
    msgbox(gText_DrumCircle_ThatsWhatImTalking,,SPEAKER_TONALLI)
    startquest(QUEST_DRUMCIRCLE)
    goto(Quest_DrumCircle_ActiveDialogue)
}

script Quest_DrumCircle_ActiveDialogue{
    msgbox(gText_DrumCircle_WantAHeal,MSGBOX_YESNO,SPEAKER_TONALLI)

    if (var(VAR_RESULT) == YES){
        special(HealPlayerParty)
    }

    goto(Quest_DrumCircle_BattleA_Script)
}

script Quest_DrumCircle_BattleA_Script{
    applymovement(DRUMMER_A,Quest_DrumCircle_HopFace_Movement)
    playse(SE_BIKE_HOP)
    waitse
    waitmovement(DRUMMER_A)
    closemessage
    msgbox(gText_DrumCircle_MyTurn,,SPEAKER_DRUMMER_D)
    trainerbattle_no_intro(TRAINER_DRUMCIRCLE_A,gText_DrumCircle_OhNo)
    goto(Quest_DrumCircle_BattleB_Script)
}

script Quest_DrumCircle_BattleB_Script{
    applymovement(DRUMMER_B,Quest_DrumCircle_HopFace_Movement)
    playse(SE_BIKE_HOP)
    waitse
    waitmovement(DRUMMER_B)
    closemessage
    msgbox(gText_DrumCircle_MyTurn,,SPEAKER_DRUMMER_C)
    trainerbattle_no_intro(TRAINER_DRUMCIRCLE_B,gText_DrumCircle_OhNo)
    goto(Quest_DrumCircle_BattleC_Script)
}
script Quest_DrumCircle_BattleC_Script{
    applymovement(DRUMMER_C,Quest_DrumCircle_HopFace_Movement)
    playse(SE_BIKE_HOP)
    waitse
    waitmovement(DRUMMER_C)
    closemessage
    msgbox(gText_DrumCircle_MyTurn,,SPEAKER_DRUMMER_B)
    trainerbattle_no_intro(TRAINER_DRUMCIRCLE_C,gText_DrumCircle_OhNo)
    goto(Quest_DrumCircle_BattleD_Script)
}
script Quest_DrumCircle_BattleD_Script{
    applymovement(DRUMMER_D,Quest_DrumCircle_HopFace_Movement)
    playse(SE_BIKE_HOP)
    waitse
    waitmovement(DRUMMER_D)
    closemessage
    msgbox(gText_DrumCircle_MyTurn,,SPEAKER_TONALLI)
    trainerbattle_no_intro(TRAINER_DRUMCIRCLE_D,gText_DrumCircle_OhNo)
    goto(Quest_DrumCircle_RewardCutscene)
}

script Quest_DrumCircle_RewardCutscene{
    msgbox(gText_DrumCircle_YoureNoJoke,,SPEAKER_DRUMMER_B)
    msgbox(gText_DrumCircle_WayToGoIdiot,,SPEAKER_DRUMMER_D)
    msgbox(gText_DrumCircle_DudeIsntThat,,SPEAKER_DRUMMER_C)
    msgbox(gText_DrumCircle_HowWasISupposed,,SPEAKER_TONALLI)
    msgbox(gText_DrumCircle_ThatWasOneOfAKind,,SPEAKER_DRUMMER_D)
    msgbox(gText_DrumCircle_IgnoreThemYouWon,,SPEAKER_TONALLI)

    giveitem(ITEM_OLD_GATEAU,1)

    msgbox(gText_DrumCircle_ComeBackSoWeCanWin,,SPEAKER_TONALLI)
    completequest(QUEST_DRUMCIRCLE)
    release
    end
}

script Quest_DrumCircle_CheckVenueCompletion_Script{
    questmenu(QUEST_MENU_CHECK_ACTIVE,QUEST_LASTMUSICVENUEINSANFRANCISCO)
    if (var(VAR_RESULT)){
        goto(Quest_LastMusicVenue_InformDrummers_Dialogue)
    }else{
        goto(Quest_DrumCircle_Completed_NoVenue_Dialogue)
    }
}

script Quest_DrumCircle_Completed_NoVenue_Dialogue{
    msgbox(gText_DrumCircle_YouGuysAreDrummers,,SPEAKER_ZOEY)


    msgbox(gText_DrumCircle_WeLostOurVenue,,SPEAKER_TONALLI)
    msgbox(gText_DrumCircle_MakingEnds,,SPEAKER_TONALLI)
    closemessage
    release
    end
}
movement Quest_DrumCircle_HopFace_Movement{
    face_player
    lock_facing_direction
    jump_in_place_up * 2
    step_end
}

text gText_DrumCircle_YoureATrainerWantAChallenge{
    format("youre a trainer, want a challenge?")
}
text gText_DrumCircle_BattleAllOfUs{
    format("battle all of us back to back, no healing at all")
}
text gText_DrumCircle_IfYouWin{
    format("if you win, you get a super rare prize")
}
text gText_DrumCircle_IfYouLose{
    format("if you lose, you gotta fork over double the prize money")
}
text gText_DrumCircle_WhatDoYouSay{
    format("sound good?")
}
text gText_DrumCircle_HehNotReady{
    format("not ready for all that? i get it.")
}
text gText_DrumCircle_ThatsWhatImTalking{
    format("that's what I'm talking about!")
}
text gText_DrumCircle_WantAHeal{
    format("wanna heal before we get started?")
}
text gText_DrumCircle_MyTurn{
    format("my turn!")
}
text gText_DrumCircle_OhNo{
    format("oh no!")
}
text gText_DrumCircle_YoureNoJoke{
    format("you're no joke")
}
text gText_DrumCircle_WayToGoIdiot{
    format("way to go idiot!")
}
text gText_DrumCircle_DudeIsntThat{
    format("dude isn't that {PLAYER}")
}
text gText_DrumCircle_HowWasISupposed{
    format("how was I supposed to know it was them")
}
text gText_DrumCircle_ThatWasOneOfAKind{
    format("that was one of a kind! mom is gonna kill us.")
}
text gText_DrumCircle_IgnoreThemYouWon{
    format("look, ignore them. you won fair and square, take this.")
}
text gText_DrumCircle_ComeBackSoWeCanWin{
    format("come challenge us again someday so we can win it back!")
}
text gText_DrumCircle_YouGuysAreDrummers{
    format("you guys are drummers, right? why are you doing high stakes battles?")
}
text gText_DrumCircle_WeLostOurVenue{
    format("we used to play around the Gym. Pryce was cool with us and we got paid as performers and everything. when the league came in, they kicked us out and all the local venues are getting bought up by the league to become arenas")
}
text gText_DrumCircle_MakingEnds{
    format("making ends meet without a venue has been tough, so we had to get creative! and it was working until you showed up...")
}
