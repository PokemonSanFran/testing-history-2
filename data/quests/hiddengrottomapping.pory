const PANNEN = 1
const FLAG_HIDE_PANNEN = FLAG_TEMP_2
const FLAG_HIDE_JOURNAL = FLAG_TEMP_3

//script Quest_Hiddengrottomapping_
//text Hiddengrottomapping_

script Quest_Hiddengrottomapping_MarkSubquestBiome_Script{
    addvar(VAR_TEMP_0,1)

    special(Quest_Hiddengrottomapping_MarkSubquestBiome)

    specialvar(VAR_RESULT,Quest_Hiddengrottomapping_CheckSubquestsForReward)
    if (var(VAR_RESULT)){
        questmenu(QUEST_MENU_SET_REWARD,QUEST_HIDDENGROTTOMAPPING)
    }
    return
}

script Quest_Hiddengrottomapping_PannenVisibility_Script{
    questmenu(QUEST_MENU_CHECK_INACTIVE,QUEST_HIDDENGROTTOMAPPING)
    
    if (!var(VAR_RESULT)){
        setflag(FLAG_HIDE_PANNEN)
    }
    return
}

script Quest_Hiddengrottomapping_StartQuest_Dialogue{
    addvar(VAR_TEMP_0,1)
    questmenu(QUEST_MENU_CHECK_INACTIVE,QUEST_HIDDENGROTTOMAPPING)
    
    if (!var(VAR_RESULT)){
        end
    }

    lock
    applymovement(PANNEN,Quest_Hiddengrottomapping_WalkToPlayer_Movement)
    waitmovement(PANNEN)
    msgbox(gText_Hiddengrottomapping_WhyHello,,SPEAKER_PANNEN)
    applymovement(PANNEN,Common_Movement_FaceUp)
    msgbox(gText_Hiddengrottomapping_WildPokemonBuild,,SPEAKER_PANNEN)
    applymovement(PANNEN,Common_Movement_FaceDown)
    waitmovement(PANNEN)
    msgbox(gText_Hiddengrottomapping_ButNobodyKnows,,SPEAKER_PANNEN)
    msgbox(gText_Hiddengrottomapping_DoMeAFavor,,SPEAKER_PANNEN)
    applymovement(PANNEN,Quest_Hiddengrottomapping_LeaveGrotto_Movement)
    fadescreen(FADE_TO_BLACK)
    startquest(QUEST_HIDDENGROTTOMAPPING)
    removeobject(PANNEN)
    playse(SE_EXIT)
    waitse
    fadescreen(FADE_FROM_BLACK)
    release
    end
}

script Quest_Hiddengrottomapping_Event_Script{
    goto_if_quest_reward(QUEST_HIDDENGROTTOMAPPING,Quest_Hiddengrottomapping_Reward_Dialogue)
    goto_if_quest_active(QUEST_HIDDENGROTTOMAPPING,Quest_Hiddengrottomapping_Active_Dialogue)
    goto(Gldngtepark_Pannen_Wife_Default_Dialogue)
}

script Quest_Hiddengrottomapping_Reward_Dialogue{
    completequest(QUEST_HIDDENGROTTOMAPPING)
    lock
    msgbox(gText_Hiddengrottomapping_SighHeToldYou,,SPEAKER_NANCY)
    msgbox(gText_Hiddengrottomapping_IHaventHeard,,SPEAKER_NANCY)
    msgbox(gText_Hiddengrottomapping_HeTaughtMe,,SPEAKER_NANCY)

    specialvar(VAR_RESULT,Quest_Hiddengrottomapping2_CheckForJournalPage)

    if(var(VAR_RESULT)){
        startquest(QUEST_HIDDENGROTTOMAPPING2)
        goto(Quest_Hiddengrottomapping2_TakeJournalPage_Script)
    }

    msgbox(gText_Hiddengrottomapping_CouldYouKeep,,SPEAKER_NANCY)
    startquest(QUEST_HIDDENGROTTOMAPPING2)
    closemessage
    end
}

script Quest_Hiddengrottomapping_Active_Dialogue{
    msgbox(gText_Hiddengrottomapping_OhYouVeMet,,SPEAKER_NANCY)
    closemessage
    end
}

movement Quest_Hiddengrottomapping_WalkToPlayer_Movement{
    walk_down * 5
    step_end
}

movement Quest_Hiddengrottomapping_LeaveGrotto_Movement{
    walk_right
    walk_down
    step_end
}

text gText_Hiddengrottomapping_WhyHello{
    format("Why hello there! Wasn't expecting to see another Trainer in here.")
}

text gText_Hiddengrottomapping_WildPokemonBuild{
    format("We're in a Hidden Grotto. Wild Pokemon create these spaces! Soetimes there are exceptionally rare Pokemon inside, or items they've left behind.")
}

text gText_Hiddengrottomapping_ButNobodyKnows{
    format("But nobody knows why they make them! That's what I research.")
}

text gText_Hiddengrottomapping_DoMeAFavor{
    format("I see you've got a phone, and if you're Trainer, you're tracking battle data, right? Keep traveling the region. I think there are five different types of Hidden Grotto biomes in Sanfran... once you've been to them all, come to my house in Gldngtepark! I'll extract the data from your phone.")
}

text gText_Hiddengrottomapping_SighHeToldYou{
    format("He told you to come here? Did he think he'd be here, or was he hoping I'd get the data for you...")
}
text gText_Hiddengrottomapping_IHaventHeard{
    format("Truth be told, I haven't heard from him a while. I know he gets so caught up in his work though.")
}
text gText_Hiddengrottomapping_HeTaughtMe{
    format("He taught me how to withdraw data from Trainer's phones, so if you let me see that for a second...")
}
text gText_Hiddengrottomapping_CouldYouKeep{
    format("Thanks. I'll upload it to his computer. Hey, could you keep an eye out for anything that might indicate his whereabouts? Its been a while since I've seen him, and I'm starting to get worried.")
}
text gText_Hiddengrottomapping_OhYouVeMet{
    format("Ah, so you met my husband, Pannen! Oh, and you're helping with his research. That's excellent! Maybe he'll come home more often.")
}
