const AUGUSTA = 1
const PLAYER = OBJ_EVENT_ID_PLAYER

//gText_Kitchenvolunteering_
//Quest_Kitchenvolunteering_

script Quest_Kitchenvolunteering_First_Interact_Augusta_Script{
    goto_if_quest_complete(QUEST_KITCHENVOLUNTEERING,Quest_Kitchenvolunteering_Second_Interact_Script)
    goto_if_quest_reward(QUEST_KITCHENVOLUNTEERING,Quest_Kitchenvolunteering_Reward_Dialogue)
    goto_if_quest_active(QUEST_KITCHENVOLUNTEERING,Quest_Kitchenvolunteering_YoureNotDone_Dialogue)
    goto(Quest_Kitchenvolunteering_StartQuest_Dialogue)
}

script Quest_Kitchenvolunteering_StartQuest_Dialogue{
    msgbox(gText_Kitchenvolunteering_OhYoureHere,MSGBOX_NPC,SPEAKER_AUGUSTA)
    msgbox(gText_Kitchenvolunteering_HeadDownToTheBasement,,SPEAKER_AUGUSTA)
    msgbox(gText_Kitchenvolunteering_ComeBackWhenYoure,,SPEAKER_AUGUSTA)
    call(Quest_Kitchenvolunteering_GiveKeyAndGenerateSeed_Script)
    msgbox(gText_Kitchenvolunteering_IThinkThey,,SPEAKER_ZOEY,TAIL_THOUGHT,EMOTE_SWEAT)
    startquest(QUEST_KITCHENVOLUNTEERING)
    call(Quest_Kitchenvolunteering_ReturnToWork_Script)
}

script Quest_Kitchenvolunteering_ReturnToWork_Script{
    applymovement(AUGUSTA,Common_Movement_FaceOriginalDirection)
    waitmovement(AUGUSTA)
    release
    return
}

script Quest_Kitchenvolunteering_YoureNotDone_Dialogue{
    msgbox(gText_Kitchenvolunteering_OhYoureNotDone,MSGBOX_NPC,SPEAKER_AUGUSTA)
    call(Quest_Kitchenvolunteering_ReturnToWork_Script)
    end
}

script Quest_Kitchenvolunteering_RemoveItem_Script{
    removeitem(ITEM_GOLD_TEETH,1)
    removeitem(ITEM_CARD_KEY,1)
    removeitem(ITEM_LIFT_KEY,1)
    removeitem(ITEM_SILPH_SCOPE,1)
    removeitem(ITEM_TRI_PASS,1)
    return
}

script Quest_Kitchenvolunteering_Reward_Dialogue{
    call(Quest_Kitchenvolunteering_ThankPlayer_Dialogue)
    msgbox(gText_Kitchenvolunteering_TheyNever,,SPEAKER_ZOEY,TAIL_THOUGHT,EMOTE_SWEAT)
    call(Quest_Kitchenvolunteering_RemoveItem_Script)
    completequest(QUEST_KITCHENVOLUNTEERING)
    release
    end
}

script Quest_Kitchenvolunteering_ThankPlayer_Dialogue{
    msgbox(gText_Kitchenvolunteering_OhThankGod,MSGBOX_NPC,SPEAKER_AUGUSTA)
    special(RandomlyBoostPartyMemberFriendship)
    msgbox(gText_Kitchenvolunteering_OhAndTheKey,,SPEAKER_AUGUSTA)
    removeitem(ITEM_STORAGE_KEY,1)
    setflag(FLAG_DAILY_KITCHEN_VOLUNTEER)
    msgbox(gText_Kitchenvolunteering_ComeBackTomorrow,,SPEAKER_AUGUSTA)
    call(Quest_Kitchenvolunteering_ReturnToWork_Script)
    return
}

//PSF TODO make it so the player can only do this once per "day"
script Quest_Kitchenvolunteering_Second_Interact_Script{
    if (flag(FLAG_DAILY_KITCHEN_VOLUNTEER)){
        goto(Quest_Kitchenvolunteering_TooBusy_Dialogue)
    }

    checkitem(ITEM_SECRET_KEY,1)
    if (var(VAR_RESULT)){
        call(Quest_Kitchenvolunteering_CheckForItem_Script)
        goto(Quest_Kitchenvolunteering_YoureNotDone_Dialogue)
    }

    goto(Quest_Kitchenvolunteering_RepeatQuest_Dialogue)
}

script Quest_Kitchenvolunteering_CheckForItem_Script{
    //PSF TODO change this to a random item that was previous assigned
    checkitem(ITEM_TEA,1)

    if (var(VAR_RESULT)){
        goto(Quest_Kitchenvolunteering_ThankPlayer_Dialogue)
            end
    }
    return
}

script Quest_Kitchenvolunteering_TooBusy_Dialogue{
    msgbox(gText_Kitchenvolunteering_NowWhereDidIPut,,SPEAKER_AUGUSTA,TAIL_WHISPER)
    closemessage
    end
}

script Quest_Kitchenvolunteering_RepeatQuest_Dialogue{
    msgbox(gText_Kitchenvolunteering_OhWowTheyNever,MSGBOX_NPC,SPEAKER_AUGUSTA) 
    msgbox(gText_Kitchenvolunteering_ThisTimeCanYouGrab,,SPEAKER_AUGUSTA)
    call(Quest_Kitchenvolunteering_GiveKeyAndGenerateSeed_Script)
    release
    end
}

script Quest_Kitchenvolunteering_GiveKeyAndGenerateSeed_Script{
    msgbox(gText_Kitchenvolunteering_OhRightYouNeedTheKey,,SPEAKER_AUGUSTA)
    giveitem(ITEM_STORAGE_KEY,1)
    special(GenerateMazeSeed)
    return
}

script Quest_Kitchenvolunteering_FindItem_Script{
    special(Quest_Kitchenvolunteering_PickRandomItem)
    callstd(STD_FIND_ITEM)
    special(Quest_Kitchenvolunteering_CheckProgressAndSetReward)
    end
}

text gText_Kitchenvolunteering_OhYoureHere{
    format("Oh you're here! Finally. Okay let's see...")
}

text gText_Kitchenvolunteering_HeadDownToTheBasement{
    format("I need you to head down into the basement and grab some items for me... Item1, Item2, Item3, Item4, and Item5.")
}
text gText_Kitchenvolunteering_ComeBackWhenYoure{
    format("Come back when you've got all five.")
}
text gText_Kitchenvolunteering_IThinkThey{
    format("I don't think they realize that I'm the wrong person...")
}
text gText_Kitchenvolunteering_OhYoureNotDone{
    format("Oh you're not done? Let me know when you are.")
}
text gText_Kitchenvolunteering_TheyNever{
    format("They never realzied they've got the wrong guy...")
}
text gText_Kitchenvolunteering_OhThankGod{
    format("Oh thank God! I'll take those. Thank you so much!")
}
text gText_Kitchenvolunteering_OhAndTheKey{
    format("Oh! And the key. I'll take that.")
}
text gText_Kitchenvolunteering_ComeBackTomorrow{
    format("You did good today. Come back tomorrow if you're willing to help out again.")
}
text gText_Kitchenvolunteering_NowWhereDidIPut{
    format("Now, where did I put that ladle...")
}
text gText_Kitchenvolunteering_OhWowTheyNever{
    format("Oh wow! They never come back.")
}
text gText_Kitchenvolunteering_ThisTimeCanYouGrab{
    format("Okay, today I just need you to grab one Item1.")
}
text gText_Kitchenvolunteering_OhRightYouNeedTheKey{
    format("Oh right! You need the key again. Here you go.")
}
