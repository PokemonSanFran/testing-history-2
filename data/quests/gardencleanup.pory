const MARIA_A = 4
const FLAG_HIDE_MARIA = FLAG_TEMP_2
//Quest_GardenCleanUp_
//gText_GardenCleanUp_

script Quest_GardenCleanUp_Debug_Script{
    addvar(VAR_TEMP_0,1)
        setvar(VAR_DEFEATED_GARDEN_POKEMON,47)
        callnative(CountDefeatedGardenMons)
        msgbox(format("if the garden quest is active, go defeat one more on Psfroute21"))
        warp(MAP_PSFROUTE21,8,7)
}

script Quest_GardenCleanUp_HideObjects_Script{
    questmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_GARDENCLEANUP)
        if (var(VAR_RESULT)){
            setflag(FLAG_HIDE_MARIA) 
        }
    return
}

script Quest_GardenCleanUp_ClearGarden_Script{
    questmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_GARDENCLEANUP)
        if (var(VAR_RESULT)){
            //PSF TODO clear the entire grassy patch
            special(DrawWholeMapView)
        }
    return
}

script Quest_GardenCleanUp_CheckCompletion_Script{
    //PSF TODO if first quest is completed AND it is AT LEAST the day after the completion of the first quest, go to second quest
    questmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_GARDENCLEANUP)
        if (var(VAR_RESULT)){
            goto(Quest_GardenCleanUpAdvanced_Event_Script)
        }
    return
}

script Quest_GardenCleanUp_Event_Script{
    lock

        call(Quest_GardenCleanUp_CheckCompletion_Script)

        returnqueststate(QUEST_GARDENCLEANUP)

        switch(var(VAR_RESULT)){
            case QUEST_MENU_COMPLETE_QUEST: goto(Quest_GardenCleanUp_Complete_Dialogue)
            case QUEST_MENU_SET_REWARD: goto(Quest_GardenCleanUp_Reward_Cutscene)
            case QUEST_MENU_SET_ACTIVE: goto(Quest_GardenCleanUp_Active_Dialogue)
            default: goto(Quest_GardenCleanUp_Start_Dialogue)
        }
}

script Quest_GardenCleanUp_Start_Dialogue{
    lock
        faceplayer
        msgbox(gText_GardenCleanUp_ILearned)
        msgbox(gText_GardenCleanUp_ITriedTo)
        msgbox(gText_GardenCleanUp_CouldYouHelp)
        startquest(QUEST_GARDENCLEANUP)
        setvar(VAR_DEFEATED_GARDEN_POKEMON,0)
        release
        end
}
script Quest_GardenCleanUp_Active_Dialogue{

    if (var(VAR_DEFEATED_GARDEN_POKEMON) > 46){
        msgbox(gText_GardenCleanUp_SoClose,MSGBOX_NPC)
    }elif (var(VAR_DEFEATED_GARDEN_POKEMON) > 40){
        msgbox(gText_GardenCleanUp_ItsReallyStarting,MSGBOX_NPC)
    }elif (var(VAR_DEFEATED_GARDEN_POKEMON) > 30){
        msgbox(gText_GardenCleanUp_WowYouBeatALot,MSGBOX_NPC)
    }elif (var(VAR_DEFEATED_GARDEN_POKEMON) > 20){
        msgbox(gText_GardenCleanUp_ITriedToGoEarlier,MSGBOX_NPC)
    }elif (var(VAR_DEFEATED_GARDEN_POKEMON) > 10){
        msgbox(gText_GardenCleanUp_DidYouGoOver,MSGBOX_NPC)
    }else{
        msgbox(gText_GardenCleanUp_ThankYouForYourHelp,MSGBOX_NPC)
    }
    end
}
script Quest_GardenCleanUp_Reward_Cutscene{
    faceplayer
        msgbox(gText_GardenCleanUp_OhMyGodYoureDone)

        applymovement(MARIA_A,Quest_GardenCleanUp_RunToGarden_Movement)
        waitmovement(MARIA_A)
        removeobject(MARIA_A)
        completequest(QUEST_GARDENCLEANUP)
        setvar(VAR_QUEST_GARDENCLEANUPADVANCED,HAS_NOT_INTERACTED)
        release
        end
}
script Quest_GardenCleanUp_Complete_Dialogue{
    msgbox(gText_GardenCleanUp_ImSoExcitedToStartPlanting,MSGBOX_NPC)
        end
}

movement Quest_GardenCleanUp_RunToGarden_Movement{
    walk_faster_down * 6
        step_end
}
text gText_GardenCleanUp_ILearned{
    format("I learned about community gardens from my older brother")
}
text gText_GardenCleanUp_ITriedTo{
    format("i tried to start one, but there's too many strong and scary pokemon on Psfroute21")
}
text gText_GardenCleanUp_CouldYouHelp{
    format("could you help me clear the garden?")
}
text gText_GardenCleanUp_SoClose{
    format("its looking a lot less scary, we're so close")
}
text gText_GardenCleanUp_ItsReallyStarting{
    format("its really starting to look great")
}
text gText_GardenCleanUp_WowYouBeatALot{
    format("wow, you beat a lot of them")
}
text gText_GardenCleanUp_ITriedToGoEarlier{
    format("i tried to go earlier, and I almost got attacked!")
}
text gText_GardenCleanUp_DidYouGoOver{
    format("did you head over there yet")
}
text gText_GardenCleanUp_ThankYouForYourHelp{
    format("thanks for your help")
}
text gText_GardenCleanUp_OhMyGodYoureDone{
    format("oh my god youre done?")
}
text gText_GardenCleanUp_ImSoExcitedToStartPlanting{
    format("im so excited to start planting!")
}
