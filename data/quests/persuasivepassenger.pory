//gText_Persuasivepassenger_
//Quest_Persuasivepassenger_

const GRUNT2 = 2
const FLAG_HIDE_UBER_CEO = FLAG_TEMP_1

script Quest_Persuasivepassenger_CEO_Visbility_Script{
    if(defeated(TRAINER_UBER_CEO)){
        setflag(FLAG_HIDE_UBER_CEO)
    }
    return
}

script Quest_Persuasivepassenger_InteractLeader_Script{
    lock
    faceplayer
    specialvar(VAR_RESULT,Quest_Taxicabturnaround_CheckReadyForNext)
    if (var(VAR_RESULT)){
        goto(Quest_Persuasivepassenger_Event_Script)
    }

    msgbox(gText_Persuasivepassenger_WelcomeTo,MSGBOX_NPC)
    end
}

script Quest_Persuasivepassenger_Event_Script{
    goto_if_quest_complete(QUEST_PERSUASIVEPASSENGER,Quest_Persuasivepassenger_Complete_Dialogue)
    goto_if_quest_reward(QUEST_PERSUASIVEPASSENGER,Quest_Persuasivepassenger_Reward_Dialogue)
    goto_if_quest_active(QUEST_PERSUASIVEPASSENGER,Quest_Persuasivepassenger_Active_Dialogue)
    goto(Quest_Persuasivepassenger_StartQuest_Script)
}

script Quest_Persuasivepassenger_StartQuest_Script{
    msgbox(gText_Persuasivepassenger_OhTheFamous)
    msgbox(gText_Persuasivepassenger_IllGetRightTo)
    msgbox(gText_Persuasivepassenger_JustLikeYouWorked)
    msgbox(gText_Persuasivepassenger_WeveDoneSomeResearch)
    msgbox(gText_Persuasivepassenger_CanYouSeeIfYou)
    startquest(QUEST_PERSUASIVEPASSENGER)
    goto(Quest_Persuasivepassenger_Active_Dialogue)
    release
    end
}

script Quest_Persuasivepassenger_Active_Dialogue{
    specialvar(VAR_TEMP_1,Quest_Persuasivepassenger_CountRemainingSubquests)
    switch(var(VAR_TEMP_1)){
        case 3:
            msgbox(gText_Persuasivepassenger_TheFirstDriver)
            msgbox(gText_Debug_WarpUber)
            warp(MAP_UBER_INTERIOR,1,3)
        case 2:
            msgbox(gText_Persuasivepassenger_TheNextDriver)
            msgbox(gText_Debug_WarpUber)
            warp(MAP_UBER_INTERIOR,10,3)
        default:
            msgbox(gText_Persuasivepassenger_TheresAnother)
            msgbox(gText_Debug_WarpUber)
            warp(MAP_UBER_INTERIOR,20,15)
    }
    //PSF TODO remove debug
    closemessage
    release
    end
}

script Quest_Persuasivepassenger_Reward_Dialogue{
    msgbox(gText_Persuasivepassenger_WeveGotten,MSGBOX_NPC)
    completequest(QUEST_PERSUASIVEPASSENGER)
    addvar(VAR_STORYLINE_STATE,1)
    end
}

script Quest_Persuasivepassenger_SetCutsceneVar_Script{
    specialvar(VAR_TEMP_1,Quest_Persuasivepassenger_CountRemainingSubquests)
    return
}

script Quest_Persuasivepassenger_Driver1_Event_Script{
    setvar(VAR_TEMP_1,5)
    lock

    msgbox(gText_Persuasivepassenger_HeyKid,,SPEAKER_UBER_DRIVER1)
    msgbox(gText_Persuasivepassenger_HowsItGoing,,SPEAKER_ZOEY)
    msgbox(gText_Persuasivepassenger_JustPickingUp,,SPEAKER_UBER_DRIVER1)

    specialvar(VAR_RESULT,Quest_Persuasivepassenger_CheckNeededItems)
    if (var(VAR_RESULT)){
        call(Quest_Persuasivepassenger_HasItems_Dialogue)
    }else{
        msgbox(gText_Persuasivepassenger_MyKidIsSick,,SPEAKER_UBER_DRIVER1)
        msgbox(gText_Persuasivepassenger_OhIHope,,SPEAKER_ZOEY)
        bufferitemnameplural(STR_VAR_1,ITEM_SITRUS_BERRY,3)
        bufferitemnameplural(STR_VAR_2,ITEM_HONEY,5)
        msgbox(gText_Persuasivepassenger_MyMomUsed,,SPEAKER_UBER_DRIVER1)
        bufferitemnameplural(STR_VAR_1,ITEM_WATMEL_BERRY,1)
        bufferitemnameplural(STR_VAR_2,ITEM_LEMONADE,2)
        msgbox(gText_Persuasivepassenger_IngredentsTwo,,SPEAKER_UBER_DRIVER1)
        call(Quest_Persuasivepassenger_DebugDriver1)
    }
    msgbox(gText_Persuasivepassenger_WereHere,,SPEAKER_UBER_DRIVER1)
    closemessage
    //special(CompleteUberDrive)
    warp(MAP_BERNALHEIGHTS,0)
}

script Quest_Persuasivepassenger_DebugDriver1{
            additem(ITEM_SITRUS_BERRY,3)
            additem(ITEM_HONEY,5)
            additem(ITEM_WATMEL_BERRY,1)
            additem(ITEM_LEMONADE,2)
            msgbox(gText_Debug_GiveItems)
return
}


script Quest_Persuasivepassenger_HasItems_Dialogue{
    msgbox(gText_Persuasivepassenger_ForYourSickKid,,SPEAKER_ZOEY)
    msgbox(gText_Persuasivepassenger_OhRightDidI,,SPEAKER_UBER_DRIVER1)
    msgbox(gText_Persuasivepassenger_GiveItems,MSGBOX_YESNO)

        if (var(VAR_RESULT) == YES){
            special(Quest_Persuasivepassenger_TakeNeededItems)
            subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_PERSUASIVEPASSENGER,SUB_QUEST_1)
            msgbox(gText_Persuasivepassenger_IGotEverything,,SPEAKER_ZOEY)
            msgbox(gText_Persuasivepassenger_PlayerGave,MSGBOX_AUTOCLOSE)
            msgbox(gText_Persuasivepassenger_ThankYouSoMuch,,SPEAKER_UBER_DRIVER1)
            msgbox(gText_Persuasivepassenger_TheUnion,,SPEAKER_ZOEY)
            msgbox(gText_Persuasivepassenger_ThatsTight,,SPEAKER_UBER_DRIVER1)
            closemessage
        }else{
            return
        }
    return
}

script Quest_Persuasivepassenger_Driver2_Event_Script{
    setvar(VAR_TEMP_1,5)
    lock
    msgbox(gText_Persuasivepassenger_HowYaDoin,,SPEAKER_ZOEY)
    msgbox(gText_Persuasivepassenger_YouKnow,,SPEAKER_UBER_DRIVER2)

    if (flag(FLAG_QUEST_PERSUASIVE_SIBLING)){
        goto(Quest_Persuasivepassenger_Driver2_FoundSibling_Dialogue)
    }

    if (defeated(TRAINER_PERSUASIVE_LANDLORD)){
        goto(Quest_Persuasivepassenger_Driver2_DefeatedLandlord_Dialogue)
    }

    msgbox(gText_Persuasivepassenger_MyBrothersLandlord,,SPEAKER_UBER_DRIVER2)
    msgbox(gText_Persuasivepassenger_SoImTrying,,SPEAKER_UBER_DRIVER2)
    msgbox(gText_Persuasivepassenger_WaitThreeTimes,,SPEAKER_ZOEY)
    msgbox(gText_Persuasivepassenger_YeahSoMyBrotherIsOut,,SPEAKER_UBER_DRIVER2)
    msgbox(gText_Persuasivepassenger_ThanksForListening,,SPEAKER_UBER_DRIVER2)

    goto(Quest_Persuasivepassenger_Driver2_End_Dialogue)
}

script Quest_Persuasivepassenger_Driver2_End_Dialogue{
    msgbox(gText_Persuasivepassenger_WereAtTheSpot,,SPEAKER_UBER_DRIVER2)
        //special(CompleteUberDrive)
        warp(MAP_ALAMEDA,0)
}

script Quest_Persuasivepassenger_Driver2_DefeatedLandlord_Dialogue{
    msgbox(gText_Persuasivepassenger_MyBrotherExLandlord,,SPEAKER_UBER_DRIVER2)
    msgbox(gText_Persuasivepassenger_WellISawOnTheNews,,SPEAKER_UBER_DRIVER2)
    msgbox(gText_Persuasivepassenger_ThatsNotWhat,,SPEAKER_ZOEY,TAIL_THOUGHT,EMOTE_SHOCK)
    msgbox(gText_Persuasivepassenger_ImGonnaKeepWorking,,SPEAKER_UBER_DRIVER2)
    msgbox(gText_Persuasivepassenger_ThanksForListening,,SPEAKER_UBER_DRIVER2)
    goto(Quest_Persuasivepassenger_Driver2_End_Dialogue)
}

script Quest_Persuasivepassenger_Driver2_FoundSibling_Dialogue{
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_PERSUASIVEPASSENGER,SUB_QUEST_2)
    msgbox(gText_Persuasivepassenger_MyBrotherCalled,,SPEAKER_UBER_DRIVER2)
    msgbox(gText_Persuasivepassenger_HeWasGoing,,SPEAKER_UBER_DRIVER2)
    msgbox(gText_Persuasivepassenger_HeMentioned,,SPEAKER_UBER_DRIVER2)
    msgbox(gText_Persuasivepassenger_OhIm,,SPEAKER_ZOEY)
    msgbox(gText_Persuasivepassenger_ThisIsMyLast,,SPEAKER_UBER_DRIVER2)
    goto(Quest_Persuasivepassenger_Driver2_End_Dialogue)
}

script Quest_Persuasivepassenger_Landlord_Battle_Script{
    msgbox(gText_Persuasivepassenger_AreYouTripling,,SPEAKER_ZOEY)
    msgbox(gText_Persuasivepassenger_OfCourse,,SPEAKER_PERSUASIVE_LANDLORD)
    msgbox(gText_Persuasivepassenger_WaitYoure,,SPEAKER_PERSUASIVE_LANDLORD)
    msgbox(gText_Persuasivepassenger_YeahAnd,,SPEAKER_ZOEY)
    msgbox(gText_Persuasivepassenger_KidsAreSoft,,SPEAKER_PERSUASIVE_LANDLORD)

    trainerbattle_no_intro(TRAINER_PERSUASIVE_LANDLORD,gText_Persuasivepassenger_Wait)
    goto(Quest_Persuasivepassenger_PostBattle_Dialogue)
}

script Quest_Persuasivepassenger_PostBattle_Dialogue{
    msgbox(gText_Persuasivepassenger_PleaseDontHurt,,SPEAKER_PERSUASIVE_LANDLORD)
    msgbox(gText_Persuasivepassenger_IWasntPlanning,,SPEAKER_ZOEY,TAIL_THOUGHT,EMOTE_SHOCK)
    msgbox(gText_Persuasivepassenger_IDontWantAnythingBut,,SPEAKER_ZOEY)
    msgbox(gText_Persuasivepassenger_OkayOkay,,SPEAKER_PERSUASIVE_LANDLORD)
    msgbox(gText_Persuasivepassenger_LetsHope,,SPEAKER_ZOEY)
    closemessage

    applymovement(VAR_LAST_TALKED,Quest_Persuasivepassenger_WalkAway_Movement)
    fadescreen(FADE_TO_BLACK)
    removeobject(VAR_LAST_TALKED)
    fadescreen(FADE_FROM_BLACK)
    release
    end
}

script Quest_Persuasivepassenger_Sibling_Dialogue{
    lockall
    msgbox(gText_Persuasivepassenger_DidYouUsed,,SPEAKER_ZOEY,,EMOTE_SHOCK)
    msgbox(gText_Persuasivepassenger_YeahHowDid,,SPEAKER_PERSUASIVE_SIBLING,,EMOTE_SHOCK)
    msgbox(gText_Persuasivepassenger_IThinkThatGuy,,SPEAKER_ZOEY)
    msgbox(gText_Persuasivepassenger_WhatHowDo,,SPEAKER_PERSUASIVE_SIBLING)
    msgbox(gText_Persuasivepassenger_IHeardTheTaxiUnion,,SPEAKER_ZOEY)
    msgbox(gText_Persuasivepassenger_OhWowMySibling,,SPEAKER_PERSUASIVE_SIBLING)
    msgbox(gText_Persuasivepassenger_ThanksForLetting,,SPEAKER_PERSUASIVE_SIBLING)
    closemessage
    fadescreen(FADE_TO_BLACK)
    removeobject(VAR_LAST_TALKED)
    fadescreen(FADE_FROM_BLACK)
    release
    end
}

script Quest_Persuasivepassenger_Driver3_Event_Script{
    setvar(VAR_TEMP_1,5)
    lock
    if(!flag(FLAG_QUEST_PERSUASIVE_PASSENGER_PLAYER_KIDNAPPED)){
        msgbox(gText_Persuasivepassenger_HeyPlayer,,SPEAKER_UBER_DRIVER3)
        msgbox(gText_Persuasivepassenger_WhatTheHell,,SPEAKER_ZOEY)
    }else{
        msgbox(gText_Persuasivepassenger_WaitASecondHowDid,,SPEAKER_UBER_DRIVER3)
    }
    closemessage
    warpsilent(MAP_UBER_HQ_BASEMENT,5,5)
}

script Quest_Persuasivepassenger_WokeUp_Dialogue{
    lock
    addvar(VAR_TEMP_0,1)

    getplayerxy(VAR_TEMP_3,VAR_TEMP_4)

    if (var(VAR_TEMP_3) != 5){
    //5 is the X coordinate of where you wake up when kidnapped
        release
        return
    }

    if(flag(FLAG_QUEST_PERSUASIVE_PASSENGER_PLAYER_KIDNAPPED)){
        msgbox(gText_Persuasivepassenger_OkayIGuess,,SPEAKER_ZOEY,TAIL_THOUGHT,EMOTE_SWEAT)
    }else{
        msgbox(gText_Persuasivepassenger_WhereAmI,,SPEAKER_ZOEY,TAIL_THOUGHT,EMOTE_CONFUSE)
        setflag(FLAG_QUEST_PERSUASIVE_PASSENGER_PLAYER_KIDNAPPED)
    }

    closemessage
    release
    end
}

script Quest_Persuasivepassenger_Grunt1_Event_Script{
	trainerbattle_single(TRAINER_UBER_GRUNT1, gText_Persuasivepassenger_SoYouThink, gText_Persuasivepassenger_IGiveUp)
	msgbox(gText_Persuasivepassenger_HonestlyIDont, MSGBOX_AUTOCLOSE)
	end
}

script Quest_Persuasivepassenger_Grunt2_Event_Script{
    lock
    msgbox(gText_Persuasivepassenger_WhoIsIn,,SPEAKER_ZOEY,TAIL_SHOUT,EMOTE_ANGRY)
    msgbox(gText_Persuasivepassenger_IfYoureHere,,SPEAKER_UBER_GRUNT2,,EMOTE_SHOCK)
    closemessage
    trainerbattle_no_intro(TRAINER_UBER_GRUNT2,gText_Persuasivepassenger_IGottaTell)
    goto(Quest_Persuasivepassenger_DefeatedGrunt2_Script)
}

script Quest_Persuasivepassenger_DefeatedGrunt2_Script{
    lock
    applymovement(GRUNT2,Quest_Persuasivepassenger_RunUpstairs_Movement)
    playse(SE_EXIT)
    waitmovement(GRUNT2)
    removeobject(GRUNT2)
    waitse
}

script Quest_Persuasivepassenger_Uber_CEO_Dialogue{
    lock

        if (var(VAR_QUEST_PERSUASIVE_PASSENGER_STATE) != SAVED_BEFORE_UBER_CEO){
        if(defeated(TRAINER_UBER_CEO_RAID)){
            goto(Quest_Persuasivepassenger_PostRaid_Uber_CEO_Dialogue)
        }

        setvar(VAR_QUEST_PERSUASIVE_PASSENGER_STATE,SAVED_BEFORE_UBER_CEO)
        msgbox(gText_Persuasivepassenger_SoYoure,MSGBOX_NPC,SPEAKER_UBER_CEO)
        msgbox(gText_Persuasivepassenger_WhatsTheDeal,,SPEAKER_ZOEY,,EMOTE_ANGRY)
        msgbox(gText_Persuasivepassenger_WeDoThatMore,,SPEAKER_UBER_CEO)
        lockall
        closemessage
        startmenu_savescreen
    }
    goto(Quest_Persuasivepassenger_Uber_CEO_Battle_Script)
}

script Quest_Persuasivepassenger_Uber_CEO_Battle_Script{
    msgbox(gText_Persuasivepassenger_TimeToPut,MSGBOX_NPC,SPEAKER_UBER_CEO)
    closemessage
    trainerbattle_no_intro(TRAINER_UBER_CEO,gText_Persuasivepassenger_YoureTough)
    goto(Quest_Persuasivepassenger_Post_CEO_Battle_Script)
}

script Quest_Persuasivepassenger_PostRaid_Uber_CEO_Dialogue{
    msgbox(gText_Persuasivepassenger_IShouldHaveGuessed,MSGBOX_NPC,SPEAKER_UBER_CEO)
    lockall
    closemessage
    startmenu_savescreen
    goto(Quest_Persuasivepassenger_Uber_CEO_Battle_Script)
}

script Quest_Persuasivepassenger_Post_CEO_Battle_Script{
    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_PERSUASIVEPASSENGER,SUB_QUEST_3)
    questmenu(QUEST_MENU_SET_REWARD,QUEST_PERSUASIVEPASSENGER)
    msgbox(gText_Persuasivepassenger_WatchYourBack,MSGBOX_NPC)
    fadescreen(FADE_TO_BLACK)
    removeobject(VAR_LAST_TALKED)
    fadescreen(FADE_FROM_BLACK)
    end
}

script Quest_Persuasivepassenger_Complete_Dialogue{
    msgbox(gText_Persuasivepassenger_ThanksAgain,,MSGBOX_NPC)
    end
}

movement Quest_Persuasivepassenger_WalkAway_Movement{
    walk_left
    step_end
}

movement Quest_Persuasivepassenger_RunUpstairs_Movement{
    walk_faster_up
    step_end
}

text gText_Persuasivepassenger_WelcomeTo{
    format("Welcome to the union of taxi drivers!")
}
text gText_Persuasivepassenger_OhTheFamous{
    format("Oh! The famous {PLAYER}.")
}
text gText_Persuasivepassenger_IllGetRightTo{
    format("I'll get right to it. We think the best way to fight Uber is to thin out their numbers and get their drivers to join us.")
}
text gText_Persuasivepassenger_JustLikeYouWorked{
    format("Just like you worked with our driers to expand the fleet, we want you to work with uber drivers to help them too.")
}
text gText_Persuasivepassenger_WeveDoneSomeResearch{
    format("We've done some research, and we've identfied some influential Uber drivers, who are also going through some rough times. We're thinking if we can convince them that the taxi union is a better fit for them, they'll come over and bring others with them.")
}
text gText_Persuasivepassenger_CanYouSeeIfYou{
    format("Can you see if you can help them out?")
}
text gText_Persuasivepassenger_TheFirstDriver{
    format("The first driver tends to drive out of Alameda. Try grabbing an User from there!")
}
text gText_Persuasivepassenger_TheNextDriver{
    format("We've found a driver we can turn that drives out of Haightashbury. Head over there!")
}
text gText_Persuasivepassenger_TheresAnother{
    format("There's another driver that came out of nowhere, but we're not certain we know where they drive out of... I guess we'll have to try a few different places!")
}
text gText_Persuasivepassenger_HeyKid{
    format("Hey kid, how are you?")
}
text gText_Persuasivepassenger_HowsItGoing{
    format("Great, how's it going?")
}
text gText_Persuasivepassenger_JustPickingUp{
    format("Just trying to pick up some extra shifts, get in as many rides as I can.")
}
text gText_Persuasivepassenger_MyKidIsSick{
    format("My kid is sick with something fierce, so I'm trying to make some extra cash so we can buy some meds.")
}
text gText_Persuasivepassenger_OhIHope{
    format("Oh! I hope they're okay...")
}
text gText_Persuasivepassenger_MyMomUsed{
    format("You know, my mom used to make this insane medicine when I was little. It's hard to get the ingredients here, but I'd love to make it again... I think it was 3 {STR_VAR_1}, 5 {STR_VAR_2},")
}
text gText_Persuasivepassenger_IngredentsTwo{
    format("1 {STR_VAR_1}, and 2 {STR_VAR_2}.")
}
text gText_Persuasivepassenger_WereHere{
    format("Okay, we're here. Have a great day!")
}
text gText_Persuasivepassenger_ForYourSickKid{
    format("For your sick kid, right?")
}
text gText_Persuasivepassenger_OhRightDidI{
    format("Oh right, did I ride with you the other day?")
}
text gText_Persuasivepassenger_GiveItems{
    format("Do you want to give the items to the driver?")
}
text gText_Persuasivepassenger_IGotEverything{
    format("I got everything you mentioned. I'll leave it all back here for you.")
}
text gText_Persuasivepassenger_PlayerGave{
    format("{PLAYER} gave the items to the driver.")
}
text gText_Persuasivepassenger_ThankYouSoMuch{
    format("...thank you so much! You're like an angel, where did you come from?")
}
text gText_Persuasivepassenger_TheUnion{
    format("Actually, the taxi union sent me. They're all about working together to help their fellow workers, and they heard some other drivers could use some help.")
}
text gText_Persuasivepassenger_ThatsTight{
    format("That's tight! Maybe I'll check them out after all. Driving for Uber is intense.")
}
text gText_Persuasivepassenger_HowYaDoin{
    format("How you doing?")
}
text gText_Persuasivepassenger_YouKnow{
    format("You know, just trying to get by!")
}
text gText_Persuasivepassenger_MyBrothersLandlord{
    format("My brother's landlord jacked up his rent by some insane amount...like three times! So he's out on the street of Mission now.")
}
text gText_Persuasivepassenger_SoImTrying{
    format("So, I'm trying to pick some shifts")
}
text gText_Persuasivepassenger_WaitThreeTimes{
    format("Wait, three times the rent?!")
}
text gText_Persuasivepassenger_YeahSoMyBrotherIsOut{
    format("Yeah, so my brother is out on the streets... somewhere in the Mission.")
}
text gText_Persuasivepassenger_ThanksForListening{
    format("Thanks for listening though, appreciate it.")
}
text gText_Persuasivepassenger_WereAtTheSpot{
    format("We are at the spot! Have a great one.")
}
text gText_Persuasivepassenger_MyBrotherExLandlord{
    format("My brother's ex landlord raised his rents by three times...and it was pretty crazy.")
}
text gText_Persuasivepassenger_WellISawOnTheNews{
    format("Well, I saw on the news that he had a change of heart! He LOWERED by rent by a bigger amount than he raised it by, and then turned himself into the police for some drug stuff. Crazy!")
}
text gText_Persuasivepassenger_ThatsNotWhat{
    format("That's certainly better than I was expected!")
}
text gText_Persuasivepassenger_ImGonnaKeepWorking{
    format("I'm gonna keep working these shifts until my brother is back home. I gotta head down to Mission and let him know, but I haven't had time...")
}
text gText_Persuasivepassenger_MyBrotherCalled{
    format("My brother called me, and there was some awesome news!")
}
text gText_Persuasivepassenger_HeWasGoing{
    format("He heard about his apartment and was able to move back into his place.")
}
text gText_Persuasivepassenger_HeMentioned{
    format("He mentioned that the Taxi Union had gotten involved, I guess because that rent hike had messed with one of their own.")
}
text gText_Persuasivepassenger_OhIm{
    format("Oh yeah, I'm working with them a little too.")
}
text gText_Persuasivepassenger_ThisIsMyLast{
    format("This is my last shift with Uber... because I'm going to hit up the Taxi company tomorrow. If that's how they treat their people, there's somewhere I wanna be!")
}
text gText_Persuasivepassenger_AreYouTripling{
    format("Are you TRIPLING your rent?!?!")
}
text gText_Persuasivepassenger_OfCourse{
    format("Of course, that's market value! Why wouldn't I?")
}
text gText_Persuasivepassenger_WaitYoure{
    format("Wait, you don't look familiar, you don't even live here!")
}
text gText_Persuasivepassenger_YeahAnd{
    format("Yeah, and?")
}
text gText_Persuasivepassenger_KidsAreSoft{
    format("You kids are all too soft and sticking your nose in where it sounds belong!")
}
text gText_Persuasivepassenger_Wait{
    format("Wait a minute... a kid with a tough {STR_VAR_1}... you're the enforce from the Hellsangels!")
}
text gText_Persuasivepassenger_PleaseDontHurt{
    format("Please, don't hurt me! I told Jimmy I'll have his money!")
}
text gText_Persuasivepassenger_IWasntPlanning{
    format("I have no idea what he's talking about... but maybe I can abuse this.")
}
text gText_Persuasivepassenger_IDontWantAnythingBut{
    format("...you can keep your kneecaps in tact for now. And normally I don't care how you get the money. But you can't be screwing over innocent grandmas and people, okay? You bring that rent back down to where it was. Figure something else out. And get me my damn money.")
}
text gText_Persuasivepassenger_OkayOkay{
    format("Okay... okay!")
}
text gText_Persuasivepassenger_LetsHope{
    format("Let's hope that works...")
}
text gText_Persuasivepassenger_DidYouUsed{
    format("Did you used to live in the apartments in Alameda?")
}
text gText_Persuasivepassenger_YeahHowDid{
    format("Yeah, how did you know?")
}
text gText_Persuasivepassenger_IThinkThatGuy{
    format("I think that guy... the landlord, had a change a heart. I think rents are going back down.")
}
text gText_Persuasivepassenger_WhatHowDo{
    format("What? How do you know?")
}
text gText_Persuasivepassenger_IHeardTheTaxiUnion{
    format("I heard the Taxi union got involved. Guess he crossed one of theirs, and they don't mess around.")
}
text gText_Persuasivepassenger_OhWowMySibling{
    format("Oh wow! My sibling drives for uber. I should tell him!")
}
text gText_Persuasivepassenger_ThanksForLetting{
    format("Thanks for letting me know!")
}
text gText_Persuasivepassenger_HeyPlayer{
    format("Hey {PLAYER}! We're going to go for a quick detour...")
}

text gText_Persuasivepassenger_WhatTheHell{
    format("What the hell do you mean by a detour?")
}

text gText_Persuasivepassenger_WaitASecondHowDid{
    format("Wait a second, how did you get out of that basement?!?")
}

text gText_Persuasivepassenger_OkayIGuess{
    format("Okay, I guess we're back here again...")
}

text gText_Persuasivepassenger_WhereAmI{
    format("Where am I...?")
}
text gText_Persuasivepassenger_WhoIsIn{
	format("Who is in charge here?!?!")
}
text gText_Persuasivepassenger_IfYoureHere{
	format("If you're here, that's means... uh oh.")
}
text gText_Persuasivepassenger_IGottaTell{
	format("I gotta tell the boss!")
}
text gText_Persuasivepassenger_SoYouThink{
	format("So you think you can mess with Uber, huh?")
}
text gText_Persuasivepassenger_IGiveUp{
	format("I give up!")
}
text gText_Persuasivepassenger_HonestlyIDont{
	format("Honestly I don't know what's going on. They just told me to beat you up if you woke up.")
}
text gText_Persuasivepassenger_SoYoure{
    format("So, you're {PLAYER}.")
}
text gText_Persuasivepassenger_WhatsTheDeal{
    format("What's the deal with the kidnapping?!")
}
text gText_Persuasivepassenger_WeDoThatMore{
    format("We do that more than you think...")
}
text gText_Persuasivepassenger_TimeToPut{
    format("Time to put this into overdrive.")
}
text gText_Persuasivepassenger_YoureTough{
    format("You're tough!")
}
text gText_Persuasivepassenger_IShouldHaveGuessed{
    format("I should have guessed you'd be behind these... disruptions.")
}
text gText_Persuasivepassenger_WeveGotten{
    format("We've gotten a ton of new members, and Uber is slowing down! Thank you!")
}
text gText_Persuasivepassenger_ThanksAgain{
    format("Thanks again!")
}
text gText_Persuasivepassenger_WatchYourBack{
    format("Watch your back...")
}
text gText_Debug_WarpUber{
    format("but we haven't built the uber menu yet, so we'll warp you now.")
}
text gText_Debug_GiveItems{
    format("your bag now has what he needs.")
}
