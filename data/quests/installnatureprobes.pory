//Quest_InstallNatureProbes_
//gText_InstallNatureProbes_

const PLAYER = OBJ_EVENT_ID_PLAYER
const LIIDIA = 2

script Quest_InstallNatureProbes_Interact_Liidia_Script
{
	lock
    call(Quest_InstallNatureProbes_CheckCompletionRate_Script)
	returnqueststate(QUEST_INSTALLNATUREPROBES)

	switch (var(VAR_RESULT))
	{
		case QUEST_MENU_SET_ACTIVE: goto(Quest_InstallNatureProbes_Active_Dialogue)
		case QUEST_MENU_SET_REWARD: goto(Quest_InstallNatureProbes_Reward_Cutscene)
		default: goto(Quest_InstallNatureProbes_StartQuest_Dialogue)
	}
}

script Quest_InstallNatureProbes_StartQuest_Dialogue
{
	msgbox(gText_InstallNatureProbes_GeezIGottaWorkTonight, MSGBOX_NPC,SPEAKER_DEFAULT)
	msgbox(gText_InstallNatureProbes_CanYouHelp, MSGBOX_NPC,SPEAKER_DEFAULT)
	msgbox(gText_InstallNatureProbes_YouHaveAPokedex, MSGBOX_NPC,SPEAKER_DEFAULT)

	startquest(QUEST_INSTALLNATUREPROBES)
	release
	end
}

script Quest_InstallNatureProbes_Active_Dialogue
{
    call(Quest_InstallNatureProbes_CheckCompletionRate_Script)
	msgbox(gText_InstallNatureProbes_WhatProbes,MSGBOX_NPC,SPEAKER_DEFAULT)

    if (var(VAR_TEMP_1) == 0){
        msgbox(gText_InstallNatureProbes_TheProbeInTheForest,MSGBOX_NPC,SPEAKER_DEFAULT)
    }

    if (var(VAR_TEMP_2) == 0){
        msgbox(gText_InstallNatureProbes_TheProbeOnTheHill,MSGBOX_NPC,SPEAKER_DEFAULT)
    }

    if (var(VAR_TEMP_3) == 0){
        msgbox(gText_InstallNatureProbes_TheProbeByTheShore,MSGBOX_NPC,SPEAKER_DEFAULT)
    }
}

script Quest_InstallNatureProbes_InstallForestProbe_Script{
    lock

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_INSTALLNATUREPROBES,SUB_QUEST_1)
    if(var(VAR_RESULT)){
        msgbox(gText_InstallNatureProbes_ThereIsAProbe,MSGBOX_SIGN)
        release
        end
    }

    msgbox(gText_InstallNatureProbes_YeahThisSeemsToMatch,,SPEAKER_ZOEY)


    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_INSTALLNATUREPROBES,SUB_QUEST_1)
    call(Quest_InstallNatureProbes_CheckCompletionRate_Script)
    playse(SE_CONTEST_HEART)
    msgbox(gText_InstallNatureProbes_PlayerInstalledForest)
    waitse
    waitmessage

    release
    return
}
script Quest_InstallNatureProbes_InstallHillProbe_Script{
    lock

    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_INSTALLNATUREPROBES,SUB_QUEST_2)
    if(var(VAR_RESULT)){
        msgbox(gText_InstallNatureProbes_ThereIsAProbe,MSGBOX_SIGN)
        release
        end
    }
    msgbox(gText_InstallNatureProbes_ThisIsAsGoodOfAHill,,SPEAKER_ZOEY)


    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_INSTALLNATUREPROBES,SUB_QUEST_2)
    call(Quest_InstallNatureProbes_CheckCompletionRate_Script)
    playse(SE_CONTEST_HEART)
    msgbox(gText_InstallNatureProbes_PlayerInstalledHill)
    waitse
    waitmessage

    release
    end
}
script Quest_InstallNatureProbes_InstallShoreProbe_Script{
    lock
    subquestmenu(QUEST_MENU_CHECK_COMPLETE,QUEST_INSTALLNATUREPROBES,SUB_QUEST_3)
    if(var(VAR_RESULT)){
        msgbox(gText_InstallNatureProbes_ThereIsAProbe,MSGBOX_SIGN)
        release
        end
    }

    msgbox(gText_InstallNatureProbes_JustGottaMakeSure,,SPEAKER_ZOEY)


    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_INSTALLNATUREPROBES,SUB_QUEST_3)
    call(Quest_InstallNatureProbes_CheckCompletionRate_Script)
    playse(SE_CONTEST_HEART)
    msgbox(gText_InstallNatureProbes_PlayerInstalledShore)
    waitse
    waitmessage

    release
    end
}

script Quest_InstallNatureProbes_CheckCompletionRate_Script
{
    questmenu(QUEST_MENU_CHECK_ACTIVE, QUEST_INSTALLNATUREPROBES)

    if (!var(VAR_RESULT)){
        release
        return
    }

	setvar(VAR_TEMP_0, 0)
	setvar(VAR_TEMP_1, 0)
	setvar(VAR_TEMP_2, 0)
	setvar(VAR_TEMP_3, 0)

	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_INSTALLNATUREPROBES, SUB_QUEST_1)
	if (var(VAR_RESULT))
	{
		addvar(VAR_TEMP_0, 1)
		addvar(VAR_TEMP_1, 1)
	}
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_INSTALLNATUREPROBES, SUB_QUEST_2)
	if (var(VAR_RESULT))
	{
		addvar(VAR_TEMP_0, 1)
		addvar(VAR_TEMP_2, 1)
	}
	subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_INSTALLNATUREPROBES, SUB_QUEST_3)
	if (var(VAR_RESULT))
	{
		addvar(VAR_TEMP_0, 1)
		addvar(VAR_TEMP_3, 1)
	}

	if (var(VAR_TEMP_0) == 3)
	{
		questmenu(QUEST_MENU_SET_REWARD, QUEST_INSTALLNATUREPROBES)
	}
}

script Quest_InstallNatureProbes_Reward_Cutscene
{
    msgbox(gText_InstallNatureProbes_WowYouGotAll,MSGBOX_NPC,SPEAKER_DEFAULT)

    applymovement(LIIDIA,Common_Movement_FaceAwayPlayer)
    waitmovement(LIIDIA)

    playse(SE_PC_LOGIN)
    waitse

    applymovement(LIIDIA,Common_Movement_FacePlayer)
    waitmovement(LIIDIA)
    msgbox(gText_InstallNatureProbes_TheTrackingIsWorking,MSGBOX_NPC,SPEAKER_DEFAULT)
    closemessage

    msgbox(gText_InstallNatureProbes_OkayDone,MSGBOX_NPC,SPEAKER_DEFAULT)
    playfanfare(MUS_OBTAIN_ITEM)
    msgbox(gText_InstallNatureProbes_PokedexUpgradedWith)
    waitmessage
    waitfanfare
    //PSF TODO find a different rewards for this app and update the text
    closemessage

    playse(SE_POKENAV_CALL)
    waitse

    msgbox(gText_InstallNatureProbes_ThatsMyBoss,,SPEAKER_LIIDIA)
    applymovement(LIIDIA,Common_Movement_FaceAwayPlayer)
    waitmovement(LIIDIA)

    msgbox(gText_InstallNatureProbes_GoForLiidia,,,PHONE_ON)
    closemessage
    delay(32)
    msgbox(gText_InstallNatureProbes_IUnderstand,,,PHONE_ON)
    closemessage

    playse(SE_POKENAV_HANG_UP)
    waitse
    applymovement(LIIDIA,Common_Movement_FacePlayer)
    waitmovement(LIIDIA)

    msgbox(gText_InstallNatureProbes_HeThankedMe,,SPEAKER_LIIDIA)
    msgbox(gText_InstallNatureProbes_IRealizeNow,,SPEAKER_LIIDIA)
    closemessage

    applymovement(LIIDIA,Quest_InstallNatureProbes_LeaveStation_Movement)
    applymovement(PLAYER,Quest_InstallNatureProbes_WatchLiidiaLeave_Movement)

    waitmovement(LIIDIA)
    removeobject(LIIDIA)
    completequest(QUEST_INSTALLNATUREPROBES)
    release
    end
}

text gText_InstallNatureProbes_GeezIGottaWorkTonight{
	format("Geez, I gotta work late tonight if I'm going to finish these reports and the install...\pActually, you're a Trainer! Trainers are exploring places and shit, right?")
}
text gText_InstallNatureProbes_CanYouHelp{
	format("Can you help me with something? I don't think it'll put you too far out of your way. I need to install three probes in Muirwoods for tracking Pokémon movement. One in the deepest part of the forest, one up on the tallest hill, and one over by the lakeshore. Installing is really easy, you just clip them to the sign posts that we've already got set up there.")
}
text gText_InstallNatureProbes_YouHaveAPokedex{
    format("You've got a PokéDex? Great. I can upgrade it with the SanFran Habitat Mode if you help me out.")
}
text gText_InstallNatureProbes_WhatProbes{
	format("What probes did you already install in Muirwoods?")
}
text gText_InstallNatureProbes_TheProbeInTheForest{
	format("One probe goes in the deepest part of the forest.")
}
text gText_InstallNatureProbes_TheProbeOnTheHill{
	format("One probe goes on the tallest hill.")
}
text gText_InstallNatureProbes_TheProbeByTheShore{
	format("One probe goes by the lake shore.")
}
text gText_InstallNatureProbes_WowYouGotAll{
	format("Wow! You got all of them in? Let's test that...")
}
text gText_InstallNatureProbes_TheTrackingIsWorking{
	format("The tracking is working! You did great, thank you so much. Could I see your Pokédex please? Oh, this isn't the Sanfran model... Orre? Fascinating. It should work the same...")
}
text gText_InstallNatureProbes_ThatsMyBoss{
	format("That's my boss, let me just answer that.")
}
text gText_InstallNatureProbes_GoForLiidia{
	format("Go for Liidia!")
}
text gText_InstallNatureProbes_IUnderstand{
	format("...I understand.")
}
text gText_InstallNatureProbes_HeThankedMe{
	format("He thanked me... and then told me that my job was redundant. He told I'm fired, and have to leave the premises immediately.")
}
text gText_InstallNatureProbes_IRealizeNow{
	format("I realize now, I think you put me out of a job. How could I be so... naive?")
}
text gText_InstallNatureProbes_YeahThisSeemsToMatch{
    format("Yeah, this seems to match the description... deepest part of the forest.")
}
text gText_InstallNatureProbes_PlayerInstalledForest{
    format("{PLAYER} installed the probe in the forest.")
}
text gText_InstallNatureProbes_ThisIsAsGoodOfAHill{
    format("This is as good as a hill as I'm going to find, right?")
}
text gText_InstallNatureProbes_PlayerInstalledHill{
    format("{PLAYER} installed the probe on the hill.")
}
text gText_InstallNatureProbes_JustGottaMakeSure{
    format("Just gotta make sure its attached correctly...")
}
text gText_InstallNatureProbes_PlayerInstalledShore{
    format("{PLAYER} installed the probe by the shore.")
}
text gText_InstallNatureProbes_ThereIsAProbe{
    format("There is a probe attached to the sign post.")
}

text gText_InstallNatureProbes_OkayDone{
    format("Okay, done!")
}
text gText_InstallNatureProbes_PokedexUpgradedWith{
    format("{PLAYER}'s Pokédex was upgraded with Habitat Mode!")
}
movement Quest_InstallNatureProbes_LeaveStation_Movement{
    walk_right
    walk_down * 7
    step_end
}
movement Quest_InstallNatureProbes_WatchLiidiaLeave_Movement{
    delay_16
    face_right
    face_down
    step_end
}
