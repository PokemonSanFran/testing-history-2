const PLAYER = OBJ_EVENT_ID_PLAYER

mapscripts Psfcity8_Compound_2F_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_LITTLEROOT_INTRO_STATE, 2: Swagbag_IncrementIntroState_Script 
    ]
}
script Swagbag_IncrementIntroState_Script {
    setvar(VAR_LITTLEROOT_INTRO_STATE,3)
        end
}

script RestInBed_Script {

    if (var(VAR_LITTLEROOT_INTRO_STATE) == 4) {
        setvar(VAR_LITTLEROOT_INTRO_STATE,5)
    }

    lockall
        msgbox(gText_RestInBed_Text1)
        closemessage
        call(PlayersHouse_2F_EventScript_SetWallClock) //TODO replace with clock menu
        call(Common_EventScript_OutOfCenterPartyHeal)
        call(RestInBed_Progression_Script)
        closemessage
        releaseall
        end
}

script RestInBed_Progression_Script {
    checkitem(ITEM_FASHION_CASE,1)
        if (var(VAR_RESULT) == 0 && var(VAR_LITTLEROOT_INTRO_STATE) == 5) {
            giveitem(ITEM_FASHION_CASE)
        }

    if (var(VAR_STORYLINE_STATE) == 9){
        addvar(VAR_STORYLINE_STATE,1)
            pokenavcall(blank)
            waitmessage
            setvar(VAR_0x8000, MUGSHOT_HILDA)
            callnative(DrawMugshot)
            msgbox(gText_ThatsDifferent)
            callnative(ClearMugshot)
            pokenavcall(gText_FerryRepaired)
            waitmessage
            setvar(VAR_0x8000, MUGSHOT_HILDA)
            callnative(DrawMugshot)
            msgbox(gText_TooBadTheresNoFerry)
            callnative(ClearMugshot)

            if (var(VAR_FAINTED_FOG_STATE) > 1)
            {
                setvar(VAR_0x8000, MUGSHOT_HILDA)
                    callnative(DrawMugshot)
                    msgbox(gText_FindAWayThrough)
                    callnative(ClearMugshot)
            }
    }
}

script FashionCaseReminder_Script {
    //TODO Add FLAG_USED_FASHION_CASE to the fashion case script when I write it... someday.
    if (!flag(FLAG_USED_FASHION_CASE)) {
        applymovement(PLAYER,Common_Movement_ExclamationMark)
            waitmovement(0)
            setflag(FLAG_USED_FASHION_CASE)
            lockall
            msgbox(gText_FashionCaseReminder_Text1)
            special(OpenPokeblockCaseOnFeeder)
            waitstate
            msgbox(gText_FashionCaseReminder_Text2)
            closemessage
            release  
            end
    } else {
        release
    }
}

text gText_RestInBed_Text1 {
    format("Let's get some rest...when should we wake up?")
}

text gText_FashionCaseReminder_Text1 {
    format("What outfit do you want to wear today?")
}
text gText_FashionCaseReminder_Text2 {
    format("You can change your outfit whenever you like by using the Fashion Case, found in your Bag's Key Items pocket.")
}
text blank {
    format("")
}
text gText_FerryRepaired {
    format("A message from the PSFSUBWAY System. 

The following service routes are now operational:\p

Psfplace11 to Psfcity17\p

Psfcity17 to Psfcity9\p

We appreciate your patience while the remaining routes are restored.
","1_latin",175)
}
text gText_ThatsDifferent{
	format("That's a different notification...")
}
text gText_TooBadTheresNoFerry{
	format("Looks like east Psfregion1 is next! Too bad there's no ferry directly to the Gym in Psfcity11...")
}
text gText_FindAWayThrough{
	format("Hopefully, I'll find a way through the fog after that...")
}
