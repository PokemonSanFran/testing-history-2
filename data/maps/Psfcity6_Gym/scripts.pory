const PLAYER = OBJ_EVENT_ID_PLAYER
const PSFLEADER1 = 1

//VAR_GYM_1_STATE
//1: Solved puzzles, ready to fight
//2: Psfleader1 has walked to the battlefield
//3: Saved in front of Psfleader1
//4: Defeated Psfleader1
//5: Hear explanation of badge and TM

mapscripts Psfcity6_Gym_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        switch (var (VAR_GYM_1_STATE)) {
        case 3:
            goto(EnterPsfleader1_StartGymBattle_ObjectSetup)
        }
    }
}

script EnterPsfleader1_StartGymBattle_ObjectSetup {
    setobjectxyperm(PSFLEADER1, 6, 5)
    setobjectmovementtype(PSFLEADER1, MOVEMENT_TYPE_FACE_LEFT)
    end
}


script EnterPsfleader1_Event_Script {
    switch (var (VAR_GYM_1_STATE)) {
    case 3:
        goto(EnterPsfleader1_StartGymBattle_Script)
    case 4:
        goto(EnterPsfleader1_PostDefeat1_Dialogue)
    case 5:
        goto(EnterPsfleader1_PostDefeat2_Dialogue)
    default:
        goto(EnterPsfleader1_PutMeatInOven_Dialogue)
    }
}

script EnterPsfleader1_PutMeatInOven_Dialogue {
    lockall
    setvar(VAR_GYM_1_STATE, 3)
    setvar(VAR_0x8000, MUGSHOT_FALKNER)
    callnative(DrawMugshot)
    msgbox(gText_EnterPsfleader1_OneMoreLadle)
    callnative(ClearMugshot)
    closemessage
    applymovement(PSFLEADER1, Common_Movement_Delay48)
    waitmovement(0)
    applymovement(PSFLEADER1, Common_Movement_WalkInPlaceUp)
    waitmovement(0)
    closemessage
    setvar(VAR_0x8000, MUGSHOT_FALKNER)
    callnative(DrawMugshot)
    msgbox(gText_EnterPsfleader1_MeatsInOven, MSGBOX_NPC)
    callnative(ClearMugshot)
    closemessage
    call(EnterPsfleader1_DetermineMovementToBattlefield)
    goto(EnterPsfleader1_Intro_Dialogue)
}

script EnterPsfleader1_DetermineMovementToBattlefield {
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    if (var (VAR_TEMP_1) == 2) {
        applymovement(PSFLEADER1, EnterPsfleader1_WalkSouthToBattlefield)

        if (var (VAR_TEMP_0) == 7) {
            applymovement(PLAYER, EnterPsfleader1_PlayerWalkToBattlefieldFromLeft)
        } else {
            applymovement(PLAYER, EnterPsfleader1_PlayerWalkToBattlefieldFromRight)
        }
    } else {
        applymovement(PSFLEADER1, EnterPsfleader1_WalkWestToBattlefield)
        applymovement(PLAYER, EnterPsfleader1_PlayerWalkToBattlefieldFromDown)
    }
    waitmovement(0)
}

script EnterPsfleader1_Intro_Dialogue {
    setvar(VAR_0x8000, MUGSHOT_FALKNER)
    callnative(DrawMugshot)
    msgbox(gText_EnterPsfleader1_MyLatestCreation)
    msgbox(gText_EnterPsfleader1_YourSenseOfTaste)
    callnative(ClearMugshot)
    call(Common_EventScript_SaveGame)
    closemessage
    goto(EnterPsfleader1_StartGymBattle_Script)
}

script EnterPsfleader1_StartGymBattle_Script {
    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    trainerbattle_single(TRAINER_ROXANNE_1, gText_EnterPsfleader1_IThinkYouTalk, gText_EnterPsfleader1_WoahSuperSour, EnterPsfleader1_Psfleader1Defeated_Script, NO_MUSIC)
}

script EnterPsfleader1_Psfleader1Defeated_Script {
    lockall
    setflag(FLAG_BADGE01_GET)
    setvar(VAR_GYM_1_STATE, 4)
    setvar(VAR_0x8000, MUGSHOT_FALKNER)
    callnative(DrawMugshot)
    msgbox(gText_EnterPsfleader1_ThatWasDelicious)
    callnative(ClearMugshot)
    msgbox(gText_EnterPsfleader1_BadgeGet)
    call(Common_EventScript_PlayGymBadgeFanfare)
    giveitem(ITEM_TM01)
    setvar(VAR_0x8000, MUGSHOT_FALKNER)
    callnative(DrawMugshot)
    msgbox(gText_EnterPsfleader1_OhTheMeat)
    callnative(ClearMugshot)
    closemessage
    call(EnterPsfleader1_DetermineMovementBackToStove_Script)
    waitmovement(0)
    addvar(VAR_STORYLINE_STATE,1)
    call(Act1_1_Check_Script)
    releaseall
    end
}

script EnterPsfleader1_DetermineMovementBackToStove_Script {
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)

    if (var (VAR_TEMP_0) == 7) {
        applymovement(PSFLEADER1, EnterPsfleader1_WalkNorthBackToStove)
    } else {
        applymovement(PSFLEADER1, EnterPsfleader1_WalkEastBackToStove)
    }
}

script EnterPsfleader1_PostDefeat1_Dialogue {
    lockall
    setvar(VAR_GYM_1_STATE, 5)
    setvar(VAR_0x8000, MUGSHOT_FALKNER)
    callnative(DrawMugshot)
    msgbox(gText_EnterPsfleader1_WithThatBadge)
    callnative(ClearMugshot)
    closemessage
    releaseall
    end
}

script EnterPsfleader1_PostDefeat2_Dialogue {
    lockall
    setvar(VAR_0x8000, MUGSHOT_FALKNER)
    callnative(DrawMugshot)
    msgbox(gText_EnterPsfleader1_LetsCookTogether)
    callnative(ClearMugshot)
    closemessage
    releaseall
    end
}

movement EnterPsfleader1_WalkSouthToBattlefield {
    walk_down * 3
    walk_left * 2
    face_left
    step_end
}

movement EnterPsfleader1_WalkWestToBattlefield {
    walk_left * 2
    walk_down * 3
    face_left
    step_end
}
movement EnterPsfleader1_PlayerWalkToBattlefieldFromLeft {
    delay_16
    delay_16
    delay_16
    walk_left * 4
    walk_down * 3
    face_right
    step_end
}

movement EnterPsfleader1_PlayerWalkToBattlefieldFromDown {
    delay_16
    delay_16
    delay_16
    walk_left * 5
    walk_down * 2
    face_right
    step_end
}

movement EnterPsfleader1_PlayerWalkToBattlefieldFromRight {
    delay_16
    delay_16
    delay_16
    walk_left * 6
    walk_down * 3
    face_right
    step_end
}

movement EnterPsfleader1_WalkEastBackToStove {
    emote_exclamation_mark
    walk_right * 3
    walk_up * 3
    walk_right
    face_up
    walk_left
    face_up
    step_end
}

movement EnterPsfleader1_WalkNorthBackToStove {
    emote_exclamation_mark
    walk_up * 3
    walk_right * 3
    walk_right
    face_up
    walk_left
    face_up
    step_end
}

text gText_EnterPsfleader1_OneMoreLadle {
    format("Hold on! I've got one more ladle of Pinap braising liquid, and then into the oven...")
}

text gText_EnterPsfleader1_MeatsInOven {
    format("Cool, meat's in the oven. ")
}

text gText_EnterPsfleader1_MyLatestCreation {
    format("Ticket said you're {PLAYER}, right? I'm Psfleader1, head chef of Poc-Chuc and the Gym Leader here. After you lose, I'll treat you to my latest creation: Galifeu au Vin Burrito with a Pinap glaze. It's my first time making it, but from that amazing smell, it's gonna be great.")
}

text gText_EnterPsfleader1_YourSenseOfTaste {
    format("Did you know that a lot of your sense of taste actually is because of your sense of smell? All of your senses work together to create your culinary experiences. I'll show how I do that in battle too! Let's see if I can overwhelm your palette before my meat burns!")
}

text gText_EnterPsfleader1_IThinkYouTalk {
    format("I think you talk more about food than you do battling...")
}

text gText_EnterPsfleader1_FirstHitOfSpice {
    format("Whoa, that felt just like the first hit of spice on the tip of my tongue! ")
}

text gText_EnterPsfleader1_SimmerWasntEnough {
    format("Guess a simmer wasn't enough - I've gotta crank it up to a rolling boil!")
}

text gText_EnterPsfleader1_WhatABitterDefeat {
    format("Ouch, what a bitter defeat. Try adding Sitrus Berries?")
}

text gText_EnterPsfleader1_WoahSuperSour {
    format("Woah, super sour! I'll add some Micle Berry.")
}

text gText_EnterPsfleader1_ThatWasDelicious {
    format("That was delicious! I'm absolutely satiated. Here's the Psfbadge1, and my favorite Technical Machine.")
}

text gText_EnterPsfleader1_OhTheMeat {
    format("Oh, the meat!")
}

text gText_EnterPsfleader1_BadgeGet {
    format("You recieved the Psfbadge1 Badge!")
}

text gText_EnterPsfleader1_WithThatBadge {
    format("With that Badge, traded Pokémon up to Level Psflevel1 will now obey you. And Psfgymattacknum1 is one of my most delicious moves, it allows your Pokémon to Psfgymattackdesc1.")
}

text gText_EnterPsfleader1_LetsCookTogether {
    format("Let's cook together again soon! Now if you'll excuse me...")
}
