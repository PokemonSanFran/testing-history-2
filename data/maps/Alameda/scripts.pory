mapscripts Alameda_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call(Alameda_LayoutSwap_Script)
        call(Quest_StealTheAxe_HideLeader_Script)
        call(Alameda_ObjectVisibility_Script)
        call(Quest_CutePokemon_HideMelissaD_Script)
        call(Earthquake_HideTransport_Script)
    }
    MAP_SCRIPT_ON_LOAD {
        goto(Alameda_MapScripts_OnLoad)
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_ALAMEDA_STATE, START_SURVIVAL_CHANCE: SurvivalChance333_ExplainOperation
        VAR_ALAMEDA_STATE, WHY_ARE_YOU_HELPING_THEM: WhyAreYouHelpingThem_MissionDebrief_Dialogue
        VAR_ALAMEDA_STATE, POST_EARTHQUAKE: Earthquake_ShowDestroyedCity_Script
        VAR_ALAMEDA_STATE, POST_EARTHQUAKE_RESCUE: Earthquake_PostRescue_Dialogue
        VAR_STORYLINE_STATE, STORY_POST_DYNAMAX_BOSS: LetsFinishThis_MovePlayer_Script
    ]
}

script Alameda_ObjectVisibility_Script {

    switch (var(VAR_ALAMEDA_STATE)){
        case 0: goto(SurvivalChance333_HideObject_Alameda_Script)
        case SAVE_ARCHER_ALAMEDA: call(SurvivalChance333_HideObject_Alameda_Script)
        case START_SURVIVAL_CHANCE: call(AssholesHome_HideObject_Script)
        case WHY_ARE_YOU_HELPING_THEM: call(WhyAreYouHelpingThem_ObjectDebriefSetUp_Script)
        case POST_EARTHQUAKE: call(Earthquake_HideObject_Script)
        case POST_EARTHQUAKE_RESCUE: call(Earthquake_HideObject_Script)
        case POST_THIS_ISNT_RANDOM: call(LetsFinishThis_ObjectSetUp_Script)
        default:
            call(AssholesHome_HideObject_Script)
            call(SurvivalChance333_HideObject_Alameda_Script)
    }
    return
}

script Alameda_LayoutSwap_Script {
    if (var(VAR_ALAMEDA_STATE) > START_CHAMPIONSHIP_CLEAN_RAID){
        setmaplayoutindex(LAYOUT_ALAMEDA_POST_EARTHQUAKE)
    }
}

script Alameda_MapScripts_OnLoad {
        if (var(VAR_ALAMEDA_STATE) > START_SURVIVAL_CHANCE && var(VAR_ALAMEDA_STATE) < START_CHAMPIONSHIP_CLEAN_RAID){
            call(Navalbase_PostRaidTiles_Script)
        }
}

script Navalbase_PostRaidTiles_Script {
    call(SurvivalChance333_FreezeIce)
    call(SurvivalChance333_SetLava)
    call(SurvivalChance333_BlowOpenDoors)
}

script Alameda_ChooseFerryDestination_Script {
    msgbox(gText_WhereWould,MSGBOX_NPC)
    goto(Alameda_UnlockedDestinations_Script)
}

script Alameda_UnlockedDestinations_Script {
    switch (var(VAR_FERRY_STATE)){
        case FERRY_DEFAULT:
        case FERRY_ALAMEDA_AVAILABLE: goto(Alameda_FerryState0_Script)
        case FERRY_ALCATRAZ_AVAILABLE:goto(Alameda_FerryState1_Script)
    }
}

script Alameda_FerryState0_Script {
    multichoicedefault(18,8,ALAMEDA_FERRY_DESTINATIONS_VER1,2,FALSE)

        switch (var(VAR_RESULT)){
            case 0: warp(MAP_FERRYBUILDING,8,11)
            case 1: goto(SlateportCity_Harbor_EventScript_CancelDestinationSelect)
            case MULTI_B_PRESSED: goto(SlateportCity_Harbor_EventScript_CancelDestinationSelect)
        }
    call(Common_EventScript_FerryDepartIsland)
}

script Alameda_FerryState1_Script{
    multichoicedefault(18,8,ALAMEDA_FERRY_DESTINATIONS_VER2,2,FALSE)

        switch (var(VAR_RESULT)){
            case 0: warp(MAP_FERRYBUILDING,8,11)
            case 1: warp(MAP_ALCATRAZ,48,61)
            case 2: goto(SlateportCity_Harbor_EventScript_CancelDestinationSelect)
            case MULTI_B_PRESSED: goto(SlateportCity_Harbor_EventScript_CancelDestinationSelect)
        }
    call(Common_EventScript_FerryDepartIsland)
}

script Alameda_Person1_Fisherman {
    if (var(VAR_ALAMEDA_STATE) == START_EARTHQUAKE_RESCUE){
        addvar(VAR_ALAMEDA_STATE,2)
        msgbox(format("thanks for saving me!"),MSGBOX_NPC)
        end
    }else {
        msgbox(format("i am a dude!"),MSGBOX_NPC)
    }
    end
}

script Alameda_Stan_Interact_Script{
    lock
    call(Quest_WallaceArmy_LoadStanSpeech_Script)
    msgbox(gText_WallaceArmy_OhWallaceSama,MSGBOX_NPC)
    call(Quest_WallaceArmy_PreBattleStan_Script)

    trainerbattle_single(TRAINER_ALAMEDA_STAN,gText_WallaceArmy_MarinSolarTheySpoke,gText_WallaceArmy_MyLord,Alameda_Stan_PostBattle_Script)
    goto(Alameda_Stan_PostBattle_Script)
}

script Alameda_Stan_PostBattle_Script{

    subquestmenu(QUEST_MENU_COMPLETE_QUEST,QUEST_WALLACEARMY,SUB_QUEST_1)
    call(Quest_WallaceArmy_CheckCompletionRate_Script)
    questmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_WALLACEARMY)
        if (!var(VAR_RESULT)){
        msgbox(gText_WallaceArmy_PostBattlePlaceholder1)
        }else {
            msgbox(gText_WallaceArmy_PostQuestPlaceholder1)
        }

        release
        end
}

script Alameda_Landlord_Dialogue{
    msgbox(gText_Alameda_ManAfterThese,MSGBOX_NPC)

    specialvar(VAR_RESULT,Quest_Persuasivepassenger_CountRemainingSubquests)
    if (var(VAR_RESULT) == 2){
       goto(Quest_Persuasivepassenger_Landlord_Battle_Script)
    }
    end
}

text gText_Alameda_ManAfterThese{
    format("Man, after this rent hikes, I'm gonna make a killing on this building!")
}
