const OPPONENT = 1
const ATTENDANT_1 = 2

const NONE_UNHEARD = 50
const NONE_HEARD = 52
const SILVER_UNHEARD = 100
const SILVER_HEARD = 102

const HAS_NO_SYMBOLS = 0
const HAS_SILVER_SYMBOL = 1
const HAS_GOLD_SYMBOL = 2

const VAR_HEARD_MAIDEN_INTRO = VAR_TEMP_2
const VAR_CURRENT_BATTLE_NUM = VAR_TEMP_E
const VAR_SYMBOL_SPEECH_STATUS = VAR_TEMP_D
const VAR_BRAIN_STATUS = VAR_TEMP_F

script Haightashbury_Gym_Battle_Room_EventScript_EnterRoom{
    setvar(VAR_TEMP_0, 1)
    applymovement(OBJ_EVENT_ID_PLAYER, Haightashbury_Gym_Battle_Room_Movement_PlayerEnter)
    waitmovement(0)

    frontier_get(FRONTIER_DATA_BATTLE_NUM)
    if(var(VAR_RESULT) == 0){
        goto(Haightashbury_Gym_Battle_Room_EventScript_OpponentEnter)
    }

    applymovement(ATTENDANT_1, Haightashbury_Gym_Battle_Room_Movement_AttendantApproachPlayer)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Haightashbury_Gym_Battle_Room_Movement_PlayerFaceAttendant)
    waitmovement(0)

    setvar(VAR_HEARD_MAIDEN_INTRO, 1)
    frontier_set(FRONTIER_DATA_RECORD_DISABLED, TRUE)
    goto(Haightashbury_Gym_Battle_Room_EventScript_AskReadyForOpponent)
}

script Haightashbury_Gym_Battle_Room_EventScript_OpponentEnter{
    tower_setopponent

    addobject(OPPONENT)
    applymovement(OPPONENT, Haightashbury_Gym_Battle_Room_Movement_OpponentEnter)
    waitmovement(0)

    tower_getopponentintro(0)
    msgbox(gStringVar4, MSGBOX_DEFAULT)
    waitmessage

    call(Haightashbury_Gym_Battle_Room_EventScript_DoTowerBattle)
    if(var(VAR_RESULT) == B_OUTCOME_WON){
        goto(Haightashbury_Gym_Battle_Room_EventScript_DefeatedOpponent)
    }
    goto(BattleTower_EventScript_WarpToLobbyLost)
}

script BattleTower_EventScript_SetChallengeStatus{
    tower_set(TOWER_DATA_LVL_MODE)
    setvar(VAR_0x8004, FANCOUNTER_USED_BATTLE_TOWER)
    special(Script_TryGainNewFanFromCounter)
    goto(Haightashbury_Gym_Battle_Room_EventScript_WarpToLobby)
}

script BattleTower_EventScript_WarpToLobbyLost{
    frontier_set(FRONTIER_DATA_CHALLENGE_STATUS, CHALLENGE_STATUS_LOST)
    goto(BattleTower_EventScript_SetChallengeStatus)
}

script Haightashbury_Gym_Battle_Room_EventScript_WarpToLobbyWon{
    frontier_set(FRONTIER_DATA_CHALLENGE_STATUS, CHALLENGE_STATUS_WON)
    goto(BattleTower_EventScript_SetChallengeStatus)
}

script Haightashbury_Gym_Battle_Room_EventScript_DefeatedOpponent{
    frontier_incrementstreak
    tower_setbattlewon
    if(var(VAR_RESULT) == FRONTIER_STAGES_PER_CHALLENGE){
        goto(Haightashbury_Gym_Battle_Room_EventScript_WarpToLobbyWon)
    }

    applymovement(OPPONENT, Haightashbury_Gym_Battle_Room_Movement_OpponentExit)
    waitmovement(0)
    removeobject(OPPONENT)

    frontier_getbrainstatus

    applymovement(ATTENDANT_1, Haightashbury_Gym_Battle_Room_Movement_AttendantApproachPlayer)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Haightashbury_Gym_Battle_Room_Movement_PlayerFaceAttendant)
    waitmovement(0)

    msgbox(Haightashbury_Gym_Battle_Room_Text_RestoreMonsToFullHealth, MSGBOX_DEFAULT)
    playfanfare(MUS_HEAL)
    waitfanfare
    special(HealPlayerParty)

    goto(Haightashbury_Gym_Battle_Room_EventScript_AskReadyForOpponent)
}
script BattleFrontier_Construct_GoOn_Record_Rest_Retire_Menu{

    waitmessage
    dynmultipush(gText_GoOn,0)

    call(BattleFrontier_EventScript_GetCantRecordBattle)
    if(var(VAR_RESULT) == FALSE){
        dynmultipush(gText_Record2,1)
    }

    dynmultipush(gText_Rest,2)
    dynmultipush(gText_Retire,3)

    return
}


script Haightashbury_Gym_Battle_Room_EventScript_AskReadyForOpponent{
    frontier_getbrainstatus
    copyvar(VAR_BRAIN_STATUS, VAR_RESULT)

    frontier_get(FRONTIER_DATA_BATTLE_NUM)
    call(Haightashbury_Gym_Battle_Room_EventScript_ReadyForOpponent)
    call(BattleFrontier_BattleTower_ConstructAndCall_Menu)
}

script BattleFrontier_BattleTower_ConstructAndCall_Menu{

    call(BattleFrontier_Construct_GoOn_Record_Rest_Retire_Menu)
    dynmultistack(19,2,FALSE,4,TRUE,0,DYN_MULTICHOICE_CB_NONE)
    switch(var(VAR_RESULT)){
        case 0:goto(Haightashbury_Gym_Battle_Room_EventScript_ContinueChallenge)
        case 1:goto(Haightashbury_Gym_Battle_Room_EventScript_AskRecordBattle)
        case 2:goto(Haightashbury_Gym_Battle_Room_EventScript_AskPauseChallenge)
        case 3:goto(Haightashbury_Gym_Battle_Room_EventScript_AskRetireChallenge)
        case MULTI_B_PRESSED:goto(Haightashbury_Gym_Battle_Room_EventScript_AskReadyForOpponent)
    }
}

script Haightashbury_Gym_Battle_Room_EventScript_AskRecordBattle{
    message(Haightashbury_Gym_Battle_Room_Text_RecordYourBattle)
    waitmessage
    multichoicedefault(20, 8, MULTI_YESNO, 1, FALSE)
    if(var(VAR_RESULT) == 0){
        call(BattleFrontier_EventScript_SaveBattle)
    }
    goto(Haightashbury_Gym_Battle_Room_EventScript_AskReadyForOpponent)
}

script Haightashbury_Gym_Battle_Room_EventScript_AskPauseChallenge{
    msgbox(Haightashbury_Gym_Battle_Room_Text_SaveAndQuitGame, MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        goto(Haightashbury_Gym_Battle_Room_EventScript_PauseChallenge)
    }

    goto(Haightashbury_Gym_Battle_Room_EventScript_AskReadyForOpponent)
}

script Haightashbury_Gym_Battle_Room_EventScript_AskRetireChallenge{
    message(Haightashbury_Gym_Battle_Room_Text_CancelYourChallenge)
    waitmessage
    multichoicedefault(20, 8, MULTI_YESNO, 1, FALSE)
    if(var(VAR_RESULT) == 0){
        goto(Haightashbury_Gym_Battle_Room_EventScript_RetireChallenge)
    }

    goto(Haightashbury_Gym_Battle_Room_EventScript_AskReadyForOpponent)
}

script Haightashbury_Gym_Battle_Room_EventScript_ContinueChallenge{
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, Haightashbury_Gym_Battle_Room_Movement_PlayerFaceBattle)
    waitmovement(0)
    applymovement(ATTENDANT_1, Haightashbury_Gym_Battle_Room_Movement_AttendantReturnToPos)
    waitmovement(0)

    goto(Haightashbury_Gym_Battle_Room_EventScript_OpponentEnter)
}


script Haightashbury_Gym_Battle_Room_EventScript_PauseChallenge{
    message(Haightashbury_Gym_Battle_Room_Text_SavingPleaseWait)
    waitmessage

    tower_save(CHALLENGE_STATUS_PAUSED)
    playse(SE_SAVE)
    waitse

    fadescreen(FADE_TO_BLACK)
    frontier_reset
    end
}

script Haightashbury_Gym_Battle_Room_EventScript_ReadyForOpponent{
    copyvar(VAR_CURRENT_BATTLE_NUM, VAR_RESULT)
    addvar(VAR_CURRENT_BATTLE_NUM,1)
    buffernumberstring(STR_VAR_3,VAR_CURRENT_BATTLE_NUM)

    message(Haightashbury_Gym_Battle_Room_Text_ReadyForXOpponent)
    waitmessage

    return
}

script Haightashbury_Gym_Battle_Room_EventScript_BattleAnabel{
    frontier_setbrainobj
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Haightashbury_Gym_Battle_Room_Movement_PlayerFaceBattle)
    waitmovement(0)
    applymovement(ATTENDANT_1, Haightashbury_Gym_Battle_Room_Movement_AttendantReturnToPos)
    waitmovement(0)
    addobject(OPPONENT)
    applymovement(OPPONENT,Haightashbury_Gym_Battle_Room_Movement_OpponentEnter)
    waitmovement(0)
    goto(Haightashbury_Gym_Battle_Room_EventScript_Anabel)
}

script Haightashbury_Gym_Battle_Room_EventScript_DetermineIntroType{
    setvar(VAR_SYMBOL_SPEECH_STATUS,0)

    frontier_get(FRONTIER_DATA_HEARD_BRAIN_SPEECH)
    if(var(VAR_RESULT) == TRUE){
        addvar(VAR_SYMBOL_SPEECH_STATUS,2)
    }else{
        addvar(VAR_SYMBOL_SPEECH_STATUS,0)
    }

    switch(var(VAR_BRAIN_STATUS)){
        case FRONTIER_BRAIN_SILVER: addvar(VAR_SYMBOL_SPEECH_STATUS,50)
        case FRONTIER_BRAIN_GOLD: addvar(VAR_SYMBOL_SPEECH_STATUS,100)
        case FRONTIER_BRAIN_STREAK:
        case FRONTIER_BRAIN_STREAK_LONG: addvar(VAR_SYMBOL_SPEECH_STATUS,200)
    }
    return
}

script Haightashbury_Gym_Battle_Room_EventScript_AnabelIntro{
    call(Haightashbury_Gym_Battle_Room_EventScript_DetermineIntroType)

    if(var(VAR_SYMBOL_SPEECH_STATUS) == NONE_UNHEARD){
        msgbox(Haightashbury_Gym_Battle_Room_Text_GreetingsImAnabel, MSGBOX_DEFAULT)
        frontier_set(FRONTIER_DATA_HEARD_BRAIN_SPEECH)
}
    if(var(VAR_SYMBOL_SPEECH_STATUS) == SILVER_UNHEARD){
        msgbox(Haightashbury_Gym_Battle_Room_Text_AnabelYouCameBack, MSGBOX_DEFAULT)
        frontier_set(FRONTIER_DATA_HEARD_BRAIN_SPEECH)
    }

    if(var(VAR_SYMBOL_SPEECH_STATUS) < SILVER_UNHEARD){
        msgbox(Haightashbury_Gym_Battle_Room_Text_LetMeSeeYourTalent, MSGBOX_DEFAULT)
    }
    if(var(VAR_SYMBOL_SPEECH_STATUS) > NONE_HEARD){
        msgbox(Haightashbury_Gym_Battle_Room_Text_LetsBeginShallWe, MSGBOX_DEFAULT)
    }
}

script Haightashbury_Gym_Battle_Room_EventScript_Anabel{
    call(Haightashbury_Gym_Battle_Room_EventScript_AnabelIntro)

    call(Haightashbury_Gym_Battle_Room_EventScript_DoTowerBattle)
    if(var(VAR_RESULT) == B_OUTCOME_WON){
        goto(Haightashbury_Gym_Battle_Room_EventScript_DefeatedAnabel)
    }

    goto(BattleTower_EventScript_WarpToLobbyLost)
}

script Haightashbury_Gym_Battle_Room_EventScript_DoTowerBattle{
    closemessage

    setvar(VAR_HEARD_MAIDEN_INTRO, 0)
    frontier_set(FRONTIER_DATA_RECORD_DISABLED, FALSE)

    special(HealPlayerParty)
    setvar(VAR_0x8004, SPECIAL_BATTLE_TOWER)
    setvar(VAR_0x8005, 0)

    special(DoSpecialTrainerBattle)
    waitstate
    copyvar(VAR_0x8004, VAR_FRONTIER_BATTLE_MODE)

    if(var(VAR_0x8004) != FRONTIER_MODE_LINK_MULTIS){
        frontier_restorehelditems
        special(HealPlayerParty)
        frontier_resetsketch
    }

    tower_setinterviewdata
    frontier_get(FRONTIER_DATA_BATTLE_OUTCOME)
    return
}

script Haightashbury_Gym_Battle_Room_EventScript_DefeatedAnabel{
    frontier_incrementstreak
    frontier_getsymbols

    if(var(VAR_RESULT) == HAS_GOLD_SYMBOL){
        goto(Haightashbury_Gym_Battle_Room_EventScript_WarpToLobbyWon)
    }

    call(BattleTower_EventScript_AnabelCongratsText)

    playfanfare(MUS_OBTAIN_SYMBOL)
    call(BattleTower_EventScript_FrontierPassText)
    waitmessage
    waitfanfare
    frontier_givesymbol

    call(BattleTower_EventScript_AnabelConclusionText)

    goto(Haightashbury_Gym_Battle_Room_EventScript_WarpToLobbyWon)
}

script BattleTower_EventScript_AnabelCongratsText{
    if(var(VAR_RESULT) == HAS_NO_SYMBOLS){
        msgbox(Haightashbury_Gym_Battle_Room_Text_AnabelTalentShallBeRecognized, MSGBOX_DEFAULT)
    }
    if(var(VAR_RESULT) == HAS_SILVER_SYMBOL){
        msgbox(Haightashbury_Gym_Battle_Room_Text_AnabelCongratsYourPassPlease, MSGBOX_DEFAULT)
    }
    return
}

script BattleTower_EventScript_FrontierPassText{
if(var(VAR_RESULT) == HAS_NO_SYMBOLS){
       message(Haightashbury_Gym_Battle_Room_Text_ReceivedAbilitySymbol)
    }
    if(var(VAR_RESULT) == HAS_SILVER_SYMBOL){
        message(Haightashbury_Gym_Battle_Room_Text_AbilitySymbolTookGoldenShine)
    }
return
}

script BattleTower_EventScript_AnabelConclusionText{
    if(var(VAR_RESULT) == HAS_NO_SYMBOLS){
        msgbox(Haightashbury_Gym_Battle_Room_Text_UntilNextTime, MSGBOX_DEFAULT)
    }
    if(var(VAR_RESULT) == HAS_SILVER_SYMBOL){
        msgbox(Haightashbury_Gym_Battle_Room_Text_WishICouldBattleYouAgain, MSGBOX_DEFAULT)
    }
    return
}

movement Haightashbury_Gym_Battle_Room_Movement_PlayerEnter{
    walk_up * 3
    face_right
    step_end
}

movement Haightashbury_Gym_Battle_Room_Movement_PlayerFaceAttendant{
    face_left
        step_end
}

movement Haightashbury_Gym_Battle_Room_Movement_PlayerFaceBattle{
    face_right
        step_end
}

movement Haightashbury_Gym_Battle_Room_Movement_OpponentEnter{
    walk_down * 4
        face_left
        step_end
}

movement Haightashbury_Gym_Battle_Room_Movement_OpponentExit{
    walk_up * 4
        step_end
}

movement Haightashbury_Gym_Battle_Room_Movement_AttendantApproachPlayer{
    walk_right * 2
    step_end
}

movement Haightashbury_Gym_Battle_Room_Movement_AttendantReturnToPos{
    walk_left * 2
    face_right
    step_end
}

script Haightashbury_Gym_Battle_Room_EventScript_WarpToLobby{
    copyvar(VAR_RESULT, VAR_FRONTIER_BATTLE_MODE)
    switch(var(VAR_RESULT)){
        case FRONTIER_MODE_SINGLES:
            warp(MAP_HAIGHTASHBURY_GYM_LOBBY, 4, 5)
        case FRONTIER_MODE_DOUBLES:
            warp(MAP_HAIGHTASHBURY_GYM_LOBBY, 9, 5)
        case FRONTIER_MODE_MULTIS:
            warp(MAP_HAIGHTASHBURY_GYM_LOBBY, 12, 5)
        case FRONTIER_MODE_LINK_MULTIS:
            tower_closelink
            warp(MAP_HAIGHTASHBURY_GYM_LOBBY, 16, 5)
    }
    waitstate
    end
}

script Haightashbury_Gym_Battle_Room_EventScript_RetireChallenge{
    setflag(FLAG_CANCEL_BATTLE_ROOM_CHALLENGE)
        goto(BattleTower_EventScript_WarpToLobbyLost)
        end
}

text Haightashbury_Gym_Battle_Room_Text_RestoreMonsToFullHealth{
	"We will restore your POKéMON to\nfull health."
}

text Haightashbury_Gym_Battle_Room_Text_ReadyForXOpponent{
    "You will be facing opponent no. {STR_VAR_3}.\nAre you ready?"
}

text Haightashbury_Gym_Battle_Room_Text_RecordYourBattle{
    "Record your battle on your\nFRONTIER PASS?"
}

text Haightashbury_Gym_Battle_Room_Text_SaveAndQuitGame{
    "Would you like to save and\nquit the game?"
}

text Haightashbury_Gym_Battle_Room_Text_SavingPleaseWait{
    "Saving your battle data.\nPlease wait."
}

text Haightashbury_Gym_Battle_Room_Text_CancelYourChallenge{
    "Would you like to cancel your BATTLE\nROOM challenge?"
}

text Haightashbury_Gym_Battle_Room_Text_RecordCouldntBeSaved{
    "There was an error of some sort.\nYour record could not be saved."
}

text Haightashbury_Gym_Battle_Room_Text_GreetingsImAnabel{
    "Greetings…\nMy name is ANABEL.\pI am the SALON MAIDEN, and I am in\ncharge of running the BATTLE TOWER…\pI have heard several rumors\nabout you…\pIn all honesty, what I have heard does\nnot seem attractive in any way…\pThe reason I've come to see you…\nWell, there is but one reason…"
}

text Haightashbury_Gym_Battle_Room_Text_LetMeSeeYourTalent{
    "Let me see your talent in\nits entirety…"
}

text Haightashbury_Gym_Battle_Room_Text_AnabelTalentShallBeRecognized{
    "ANABEL: Fufufu, nicely done…\pYour FRONTIER PASS, please…\nYour talent shall be recognized."
}

text Haightashbury_Gym_Battle_Room_Text_ReceivedAbilitySymbol{
    "The Ability Symbol was embossed on\nthe FRONTIER PASS!"
}

text Haightashbury_Gym_Battle_Room_Text_UntilNextTime{
    "… … … … … …\pYou have confidence in your POKéMON\nbattling talent, don't you?\pI urge you to keep battling and\nkeep on winning.\pI will be waiting for you.\nUntil the next time we meet…"
}

text Haightashbury_Gym_Battle_Room_Text_AnabelYouCameBack{
    "ANABEL: You really did come back to\nsee me…\p… … … … … …\pYou've won straight matches to see me…\nI won't have to hold back against you…\pIt's been too long now…\pToo long since I've been able to battle\nwithout thinking about anything…"
}

text Haightashbury_Gym_Battle_Room_Text_LetsBeginShallWe{
    "Let's begin, shall we?"
}

text Haightashbury_Gym_Battle_Room_Text_AnabelCongratsYourPassPlease{
    "ANABEL: Fufu, congratulations…\nYour FRONTIER PASS, please…"
}

text Haightashbury_Gym_Battle_Room_Text_AbilitySymbolTookGoldenShine{
    "The Ability Symbol took on\na golden shine!"
}

text Haightashbury_Gym_Battle_Room_Text_WishICouldBattleYouAgain{
    "That was fun…\pI have never had a POKéMON battle\nso enjoyable before…\pI wish I could battle with you again…"
}
