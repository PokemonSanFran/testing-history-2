// Thanks for choosing Lumi Cab!
// The base fare's $710. You want a ride?
//  	Y/N/something else
// 
//  Got it. And where are we headed?
//  	fly menu
 // We're going to X placem with the stuff and the things?
//  	Y/N
//  Then here we go!
// 
//  Okay kid, here's your stop
// 
//  let's see your total will be $2000
// 
//  Hey just a minute there friend!
//  You don't even have enough for the fare?!
//  I come out to earn a living, don't I?
//  So why you gotta tax me like this?
//  	singlebattle
// 
//  Ain't you strong one?
//  Too strong for me!
// 
// Now I won't even have a copper to send back to my family in Hoenn...
// Thanks a lot, pal.

script Taxi_Interact_Dialogue{
    lock
    bufferstring(STR_VAR_1,"2000")
    msgbox(gText_Taxi_ThanksForChoosing)
    //PSF TODO replace 2000 with base fare constant
    closemessage
    call(Taxi_ConstructMenu_Script)
    dynmultistack(0,0,TRUE,3,TRUE,1,DYN_MULTICHOICE_CB_NONE)

    switch(var(VAR_RESULT)){
        case 0: goto(Taxi_Refuse_Dialogue)
        case 1: goto(Taxi_ChooseLocation_Dialogue)
        case 2: goto(Quest_Taxicabturnaround_AskPokemon_Dialogue)
    }
}

script Taxi_ConstructMenu_Script{
    dynmultipush("No.",NO)
    dynmultipush("Yes!",YES)

    specialvar(VAR_0x8008,Quest_Taxicabturnaround_IsSubquestComplete)
    returnqueststate(QUEST_TAXICABTURNAROUND)

    if ((var(VAR_RESULT) > QUEST_MENU_UNLOCK_QUEST) && (!var(VAR_0x8008))) {
        dynmultipush("Something else.",2)
    }
    return
}

script Taxi_Refuse_Dialogue{
    msgbox(gText_Taxi_ChangedYourMind)
    closemessage
    release
    end
}

script Taxi_ChooseLocation_Dialogue{
    fadescreen(FADE_TO_BLACK)
    //special(OpenTaxiMenu)
    //waitstate
    call(Taxi_ConstructLocations_List)
    dynmultistack(0,0,TRUE,3,TRUE,0,DYN_MULTICHOICE_CB_NONE)

    if (var(VAR_RESULT) == MULTI_B_PRESSED){
        fadescreen(FADE_FROM_BLACK)
        goto(Taxi_Refuse_Dialogue)
    }

    specialvar(VAR_RESULT,Taxi_CalculateFeeAndCharge)
        if (!var(VAR_RESULT)){
            call(Taxi_StartBattle_Dialogue)
        }

    call(Taxi_WarpToLocation_Script)

    release
    end
}

script Taxi_WarpToLocation_Script{
    switch(var(VAR_TAXI_DESTINATION)){
        case 0: warpsilent(MAP_TENDERLOIN,0)
        case 1: warpsilent(MAP_BERKELEY,0)
        case 2: warpsilent(MAP_PACIFICA,0)
    }
}

script Taxi_ConstructLocations_List{
    if (!flag(FLAG_VISITED_LITTLEROOT_TOWN)){
        dynmultipush("Caphe City",0)
    }
    if (!flag(FLAG_VISITED_OLDALE_TOWN)){
        dynmultipush("Chasilla",1)
    }
    if (!flag(FLAG_VISITED_DEWFORD_TOWN)){
        dynmultipush("Cresalta Vista",2)
    }

    dynmultipush("Never mind.",MULTI_B_PRESSED)
    return
}

script Taxi_StartBattle_Dialogue{
    showmoneybox(0,0)
    special(Taxi_BufferDestinationMapName)
    msgbox(gText_Taxi_HereWeAre)
    msgbox(gText_Taxi_ItllCost)
    msgbox(gText_Taxi_WhatDoYouMean)
    closemessage
    hidemoneybox
    trainerbattle_no_intro(TRAINER_CABBIE_HALE,gText_Taxi_AintYouStrong)
    fadescreen(FADE_TO_BLACK)
    msgbox(gText_Taxi_NowIWont)
    closemessage
    return
}

text gText_Taxi_ThanksForChoosing{
    format("Thanks for choosing Taxicab! The base fare is ¥{STR_VAR_1}, do you want a ride?")
}
text gText_Taxi_ChangedYourMind{
    format("Changed your mind? That's fine. We'll be here!")
}
text gText_Taxi_HereWeAre{
    format("Okay kid, here's your stop, {STR_VAR_1}.")
}
text gText_Taxi_ItllCost{
    format("That'll be ¥{STR_VAR_2}.")
}
text gText_Taxi_WhatDoYouMean{
    format("Hey, just a minute there friend! You don't even have enough for the fare?! I come out to earn a living, don't I? So why you gotta tax me like this?")
}
text gText_Taxi_AintYouStrong{
    format("Well aren't you a strong one? Too strong for me!")
}
text gText_Taxi_NowIWont{
    format("Now I won't make enough money to send back home to my family in Hoenn...")
}
