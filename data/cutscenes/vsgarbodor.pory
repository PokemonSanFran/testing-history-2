const PLAYER = OBJ_EVENT_ID_PLAYER
const BIANCA = 1
const OAK = 2
const GIOVANNI = 3
const ROCKET_A = 4
const ROCKET_B = 5
const CROWD_A = 6
const CROWD_B = 7
const CROWD_C = 8
const POLICE_A = 9
const POLICE_B = 10
const ROSE = 11
const ORI = 12


const FLAG_HIDE_BIANCA_COMPOUND = FLAG_TEMP_1
const FLAG_HIDE_OAK_COMPOUND = FLAG_TEMP_2
const FLAG_HIDE_BIANCA = FLAG_TEMP_1

script VSGarbodor_Arrest_ObjectSetup_Script {
    setobjectxy(ROCKET_A, 26, 19)
        setobjectxy(ROCKET_B, 26, 20)
        setobjectxy(BIANCA, 21, 21)
        setobjectxy(OAK, 21, 22)

        setobjectxyperm(ROCKET_A, 26, 19)
        setobjectxyperm(ROCKET_B, 26, 20)
        setobjectxyperm(BIANCA, 21, 21)
        setobjectxyperm(OAK, 21, 22)
}

script VSGarbodor_ObjectCleanUp_Script{
    if ((var(VAR_STORYLINE_STATE) > STORY_FERRY_FIXED) && ((var(VAR_STORYLINE_STATE) != STORY_DEFEATED_GARBODOR))){
        setflag(FLAG_HIDE_BIANCA_COMPOUND)
            setflag(FLAG_HIDE_OAK_COMPOUND)
            return
    }
}

script VSGarbodor_ConstructionObjectCleanUp_Script{
    if (var(VAR_CONSTRUCTION_STRIKE_STATE) != START_VS_GARBODOR){
        setflag(FLAG_HIDE_BIANCA)
    }
    return
}

script VSGarbodor_Scouting_Dialogue {
    lock
        applymovement(PLAYER, Common_Movement_FaceRight)
        waitmovement(0)
        msgbox(gText_VSGarbodor_IDidSomeScouting,,SPEAKER_BIANCA)
        multichoicedefault(18, 6, MULTI_GARBADOR_CHOOSE, 3, TRUE)
        goto(VSGarbodor_ChooseSide_Script)
}

script VSGarbodor_RandomSide_Script {
    random(2)
        goto(VSGarbodor_ChooseSide_Script)
}

script VSGarbodor_ChooseSide_Script {
    switch (var (VAR_RESULT)) {
        case 0:
            goto(VSGarbodor_ChooseLeft_Script)
        case 1:
                goto(VSGarbodor_ChooseRight_Script)
        case 2:
                    goto(VSGarbodor_RandomSide_Script)
    }
}

script VSGarbodor_ChooseLeft_Script {
    setvar(VAR_INNER_CONSTRUCTION_SITE_STATE,PLAYER_LEFT_SIDE)
        msgbox(gText_VSGarbodor_ThenIllTakeTheRight,,SPEAKER_BIANCA)

        closemessage

        applymovement(BIANCA, VSGarbodor_WalkFurtherRight)
        delay(60)
        applymovement(PLAYER, VSGarbodor_WalkTowardsLeftSide)
        waitmovement(0)
        removeobject(BIANCA)
        release
        end
}

script VSGarbodor_ChooseRight_Script {
    setvar(VAR_INNER_CONSTRUCTION_SITE_STATE, PLAYER_RIGHT_SIDE)
        msgbox(gText_VSGarbodor_CoolIWanted,,SPEAKER_BIANCA)

        closemessage

        applymovement(BIANCA, VSGarbodor_WalkFurtherLeft)
        delay(60)
        applymovement(PLAYER, VSGarbodor_WalkTowardsRightSide)
        waitmovement(0)
        removeobject(BIANCA)
        release
        end
}

script VSGarbodor_TeamRocketGrunt1_Battle_Script {
    trainerbattle_single(TRAINER_FAKEROCKET_A, gText_VSGarbodor_ScrewThisStadium, gText_VSGarbodor_DamnTheyWereRight, VSGarbodor_TeamRocketGrunt1_Battle_Script)
        msgbox(gText_VSGarbodor_WelpIDidWhat, MSGBOX_AUTOCLOSE,SPEAKER_ROCKET_MEMBER_A)
        goto(VSGarbodor_RemoveGrunt)
        end
}

script VSGarbodor_TeamRocketGrunt3_Battle_Script {
    trainerbattle_single(TRAINER_FAKEROCKET_B, gText_VSGarbodor_IsTheName, gText_VSGarbodor_TheyDidntPrepare, VSGarbodor_TeamRocketGrunt3_Battle_Script)
        msgbox(gText_VSGarbodor_OhThankGiratina, MSGBOX_AUTOCLOSE,SPEAKER_ROCKET_MEMBER_B)
        goto(VSGarbodor_RemoveGrunt)
        end
}

script VSGarbodor_TeamRocketGrunt2_Battle_Script {
    trainerbattle_single(TRAINER_FAKEROCKET_A, gText_VSGarbodor_RightOnCue, gText_VSGarbodor_HmmIGuess, VSGarbodor_TeamRocketGrunt2_Battle_Script)
        msgbox(gText_VSGarbodor_BeingBad, MSGBOX_AUTOCLOSE,SPEAKER_ROCKET_MEMBER_A)
        goto(VSGarbodor_RemoveGrunt)
        end
}

script VSGarbodor_TeamRocketGrunt4_Battle_Script {
    trainerbattle_single(TRAINER_FAKEROCKET_B, gText_VSGarbodor_LookAtYou, gText_VSGarbodor_ThatsRight, VSGarbodor_TeamRocketGrunt4_Battle_Script)
        msgbox(gText_VSGarbodor_HaveFunCleaning, MSGBOX_AUTOCLOSE)
        goto(VSGarbodor_RemoveGrunt)
        end
}

script VS_Garbodor_CoreEncounter_Script {
    startmenu_savescreen
        faceplayer
        setwildbattle(SPECIES_GARBODOR, 40, ITEM_NONE)
        playmoncry(SPECIES_GARBODOR, CRY_MODE_ENCOUNTER)
        delay(40)
        waitmoncry
        setflag(FLAG_SYS_CTRL_OBJ_DELETE)
        closemessage
        dowildbattle
        clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
        specialvar(VAR_RESULT, GetBattleOutcome)
        switch (var (VAR_RESULT)) {
            case B_OUTCOME_CAUGHT:
            case B_OUTCOME_WON:
                goto(VSGarbodor_RemoveGrunt)
        }
    release
        end
}

script VSGarbodor_RemoveGrunt {
    lock
        closemessage
        fadescreenswapbuffers(FADE_TO_BLACK)
        removeobject(VAR_LAST_TALKED)
        fadescreenswapbuffers(FADE_FROM_BLACK)
        call(VSGarbodor_CheckDefeated_Script)
        release
        end
}

script VSGarbodor_CheckDefeated_Script {
    if (flag(FLAG_DEFEATED_CONSTRUCTION_ROCKET_B) && flag(FLAG_DEFEATED_CONSTRUCTION_ROCKET_A) && flag(FLAG_DEFEATED_CONSTRUCTION_MON_A) && flag(FLAG_DEFEATED_CONSTRUCTION_MON_B)) {
        goto(VSGarbodor_MoveBianca_Script)
    }
    end
}

script VSGarbodor_PreventCrossingSides_Script {
    lock
        msgbox(gText_VSGarbodor_FeelingLonely,,SPEAKER_BIANCA)
        closemessage


        if (var (VAR_INNER_CONSTRUCTION_SITE_STATE) == PLAYER_RIGHT_SIDE) {
            applymovement(PLAYER, Common_Movement_WalkRight)
        } else {
            applymovement(PLAYER, Common_Movement_WalkLeft)
        }
    waitmovement(0)
        release
        end
}

script VSGarbodor_MoveBianca_Script {
    lock

        getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
        clearflag(FLAG_TEMP_1)
        addobject(BIANCA)

        if (var (VAR_TEMP_0) < 10) {
            applymovement(PLAYER, Common_Movement_FaceRight)
                if (var (VAR_TEMP_1) > 9) {
                    setobjectxy(BIANCA, 11, 12)
                        copyobjectxytoperm(BIANCA)
                        applymovement(BIANCA, BiancaApproachSW)
                } else {
                    setobjectxy(BIANCA, 13, 6)
                        copyobjectxytoperm(BIANCA)
                        applymovement(BIANCA, BiancaApproachNW)
                }
        } else {
            if (var (VAR_TEMP_1) > 9) {
                applymovement(PLAYER, Common_Movement_FaceUp)
                    setobjectxy(BIANCA, 22, 4)
                    copyobjectxytoperm(BIANCA)
                    applymovement(BIANCA, BiancaApproachSE)
            } else {
                applymovement(PLAYER, Common_Movement_FaceLeft)
                    setobjectxy(BIANCA, 10, 6)
                    copyobjectxytoperm(BIANCA)
                    applymovement(BIANCA, BiancaApproachNE)
            }
        }
    waitmovement(0)
        goto(VSGarbodor_DefeatedAll_Dialogue)
}

script VSGarbodor_DefeatedAll_Dialogue {
    setvar(VAR_CONSTRUCTION_STRIKE_STATE,DEFEATED_GARBODOR)

        msgbox(gText_VSGarbodor_GodYouWereTaking,,SPEAKER_BIANCA)
        closemessage


        applymovement(BIANCA, VSGarbodor_LookAround)
        waitmovement(0)
        msgbox(gText_VSGarbodor_WhateverMineWereEasy,,SPEAKER_BIANCA)
        closemessage


        warpsilent(MAP_PSFROUTE22, 255, 20, 22)
        delay(100)
}


script VSGarbodor_Arrest_Script {
    setvar(VAR_CONSTRUCTION_STRIKE_STATE,TEAM_ROCKET_ARRESTED)
        setvar(VAR_STORYLINE_STATE,STORY_DEFEATED_GARBODOR)

        applymovement(ROSE,Common_Movement_FaceUp)
        applymovement(BIANCA,Common_Movement_FaceUp)
        applymovement(OAK,Common_Movement_FaceUp)
        applymovement(PLAYER,Common_Movement_FaceUp)
        waitmovement(0)

        applymovement(ROCKET_A, VSGarbodor_PerpWalk)
        applymovement(ROCKET_B, VSGarbodor_PerpWalk)
        applymovement(POLICE_A, VSGarbodor_PerpWalk)
        applymovement(POLICE_B, VSGarbodor_PerpWalk)
        waitmovement(0)

        call(VSGarbodor_CleanUpPerpWalk)
        applymovement(ROSE, Common_Movement_FaceDown)
        applymovement(BIANCA, Common_Movement_FaceDown)
        waitmovement(0)
        goto(VSGarbodor_Negotiating_Dialogue)
}

script VSGarbodor_CleanUpPerpWalk {
    removeobject(ROCKET_A)
        removeobject(ROCKET_B)
        removeobject(POLICE_A)
        removeobject(POLICE_B)
        removeobject(GIOVANNI)
}

script VSGarbodor_Negotiating_Dialogue {
    msgbox(gText_VSGarbodor_AsWeWereNegotiating,,SPEAKER_ROSE)

        msgbox(gText_VSGarbodor_DoneLikeYouGuys,,SPEAKER_ZOEY)

        msgbox(gText_VSGarbodor_ThankYouBoth,,SPEAKER_ROSE)

        closemessage

        msgbox(gText_VSGarbodor_YeahImBeat, MSGBOX_YESNO,,SPEAKER_BIANCA)

        if (var (VAR_RESULT) == NO) {

            msgbox(gText_VSGarbodor_WeirdButOkay,,SPEAKER_BIANCA)

                closemessage
                call(VSGarbodor_FlyWithoutPlayer_Script)
                release
                end
        } else {

            msgbox(gText_VSGarbodor_LetsBounce,,SPEAKER_BIANCA)

                closemessage
                warpsilent(MAP_SOUTHBAY_COMPOUND_1F, 7, 3)
                release
                end
        }
}

script VSGarbodor_FlyWithoutPlayer_Script {
    applymovement(BIANCA, Common_Movement_WalkInPlaceFasterDown)
        waitmovement(0)
        delay(50)
        setfieldeffectargument(0,1)
        dofieldeffect(FLDEFF_NPCFLY_OUT)
        delay(15)
        removeobject(BIANCA)
        waitfieldeffect(FLDEFF_NPCFLY_OUT)
}

script VSGarbodor_ReturnToTheCompound_ObjectSetUp {
    if (var(VAR_STORYLINE_STATE) == STORY_DEFEATED_GARBODOR){
        setobjectxy(BIANCA,7,2)
            setobjectxyperm(BIANCA,7,2)
            setobjectmovementtype(BIANCA,MOVEMENT_ACTION_FACE_DOWN)
            setflag(FLAG_HIDE_OAK_COMPOUND)
            return
    }
}

script VSGarbodor_ReturnToTheCompound_Dialogue {
    getplayerxy(VAR_TEMP_0,VAR_TEMP_1)
        if (var(VAR_TEMP_0) != 7){
            warpsilent(MAP_SOUTHBAY_COMPOUND_1F, 7, 3)
                waitstate
        }else{
            applymovement(PLAYER,Common_Movement_FaceUp)
                waitmovement(0)
                msgbox(gText_VSGarbodor_ImGonnaGet,,SPEAKER_BIANCA)

                msgbox(gText_VSGarbodor_HehWellSee,,SPEAKER_ZOEY)

                closemessage

                applymovement(BIANCA, VSGarbodor_WalkUpstairs)
                waitmovement(0)
                playse(SE_EXIT)
                waitse
                removeobject(BIANCA)
                setvar(VAR_STORYLINE_STATE,STORY_NEED_SLEEP_BEFORE_SIEBOLD)

                release
                end
        }
}

movement VSGarbodor_WalkTowardsLeftSide {
    walk_up * 8
        walk_left * 2
        step_end
}

movement VSGarbodor_WalkFurtherLeft {
    walk_up * 15
        walk_left * 8
        step_end
}

movement VSGarbodor_WalkFurtherRight {
    walk_up * 8
        walk_right * 8
        step_end
}

movement VSGarbodor_WalkTowardsRightSide {
    walk_up * 8
        walk_right * 2
        step_end
}

movement VSGarbodor_LookAround {
    face_up
        delay_16
        face_left
        delay_16
        face_right
        delay_16
        step_end
}
movement BiancaApproachSW {
    walk_left * 6
        step_end
}
movement BiancaApproachNW {
    walk_left * 6
        step_end
}
movement BiancaApproachSE {
    walk_down * 6
        step_end
}
movement BiancaApproachNE {
    walk_right * 7
        step_end
}

movement VSGarbodor_PerpWalk {
    walk_left * 15
        step_end
}

movement VSGarbodor_WalkUpstairs {
    walk_up
        step_end
}

text gText_VSGarbodor_IDidSomeScouting {
    format("I did some scouting while waiting for you. Both sides have two Garbodor and two Trainers guarding ‘em. Do you care which side you take?")
}

text gText_VSGarbodor_ThenIllTakeTheRight {
    format("Then I'll take the right side. See you on the other side. Don't take too long!")
}

text gText_VSGarbodor_CoolIWanted {
    format("Cool, I wanted the left side anyway.  See you on the other side. Don't take too long!")
}

text gText_VSGarbodor_ScrewThisStadium {
    format("Heh, we're TeamRocket! Screw this stadium and screw Ycombinator!")
}

text gText_VSGarbodor_IsTheName {
    format("TeamRocket is the name, and shutting this shit down is our game!")
}

text gText_VSGarbodor_DamnTheyWereRight {
    format("Damn, they were right, you're strong…")
}

text gText_VSGarbodor_TheyDidntPrepare {
    format("They didn't prepare us for this!")
}

text gText_VSGarbodor_WelpIDidWhat {
    format("Welp, I did what I could! Good luck with that stinky shit.")
}

text gText_VSGarbodor_OhThankGiratina {
    format("Oh thank Giratina, I can go back to the office now…")
}

text gText_VSGarbodor_RightOnCue {
    format("Right on cue, here come the heroes…")
}

text gText_VSGarbodor_LookAtYou {
    format("Look at you, thinking you're making any difference!")
}

text gText_VSGarbodor_HmmIGuess {
    format("Hmm, I guess I get to be the bad guy today!")
}

text gText_VSGarbodor_ThatsRight {
    format("That's right, dance puppets!")
}

text gText_VSGarbodor_BeingBad {
    format("Being bad was fun for a day!")
}

text gText_VSGarbodor_HaveFunCleaning {
    format("Have fun cleaning up after us, kid.")
}

text gText_VSGarbodor_FeelingLonely {
    format("Feeling lonely over there? I got this side, you handle that side!")
}

text gText_VSGarbodor_GodYouWereTaking {
    format("God, you were taking forever. They don't teach efficiency in Orre, do they?")
}

text gText_VSGarbodor_WhateverMineWereEasy {
    format("Whatever. Mine were easy, they just both used Self-Destruct before I could really get a move in. I already texted Oak, he said meet in front of the construction site.")
}

text gText_VSGarbodor_AsWeWereNegotiating {
    format("As we were negotiating, things started to get a little rowdy. The police aren't taking any chances this time, so they've halted escalation… for now. I'm hoping to continue the conversations over email, which are a little more civilized. How's the YCombinator Task Force?")
}

text gText_VSGarbodor_DoneLikeYouGuys {
    format("Done. Like you guys said, TeamRocket up to their shit as usual.")
}

text gText_VSGarbodor_ThankYouBoth {
    format("Thank you both, and I'm sorry you had to get dragged into this. I'll work with our team to figure out what to do about the Championship. I think you two should head back to the Compound for now.")
}

text gText_VSGarbodor_YeahImBeat {
    format("Yeah, I'm beat. Do you want a ride back to the Compound?")
}

text gText_VSGarbodor_WeirdButOkay {
    format("Weird, but okay. I'll see you around.")
}

text gText_VSGarbodor_LetsBounce {
    format("Let's bounce.")
}

text gText_VSGarbodor_ImGonnaGet {
    format("I'm gonna get some sleep. Good shit on finishing your Badges. Can't wait to kick your ass at the Championship… whenever that is.")
}

text gText_VSGarbodor_HehWellSee {
    format("Heh, we'll see. Good night.")
}
