const PLAYER = OBJ_EVENT_ID_PLAYER
const ARCHER = 1
const ARIANA = 2
const DRASNA = 3
const PKMNC = 6

const FLAG_HIDE_SEARCH = FLAG_TEMP_1

script WaitEvenThen_TurnOnMachine_Script {
    setmetatile(32, 2, 0x2D8, TRUE)
        setmetatile(33, 2, 0x2D9, TRUE)
        setmetatile(34, 2, 0x2DA, TRUE)
        setmetatile(35, 2, 0x2DB, TRUE)
        setmetatile(32, 3, 0x2E0, TRUE)
        setmetatile(33, 3, 0x2E1, TRUE)
        setmetatile(34, 3, 0x2E2, TRUE)
        setmetatile(35, 3, 0x2E3, TRUE)
        special(DrawWholeMapView)
}

script WaitEvenThen_BossSetUp_Script {
        clearflag(FLAG_HIDE_SEARCH)

        addobject(ARIANA)
        addobject(DRASNA)
        addobject(ARCHER)

        setobjectmovementtype(DRASNA,MOVEMENT_TYPE_FACE_UP)
        setobjectxyperm(DRASNA,39,4)
        setobjectxy(DRASNA,39,4)

        setobjectmovementtype(ARCHER,MOVEMENT_TYPE_FACE_UP)
        setobjectxy(ARCHER,34,6)
        setobjectxyperm(ARCHER,34,6)

        setobjectmovementtype(ARIANA,MOVEMENT_TYPE_FACE_UP)
        setobjectxy(ARIANA,33,7)
        setobjectxyperm(ARIANA,33,7)

        setobjectxy(PKMNC,33,5)
        setobjectxyperm(PKMNC,33,5)
}

script WaitEvenThen_ObjectSetUp_Script {
    fadescreen(FADE_TO_BLACK)

        setobjectxy(PLAYER,25,4)
        TeleportCamera(25,4)
        applymovement(PLAYER,Common_Movement_FaceRight)

        clearflag(FLAG_HIDE_SEARCH)

        addobject(ARCHER)
        setobjectxy(ARCHER,25,5)
        applymovement(ARCHER,Common_Movement_FaceRight)

        addobject(ARIANA)
        setobjectxy(ARIANA,24,4)
        applymovement(ARIANA,Common_Movement_FaceRight)

        addobject(DRASNA)
        setobjectxy(DRASNA,24,5)
        applymovement(DRASNA,Common_Movement_FaceRight)

        fadescreen(FADE_FROM_BLACK)
}

script WaitEvenThen_PrepareBossEncounter_Dialogue {
    lock
        call(WaitEvenThen_ObjectSetUp_Script)

        applymovement(PLAYER,WaitEvenThen_WalkToSleepingA)
        applymovement(ARCHER,WaitEvenThen_WalkToSleepingB)
        applymovement(ARIANA,WaitEvenThen_WalkToSleepingC)
        applymovement(DRASNA,WaitEvenThen_WalkToSleepingD)
        waitmovement(DRASNA)

        setvar(VAR_MUGSHOT, MUGSHOT_GRIMSLEY)
        callnative(DrawMugshot)
        msgbox(gText_WaitEvenThen_IFoundTheAccess)

        setvar(VAR_MUGSHOT, MUGSHOT_HUGH)
        callnative(DrawMugshot)
        msgbox(gText_WaitEvenThen_AndICanHandle)
        callnative(ClearMugshot)
        closemessage

        applymovement(DRASNA,WaitEvenThen_WalkToComputer)
        waitmovement(DRASNA)

        playse(SE_PC_LOGIN)
        waitse
        delay(10)
        playse(SE_PC_ON)
        waitse

        call(WaitEvenThen_TurnOnMachine_Script)
        applymovement(PKMNC,Common_Movement_WalkDown)

        playmoncry(SPECIES_EXCADRILL,CRY_MODE_ENCOUNTER)
        waitmoncry
        setvar(VAR_UNDERGROUNDLAB_STATE,SAVE_BEFORE_DYNAMAX)

        msgbox(gText_IGuessWeShouldBeNiceNow_Roar,MSGBOX_AUTOCLOSE)

        setobjectxy(ARCHER,34,6)
        setobjectxyperm(ARCHER,34,6)
        setobjectxy(ARIANA,33,7)
        setobjectxyperm(ARIANA,33,7)
        setobjectxyperm(DRASNA,39,4)
        setobjectxy(DRASNA,39,4)
        setobjectnewmovementtype(DRASNA,MOVEMENT_TYPE_FACE_UP)
        call(Common_EventScript_SaveGame)
        goto(WaitEvenThen_BossEncounter_Script)
}

script WaitEvenThen_MovePlayerToBoss {
        getplayerxy(VAR_TEMP_0,VAR_TEMP_1)

    if (var(VAR_TEMP_0) != 33 || (var(VAR_TEMP_1) != 6)){
        fadescreen(FADE_TO_BLACK)
        setobjectxy(PLAYER,33,6)
        TeleportCamera(33,6)
        applymovement(PLAYER,Common_Movement_FaceUp)
        fadescreen(FADE_FROM_BLACK)
    }
        goto(WaitEvenThen_BossEncounter_Script)
}
        
script WaitEvenThen_BossEncounter_Script {
    //PSF TODO Rewrite to have two pokemon OR make it a 1v1 battle instead
    setwildbattle(SPECIES_EXCADRILL,70,ITEM_MUSCLE_BAND,SPECIES_NONE)
        playmoncry(SPECIES_EXCADRILL,CRY_MODE_ENCOUNTER)
        msgbox(gText_IGuessWeShouldBeNiceNow_Roar,MSGBOX_AUTOCLOSE)
        delay(40)
        waitmoncry
        setflag(FLAG_SYS_CTRL_OBJ_DELETE)
        multi_fixed_wild(TRAINER_WALLY_VR_2,TRAINER_BACK_PIC_WALLY)
        clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
        specialvar(VAR_RESULT, GetBattleOutcome)
        switch (var (VAR_RESULT)) {
            case B_OUTCOME_CAUGHT:
            case B_OUTCOME_WON:
                goto(WaitEvenThen_ClearBossPokemon_Script)
        }
    release
        end
}

script WaitEvenThen_ClearBossPokemon_Script {
        setvar(VAR_UNDERGROUNDLAB_STATE,DEFEATED_DYNAMAX)

        lock
        closemessage
        fadescreenswapbuffers(FADE_TO_BLACK)
        removeobject(PKMNC)
        fadescreenswapbuffers(FADE_FROM_BLACK)
        goto(WaitEvenThen_PostBossDialogue)
}

script WaitEvenThen_PostBossDialogue {
    lock

        setvar(VAR_STORYLINE_STATE,STORY_POST_DYNAMAX_BOSS)
        setvar(VAR_ALAMEDA_STATE,POST_THIS_ISNT_RANDOM)

        applymovement(PLAYER,Common_Movement_FaceDown)
        applymovement(ARCHER,Common_Movement_FaceDown)
        applymovement(DRASNA,Common_Movement_FaceDown)

        setvar(VAR_MUGSHOT, MUGSHOT_HUGH)
        callnative(DrawMugshot)
        msgbox(gText_WaitEvenThen_DamnAPowerSpot)

        setvar(VAR_MUGSHOT, MUGSHOT_GRIMSLEY)
        callnative(DrawMugshot)
        msgbox(gText_WaitEvenThen_AndTheGovernment)

        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_WaitEvenThen_IllHaveSomeOther)
        callnative(ClearMugshot)
        closemessage

        applymovement(DRASNA,WaitEvenThen_LeaveLabA)
        applymovement(ARIANA,WaitEvenThen_LeaveLabB)
        waitmovement(ARIANA)

        applymovement(ARCHER,Common_Movement_FaceLeft)
        applymovement(PLAYER,Common_Movement_FaceRight)

        setvar(VAR_MUGSHOT, MUGSHOT_HUGH)
        callnative(DrawMugshot)
        msgbox(gText_WaitEvenThen_ComingWithMe,MSGBOX_YESNO)
        callnative(ClearMugshot)
        closemessage

        if (var(VAR_RESULT) == YES){
            warp(MAP_ALAMEDA,255,34,13)
                end
        }else {
            setvar(VAR_MUGSHOT, MUGSHOT_HUGH)
                callnative(DrawMugshot)
                msgbox(gText_WaitEvenThen_MeetUsIn)
                closemessage
                callnative(ClearMugshot)
                applymovement(ARCHER,WaitEvenThen_WalkAwayFromPlayer)
                waitmovement(ARCHER)

                removeobject(ARCHER)
                removeobject(DRASNA)
                removeobject(ARIANA)
        }
    release
        end
}

text gText_WaitEvenThen_IFoundTheAccess {
    format("I found the access commands. I think I can turn this off, but I’m certain that it's going to attack us when we do.")
}

text gText_WaitEvenThen_AndICanHandle {
    format("{PLAYER} and I can handle it. Let it rip!")
}

text gText_WaitEvenThen_Yerrrraaaa {
    format("Yerrrraaaaaa!")
}

text gText_WaitEvenThen_DamnAPowerSpot {
    format("Damn, a Power Spot, all the way out here?")
}

text gText_WaitEvenThen_AndTheGovernment {
    format("Between Ycombinator and the government, I guess nothing is off limits.")
}

text gText_WaitEvenThen_IllHaveSomeOther {
    format("I’ll have some other TeamRocket members clear out the rest of the trapped Pokémon… Let’s go back to Alameda.")
}

text gText_WaitEvenThen_ComingWithMe {
    format("Coming with me, {PLAYER}?")
}

text gText_WaitEvenThen_MeetUsIn {
    format("Meet us in the center of town, okay?")
}

movement WaitEvenThen_WalkToSleepingA {
    walk_right * 6
        walk_down * 2
        walk_right * 2
        face_up
        step_end
}

movement WaitEvenThen_WalkToSleepingB {
    walk_right * 6
        walk_down
        walk_right * 3
        face_up
        step_end
}

movement WaitEvenThen_WalkToSleepingC {
    walk_right * 7
        walk_down * 3
        walk_right * 2
        face_up
        step_end
}

movement WaitEvenThen_WalkToSleepingD {
    walk_right * 7
        walk_down * 2
        walk_right * 3
        face_up
        step_end
}

movement WaitEvenThen_WalkToComputer {
    walk_right
        walk_up
        walk_right * 2
        walk_up * 2
        walk_right * 2
        face_up
        step_end
}

movement WaitEvenThen_LeaveLabA {
    walk_down * 2
        walk_left * 14
        step_end
}

movement WaitEvenThen_LeaveLabB {
    walk_left * 6
        walk_up * 3
        walk_left * 4
        step_end
}

movement WaitEvenThen_WalkAwayFromPlayer {
    walk_up
        walk_left * 9
        step_end
}
