const PLAYER = OBJ_EVENT_ID_PLAYER
const ARCHER = 1
const FLAG_HIDE_ARCHER = FLAG_TEMP_2

script LetsGrabLunch_HideArcher_Outside_Script{
    if (var(VAR_STORYLINE_STATE) != STORY_SAVE_ARCHER_TREASUREISLAND){
        setflag(FLAG_HIDE_ARCHER)
    }
    return
}
script LetsGrabLunch_HideArcher_Inside_Script{
    if (var(VAR_STORYLINE_STATE) != STORY_POST_BATTLE_ARCHER_TREASUREISLAND && var(VAR_STORYLINE_STATE) != STORY_ARCHER_EXPLAIN_RESTORATION){
        setflag(FLAG_HIDE_ARCHER)
    }
    return
}

script LetsGrabLunch_StartConversation_Dialogue {
    lockall
        setvar(VAR_STORYLINE_STATE,STORY_SAVE_ARCHER_TREASUREISLAND)
        clearflag(FLAG_TEMP_1)
        addobject(ARCHER)
        playbgm(MUS_ENCOUNTER_CHAMPION,TRUE)
        applymovement(PLAYER,Common_Movement_FaceRight)
        applymovement(ARCHER,LetsGrabLunch_WalkUpToPlayer)
        waitmovement(0)

        msgbox(gText_LetsGrabLunch_HuhWhatAreYouDoing,,SPEAKER_ARCHER)

        msgbox(gText_LetsGrabLunch_TakingABreak,,SPEAKER_ZOEY)

        msgbox(gText_LetsGrabLunch_YeahBeingOfficial,,SPEAKER_ARCHER)

        msgbox(gText_LetsGrabLunch_ThisAgain,,SPEAKER_ZOEY)

        msgbox(gText_LetsGrabLunch_HeyYouBeatMe,,SPEAKER_ARCHER)

        call(Common_EventScript_SaveGame)
        closemessage
        goto(LetsGrabLunch_StartBattle_Script)
}

script LetsGrabLunch_InteractBattle_Script {
    lockall
        faceplayer
        goto(LetsGrabLunch_StartBattle_Script)
}

script LetsGrabLunch_StartBattle_Script {
    lockall
        msgbox(gText_LetsGrabLunch_ButLetsSee,,SPEAKER_ARCHER)
        closemessage


        trainerbattle_no_intro(TRAINER_WALLY_VR_4,gText_LetsGrabLunch_NahNotRobbed)
        goto(LetsGrabLunch_PostBattle_Dialogue)
}

script LetsGrabLunch_PostBattle_Dialogue {
    setvar(VAR_STORYLINE_STATE,STORY_POST_BATTLE_ARCHER_TREASUREISLAND)

        msgbox(gText_LetsGrabLunch_GoodGame,,SPEAKER_ZOEY)

        msgbox(gText_LetsGrabLunch_ImGoingToBe,,SPEAKER_ARCHER)

        msgbox(gText_LetsGrabLunch_HaventYouHad,,SPEAKER_ZOEY)

        msgbox(gText_LetsGrabLunch_NahThisIsCool,,SPEAKER_ARCHER)

        closemessage
        fadedefaultbgm
        warpsilent(MAP_TREASUREISLAND_RESTAURANT,14,9)
        releaseall
        end
}

script LetsGrabLunch_DinerBooth_Dialogue {
    lockall
        setvar(VAR_STORYLINE_STATE,STORY_ARCHER_EXPLAIN_RESTORATION)
        applymovement(PLAYER,Common_Movement_FaceUp)
        msgbox(gText_LetsGrabLunch_OkayLetsHear,,SPEAKER_ZOEY)

        msgbox(gText_LetsGrabLunch_IWantToCreate,,SPEAKER_ARCHER)

        msgbox(gText_LetsGrabLunch_HuhThatsActually,,SPEAKER_ZOEY)

        msgbox(gText_LetsGrabLunch_YeahITried,,SPEAKER_ARCHER)

        msgbox(gText_LetsGrabLunch_Who,,SPEAKER_ZOEY)

        msgbox(gText_LetsGrabLunch_OhTheOldLeaders,,SPEAKER_ARCHER)

        msgbox(gText_LetsGrabLunch_YouKnowTheyre,,SPEAKER_ZOEY)

        msgbox(gText_LetsGrabLunch_IKnowItsHard,,SPEAKER_ARCHER)

        msgbox(gText_LetsGrabLunch_HmmNeverThought,,SPEAKER_ZOEY,TAIL_THOUGHT)

        msgbox(gText_LetsGrabLunch_YeahThisSounds,,SPEAKER_ZOEY)

        msgbox(gText_LetsGrabLunch_ItWouldBeSuch,,SPEAKER_ARCHER)

        msgbox(gText_LetsGrabLunch_HahaHowCanIHelp,,SPEAKER_ZOEY)

        msgbox(gText_LetsGrabLunch_CanYouTryTalking,,SPEAKER_ARCHER)

        msgbox(gText_LetsGrabLunch_WhatAreYouGoing,,SPEAKER_ZOEY)

        msgbox(gText_LetsGrabLunch_ImGoingToBring,,SPEAKER_ARCHER)

        msgbox(gText_LetsGrabLunch_Yeah,,SPEAKER_ZOEY)

        closemessage
        end
}

script LetsGrabLunch_InteractArcherDiner_Dialogue {
    msgbox(gText_LetsGrabLunch_ImJustPlanning,,SPEAKER_ARCHER)
    goto(LetsGrabLunch_ShowGymList_Dialogue)
}

script LetsGrabLunch_ShowGymList_Dialogue{
    msgbox(gText_LetsGrabLunch_HaveYouDecided,,SPEAKER_ARCHER,,EMOTE_CONFUSE)

    dynmultichoice(1,1,FALSE,3,0,DYN_MULTICHOICE_CB_NONE,
        "Chinatown",
        "Marin",
        "Treasureisland",
        "What about Haightashbury?",
        "Nowhere yet."
    )

        switch (var(VAR_RESULT)){
            case 0: goto(LetsGrabLunch_Chinatown)
            case 1: goto(LetsGrabLunch_Marin)
            case 2: goto(LetsGrabLunch_Treasureisland)
            case 3: goto(LetsGrabLunch_Haightashbury)
            default: goto(LetsGrabLunch_CloseMenu)
        }
}


script LetsGrabLunch_CloseMenu{

    closemessage
        releaseall
        end
}

script LetsGrabLunch_Chinatown {
    msgbox(gText_LetsGrabLunch_TheLeaderOfThat,,SPEAKER_ARCHER)

        msgbox(gText_LetsGrabLunch_DoYouWantChinatown,MSGBOX_YESNO)

        if (var(VAR_RESULT) == NO){

            closemessage
                releaseall
                end
        }else {
            warpsilent(MAP_CHINATOWN,16,7)
        }
}
script LetsGrabLunch_Marin {
    msgbox(gText_LetsGrabLunch_WasALittle,,SPEAKER_ARCHER)

        msgbox(gText_LetsGrabLunch_DoYouWantMarin,MSGBOX_YESNO)
        if (var(VAR_RESULT) == NO){

            closemessage
                releaseall
                end
        }else {
            warpsilent(MAP_MARIN,8,18)
        }
}
script LetsGrabLunch_Treasureisland {
    msgbox(gText_LetsGrabLunch_AlmostWalkedOut,,SPEAKER_ARCHER)

        msgbox(gText_LetsGrabLunch_VeryRealistic,,SPEAKER_ZOEY)

        msgbox(gText_LetsGrabLunch_TheyreIntense,,SPEAKER_ARCHER)

        msgbox(gText_LetsGrabLunch_DoYouWantTreasureisland,MSGBOX_YESNO)
        if (var(VAR_RESULT) == NO){

            closemessage
                releaseall
                end
        }else {
            warpsilent(MAP_TREASUREISLAND,3,15)
        }
}

script LetsGrabLunch_Haightashbury{
    msgbox(gText_LetsGrabLunch_YouKnowId,,SPEAKER_ARCHER,,EMOTE_ANGRY)
    closemessage
    goto(LetsGrabLunch_ShowGymList_Dialogue)
}

script LetsGrabLunch_CheckRestorationProgress_Script{
    setvar(VAR_TEMP_0,0)

        call_if_quest_complete(QUEST_RESTORECHINATOWN,LetsGrabLunch_IncrementCount_Script)
        call_if_quest_complete(QUEST_RESTOREMARIN,LetsGrabLunch_IncrementCount_Script)
        call_if_quest_complete(QUEST_RESTOREHAIGHTASHBURY,LetsGrabLunch_IncrementCount_Script)
        call_if_quest_complete(QUEST_RESTORETREASUREISLAND,LetsGrabLunch_IncrementCount_Script)

        if (var(VAR_TEMP_0) > 0 && var(VAR_STORYLINE_STATE) == STORY_ARCHER_EXPLAIN_RESTORATION){
            setvar(VAR_STORYLINE_STATE,STORY_RESTORATION_1_COMPLETE)
        }
}

script LetsGrabLunch_IncrementCount_Script{
    addvar(VAR_TEMP_0,1)
        return
}

script LetsGrabLunch_Debug_Script{
    if (var(VAR_STORYLINE_STATE) > STORY_POST_BATTLE_ARCHER_TREASUREISLAND){
        special(GetCurrentMap)

            switch(var(VAR_RESULT)){
                case MAP_TREASUREISLAND: completequest(QUEST_RESTORETREASUREISLAND)
                case MAP_CHINATOWN: completequest(QUEST_RESTORECHINATOWN)
                case MAP_MARIN: completequest(QUEST_RESTOREMARIN)
                case MAP_HAIGHTASHBURY: completequest(QUEST_RESTOREHAIGHTASHBURY)
            }
    }
    else {
        msgbox(gText_LetsGrabLunch_FastForwardBlock)
    }
    return
}

script LetsGrabLunch_FastForwardFirstRestoration_Quest {
    lock
        call(LetsGrabLunch_Debug_Script)
        call(LetsGrabLunch_CheckRestorationProgress_Script)
        releaseall
        end
}
text gText_LetsGrabLunch_HuhWhatAreYouDoing {
    format("Huh. What are you doing here? Shouldn't you be busy?")
}

text gText_LetsGrabLunch_TakingABreak {
    format("Taking a break. I haven't had a chance to relax since I last saw you.")
}

text gText_LetsGrabLunch_YeahBeingOfficial {
    format("Yeah, being Ycombinator's official lapdog must be exhausting! Bummer.")
}

text gText_LetsGrabLunch_ThisAgain {
    format("This again? And besides, I know you've been busy too. Psfregion loves their “hometown hero robbed of championship” narrative.")
}

text gText_LetsGrabLunch_HeyYouBeatMe {
    format("Hey, you beat me fair and square… but the extra popularity can't hurt.")
}

text gText_LetsGrabLunch_ButLetsSee {
    format("But let's see how robbed I really was!")
}

text gText_LetsGrabLunch_NahNotRobbed {
    format("Nah, not robbed at all. You're legit as fuck.")
}

text gText_LetsGrabLunch_GoodGame {
    format("Good game, but…I thought SanFran natives weren't cool with Mega Evolution?")
}

text gText_LetsGrabLunch_ImGoingToBe {
    format("I'm going to be the best and change the world, I can't be hung up on what's “pure” and “right”. My team and I are going to use whatever tools we can to take down your bosses. As a matter of fact… I'm working on a project.")
}

text gText_LetsGrabLunch_HaventYouHad {
    format("Haven't you had enough run-ins with the police already?")
}

text gText_LetsGrabLunch_NahThisIsCool {
    format("Nah, this is cool, I swear. Actually, you said you're on break? Cool, we're grabbing lunch. My treat.")
}

text gText_LetsGrabLunch_OkayLetsHear {
    format("Okay, let's hear about your pyramid scheme.")
}

text gText_LetsGrabLunch_IWantToCreate {
    format("I want to create training centers all over SanFran, to help more Trainers get a real start, kind of like what Ycombinator gave you. I wanna use the legacy Gyms, like the one you saw earlier today, as those hubs.")
}

text gText_LetsGrabLunch_HuhThatsActually {
    format("Huh. That's actually a pretty cool idea.")
}

text gText_LetsGrabLunch_YeahITried {
    format("Yeah! I tried to talk to Lenora and Cilan, but they didn't take it nearly as well as you did.")
}

text gText_LetsGrabLunch_Who {
    format("Who…?")
}

text gText_LetsGrabLunch_OhTheOldLeaders {
    format("Oh, the old Leaders of the Treasureisland and Marin Gyms. If we can revive those Gyms and strengthen our Trainers, then Trainers from other regions will want to come here, battle us, and help stimulate the region… without any interference from Ycombinator.")
}

text gText_LetsGrabLunch_YouKnowTheyre {
    format("You know, they're not as bad as you think. I still don't understand your problem with them.")
}

text gText_LetsGrabLunch_IKnowItsHard {
    format("I know. It's hard to sit down and explain… but maybe it doesn't matter. Is that what TeamRocket got wrong? Rather than try to defeat a negative force, maybe we need to focus on being a stronger positive force.")
}

text gText_LetsGrabLunch_HmmNeverThought {
    format("Hmm. Never thought about it like that.")
}

text gText_LetsGrabLunch_YeahThisSounds {
    format("Yeah, this sounds really cool! Way better than I expected.")
}

text gText_LetsGrabLunch_ItWouldBeSuch {
    format("It would be such a boon if the Champion was helping too…")
}

text gText_LetsGrabLunch_HahaHowCanIHelp {
    format("Haha, how can I help?")
}

text gText_LetsGrabLunch_CanYouTryTalking {
    format("Hmm. Can you try talking to Lenora or Cilan? Or maybe visit Chinatown's Gym and see what we can do? Yeah, any of those are good starts, since Pryce is a lost cause...")
}

text gText_LetsGrabLunch_WhatAreYouGoing {
    format("What are you going to do?")
}

text gText_LetsGrabLunch_ImGoingToBring {
    format("I'm going to bring the idea to TeamRocket too. I think it's something everybody can rally behind, right?")
}

text gText_LetsGrabLunch_Yeah {
    format("Yeah!")
}

text gText_LetsGrabLunch_ImJustPlanning {
    format("I'm just planning out my pitch to TeamRocket.")
}

text gText_LetsGrabLunch_HaveYouDecided{
    format("What are you up to? Have you decided which place you'll hit up first?")
}

text gText_LetsGrabLunch_YouKnowId{
    format("You know, I'd love to reform that Gym and take it away from Pryce... but they're a snake. There's no reasonining with them, they've fully turned.")
}

text gText_LetsGrabLunch_TheLeaderOfThat {
    format("The Leader of that Gym more or less disappeared after Ycombinator shut it down. I know some of his Gym Trainers are still around… but I'm not sure where. ")
}

text gText_LetsGrabLunch_DoYouWantChinatown {
    format("Do you want to go to Chinatown?")
}

text gText_LetsGrabLunch_WasALittle {
    format("Lenora was a little receptive to my idea. They said it was a “good first step”, but wouldn't sign on unless I had a plan for dismantling the entire Ycombinator League, which in their eyes, is a “total failure for our Trainers”.")
}

text gText_LetsGrabLunch_DoYouWantMarin {
    format("Do you want to go to Marin?")
}

text gText_LetsGrabLunch_AlmostWalkedOut {
    format("Cilan almost walked out when I first proposed the idea. I think their exact quote was “I'd rather be dead than a “training stop” for more transplants”. They want TeamRocket to totally dismantle the new League, and restore their Gym to its former glory.")
}

text gText_LetsGrabLunch_VeryRealistic {
    format("Very realistic, I see.")
}

text gText_LetsGrabLunch_TheyreIntense {
    format("They're… intense.")
}

text gText_LetsGrabLunch_DoYouWantTreasureisland {
    format("Do you want to go to Treasureisland?")
}

text gText_LetsGrabLunch_FastForwardAdvance {
    format("You have restored the {STR_VAR_1} Gym. Good job kid. In the main game, that nickname will be replaced with the name of wherever you are.") //PSF TODO
    // Lunos: To store the current map's name in STR_VAR_1 please use the special(Script_GetMapName) in the script of choice.
}

text gText_LetsGrabLunch_FastForwardBlock {
    format("Come back when its time to do the restoration quests. Or if you already did them, go away!")
}


movement LetsGrabLunch_WalkUpToPlayer {
    walk_left * 7
        step_end
}
