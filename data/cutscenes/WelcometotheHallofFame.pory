const PLAYER = OBJ_EVENT_ID_PLAYER
const ARCHER = 1
const ROSE = 2
const FLAG_HIDE_ROSE = FLAG_TEMP_1

script WelcomeToTheHallOfFame_PostBattle_Dialogue {
    msgbox(gText_WelcomeToTheHallOfFame_YourChampion)
        closemessage
        special(DoDomeConfetti)
        playse(SE_M_ENCORE2)
        call(BattleFrontier_BattleDomeBattleRoom_EventScript_AudienceLookAround)
        delay(60)

        clearflag(FLAG_HIDE_ROSE)
        addobject(ROSE)
        applymovement(ROSE,WelcomeToTheHallOfFame_MoveRoseToPodium)
        applymovement(ARCHER,WelcomeToTheHallOfFame_WalkOutOfWay)
        waitmovement(ROSE)

        msgbox(gText_WelcomeToTheHallOfFame_ToTheHundreds,,SPEAKER_ROSE)
        
        closemessage

        playse(SE_M_ENCORE2)
        call(BattleFrontier_BattleDomeBattleRoom_EventScript_AudienceLookAround)
        fadescreen(FADE_TO_BLACK)
        setobjectxy(PLAYER,9,6)
        setobjectxy(ARCHER,8,7)
        setobjectxy(ROSE,7,7)
        turnobject(ARCHER,DIR_SOUTH)
        turnobject(ROSE,DIR_SOUTH)
        turnobject(PLAYER,DIR_SOUTH)
        TeleportCamera(9,6)
        fadescreen(FADE_FROM_BLACK)

        msgbox(gText_WelcomeToTheHallOfFame_AndNowLetsCreate,,SPEAKER_ROSE)
        
        closemessage

        setvar(VAR_CHASECENTER_TOURNAMENT_STATE,NO_TOURNAMENT)
        clearflag(FLAG_CHASECENTER_TOURNAMENT_HAPPENING)
        setvar(VAR_STORYLINE_STATE,STORY_WON_FINALS)
        setflag(FLAG_IS_CHAMPION)
        fadescreenspeed(FADE_TO_BLACK,24)

        special(HealPlayerParty)
        call(WelcometotheHallofFame_GameClear_Script)
        waitstate
        releaseall
        end
}

script WelcometotheHallofFame_GameClear_Script{
    special(GameClear)
        return
}

text gText_WelcomeToTheHallOfFame_YourChampion {
    format("SanFran! Your Champion! {PLAYER}!")
}
text gText_WelcomeToTheHallOfFame_ToTheHundreds {
    format("To the hundreds of you, here, in the stands. To the millions of you, watching from all over the world.  To all the fans. We say “Thank you, from the bottom of our hearts”. This first season was absolutely a success, and we can't wait to do it again. ")
}
text gText_WelcomeToTheHallOfFame_AndNowLetsCreate {
    format("And now, let's create history together. I am proud to present the first inductee to the Ycombinator Hall of Fame. Psfregion's Champion: {PLAYER}.")
}

movement WelcomeToTheHallOfFame_MoveRoseToPodium {
    walk_left * 4
        walk_up * 3
        walk_left * 6
        step_end
}

movement WelcomeToTheHallOfFame_WalkOutOfWay {
    walk_right
        walk_up
        face_down
        step_end
}
