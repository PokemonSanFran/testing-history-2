const ROSE = 1
const ARCHER = 10
const CAMERA = OBJ_EVENT_ID_CAMERA
const FLAG_HIDE_ROSE = FLAG_TEMP_1
const FLAG_HIDE_ARCHER = FLAG_TEMP_6

script WowYoureStrong_HideObject_Script {
    if(!flag(FLAG_BADGE03_GET) || (var(VAR_SOMA_STATE) > DEFEATED_WHITNEY)) {
        setflag(FLAG_HIDE_ROSE)
        setflag(FLAG_HIDE_ARCHER)
    }
    if (var(VAR_PSFROUTE9_STATE) == SAVED_BEFORE_ARCHER_PSFROUTE9){
        setflag(FLAG_HIDE_ARCHER)
    }
}

script WowYoureStrong_WalkToPlayer_Script {
    lockall
    setvar(VAR_SOMA_STATE,POST_WOW_YOURE_STRONG)
    playse(SE_PIN)
    playbgm(MUS_RG_ENCOUNTER_DEOXYS,TRUE) 
    applymovement(ROSE,Common_Movement_ExclamationMark)
    waitmovement(ROSE)
    applymovement(ROSE,WowYoureStrong_WalkToPlayer_Movement)
    waitmovement(ROSE)
    setobjectxyperm(ROSE,28,25)
    goto(WowYoureStrong_Intro_Dialogue)
}

script WowYoureStrong_Intro_Dialogue {
    setvar(VAR_0x8000, MUGSHOT_GHETSIS)
    callnative(DrawMugshot)
    msgbox(gText_WowYoureStrong_WhatAShow)
    callnative(ClearMugshot)
    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_WowYoureStrong_OhUhThanksALot)
    callnative(ClearMugshot)
    setvar(VAR_0x8000, MUGSHOT_GHETSIS)
    callnative(DrawMugshot)
    msgbox(gText_WowYoureStrong_WhereMyManners)
    callnative(ClearMugshot)
    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_WowYoureStrong_ShouldHaveKnown)
    callnative(ClearMugshot)
    setvar(VAR_0x8000, MUGSHOT_GHETSIS)
    callnative(DrawMugshot)
    msgbox(gText_WowYoureStrong_DreamBig)
    callnative(ClearMugshot)
    goto(WowYoureStrong_ShowTower_Script) 
}

script WowYoureStrong_ShowTower_Script {
    fadescreen(FADE_TO_BLACK)
    TeleportCamera(7,14)
    special(SpawnCameraObject)
    fadescreen(FADE_FROM_BLACK)

    applymovement(CAMERA,WowYoureStrong_PanCameraUp_Movement)
    callnative(DrawMugshot)
    msgbox(gText_WowYoureStrong_YouShouldComeBy)
    waitmovement(CAMERA)
    closemessage
    callnative(ClearMugshot)

    fadescreen(FADE_TO_BLACK)
    TeleportCamera(28,24)
    special(RemoveCameraObject)
    fadescreen(FADE_FROM_BLACK)

    goto (WowYoureStrong_End_Dialouge)
}

script WowYoureStrong_End_Dialouge {
    callnative(DrawMugshot)
    msgbox(gText_WowYoureStrong_MeetingWithMayor)
    callnative(ClearMugshot)
    closemessage
    applymovement(ROSE,WowYoureStrong_WalkToMayor_Movement)
    waitmovement(ROSE)
    removeobject(ROSE)
    playbgm(MUS_RG_VERMILLION,TRUE)
    
    if (var(VAR_PSFROUTE9_STATE) == SAVED_BEFORE_ARCHER_PSFROUTE9){
        release
        end
    }
    goto(WowYourStrong_ArcherConfrontation_Dialogue)

}

script WowYourStrong_ArcherConfrontation_Dialogue{

    applymovement(ARCHER,WowYoureStrong_ArcherToPlayer_Movement)
    waitmovement(ARCHER)

    setvar(VAR_MUGSHOT,MUGSHOT_HUGH)
    callnative(DrawMugshot)
    msgbox(gText_WowYoureStrong_IDontGetIt)
    
    setvar(VAR_MUGSHOT,MUGSHOT_HILDA)
    callnative(DrawMugshot)
    if (var(VAR_PSFROUTE9_STATE) == DEFEATED_ARCHER_PSFROUTE9){
        msgbox(gText_WowYoureStrong_YouAgain)
    }else{
        msgbox(gText_WowYoureStrong_WhatAreYouTalking)
    }

    setvar(VAR_MUGSHOT,MUGSHOT_HUGH)
    callnative(DrawMugshot)
    msgbox(gText_WowYoureStrong_IJustCant)
    closemessage
    callnative(ClearMugshot)

    applymovement(ARCHER,WowYoureStrong_WalkToMayor_Movement)
    waitmovement(ARCHER)
    removeobject(ARCHER)

    setvar(VAR_MUGSHOT,MUGSHOT_HILDA)
    callnative(DrawMugshot)
    if (var(VAR_PSFROUTE9_STATE) == DEFEATED_ARCHER_PSFROUTE9){
        msgbox(gText_WowYoureStrong_IHopeThats)
    }else{
        msgbox(gText_WowYoureStrong_ThisRegionHasSome)
    }
    closemessage
    callnative(ClearMugshot)

    release
    end
}

movement WowYoureStrong_WalkToPlayer_Movement {
    walk_up
    walk_right * 5
    face_up
    step_end
}

movement WowYoureStrong_PanCameraUp_Movement{
    walk_up * 9
        step_end
}

movement WowYoureStrong_WalkToMayor_Movement {
    walk_left * 8
    step_end
}

movement WowYoureStrong_ArcherToPlayer_Movement{
    walk_up * 2
    walk_right * 2
    face_up
    step_end
}

text gText_WowYoureStrong_WhatAShow {
    format("{PLAYER}! What a show! That was marvelous! Flying you in from Orre was absolutely worth it, for that battle alone.")
}

text gText_WowYoureStrong_OhUhThanksALot {
    format("Oh uh, thanks a lot, that's very nice. I'm guessing you work at YCombinatorâ€¦?")
}

text gText_WowYoureStrong_WhereMyManners {
    format("Hah! Oh my, I'm sorry. Where are my manners? My name is Rose, and I'm the YCombinator League Commissioner, appointed by our corporate sponsors, Twitter, Amazon, and Uber.")
}

text gText_WowYoureStrong_ShouldHaveKnown {
    format("Oh! I guess I should have known that. Well um, thanks so much for the opportunity! I'm excited to be the region's next Champion.")
}

text gText_WowYoureStrong_DreamBig {
    format("Dream big {PLAYER}! That's the attitude we wanna see. We're big dreamers over at YCombinator too. That's why we built the Salesforce Tower!")
}

text gText_WowYoureStrong_YouShouldComeBy {
    format("You should come by once you're done exploring the city proper, my office is on the 10th floor.")
}

text gText_WowYoureStrong_MeetingWithMayor {
    format("I have to run to a meeting with the Mayor. Again, excellent job against Whitney. I hope to see what else you can do!")
}

text gText_WowYoureStrong_IDontGetIt{
	format("I don't get it. Do all of you transplants just worship the ground these tycoons walk on?")
}
text gText_WowYoureStrong_YouAgain{
	format("You again? Are you following me? What is your deal?")
}
text gText_WowYoureStrong_WhatAreYouTalking{
	format("What are you talking about?")
}
text gText_WowYoureStrong_IJustCant{
	format("I just can't understood why you would willingly buy into a system that just makes these people more money. Do you think they need MORE money?")
}
text gText_WowYoureStrong_IHopeThats{
	format("I hope that this isn't going to be a regular thing...")
}
text gText_WowYoureStrong_ThisRegionHasSome{
	format("The region has some... characters.")
}
