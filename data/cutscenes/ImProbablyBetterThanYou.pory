const PLAYER = OBJ_EVENT_ID_PLAYER
const BIANCA = 1 
const OAK = 2 
const FLAG_HIDE_BIANCA = FLAG_TEMP_1
const FLAG_HIDE_OAK = FLAG_TEMP_2
const VAR_TALKED_BIANCA = VAR_TEMP_0

script ImProbablyBetterThanYou_ObjectSetUp{
    setflag(FLAG_HIDE_OAK)
    return
}

script ImProbablyBetterThanYou_MovePlayerInside_Script {
    warpdoor(MAP_SOUTHBAY_COMPOUND_1F,255,5,9)
        waitstate
        end
}

script ImProbablyBetterThanYou_StandingOutside_Dialogue {
    lockall
        setvar(VAR_PLAYER_HOME_STATE,HAS_NOT_MET_BIANCA)
        setvar(VAR_MUGSHOT, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_ImProbablyBetterThanYou_DeepBreath)
        callnative(ClearMugshot)
        closemessage
        call(ImProbablyBetterThanYou_MovePlayerInside_Script)
        applymovement(PLAYER,Common_Movement_WalkUp)
        waitmovement(0)
}

script ImProbablyBetterThanYou_BiancaIntro_Dialogue {
        setvar(VAR_PLAYER_HOME_STATE,HAS_MET_BIANCA)
        lockall
        playbgm(MUS_ENCOUNTER_BRENDAN,TRUE)
        playse(SE_PIN)
        applymovement(BIANCA,Common_Movement_ExclamationMark)
        waitmovement(0)
        applymovement(BIANCA,ImProbablyBetterThanYou_WalkToPlayer)
        waitmovement(0)
        setvar(VAR_MUGSHOT, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_ImProbablyBetterThanYou_YouMustBe)
        msgbox(gText_ImProbablyBetterThanYou_WelcomeToTheMost)
        closemessage
        callnative(ClearMugshot)
        goto (ImProbablyBetterThanYou_KindaShabby_Dialogue) 
        end
}

script ImProbablyBetterThanYou_KindaShabby_Dialogue {
        applymovement(BIANCA,ImProbablyBetterThanYou_LookAround)
        waitmovement(0)
        msgbox(gText_ImProbablyBetterThanYou_KindaShabbyHuh)
        closemessage
        setvar(VAR_MUGSHOT, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_ImProbablyBetterThanYou_OhYeahUh)
        msgbox(gText_ImProbablyBetterThanYou_ThisHouseIsFriggin)
        setvar(VAR_MUGSHOT, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_ImProbablyBetterThanYou_MyFlightWasEarlierToday)
        goto(ImProbablyBetterThanYou_FoodAndWhereYouFrom_Dialogue) 
        end
}

script ImProbablyBetterThanYou_FoodAndWhereYouFrom_Dialogue {
        msgbox(gText_ImProbablyBetterThanYou_IOrderedSomeFoodToo)
        msgbox(gText_ImProbablyBetterThanYou_WhereAreYouFrom)
        setvar(VAR_MUGSHOT, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_ImProbablyBetterThanYou_AgateVillageOrre)
        setvar(VAR_MUGSHOT, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_ImProbablyBetterThanYou_UndellaTownUnova)
        goto (ImProbablyBetterThanYou_TookTheLeftBedroom_Dialogue)
        end
}

script ImProbablyBetterThanYou_TookTheLeftBedroom_Dialogue {
        msgbox(gText_ImProbablyBetterThanYou_IKnowOurOrientation)
        applymovement(BIANCA,ImProbablyBetterThanYou_GestureUpstairs)
        waitmovement(0)
        msgbox(gText_ImProbablyBetterThanYou_ButBothRooms)
        callnative(ClearMugshot)
        closemessage
        applymovement(BIANCA,ImProbablyBetterThanYou_WalkToCouch)
        waitmovement(0)
        fadedefaultbgm
        releaseall
        end
}

script ImProbablyBetterThanYou_PreventPlayerLeaving_Script {
    lockall
        setvar(VAR_MUGSHOT, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_ImProbablyBetterThanYou_IShouldCheckOut)
        callnative(ClearMugshot)
        closemessage
        applymovement(PLAYER,Common_Movement_WalkUp)
        waitmovement(0)
        releaseall
        end
}

script ImProbablyBetterThanYou_Annoyed_Dialogue {
    setvar(VAR_MUGSHOT, MUGSHOT_BIANCA)
    callnative(DrawMugshot)

    switch (var(VAR_TALKED_BIANCA)) {
        case 1: 
            msgbox(gText_ImProbablyBetterThanYou_OhManAreYouTrying,MSGBOX_NPC)
            addvar(VAR_TALKED_BIANCA,1)
        case 2:
            msgbox(gText_ImProbablyBetterThanYou_NomNomNom)
        default:
            msgbox(gText_ImProbablyBetterThanYou_GoCheckOutYourRoom,MSGBOX_NPC)
            setvar(VAR_TALKED_BIANCA,1)
    }
    callnative(ClearMugshot)
    closemessage
        applymovement(BIANCA,ImProbablyBetterThanYou_GoBackToFood)
            waitmovement(0)
}

text gText_ImProbablyBetterThanYou_DeepBreath {
    format("*sigh* (Let's do this.)")
    //PSF TODO add asterisk to font, look at SKiLpVg.png and find best treatment for actions (color? bracket?)
}

text gText_ImProbablyBetterThanYou_YouMustBe{
    format("You must be {PLAYER}! I'm the other trainer in the Kickstart program, Bianca.")
}
           
text gText_ImProbablyBetterThanYou_WelcomeToTheMost{
    format("Welcome to the most boring place in the SanFran region... Southbay.")
}

text gText_ImProbablyBetterThanYou_KindaShabbyHuh {
    format("This place is kinda shabby, huh? I thought YCombinator was pulling out all the stops for us, but there's not a butler in sight! And no private tennis courts! Not that I ever play, but it's the principle of the thing...")
}

text gText_ImProbablyBetterThanYou_OhYeahUh {
    format("Oh yeah uh, it's too bad...")
}

text gText_ImProbablyBetterThanYou_ThisHouseIsFriggin {
    format("*internal* This house is friggin' huge! Bigger than anywhere I've ever stayed, that's for sure...")
}

text gText_ImProbablyBetterThanYou_MyFlightWasEarlierToday {
    format("My flight was earlier today, so I've just been waiting around.")
}

text gText_ImProbablyBetterThanYou_IOrderedSomeFoodToo {
    format("I ordered some food too - DoorDash is the same here as it is back in Unova, and my Mom's card is still on the account! I'm sure she gets... points or something, right?")
}

text gText_ImProbablyBetterThanYou_WhereAreYouFrom {
    format("Where are you from?")
}

text gText_ImProbablyBetterThanYou_AgateVillageOrre {
    format("Agate Village, Orre. You?")
}

text gText_ImProbablyBetterThanYou_UndellaTownUnova {
    format("Undella Town, Unova. Agate, right? I don't think I've heard of it... oh well. Probably just as boring as Undella. SO GLAD to be out of there!")
}

text gText_ImProbablyBetterThanYou_IKnowOurOrientation {
    format("I know our orientation is supposed to start soon, or something like that. I took the bedroom on the left, so the one to the right is yours. I tried to take the good one because I got here first...")
}
            
text gText_ImProbablyBetterThanYou_ButBothRooms {
format("but both rooms are exactly the same! Let's chill after you unpack.")
}

text gText_ImProbablyBetterThanYou_GoCheckOutYourRoom {
    format("Go check out your room!")
}

text gText_ImProbablyBetterThanYou_OhManAreYouTrying {
    format("Oh man, are you trying to do some bonding or something? Lemme finish this truffle burger first...")
}

text gText_ImProbablyBetterThanYou_NomNomNom {
    format("Nom nom nom...")
}

text gText_ImProbablyBetterThanYou_IShouldCheckOut {
    format("I should check out my room!")
}

movement ImProbablyBetterThanYou_WalkToPlayer {
    walk_right
        walk_down
        step_end
}

movement ImProbablyBetterThanYou_WalkToCouch {
    walk_up
        walk_left
        step_end
}

movement ImProbablyBetterThanYou_LookAround{
    face_left
        delay_16
        delay_16
        face_right
        delay_16
        delay_16
        face_down
        delay_16
        delay_16
        step_end
}

movement ImProbablyBetterThanYou_GestureUpstairs {
    face_up
        delay_16
        delay_16
        face_down
        step_end
}

movement ImProbablyBetterThanYou_GoBackToFood {
    face_original_direction
    step_end
}
