const PLAYER = OBJ_EVENT_ID_PLAYER
const HIKO = 1
const JIRACHI = 2
const CAMERA = OBJ_EVENT_ID_CAMERA

script OkayLetsFixit_HideObject_Script {
    setflag(FLAG_TEMP_1)
    setflag(FLAG_TEMP_2)
}

script OkayLetsFixIt_ShowObject_Script {
    setflag(FLAG_TEMP_2)
}

script OkayLetsFixit_ReachTop_Script {

    checkitem(ITEM_METEORITE, 1)
    if (!var (VAR_RESULT)) {
        release
        end
    }

    lock
    applymovement(PLAYER, OkayLetsFixit_WalkToPeak)
    waitmovement(0)
    msgbox(gText_OkayLetsFixIt_TheBambooStar)
    closemessage

    playse(SE_M_DETECT)
    dofieldeffectsparkle(17, 42, 0)
    waitfieldeffect(FLDEFF_SPARKLE)
    playse(SE_ORB)
    fadescreen(FADE_TO_WHITE)
    delay(150)
    clearflag(FLAG_TEMP_1)
    addobject(HIKO)
    fadescreen(FADE_FROM_WHITE)

    msgbox(gText_OkayLetsFixIt_ImGladYouCame)
    closemessage

    applymovement(PLAYER, OkayLetsFixit_LookAround)
    waitmovement(0)

    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_OkayLetsFixIt_WhatWhereDidYou)
    callnative(ClearMugshot)

    msgbox(gText_OkayLetsFixIt_HaveYouRealized)

    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_OkayLetsFixIt_InComingHere)
    callnative(ClearMugshot)

    msgbox(gText_OkayLetsFixIt_LetsLookBack)
    closemessage

    warpsilent(MAP_OKAY_LETS_FIXIT_FLASHBACK, 255, 9, 4)
    end
}

script OkayLetsFixit_StartFlashback_Script {
    lock
    addvar(VAR_STORYLINE_STATE, 1)

    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_OkayLetsFixIt_ThisWasTheNight)
    callnative(ClearMugshot)

    applymovement(HIKO, Common_Movement_FaceLeft)
    waitmovement(0)
    msgbox(gText_OkayLetsFixIt_Why)

    applymovement(PLAYER, Common_Movement_FaceRight)
    waitmovement(0)

    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_OkayLetsFixIt_IWasTryingToHelp)
    callnative(ClearMugshot)

    msgbox(gText_OkayLetsFixIt_WouldYouChoose)

    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_OkayLetsFixIt_IfICouldDoItOver)
    closemessage
    callnative(ClearMugshot)
    delay(30)
    warpsilent(MAP_TWINPEAKS, 255, 17, 43)
    end
}

script OkayLetsFixit_ReturnToPeak_Script {
    lock
    clearflag(FLAG_TEMP_1)
    addobject(HIKO)

    call(OkayLetsFixit_TimelineReset_Script)
    applymovement(PLAYER, Common_Movement_FaceLeft)
    waitmovement(0)

    applymovement(PLAYER, OkayLetsFixit_WatchCliffWalk)
    applymovement(HIKO, OkayLetsFixit_WalkOffCliff)
    special(SpawnCameraObject)
    applymovement(CAMERA, OkayLetsFixit_PanCamera)
    waitmovement(CAMERA)
    waitmovement(HIKO)
    waitmovement(PLAYER)

    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_OkayLetsFixIt_WaitWhatWhoAreYou)
    callnative(ClearMugshot)

    msgbox(gText_OkayLetsFixIt_IHopeYouMake)
    closemessage
    playse(SE_ORB)
    fadescreen(FADE_TO_WHITE) //PSF TODO Implement glowing with Hiko as source, mimic "DoOrbEffect"
    delay(150)
    removeobject(HIKO)
    clearflag(FLAG_TEMP_2)
    addobject(JIRACHI)
    fadescreen(FADE_FROM_WHITE)

    playmoncry(SPECIES_JIRACHI, CRY_MODE_ENCOUNTER)
    waitmoncry
    playmoncry(SPECIES_JIRACHI, CRY_MODE_ENCOUNTER)
    waitmoncry

    warpsilent(MAP_PRESIDIO_COMPOUND_1F, 255, 9, 7)
}

script OkayLetsFixit_TimelineReset_Script {
    setflag(FLAG_TIMELINE_TIMETRAVEL)
    clearflag(FLAG_TIMELINE_FALSE)
    setvar(VAR_STORYLINE_STATE, 42)
    setvar(VAR_RUSTURF_TUNNEL_STATE,0)
    setvar(VAR_LILYCOVE_CONTEST_LOBBY_STATE,0)
    setvar(VAR_PETALBURG_WOODS_STATE,0)
}

text gText_OkayLetsFixIt_TheBambooStar {
    format("The Bamboo Star is glowing!")
}

text gText_OkayLetsFixIt_ImGladYouCame {
    format("Ah {PLAYER}, I'm glad you came. I'm Hiko.")
}

text gText_OkayLetsFixIt_WhatWhereDidYou {
    format("What… where… did you come from?! What the hell is going on?")
}

text gText_OkayLetsFixIt_HaveYouRealized {
    format("Have you realized the error in your ways?")
}

text gText_OkayLetsFixIt_InComingHere {
    format("In coming here? Yeah, I think - ")
}

text gText_OkayLetsFixIt_LetsLookBack {
    format("Let's look back.")
}

text gText_OkayLetsFixIt_ThisWasTheNight {
    format("This was the night I decided to help Psfbadguy.")
}

text gText_OkayLetsFixIt_Why {
    format("Why?")
}

text gText_OkayLetsFixIt_IWasTryingToHelp {
    format("I was trying to help. I thought that was the right thing! But… I'm not sure now")
}

text gText_OkayLetsFixIt_WouldYouChoose {
    format("Would you choose differently?")
}

text gText_OkayLetsFixIt_IfICouldDoItOver {
    format("If I could do it over? I think so. I think I made the problem worse.")
}

text gText_OkayLetsFixIt_WaitWhatWhoAreYou {
    format("Wait what - who are you? How are you -")
}

text gText_OkayLetsFixIt_IHopeYouMake {
    format("I hope you make the right decision this time, {PLAYER}.")
}

text gText_OkayLetsFixIt_WhatTheHell {
    format("What the hell was that? That dream was… insane. What the hell did I eat for dinner?")
}

text gText_OkayLetsFixIt_IHaveNoIdea {
    format("…I have no idea what I'm going to do. Do I help Psfbadguy and the police end TeamRocket permanently? Or do I help TeamRocket clear their name and help the people of SanFran?")
}

movement OkayLetsFixit_WalkToPeak {
    walk_up * 4
    walk_left * 7
    step_end
}

movement OkayLetsFixit_LookAround {
    lock_facing_direction
    walk_right
    unlock_facing_direction
    face_up
    delay_16
    face_down
    delay_16
    face_left
    step_end
}

movement OkayLetsFixit_WalkOffCliff {
    walk_up
    walk_right * 3
    walk_up * 7
    step_end
}

movement OkayLetsFixit_PanCamera {
    walk_right
    walk_up * 6
    step_end
}

movement OkayLetsFixit_WatchCliffWalk {
    delay_16 * 5
    face_up
    walk_right
    walk_up * 4
    step_end
}

movement OkayLetsFixIt_PaceNightmare {
    walk_down
    walk_left * 2
    walk_right * 2
    step_end
}
