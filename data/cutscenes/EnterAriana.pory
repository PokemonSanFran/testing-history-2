const PLAYER = OBJ_EVENT_ID_PLAYER
const ARIANA = 1
const SIARL = 2
const PERSON1 = 10
const PERSON2 = 11
const PERSON3 = 12
const FLAG_HIDE_ARIANA = FLAG_TEMP_2

script EnterAriana_ObjectCleanUp_Script {
    checkitem(ITEM_MEGA_RING,1)

        if (var(VAR_RESULT)) {
        setobjectxyperm(PERSON1,3,44)
        setobjectxyperm(PERSON2,3,45)
        setobjectxyperm(PERSON3,3,46)
                setflag(FLAG_HIDE_ARIANA)
        }
    return
}

script EnterAriana_SetConeTiles_Script {
    checkitem(ITEM_MEGA_RING,1)
        if (var(VAR_RESULT)) {
            setmetatile(4,37,0x344,TRUE)
            setmetatile(4,43,0x344,TRUE)
            special(DrawWholeMapView)
        }
    return
}


script EnterAriana_InteractSiarl_Script {
    checkitem(ITEM_MEGA_RING,1)
        if (var(VAR_RESULT)) {
            goto(EnterAriana_GruntApologize_Dialogue)
        }
        else {
            //goto(Debug_Hop)
            goto(EnterAriana_Intro_Dialogue)
        }
}

script EnterAriana_PreventPlayerProgress_Dialogue{
    lock
    setvar(VAR_TEMP_1,0)

    checkitem(ITEM_MEGA_RING,1)
        if (var(VAR_RESULT)){
            release
            end
        }

    getplayerxy(VAR_TEMP_0,VAR_TEMP_1)
    switch(var(VAR_TEMP_1)){
        case 38: applymovement(PERSON1,Common_Movement_ExclamationMark)
        case 40: applymovement(PERSON2,Common_Movement_ExclamationMark)
        case 42: applymovement(PERSON3,Common_Movement_ExclamationMark)
    }
    waitmovement(0)

    playse(SE_PIN)
    waitse

    applymovement(PLAYER,Common_Movement_FaceDown)
    waitmovement(PLAYER)
    msgbox(gText_EnterAriana_NoCutting)
    closemessage

    applymovement(PLAYER,Common_Movement_WalkRight)
    waitmovement(PLAYER)
    release
    end
}

script EnterAriana_Intro_Dialogue{
    lockall
        msgbox(gText_EnterAriana_WhoTheHell)
        setvar(VAR_MUGSHOT, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_NoSorry)
        callnative(ClearMugshot)
        msgbox(gText_EnterAriana_TryingToHelpEverybody)
        setvar(VAR_MUGSHOT, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_ImNotInLine)
        callnative(ClearMugshot)
        msgbox(gText_EnterAriana_SwearToArceus)
        setvar(VAR_MUGSHOT, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_WaitListen)
        callnative(ClearMugshot)
        msgbox(gText_EnterAriana_NoPunk)
        applymovement(ARIANA,Common_Movement_FaceDown)
        playbgm(MUS_RG_ENCOUNTER_ROCKET,FALSE)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_HoldOn)
        closemessage
        callnative(ClearMugshot)
        applymovement(ARIANA,EnterAriana_ArianaWalkToPlayer_Movement)
        waitmovement(ARIANA)
        applymovement(SIARL,Common_Movement_FaceRight)
        waitmovement(SIARL)
        goto(EnterAriana_Explain_Dialogue)
}

script EnterAriana_Explain_Dialogue{
    setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_DidYouListen)
        closemessage
        callnative(ClearMugshot)
        applymovement(SIARL,Common_Movement_FaceDown)
        waitmovement(SIARL)
        applymovement(SIARL,Common_Movement_Delay48)
        waitmovement(SIARL)
        applymovement(SIARL,Common_Movement_FaceRight)
        waitmovement(SIARL)
        msgbox(gText_EnterAriana_TryingToCut)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_WhatYouHeard)
        callnative(ClearMugshot)
        closemessage
        applymovement(ARIANA,Common_Movement_FaceDown)
        waitmovement(ARIANA)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_WhatProblem)
        setvar(VAR_MUGSHOT, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_AsshatInterrupted)
        callnative(ClearMugshot)
        closemessage
        goto(EnterAriana_Apology_Dialogue)
}

script EnterAriana_Apology_Dialogue{
    applymovement(ARIANA,Common_Movement_FaceLeft)
        waitmovement(ARIANA)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_ShouldntTheCones)
        callnative(ClearMugshot)
        msgbox(gText_EnterAriana_IForgotToSet)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_BeratedSomebody)
        callnative(ClearMugshot)
        msgbox(gText_EnterAriana_Sorry)
        closemessage
        applymovement(ARIANA,Common_Movement_FaceDown)
        waitmovement(ARIANA)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_WhatsYourName)
        callnative(ClearMugshot)
        closemessage
        applymovement(ARIANA,Common_Movement_FaceLeft)
        waitmovement(ARIANA)
        applymovement(SIARL,Common_Movement_FaceDown)
        waitmovement(SIARL)
        msgbox(gText_EnterAriana_IGotHeated)
        callnative(ClearMugshot)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_DontRuin)
        callnative(ClearMugshot)
        closemessage
        applymovement(SIARL,Common_Movement_FaceRight)
        msgbox(gText_EnterAriana_ShouldBeNear)
        closemessage


        applymovement(ARIANA,EnterAriana_HopOverTable_Movement)
        waitmovement(ARIANA)
        applymovement(PLAYER,Common_Movement_FaceRight)
        waitmovement(PLAYER)
        applymovement(SIARL,Common_Movement_FaceDown)
        waitmovement(SIARL)


        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_NeedToWalk)
        callnative(ClearMugshot)
        closemessage
        //Debug_Hop:
        //setobjectxy(ARIANA,4,25) 

        applymovement(ARIANA,EnterAriana_WalkToLine_Movement)
        applymovement(PLAYER,EnterAriana_PlayerWalkToLine_Movement)
        //goto(Debug_Cones)
        goto(EnterAriana_ExplainHope_Dialogue)
}

script EnterAriana_ExplainHope_Dialogue{
    setvar(VAR_MUGSHOT, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_WhatIsEverybody)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_Hope)
        setvar(VAR_MUGSHOT, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_WaitHope)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_ICanGet)
        setvar(VAR_MUGSHOT, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_ThatsAmazing)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_CityIsntSuperChill)
        setvar(VAR_MUGSHOT, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_ButYoureHelping)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_YoureNotFromHere)
        callnative(ClearMugshot)
        closemessage
        //Debug_Cones:
        applymovement(ARIANA,EnterAriana_PlaceConesA_Movement)
        waitmovement(ARIANA)
            setmetatile(4,37,0x344,TRUE)
        special(DrawWholeMapView)
        applymovement(PLAYER,EnterAriana_WatchPlaceCones_Movement)
        applymovement(ARIANA,EnterAriana_PlaceConesB_Movement)
        waitmovement(ARIANA)
            setmetatile(4,43,0x344,TRUE)
        special(DrawWholeMapView)
        applymovement(ARIANA,EnterAriana_PlaceConesC_Movement)
        waitmovement(ARIANA)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_BetweenTheCones)
        callnative(ClearMugshot)
        closemessage
        call(EnterAriana_MoveLine_Script)
        goto(EnterAriana_GiveKeystone_Dialogue)
}

script EnterAriana_MoveLine_Script{
    playse(SE_PIN)
        applymovement(PERSON1,EnterAriana_MoveLine1_Movement)
        applymovement(PERSON2,EnterAriana_MoveLine1_Movement)
        applymovement(PERSON3,EnterAriana_MoveLine1_Movement)
        waitmovement(0)
        setobjectxy(PERSON1,3,44)
        setobjectxy(PERSON2,3,45)
        setobjectxy(PERSON3,3,46)
        setobjectxyperm(PERSON1,3,44)
        setobjectxyperm(PERSON2,3,45)
        setobjectxyperm(PERSON3,3,46)
}

script EnterAriana_GiveKeystone_Dialogue{
        applymovement(ARIANA,EnterAriana_FacePlayer_Movement)
        waitmovement(ARIANA)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_ThereYouGo)
        setvar(VAR_MUGSHOT, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_AGym)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_WellGoodLuck)
        callnative(ClearMugshot)
        closemessage
        applymovement(ARIANA,Common_Movement_WalkInPlaceUp)
        waitmovement(ARIANA)
        giveitem(ITEM_MEGA_RING)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_IfYouHaveThat)

        special(CheckMegaEvolutionPartyMember)

        //VAR_TEMP_1 will correspond to the party slot
        //if VAR_TEMP_2 is 1, this pokemon will evantually be able to megaevolve

        if (var(VAR_TEMP_1) < 6)
        {
            switch(var(VAR_TEMP_1))
            {
                case 0: bufferpartymonnick(0,0)
                case 1: bufferpartymonnick(0,1)
                case 2: bufferpartymonnick(0,2)
                case 3: bufferpartymonnick(0,3)
                case 4: bufferpartymonnick(0,4)
                case 5: bufferpartymonnick(0,5)
            }

            setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
                callnative(DrawMugshot)

                if (var(VAR_TEMP_2) != 1){
                    msgbox(gTextEnterAriana_CanMegaEvolve)
                }
                else{
                    msgbox(gTextEnterAriana_CanMegaEvolveSomeday)
                }
        }
    setvar(VAR_MUGSHOT, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_ThankYouSo)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_HopeThatMakes)
        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_EnterAriana_IGottaGo)
        callnative(ClearMugshot)
        closemessage
        applymovement(ARIANA,EnterAriana_WalkAway_Movement)
        waitmovement(ARIANA)
        removeobject(ARIANA)
        fadedefaultbgm
        addvar(VAR_STORYLINE_STATE,1)
        call(Act1_1_Check_Script)
        releaseall
        end
}

script EnterAriana_GruntApologize_Dialogue {
        msgbox(gText_EnterAriana_SorryAgain,MSGBOX_NPC)
        end
}

movement EnterAriana_ArianaWalkToPlayer_Movement {
    walk_down * 3
    face_left
    step_end
}
movement EnterAriana_HopOverTable_Movement {
    walk_up * 4
        delay_16
        walk_down * 4
        jump_2_down
        face_left
        step_end
}
movement EnterAriana_WalkToLine_Movement {
        walk_down * 14
        face_left
        face_player
        step_end
}
movement EnterAriana_PlayerWalkToLine_Movement {
        walk_right
        walk_down * 13
        step_end
}
movement EnterAriana_WatchPlaceCones_Movement {
        face_right
        face_up
        delay_16 * 2
        face_down
        step_end
}

movement EnterAriana_PlaceConesA_Movement {
        walk_right
        walk_up * 2
        face_left
        walk_in_place_left
        step_end
}
movement EnterAriana_PlaceConesB_Movement {
        walk_down * 6
        face_left
        walk_in_place_left
        step_end
}
movement EnterAriana_PlaceConesC_Movement {
        walk_up * 4
        face_left
    step_end
}
movement EnterAriana_MoveLine1_Movement {
    emote_exclamation_mark
        walk_down * 5
        face_up
        step_end
}
movement EnterAriana_MoveLine2_Movement {
    emote_exclamation_mark
        delay_16
        delay_16
        delay_16
        walk_right 
        walk_down
        walk_right
        walk_down * 2
        walk_left
        face_up
        step_end
}
movement EnterAriana_MoveLine3_Movement {
    emote_exclamation_mark
        delay_16
        delay_16
        delay_16
        delay_16
        walk_right * 3
        walk_down * 3
        walk_left
        face_up
        step_end
}
movement EnterAriana_FacePlayer_Movement{
    walk_left
    face_up
    step_end
}
movement EnterAriana_WalkAway_Movement {
    walk_right
        walk_up * 7
        step_end
}

text gText_EnterAriana_NoCutting{
    format("No cutting the line!")
}

text gText_EnterAriana_WhoTheHell {
    format("Woah, who the hell do you think you are?")
}

text gText_EnterAriana_NoSorry {
    format("No sorry, I'm just trying to get through, are you able to -")
}

text gText_EnterAriana_TryingToHelpEverybody {
    format("Look, we're trying to help everybody here, and we're doing the best we can. You gotta get in line like everybody else.")
}

text gText_EnterAriana_ImNotInLine {
    format("I'm not in line, I'm trying to -")
}

text gText_EnterAriana_SwearToArceus {
    format("Trying to get in front! I know! I swear to Arceus, you follow the rules like everybody else, or you get nothing.")
}

text gText_EnterAriana_WaitListen {
    format("Wait, listen to me -")
}

text gText_EnterAriana_NoPunk {
    format("NO punk, YOU listen to ME. What the hell makes you think -")
}

text gText_EnterAriana_HoldOn {
    format("Siarl, hold on.")
}

text gText_EnterAriana_DidYouListen {
    format("Siarl, did you listen to them?")
}

text gText_EnterAriana_TryingToCut {
    format("Yeah, they're trying to cut the line!")
}

text gText_EnterAriana_WhatYouHeard {
    format("Siarl, that's what you heard... but not what they said.")
}

text gText_EnterAriana_WhatProblem {
    format("Sorry. What seems to be the problem?")
}

text gText_EnterAriana_AsshatInterrupted {
    format("Thanks. Before this asshat interrupted me, I was trying to explain that your line is blocking the road. Nobody can get through. Could you do something about that?")
}

text gText_EnterAriana_ShouldntTheCones {
    format("Damn. Siarl, shouldn't the cones prevent that from happening?")
}

text gText_EnterAriana_IForgotToSet {
    format("...I forgot to set up the cones.")
}

text gText_EnterAriana_BeratedSomebody {
    format("Oh? So, you screwed up, then berated somebody when they tried to ask you to fix it. Asshat sounds about right.")
}

text gText_EnterAriana_Sorry {
    format("...Sorry Ariana.")
}

text gText_EnterAriana_WhatsYourName {
    format("What's your name? ...{PLAYER}.")
}

text gText_EnterAriana_IGotHeated {
    format("Sorry I got so heated {PLAYER}. It's been rough -")
}

text gText_EnterAriana_DontRuin {
    format("Siarl, don't ruin a perfectly good apology with an excuse. Where are the cones?")
}

text gText_EnterAriana_ShouldBeNear {
    format("...they should be near the backstock pallet.")
}

text gText_EnterAriana_NeedToWalk {
    format("I need to walk for a second. Let's walk, {PLAYER}.")
}

text gText_EnterAriana_WhatIsEverybody {
    format("What is everybody lining up for?")
}

text gText_EnterAriana_Hope {
    format("Hope.")
}

text gText_EnterAriana_WaitHope {
    format("Wait, hope?")
}

text gText_EnterAriana_ICanGet {
    format("Sorry, I can get whimsical sometimes! One of the biggest challenges of being unhoused is access to food and sanitary supplies. My colleagues and I, the RocketAcronym, set up around the city and hand out kits.")
}

text gText_EnterAriana_ThatsAmazing {
    format("Oh wow, that's amazing! But why are you in an alleyway?")
}

text gText_EnterAriana_CityIsntSuperChill {
    format("The city isn't super chill with us, for complicated reasons. We move around a lot and choose unconventional locations in order to avoid the cops. It sucks, but it works.")
}

text gText_EnterAriana_ButYoureHelping {
    format("Wait, but you're helping people! Why isn't the city supporting you?")
}

text gText_EnterAriana_YoureNotFromHere {
    format("Clearly, you're not from here, are you? I can't remember a point in my lifetime where the city helped people, {PLAYER}.")
}

text gText_EnterAriana_BetweenTheCones {
    format("Yo, clear a path between the cones.")
}

text gText_EnterAriana_ThereYouGo {
    format("There you go. Sorry about all of that. What's in Japantown?")
}

text gText_EnterAriana_AGym {
    format("A Gym! I'm taking part in the League Challenge.")
}

text gText_EnterAriana_WellGoodLuck {
    format("Ah, well good luck. Actually... a League Challenger that isn't from here... take this.")
}

text gText_EnterAriana_IfYouHaveThat {
    format("If you have that and your Pokémon is holding the appropriate Mega Stone, you'll be able to Mega Evolve that Pokémon. Most natives aren't chill with Mega Evolution, but you can make your own decisions.")
}

text gTextEnterAriana_CanMegaEvolve {
    format("Your {STR_VAR_1} is capable of Mega Evolution, if memory serves me right.")
}

text gTextEnterAriana_CanMegaEvolveSomeday {
    format("I'm pretty sure {STR_VAR_1} can do it once its fully-evolved!")
}

text gText_EnterAriana_ThankYouSo {
    format("That's amazing, thank you so much!")
}

text gText_EnterAriana_HopeThatMakes {
    format("Hope that makes up for the hassle. It fell off the back of a truck, but you can totally have it.")
}

text gText_EnterAriana_IGottaGo {
    format("I gotta go back before Siarl cusses out another innocent victim. Stay safe out there.")
}

text gText_EnterAriana_SorryAgain {
    format("Sorry again about before.")
}
