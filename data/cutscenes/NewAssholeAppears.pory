const PLAYER = OBJ_EVENT_ID_PLAYER
const PSFNATIVERIVAL = 1

script NewAssholeAppears_WalkToPlayer_Script {
    lockall
    getplayerxy(VAR_TEMP_0,VAR_TEMP_1)
    applymovement(PSFNATIVERIVAL,Common_Movement_ExclamationMark)
    playse(SE_PIN)
    playbgm(MUS_ENCOUNTER_CHAMPION,TRUE)
    waitmovement(0)

    switch(var(VAR_TEMP_0)){
        case 0: applymovement(PSFNATIVERIVAL,NewAssholeAppears_X0_WalkToPlayer_Movement)
        case 1: applymovement(PSFNATIVERIVAL,NewAssholeAppears_X1_WalkToPlayer_Movement)
        case 2: applymovement(PSFNATIVERIVAL,NewAssholeAppears_X2_WalkToPlayer_Movement)
        case 3: applymovement(PSFNATIVERIVAL,NewAssholeAppears_X3_WalkToPlayer_Movement)
        case 4: applymovement(PSFNATIVERIVAL,NewAssholeAppears_X4_WalkToPlayer_Movement)
    }
    waitmovement(0)
    
    switch(var(VAR_TEMP_1)){
        case 2: applymovement(PSFNATIVERIVAL,NewAssholeAppears_Y2_WalkToPlayer_Movement)
        case 6: applymovement(PSFNATIVERIVAL,NewAssholeAppears_Y6_WalkToPlayer_Movement)
    }
    waitmovement(0)
    goto(ArcherAlpha_Event_Script)
}

script ArcherAlpha_Event_Script{
    switch(var(VAR_PSFROUTE9_STATE)) {
        case 0: goto(NewAssholeAppears_Intro_Dialogue)
        case 1: goto(NewAssholeAppears_StartBattle_Script)
    }
}

script NewAssholeAppears_Intro_Dialogue {
    addvar(VAR_PSFROUTE9_STATE,1)
    setvar(VAR_0x8000, MUGSHOT_HUGH)
    callnative(DrawMugshot)
    msgbox(gText_NewAssholeAppears_YouLookFamiliar,MSGBOX_NPC)
    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_NewAssholeAppears_JustStarted,MSGBOX_NPC)
    goto(NewAssholeAppears_BadgeJudge_Dialogue)
}

script NewAssholeAppears_BadgeJudge_Dialogue {
    setvar(VAR_0x8000, MUGSHOT_HUGH)
    callnative(DrawMugshot)

    special(GetNumberOfBadges) 

    switch (var(VAR_TEMP_0)){
        case 0: msgbox(gText_NewAssholeAppears_NotDoingMuchGood,MSGBOX_NPC)
        case 1: msgbox(gText_NewAssholeAppears_ThoughtIRecognized,MSGBOX_NPC)
        default: msgbox(gText_NewAssholeAppears_ColonizersAreReallyGetting,MSGBOX_NPC)
    }

    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_NewAssholeAppears_WhatIsYourProblem,MSGBOX_NPC)
    callnative(ClearMugshot)
    lockall
    call(Common_EventScript_SaveGame)
    closemessage
    goto(NewAssholeAppears_StartBattle_Script)
}

script NewAssholeAppears_StartBattle_Script{
    lockall
    setvar(VAR_0x8000, MUGSHOT_HUGH)
    callnative(DrawMugshot)
    trainerbattle_single(TRAINER_WALLY_MAUVILLE,gText_NewAssholeAppears_GoMindYourBusiness,gText_NewAssholeAppears_YoureTough,NewAssholeAppears_PostBattle_Script, NO_MUSIC)
    closemessage
    callnative(ClearMugshot)
}

script NewAssholeAppears_PostBattle_Script {
    lockall
    addvar(VAR_PSFROUTE9_STATE,1)
    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_NewAssholeAppears_Whateveryournameis,MSGBOX_NPC)
    setvar(VAR_0x8000, MUGSHOT_HUGH)
    callnative(DrawMugshot)
    msgbox(gText_NewAssholeAppears_GentrificationOfMyHome,MSGBOX_NPC)
    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_NewAssholeAppears_YouSoundCrazyPerson,MSGBOX_NPC)
    setvar(VAR_0x8000, MUGSHOT_HUGH)
    callnative(DrawMugshot)
    msgbox(gText_NewAssholeAppears_NotStupidJustIgnorant,MSGBOX_NPC)
    setvar(VAR_0x8000, MUGSHOT_HUGH)
    callnative(DrawMugshot)
    msgbox(gText_NewAssholeAppears_SeeYouAround,MSGBOX_NPC)
    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_NewAssholeAppears_IHopeNot,MSGBOX_NPC)
    callnative(ClearMugshot)
    closemessage
    goto(NewAssholeAppears_ArcherExit_Script)
}

script NewAssholeAppears_ArcherExit_Script{
    getplayerxy(VAR_TEMP_0,VAR_TEMP_1)
    
    switch(var(VAR_TEMP_1)){
        case 2: applymovement(PSFNATIVERIVAL,NewAssholeAppears_ArcherWalkAwaySouth_Movement)
        case 6: applymovement(PSFNATIVERIVAL,NewAssholeAppears_ArcherWalkAwayNorth_Movement)
    }
    waitmovement(0)
    fadedefaultbgm
    removeobject(PSFNATIVERIVAL)
    addvar(VAR_STORYLINE_STATE,1)
    call(Act1_1_Check_Script)

    releaseall
    end
}

text gText_NewAssholeAppears_YouLookFamiliar {
    format("You look familiar. You flew here to do the YCombinator League Challenge, right?")
}
text gText_NewAssholeAppears_JustStarted {
    format("Yeah! Just started not too long ago. Name's {PLAYER}.")
}

text gText_NewAssholeAppears_NotDoingMuchGood {
    format("Judging from your Badge count, the program isn't doing you much good.")
}

text gText_NewAssholeAppears_ThoughtIRecognized {
    format("...yeah I thought I recognized you. Guess you transplants aren't all a total joke!")
}

text gText_NewAssholeAppears_ColonizersAreReallyGetting {
    format("Yeah, I heard about you from the guys. The colonizers are really getting a head start, huh?")
}

text gText_NewAssholeAppears_WhatIsYourProblem {
    format("Woah, what the hell is your problem?")
}

text gText_NewAssholeAppears_GoMindYourBusiness {
    format("Go mind your business in your own region.")
}

text gText_NewAssholeAppears_YoureTough {
    format("...you're tough.")
}

text gText_NewAssholeAppears_Whateveryournameis {
    format("Are you done being an ass, whateveryournameis?")
}

text gText_NewAssholeAppears_GentrificationOfMyHome {
    format("Archer, and only if you're done contributing to the gentrification of my home... but I doubt it. You're stupid, just like the rest of them.")
}

text gText_NewAssholeAppears_YouSoundCrazyPerson {
    format("Excuse me!? You sound like a crazy person, but I'm the stupid one?")
}

text gText_NewAssholeAppears_NotStupidJustIgnorant {
    format("I take that back - not stupid, just ignorant. I shouldn't be mad at you. The ignorance isn't entirely your fault. ")
}

text gText_NewAssholeAppears_SeeYouAround {
    format("See you around.")
}

text gText_NewAssholeAppears_IHopeNot {
    format("I hope not.")
}

movement NewAssholeAppears_X0_WalkToPlayer_Movement {
    walk_left * 4
    step_end
}
movement NewAssholeAppears_X1_WalkToPlayer_Movement {
    walk_left * 3
    step_end
}
movement NewAssholeAppears_X2_WalkToPlayer_Movement {
    walk_left * 2
    step_end
}
movement NewAssholeAppears_X3_WalkToPlayer_Movement {
    walk_left * 1
    step_end
}
movement NewAssholeAppears_X4_WalkToPlayer_Movement {
    step_end
}
movement NewAssholeAppears_Y2_WalkToPlayer_Movement {
    walk_up
    step_end
}
movement NewAssholeAppears_Y6_WalkToPlayer_Movement {
    walk_down
    step_end
}

movement NewAssholeAppears_ArcherWalkAwayNorth_Movement {
    walk_up * 8
    step_end
}

movement NewAssholeAppears_ArcherWalkAwaySouth_Movement {
    walk_down * 8
    step_end
}
