const PLAYER = OBJ_EVENT_ID_PLAYER
const PSFTRANSRIVAL = 1

script BeachBattle_ObjectSetUp_Script {
    setflag(FLAG_TEMP_1)
}

script BeachBattle_BattleSetUp_Script {
    setobjectxyperm(PSFTRANSRIVAL,10,24) 
}

script BeachBattle_PsftransrivalWalkToPlayer {
    lock
        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_BeachBattle_HeyPlayer)
        closemessage
        callnative(ClearMugshot)
        playse(SE_PIN)
        clearflag(FLAG_TEMP_1)
        addobject(PSFTRANSRIVAL)
        applymovement(PLAYER,BeachBattle_ExclaimAndTurn)
        waitmovement(0)
        playbgm(MUS_ENCOUNTER_BRENDAN,TRUE)
        applymovement(PSFTRANSRIVAL,BeachBattle_ApproachFromSouth)
        waitmovement(0)
        goto(BeachBattle_Psfnativerival_Event_Script)
}

script BeachBattle_Interact_Script {
        playbgm(MUS_ENCOUNTER_BRENDAN,TRUE)
    faceplayer
        goto(BeachBattle_Psfnativerival_Event_Script)
}

script BeachBattle_Psfnativerival_Event_Script {
    switch (var(VAR_ROUTE107_STATE)) {
        case 0: goto(BeachBattle_Approach_Dialogue)
        case 1: goto(BeachBattle_StartBattle_Script)
    }
}

script BeachBattle_Approach_Dialogue {
        setvar(VAR_ROUTE107_STATE,1)
        setobjectxyperm(PSFTRANSRIVAL,10,24) 
        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_BeachBattle_ImHeadingNorth)

        special(GetNumberOfBadges)

        switch (var(VAR_TEMP_0)) {
            case 8: msgbox(gText_BeachBattle_OhShit)
            case 7: goto(BeachBattle_SevenBadges_Dialogue)
            case 6: msgbox(gText_BeachBattle_TwoLeft) 
            case 5: msgbox(gText_BeachBattle_HmmOnlyFive)
            case 4: goto(BeachBattle_FourBadges_Dialogue)
        }
        goto(BeachBattle_SaveProgress)
}


script BeachBattle_SevenBadges_Dialogue {
    msgbox(gText_BeachBattle_OhNiceLastOne)

        setvar(VAR_0x8000, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_BeachBattle_JustTheOneIn)
        goto(BeachBattle_SaveProgress)
}

script BeachBattle_FourBadges_Dialogue {
        msgbox(gText_BeachBattle_ImNotSureThatSlow)

        setvar(VAR_0x8000, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_BeachBattle_IveBeenALittleDistracted)
        goto(BeachBattle_SaveProgress)
}

script BeachBattle_SaveProgress {
    callnative(ClearMugshot)
    call(Common_EventScript_SaveGame)
    closemessage
    goto(BeachBattle_StartBattle_Script)
}

script BeachBattle_StartBattle_Script {
    lock
        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_BeachBattle_MyBrainIsScrambled)
        trainerbattle_no_intro(TRAINER_MAY_ROUTE_110_MUDKIP,gText_BeachBattle_ImLegitShook)
        closemessage
        callnative(ClearMugshot)
        goto(BeachBattle_PostBattle_Dialogue)
}

script BeachBattle_PostBattle_Dialogue {
    lock
        addvar(VAR_STORYLINE_STATE,1)
        playbgm(MUS_ENCOUNTER_BRENDAN,TRUE)
        addvar(VAR_ROUTE107_STATE,1)

        setvar(VAR_0x8000, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_BeachBattle_WellImNotSurprised)

        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_BeachBattle_UghIShouldBe)

        if (flag(FLAG_BADGE05_GET)){
            msgbox(gText_BeachBattle_IfItTakesATeam)
        }

    msgbox(gText_BeachBattle_NextTimeIllHave)
        callnative(ClearMugshot)
        closemessage

        applymovement(PSFTRANSRIVAL,BeachBattle_WalkAway)
        waitmovement(0)
        fadedefaultbgm
        removeobject(PSFTRANSRIVAL)
        release
        end
}

text gText_BeachBattle_HeyPlayer {
    format("Hey, {PLAYER}!!")
}

text gText_BeachBattle_ImHeadingNorth {
    format("I'm heading north for my last Badge. How's your challenge holding up?")
}

text gText_BeachBattle_OhShit {
    format("Oh shit! You beat me to eight badges - guess I underestimated you!")
}

text gText_BeachBattle_OhNiceLastOne {
    format("Oh nice, last one too! Which one?")
}

text gText_BeachBattle_JustTheOneIn {
    format("Just the one in {STR_VAR_1} City. Headed there now.")
}

text gText_BeachBattle_TwoLeft {
    format("Two left, that's what I figured.")
}

text gText_BeachBattle_HmmOnlyFive {
    format("Hmm, only five? Guessed you'd be further along by now!")
}

text gText_BeachBattle_ImNotSureThatSlow {
    format("{PLAYER}, I'm not sure that slow and steady always win the race… you gotta pick it up!")
}

text gText_BeachBattle_IveBeenALittleDistracted {
    format("I've been a little… distracted! But I'll make it up for sure, don't worry.")
}

text gText_BeachBattle_MyBrainIsScrambled {
    format("My brain is scrambled from running around all day. Let's see if I can beat you without breaking a sweat!")
}

text gText_BeachBattle_ImLegitShook {
    format("I'm legit shook. You're even stronger than I thought!")
}

text gText_BeachBattle_WellImNotSurprised {
    format("Well, I'm not surprised you've already got seven Badges, that was tough!")
}

text gText_BeachBattle_UghIShouldBe {
    format("Ugh, I should be stronger than that.")
}

text gText_BeachBattle_IfItTakesATeam {
    format("If it takes a team as good as yours to beat Psfleader5, then I might need to rethink things…")
}

text gText_BeachBattle_NextTimeIllHave {
    format("Next time, I'll have you figured out!")
}

movement BeachBattle_ExclaimAndTurn {
    emote_exclamation_mark
        face_down
        step_end
}
movement BeachBattle_ApproachFromSouth {
    walk_right * 6
        walk_up * 4
        step_end
}
movement BeachBattle_WalkAway {
    walk_down * 7
        step_end
}
