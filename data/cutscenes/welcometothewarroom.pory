const PLAYER = OBJ_EVENT_ID_PLAYER
const ARNAV = 1
const OLIVER = 2
const OAK = 3
const BIANCA = 4
const ROSE = 5
const CHIEF = 6
const SABRINA = 7
const KOGA = 8

const FLAG_HIDE_NERDS_COPS = FLAG_TEMP_1
const FLAG_HIDE_OAK = FLAG_TEMP_2
const FLAG_HIDE_BIANCA = FLAG_TEMP_3
const FLAG_HIDE_ROSE = FLAG_TEMP_4
const FLAG_HIDE_SABRINA = FLAG_TEMP_5
const FLAG_HIDE_KOGA = FLAG_TEMP_6

script WelcomeToTheWarRoom_Object_Setup_Script{
    setflag(FLAG_HIDE_KOGA)
    setflag(FLAG_HIDE_SABRINA)
}

script WelcomeToTheWarRoom_Object_Hide_Script{
    setflag(FLAG_HIDE_KOGA)
    setflag(FLAG_HIDE_SABRINA)
    setflag(FLAG_HIDE_NERDS_COPS)
    setflag(FLAG_HIDE_BIANCA)
    setflag(FLAG_HIDE_ROSE)
    setobjectxy(OAK,34,2)
    setobjectmovementtype(OAK,MOVEMENT_TYPE_FACE_DOWN)
}

script WelcomeToTheWarRoom_FaceSpeakerRose {
    applymovement(OLIVER,Common_Movement_FaceUp)
    applymovement(ARNAV,Common_Movement_FaceUp)
    applymovement(CHIEF,Common_Movement_FaceUp)
    applymovement(OAK,Common_Movement_FaceUp)
    applymovement(BIANCA,Common_Movement_FaceUp)
    applymovement(PLAYER,Common_Movement_FaceUp)
    waitmovement(0)
}

script WelcomeToTheWarRoom_FaceSpeakerArnav {
    applymovement(OLIVER,Common_Movement_FaceRight)
    applymovement(ARNAV,Common_Movement_WalkInPlaceFasterDown)
    applymovement(CHIEF,Common_Movement_FaceUp)
    applymovement(OAK,Common_Movement_FaceRight)
    applymovement(BIANCA,Common_Movement_FaceRight)
    applymovement(PLAYER,Common_Movement_FaceRight)
    waitmovement(0)
}
script WelcomeToTheWarRoom_FaceSpeakerOliver {
    applymovement(OLIVER,Common_Movement_WalkInPlaceFasterDown)
    applymovement(ARNAV,Common_Movement_FaceLeft)
    applymovement(CHIEF,Common_Movement_FaceLeft)
    applymovement(OAK,Common_Movement_FaceUp)
    applymovement(BIANCA,Common_Movement_FaceUp)
    applymovement(PLAYER,Common_Movement_FaceUp)
    waitmovement(0)
}
script WelcomeToTheWarRoom_FaceSpeakerChief {
    applymovement(OLIVER,Common_Movement_FaceRight)
    applymovement(ARNAV,Common_Movement_FaceDown)
    applymovement(CHIEF,Common_Movement_WalkInPlaceFasterLeft)
    applymovement(OAK,Common_Movement_FaceRight)
    applymovement(BIANCA,Common_Movement_FaceRight)
    applymovement(PLAYER,Common_Movement_FaceRight)
    waitmovement(0)
}
script WelcomeToTheWarRoom_FaceSpeakerBianca {
    applymovement(OLIVER,Common_Movement_FaceDown)
    applymovement(ARNAV,Common_Movement_FaceDown)
    applymovement(CHIEF,Common_Movement_FaceLeft)
    applymovement(OAK,Common_Movement_FaceDown)
    applymovement(BIANCA,Common_Movement_WalkInPlaceFasterRight)
    applymovement(PLAYER,Common_Movement_FaceUp)
    waitmovement(0)
}
script WelcomeToTheWarRoom_FaceSpeakerPlayer {
    applymovement(OLIVER,Common_Movement_FaceDown)
    applymovement(ARNAV,Common_Movement_FaceDown)
    applymovement(CHIEF,Common_Movement_FaceLeft)
    applymovement(OAK,Common_Movement_FaceDown)
    applymovement(BIANCA,Common_Movement_FaceDown)
    applymovement(PLAYER,Common_Movement_WalkInPlaceFasterRight)
    waitmovement(0)
}
script WelcomeToTheWarRoom_FaceSpeakerOak {
    applymovement(OLIVER,Common_Movement_FaceLeft)
    applymovement(ARNAV,Common_Movement_FaceDown)
    applymovement(CHIEF,Common_Movement_FaceLeft)
    applymovement(OAK,Common_Movement_WalkInPlaceFasterRight)
    applymovement(BIANCA,Common_Movement_FaceUp)
    applymovement(PLAYER,Common_Movement_FaceUp)
    waitmovement(0)
}

script WelcomeToTheWarRoom_ExplainSitaution_Dialogue {
    lock
    setvar(VAR_SALESFORCETOWER_CONFERENCE_STATE,POST_WELCOME_TO_THE_WAR_ROOM)
    setvar(VAR_FERRYBUILDING_STATE,CHIEF_LEFT_WAR_ROOM)

    call(WelcomeToTheWarRoom_FaceSpeakerRose)
        applymovement(ROSE,WelcomeToTheWarRoom_Pace_Movement)
        waitmovement(0)

        msgbox(gText_WelcomeToTheWarRoom_OkayEverybodyLetsHearIt,,SPEAKER_ROSE)
        closemessage
        

        call(WelcomeToTheWarRoom_FaceSpeakerArnav)

        msgbox(gText_WelcomeToTheWarRoom_AllMajorSocialMedia,,SPEAKER_ARNAV)

        call(WelcomeToTheWarRoom_FaceSpeakerOliver)
        msgbox(gText_WelcomeToTheWarRoom_TheNewOwnersOfOUrAccounts,,SPEAKER_OLIVER)

        call(WelcomeToTheWarRoom_FaceSpeakerChief)
        msgbox(gText_WelcomeToTheWarRoom_WeveLongTheorized,,SPEAKER_CHIEF)

        call(WelcomeToTheWarRoom_FaceSpeakerRose)
        msgbox(gText_WelcomeToTheWarRoom_YouReallyImpressed,,SPEAKER_ROSE)

        call(WelcomeToTheWarRoom_FaceSpeakerBianca)
        msgbox(gText_WelcomeToTheWarRoom_HellYeahWedLoveTo,,SPEAKER_BIANCA)

        call(WelcomeToTheWarRoom_FaceSpeakerPlayer)
        msgbox(gText_WelcomeToTheWarRoom_ImHappyToHelp,,SPEAKER_ZOEY)

        call(WelcomeToTheWarRoom_FaceSpeakerRose)
        msgbox(gText_WelcomeToTheWarRoom_EverybodyUnderstand,,SPEAKER_ROSE)
        closemessage
        

        call(WelcomeToTheWarRoom_FaceSpeakerChief)
        msgbox(gText_WelcomeToTheWarRoom_WeveAlreadyOutlined,,SPEAKER_CHIEF)

        call(WelcomeToTheWarRoom_FaceSpeakerBianca)
        msgbox(gText_WelcomeToTheWarRoom_WhyCantWeJust,,SPEAKER_BIANCA)
        closemessage
        

        call(WelcomeToTheWarRoom_FaceSpeakerChief)
        msgbox(gText_WelcomeToTheWarRoom_GladToHear,,SPEAKER_CHIEF)


        call(WelcomeToTheWarRoom_FaceSpeakerBianca)
        msgbox(gText_WelcomeToTheWarRoom_GotItChief,,SPEAKER_BIANCA)
        closemessage
        

        call(WelcomeToTheWarRoom_FaceSpeakerChief)
        msgbox(gText_WelcomeToTheWarRoom_GreatWellStartTheOperation,,SPEAKER_CHIEF)
        closemessage

        applymovement(CHIEF,WelcomeToTheWarRoom_ChiefLeaveRoom_Movement)
        delay(30)
        fadescreen(FADE_TO_BLACK)
        removeobject(CHIEF)
        fadescreen(FADE_FROM_BLACK)

        call(WelcomeToTheWarRoom_FaceSpeakerRose)
        msgbox(gText_WelcomeToTheWarRoom_DontLet,,SPEAKER_ROSE)
        closemessage
        
        applymovement(ARNAV,WelcomeToTheWarRoom_LeaveRight_Movement)
        applymovement(OLIVER,WelcomeToTheWarRoom_LeaveLeft_Movement)
        applymovement(ROSE,WelcomeToTheWarRoom_RoseLeaveRoom_Movement)
    
        
        delay(30)
        fadescreen(FADE_TO_BLACK)
        removeobject(ROSE)
        removeobject(ARNAV)
        removeobject(OLIVER)
        fadescreen(FADE_FROM_BLACK)

        applymovement(OAK,WelcomeToTheWarRoom_AddressTrainers_Movement)
        applymovement(BIANCA,Common_Movement_FaceLeft)
        applymovement(PLAYER,Common_Movement_FaceLeft)
        waitmovement(OAK)

        msgbox(gText_WelcomeToTheWarRoom_ThatsAgainYouTwo,,SPEAKER_OAK)
        closemessage
        

        applymovement(OAK,WelcomeToTheWarRoom_OakLeaveRoom_Movement)
        delay(30)
        fadescreen(FADE_TO_BLACK)
        removeobject(OAK)
        fadescreen(FADE_FROM_BLACK)
        
        applymovement(BIANCA,Common_Movement_FaceDown)
        applymovement(PLAYER,Common_Movement_FaceUp)

        msgbox(gText_WelcomeToTheWarRoom_ImGoingToHeadOver,MSGBOX_YESNO,SPEAKER_BIANCA)

        if (var(VAR_RESULT) == NO){
            msgbox(gText_WelcomeToTheWarRoom_OkayIllMeetYou,,SPEAKER_BIANCA)
            closemessage
            
            applymovement(BIANCA,WelcomeToTheWarRoom_BiancaLeaveRoom_Movement)
            fadescreen(FADE_TO_BLACK)
            removeobject(BIANCA)
            fadescreen(FADE_FROM_BLACK)
            release
            end
        } else {
            closemessage
            
            warpsilent(MAP_PSFROUTE8,18,90)
        }
}

text gText_WelcomeToTheWarRoom_OkayEverybodyLetsHearIt {
    format("I read the summary email, but I want to hear the whole story.")
}

text gText_WelcomeToTheWarRoom_AllMajorSocialMedia {
    format("All major social media networks were attacked this morning. This hack is wild - we've been locked out of our accounts and the administrative teams of those networks can't get us back in.")
}

text gText_WelcomeToTheWarRoom_TheNewOwnersOfOUrAccounts {
    format("The new owners of our accounts have been pushing a narrative about Ycombinator's “crimes against the region”. They're calling for everybody to boycott the League and the upcoming Championship. We've found evidence that the perpetrators are at Navalbase. ")
}

text gText_WelcomeToTheWarRoom_WeveLongTheorized {
    format("We've long theorized that Navalbase is TeamRocket's primary base of operations so we've got reason to believe that they're the ones behind this cyber-attack. We have authorization to raid their headquarters and are looking -")
}

text gText_WelcomeToTheWarRoom_YouReallyImpressed {
    format("Bianca, {PLAYER} you really impressed me back at Chasecenter, and we'd like you to assist as well. You'll work under Chief Bill, and help us shut down these terrorists once and for all.")
}

text gText_WelcomeToTheWarRoom_HellYeahWedLoveTo {
    format("Hell yeah, we'd love to.")
}

text gText_WelcomeToTheWarRoom_ImHappyToHelp {
    format("I'm happy to help.")
}

text gText_WelcomeToTheWarRoom_EverybodyUnderstand {
    format("Everybody understand what they're doing?")
}

text gText_WelcomeToTheWarRoom_WeveAlreadyOutlined {
    format("We've already outlined a battle plan. Kids, you're gonna come with me and take the ferry.")
}

text gText_WelcomeToTheWarRoom_WhyCantWeJust {
    format("Why can't we just Fly over?")
}

text gText_WelcomeToTheWarRoom_GladToHear {
    format("Glad to hear I'm answering to kids now! We're splitting up our force into cells to avoid detection and increase agility. ")
}

text gText_WelcomeToTheWarRoom_GotItChief {
    format("…got it Chief.")
}

text gText_WelcomeToTheWarRoom_GreatWellStartTheOperation {
    format("Great. We'll start the operation as soon as you're ready. Meet me outside Navalbase.")
}

text gText_WelcomeToTheWarRoom_DontLet {
    format("Bianca, {PLAYER}, don't let the chief's attitude get in the way. You've all got orders, so let's get to it.")
}

text gText_WelcomeToTheWarRoom_ThatsAgainYouTwo {
    format("Thanks again you two. Be careful out there!")
}

text gText_WelcomeToTheWarRoom_ImGoingToHeadOver {
    format("I'm going to head over to Ferrybuilding. Want to come with?")
}

text gText_WelcomeToTheWarRoom_OkayIllMeetYou {
    format("Okay, I'll meet you over there.")
}
movement WelcomeToTheWarRoom_Pace_Movement {
    walk_left * 2
    walk_right *2
    walk_left * 2
    walk_right *2
    face_down
    step_end
}
movement WelcomeToTheWarRoom_ChiefLeaveRoom_Movement {
    walk_right
    walk_down * 4
    walk_left * 2
    walk_down * 3
    step_end
}
movement WelcomeToTheWarRoom_LeaveLeft_Movement{
    walk_left
    walk_down * 5
    step_end
}
movement WelcomeToTheWarRoom_LeaveRight_Movement{
    walk_right
    walk_down * 5
    step_end
}
movement WelcomeToTheWarRoom_AddressTrainers_Movement {
    walk_left
    walk_down
    face_right
    step_end
}
movement WelcomeToTheWarRoom_RoseLeaveRoom_Movement{
    walk_right * 3
    walk_down * 5
    step_end
}
movement WelcomeToTheWarRoom_OakLeaveRoom_Movement {
    walk_down * 4
    walk_right * 3
    walk_down * 3
    step_end
}
movement WelcomeToTheWarRoom_BiancaLeaveRoom_Movement {
    walk_left
    walk_down * 4
    step_end
}
