const PLAYER = OBJ_EVENT_ID_PLAYER
const PKMN1 = 1
const PKMN2 = 2
const PKMN3 = 3
const PKMN4 = 4
const MALVA = 5
const RUPERT = 6
const CAMERA = OBJ_EVENT_ID_CAMERA

script AndWeMarchOn_ObjectVisibility_Script{
    switch (var(VAR_ROUTE108_STATE)){
        case 0: call(AndWeMarchOn_ObjectSetUp_Script)
        case 1: call(AndWeMarchOn_ObjectCleanUp_Script)
    }
    return
}

script AndWeMarchOn_ObjectSetUp_Script {
    setflag(FLAG_TEMP_1)
    setflag(FLAG_TEMP_2)
}

script AndWeMarchOn_ObjectCleanUp_Script {
    setflag(FLAG_TEMP_1)
    setflag(FLAG_TEMP_2)
    setflag(FLAG_TEMP_3)
}

script AndWeMarchOn_MoveCameraToParade_Script {
        setvar(VAR_0x8004, 2)
        setvar(VAR_0x8005, 9)
        special(TeleportCamera)
        special(SpawnCameraObject)
}

//TODO: Figure out audience members (vobjects?)
//Look at "HeresHowThisIsGoingToGo", they can only be called on warp or on frame

script AndWeMarchOn_StartParadeMovement_Script {
    lock
        applymovement(PLAYER, AndWeMarchOn_WatchParade)
        waitmovement(0)
        fadescreen(FADE_TO_BLACK)
        call(AndWeMarchOn_MoveCameraToParade_Script)
        setobjectxyperm(9, 26, 10)
        setobjectxy(9, 26, 10)
        setobjectxy(10, 30, 10)
        fadescreen(FADE_FROM_BLACK)

        applymovement(CAMERA, AndWeMarchOn_PanCameraParadeStop)
        applymovement(MALVA, AndWeMarchOn_ParadeProcession)
        applymovement(10, AndWeMarchOn_ParadeProcession)
        applymovement(9, AndWeMarchOn_ParadeProcession)
        applymovement(8, AndWeMarchOn_ParadeProcession)

        setvar(VAR_0x8000, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_WowPeopleHere)
        callnative(ClearMugshot)
        closemessage
        waitmovement(0) //don't move on until parade finishes walking
        special(RemoveCameraObject)

        msgbox(gText_AndWeMarchOn_AndNowSomeWords)
        closemessage
        goto(AndWeMarchOn_Opening_Dialogue)
}

script AndWeMarchOn_Opening_Dialogue {
    playbgm(MUS_ENCOUNTER_ELITE_FOUR,TRUE)
    applymovement(MALVA, AndWeMarchOn_LookAroundCrowd)
        waitmovement(0)
        playse(SE_APPLAUSE)
        applymovement(MALVA, AndWeMarchOn_WalkToFloatCenter)
        waitmovement(0)

        setvar(VAR_0x8000, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_SanFran)
        callnative(ClearMugshot)
        closemessage

        //TODO implement fireworks on screen

        playse(SE_BANG)
        waitse
        playse(SE_BANG)
        waitse

        setvar(VAR_0x8000, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_ThankYouForSupporting)
        callnative(ClearMugshot)
        closemessage

        playse(SE_APPLAUSE)
        waitse

        applymovement(MALVA, AndWeMarchOn_PaceBackForth)
        waitmovement(0)
        goto(AndWeMarchOn_InviteOntoStage_Dialogue)
}

script AndWeMarchOn_InviteOntoStage_Dialogue {
    setvar(VAR_0x8000, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_WeHaveASpecialGuest)
        callnative(ClearMugshot)
        closemessage

        applymovement(RUPERT, AndWeMarchOn_WalkOntoStage)
        waitmovement(0)

        setvar(VAR_0x8000, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_RupertHereIsTheWinner)
        callnative(ClearMugshot)
        closemessage

        msgbox(gText_AndWeMarchOn_Boo)
        msgbox(gText_AndWeMarchOn_LetsGo)

        setvar(VAR_0x8000, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_ButThatsNotGoing)
        callnative(ClearMugshot)
        closemessage

        msgbox(gText_AndWeMarchOn_YayRupert)
        goto(AndWeMarchOn_StartCutsceneBattle_Script)
}

script AndWeMarchOn_StartCutsceneBattle_Script {
    setvar(VAR_0x8000, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_RupertAreYouReady)
        callnative(ClearMugshot)
        msgbox(gText_AndWeMarchOn_Woo)
        msgbox(gText_AndWeMarchOn_Yeah)
        closemessage

        applymovement(MALVA, AndWeMarchOn_WalkToLeftSide)
        applymovement(RUPERT, AndWeMarchOn_LookAroundCrowd)
        waitmovement(0)
        applymovement(RUPERT, Common_Movement_FaceLeft)
        waitmovement(0)

        setvar(VAR_0x8000, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_LetsRock)
        callnative(ClearMugshot)
        closemessage

        msgbox(gText_AndWeMarchOn_AndOurBattle)
        closemessage
        goto(AndWeMarchOn_CutsceneBattleScript)

}
script AndWeMarchOn_CutsceneBattleScript {

        applymovement(CAMERA,AndWeMarchOn_CenterBattleFrame)
        playse(SE_BALL_OPEN)
        playse(SE_BALL_OPEN)
        waitse
        clearflag(FLAG_TEMP_1)
        addobject(PKMN1)
        addobject(PKMN2)
        playmoncry(SPECIES_GROUDON, CRY_MODE_ENCOUNTER)
        waitmoncry
        playmoncry(SPECIES_KYOGRE, CRY_MODE_ENCOUNTER)
        waitmoncry

        playse(SE_BALL_OPEN)
        playse(SE_BALL_OPEN)
        waitse
        clearflag(FLAG_TEMP_2)
        addobject(PKMN3)
        addobject(PKMN4)
        playmoncry(SPECIES_AZUMARILL, CRY_MODE_ENCOUNTER)
        waitmoncry
        playmoncry(SPECIES_KECLEON, CRY_MODE_ENCOUNTER)
        waitmoncry

        msgbox(gText_AndWeMarchOn_AttackPatternSigma)

        setvar(VAR_0x8000, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_HelpingHand)
        closemessage
        callnative(ClearMugshot)

        applymovement(PKMN1, AndWeMarchOn_Attack)
        applymovement(PKMN2, AndWeMarchOn_Attack)
        waitmovement(0)

        //PSF TODO figure out how to flash screen back    
        fadescreen(FADE_TO_BLACK)
        playse(SE_M_THUNDERBOLT)
        waitse
        setobjectxy(PKMN3, 21, 9)
        setobjectxy(PKMN4, 21, 11)
        turnobject(PKMN3, DIR_EAST)
        turnobject(PKMN4, DIR_EAST)
        playse(SE_FAINT)
        waitse
        fadescreen(FADE_FROM_BLACK)

        msgbox(gText_AndWeMarchOn_AndWithASupercharged)
        goto(AndWeMarchOn_PostBattle_Dialogue)
}

script AndWeMarchOn_PostBattle_Dialogue {

    setvar(VAR_0x8000, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_DamnThatsHow)

        setvar(VAR_0x8000, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_LetsGiveItUp)
        closemessage
        callnative(ClearMugshot)

        playse(SE_APPLAUSE)
        waitse

        playse(SE_BALL_OPEN)
        waitse

        removeobject(PKMN1)
        removeobject(PKMN2)
        removeobject(PKMN3)
        removeobject(PKMN4)
        applymovement(RUPERT, AndWeMarchOn_WalkOffStage)
        waitmovement(0)
        goto(AndWeMarchOn_PostBattleSpeech_Dialogue)
}

script AndWeMarchOn_PostBattleSpeech_Dialogue {

    setvar(VAR_0x8000, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_ItsBeenAWildYear)

        playse(SE_APPLAUSE) //PSF TODO: change to booing sound
        waitse

        applymovement(MALVA, AndWeMarchOn_WalkToSouthStage)
        waitmovement(0)

        setvar(VAR_0x8000, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_OhItsNotAllBad)
        callnative(ClearMugshot)
        closemessage

        applymovement(MALVA, AndWeMarchOn_WalkToNorthStage)
        waitmovement(0)

        msgbox(gText_AndWeMarchOn_Boo)

        msgbox(gText_AndWeMarchOn_FuckTheseGuys)

        msgbox(gText_AndWeMarchOn_GiveUsBackOur)

        setvar(VAR_0x8000, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_ButHeyItsNot)
        callnative(ClearMugshot)
        closemessage

        msgbox(gText_AndWeMarchOn_Traitor)

        msgbox(gText_AndWeMarchOn_DownWith)

        msgbox(gText_AndWeMarchOn_WhatASlimeball)

        setvar(VAR_0x8000, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_JustNotTheWay)
        callnative(ClearMugshot)
        closemessage

        applymovement(MALVA, AndWeMarchOn_GoBackToCenter)
        waitmovement(0)

        setvar(VAR_0x8000, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_AndWeMarchOn_WeveGotAResponsibility)
        callnative(ClearMugshot)
        closemessage
        goto(AndWeMarchOn_ParadeEnd_Script)

}

script AndWeMarchOn_ParadeEnd_Script {

        playse(SE_APPLAUSE)
        playse(SE_APPLAUSE)
        waitse
        fadescreen(FADE_TO_BLACK)
        removeobject(MALVA)
        removeobject(8)
        setflag(FLAG_TEMP_3)
        addvar(VAR_ROUTE108_STATE,1)
        special(ReturnCameraToPlayer)
        fadedefaultbgm
        fadescreen(FADE_FROM_BLACK)
        release
        end
}

text gText_AndWeMarchOn_WowPeopleHere {
    format("Wow, people here really love their Double Battles… but then why is Singles the official League  format?")
}

text gText_AndWeMarchOn_AndNowSomeWords {
    format("And now, some words from our Grand Marshall, Malva!")
}

text gText_AndWeMarchOn_SanFran {
    format("SanFran!")
}

text gText_AndWeMarchOn_ThankYouForSupporting {
    format("Thank you for supporting our annual Doubles Parade! This has been a fuckin' blast! Who's having a good time?!")
}

text gText_AndWeMarchOn_WeHaveASpecialGuest {
    format("We have a special guest for you all today! Rupert, please come up to the stage!")
}

text gText_AndWeMarchOn_RupertHereIsTheWinner {
    format("Rupert here is the winner of the Mission Preparatory School graduation tournament! NORMALLY, the winner normally gets a scholarship from the SanFran League. But our new corporate overlords have decided that growing the in-region talent isn't a budget priority!")
}

text gText_AndWeMarchOn_Boo {
    format("Boo!")
}

text gText_AndWeMarchOn_LetsGo {
    format("Let's go Rupert!")
}

text gText_AndWeMarchOn_ButThatsNotGoing {
    format("But that's not going to stop us from nurturing that talent ourselves!")
}

text gText_AndWeMarchOn_YayRupert {
    format("Yaaaay Rupert!")
}

text gText_AndWeMarchOn_RupertAreYouReady {
    format("Rupert, are you ready to bring an old master out of retirement?")
}

text gText_AndWeMarchOn_Woo {
    format("Woooo!")
}

text gText_AndWeMarchOn_Yeah {
    format("Yeah!")
}

text gText_AndWeMarchOn_LetsRock {
    format("Let's rock!")
}

text gText_AndWeMarchOn_AndOurBattle {
    format("And our battle is underway!")
}

text gText_AndWeMarchOn_AttackPatternSigma {
    format("Attack pattern Sigma!")
}

text gText_AndWeMarchOn_HelpingHand {
    format("Helping Hand! Breaking Swipe!")
}

text gText_AndWeMarchOn_AndWithASupercharged {
    format("And with a supercharged Breaking Swipe, Malva ends the battle in one turn! The exhibition winner is the Elite Four's MALVA!")
}

text gText_AndWeMarchOn_DamnThatsHow {
    format("Damn, that's how she beat me too… wait, did they say Elite Four? SanFran doesn't have those, does it?")
}

text gText_AndWeMarchOn_LetsGiveItUp {
    format("Let's give it up for Rupert!")
}

text gText_AndWeMarchOn_ItsBeenAWildYear {
    format("It's been a wild year, hasn't it? Who's loving our new Pokémon League? The League that's too good for the likes of young Rupert.")
}

text gText_AndWeMarchOn_OhItsNotAllBad {
    format("Oh, it's not all bad, is it? Yeah, our beloved elected officials sold our League to the highest bidder. And yeah, they've bulldozed all of our traditions and replaced them with some soulless husk of a League. AND YEAH, they pushed our entire battling community out and replaced them with highly paid drones that couldn't battle their way out of a paper bag….")
}

text gText_AndWeMarchOn_FuckTheseGuys {
    format("Fuck these guys!")
}

text gText_AndWeMarchOn_GiveUsBackOur {
    format("Give us back our League!")
}

text gText_AndWeMarchOn_ButHeyItsNot {
    format("But hey! It's not all bad, right? Haightashbury got to keep its Gym!")
}

text gText_AndWeMarchOn_Traitor {
    format("Traitor!")
}

text gText_AndWeMarchOn_DownWith {
    format("Down with Pryce!")
}

text gText_AndWeMarchOn_WhatASlimeball {
    format("What a slimeball!")
}

text gText_AndWeMarchOn_JustNotTheWay {
    format("…just not the way we would have hoped.")
}

text gText_AndWeMarchOn_WeveGotAResponsibility {
    format("We've got a responsibility here. The profit motive can not… WILL NOT erase our history. It lives on in our hearts. In our stories Our legends. And no corporation, Ycombinator or otherwise, can take that away from us!")
}

movement AndWeMarchOn_WatchParade {
    face_down
        step_end
}

movement AndWeMarchOn_PanCameraParadeStop {
    walk_right * 14
    step_end
}

movement AndWeMarchOn_ParadeProcession {
    walk_right * 12
    step_end
}

movement AndWeMarchOn_WalkToBattle {
    walk_right * 4
        step_end
}

movement AndWeMarchOn_LookAroundCrowd {
    face_up
        delay_16
        face_down
        delay_16
        step_end
}
movement AndWeMarchOn_WalkToFloatCenter {
    walk_right
        walk_right
        walk_down
        face_right
        step_end
}
movement AndWeMarchOn_PaceBackForth {
    walk_up * 2
        delay_16
        walk_down * 4
        delay_16
        walk_up * 2
        step_end
}
movement AndWeMarchOn_WalkOntoStage {
    walk_up * 3
        face_left
        step_end
}
movement AndWeMarchOn_WalkToLeftSide {
    walk_left
        face_right
        step_end
}
movement AndWeMarchOn_Attack {
    walk_fast_right
        walk_fast_right
        step_end
}
movement AndWeMarchOn_WalkOffStage {
    walk_down * 3
        face_up
        step_end
}
movement AndWeMarchOn_WalkToSouthStage {
    walk_right * 3
        step_end
}
movement AndWeMarchOn_WalkToNorthStage {
    walk_left * 6
        step_end
}
movement AndWeMarchOn_GoBackToCenter {
    walk_right * 3
        face_down
        step_end
}
movement AndWeMarchOn_WalkCameraToParade {
    walk_left * 16
        walk_down * 2
        step_end
}
movement AndWeMarchOn_CenterBattleFrame {
    walk_right * 3
    walk_down
    step_end
}
