const PLAYER = OBJ_EVENT_ID_PLAYER
const FALKNER = 1

script EnterFalkner_StartGymBattle_ObjectSetup_Script {
    if (var (VAR_GYM_1_STATE)== GYM_SAVE_BEFORE_LEADER) {
        setobjectxyperm(FALKNER, 6, 5)
        setobjectmovementtype(FALKNER, MOVEMENT_TYPE_FACE_LEFT)
    }
    return
}

script EnterFalkner_PutMeatInOven_Dialogue {
  lockall
  setvar(VAR_GYM_1_STATE,GYM_SAVE_BEFORE_LEADER)
  msgbox(gText_EnterFalkner_OneMoreLadle,,SPEAKER_FALKNER)
    closemessage
  applymovement(FALKNER, Common_Movement_Delay48)
  waitmovement(0)
  applymovement(FALKNER, Common_Movement_WalkInPlaceUp)
  waitmovement(0)
  closemessage
  msgbox(gText_EnterFalkner_MeatsInOven, MSGBOX_NPC,SPEAKER_FALKNER)
    closemessage
  call(EnterFalkner_DetermineMovementToBattlefield_Script)
  goto(EnterFalkner_Intro_Dialogue)
}

script EnterFalkner_DetermineMovementToBattlefield_Script {
  getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
  if (var (VAR_TEMP_1) == 2) {
    applymovement(FALKNER, EnterFalkner_WalkSouthToBattlefield)

    if (var (VAR_TEMP_0) == 7) {
      applymovement(PLAYER, EnterFalkner_PlayerWalkToBattlefieldFromLeft)
    } else {
      applymovement(PLAYER, EnterFalkner_PlayerWalkToBattlefieldFromRight)
    }
  } else {
    applymovement(FALKNER, EnterFalkner_WalkWestToBattlefield)
    applymovement(PLAYER, EnterFalkner_PlayerWalkToBattlefieldFromDown)
  }
  waitmovement(0)
}

script EnterFalkner_Intro_Dialogue {
  lock
  msgbox(gText_EnterFalkner_MyLatestCreation,,SPEAKER_FALKNER)
  msgbox(gText_EnterFalkner_YourSenseOfTaste)
    call(Common_EventScript_SaveGame)
  closemessage
  goto(EnterFalkner_StartGymBattle_Script)
}

script EnterFalkner_StartGymBattle_Script {
  lock
  trainerbattle_single(TRAINER_ROXANNE_1, gText_EnterFalkner_IThinkYouTalk, gText_EnterFalkner_WoahSuperSour, EnterFalkner_FalknerDefeated_Script, NO_MUSIC)
}

script EnterFalkner_FalknerDefeated_Script {
  lockall
  setflag(FLAG_BADGE01_GET)
  setvar(VAR_GYM_1_STATE,GYM_DEFEATED_LEADER)
  setvar(VAR_FALKNER_STATE,BATTLE_1_COMPLETE)
  msgbox(gText_EnterFalkner_ThatWasDelicious,,SPEAKER_FALKNER)
    msgbox(gText_EnterFalkner_BadgeGet)
  //PSF TODO Add social media element where the player tweets out a picture of the badge upon getting it. With each badge they get, the number of likes and retweets goes up. Show this on screen while the jingle is playing
  call(Common_EventScript_PlayGymBadgeFanfare)

  giveitem(ITEM_TM01)
  msgbox(gText_EnterFalkner_OhTheMeat,,SPEAKER_FALKNER)
    closemessage
  call(EnterFalkner_DetermineMovementBackToStove_Script)
  waitmovement(0)
  addvar(VAR_STORYLINE_STATE, 1)
  call(Act1_1_Check_Script)
  releaseall
  end
}

script EnterFalkner_DetermineMovementBackToStove_Script {
  getplayerxy(VAR_TEMP_0, VAR_TEMP_1)

  if (var (VAR_TEMP_0) == 7) {
    applymovement(FALKNER, EnterFalkner_WalkNorthBackToStove)
  } else {
    applymovement(FALKNER, EnterFalkner_WalkEastBackToStove)
  }
}

script EnterFalkner_PostDefeat1_Dialogue {
  lockall
  setvar(VAR_GYM_1_STATE,GYM_DEFEATED_LEADER)
  msgbox(gText_EnterFalkner_WithThatBadge,MSGBOX_NPC,SPEAKER_FALKNER)
      applymovement(FALKNER,Common_Movement_FaceUp)
    waitmovement(0)
  closemessage
  releaseall
  end
}

script EnterFalkner_PostDefeat2_Dialogue {
  lockall
  msgbox(gText_EnterFalkner_LetsCookTogether,,SPEAKER_FALKNER)
    closemessage
  releaseall
  end
}

movement EnterFalkner_WalkSouthToBattlefield {
  walk_down * 3
  walk_left * 2
  face_left
  step_end
}

movement EnterFalkner_WalkWestToBattlefield {
  walk_left * 2
  walk_down * 3
  face_left
  step_end
}
movement EnterFalkner_PlayerWalkToBattlefieldFromLeft {
  delay_16
  delay_16
  delay_16
  walk_left * 4
  walk_down * 3
  face_right
  step_end
}

movement EnterFalkner_PlayerWalkToBattlefieldFromDown {
  delay_16
  delay_16
  delay_16
  walk_left * 5
  walk_down * 2
  face_right
  step_end
}

movement EnterFalkner_PlayerWalkToBattlefieldFromRight {
  delay_16
  delay_16
  delay_16
  walk_left * 6
  walk_down * 3
  face_right
  step_end
}

movement EnterFalkner_WalkEastBackToStove {
  emote_exclamation_mark
  walk_right * 3
  walk_up * 3
  walk_right
  face_up
  walk_left
  face_up
  step_end
}

movement EnterFalkner_WalkNorthBackToStove {
  emote_exclamation_mark
  walk_up * 3
  walk_right * 3
  walk_right
  face_up
  walk_left
  face_up
  step_end
}

text gText_EnterFalkner_OneMoreLadle {
  format("Hold on! I've got one brushing of Pinap glaze, and then into the oven...")
}

text gText_EnterFalkner_MeatsInOven {
  format("Cool, cake's in the oven. ")
}

text gText_EnterFalkner_MyLatestCreation {
  format("Ticket said you're {PLAYER}, right? I'm Falkner, head baker of Poc-Chuc and the Gym Leader here. After you lose, I'll treat you to my latest creation: crepe cake with a Pinap glaze. It's my first time making it, but from that amazing smell, it's gonna be great.")
}

text gText_EnterFalkner_YourSenseOfTaste {
  format("Did you know that a lot of your sense of taste actually is because of your sense of smell? All of your senses work together to create your culinary experiences. I'll show how I do that in battle too! Let's see if I can overwhelm your palette before my cake burns!")
}

text gText_EnterFalkner_IThinkYouTalk {
  format("I think you talk more about food than you do battling...")
}

text gText_EnterFalkner_FirstHitOfSpice {
  format("Whoa, that felt just like the first hit of spice on the tip of my tongue! ")
}

text gText_EnterFalkner_SimmerWasntEnough {
  format("I've gotta crank the heat, and quick!")
}

text gText_EnterFalkner_WhatABitterDefeat {
  format("Ouch, what a bitter defeat. Try adding Sitrus Berries?")
}

text gText_EnterFalkner_WoahSuperSour {
  format("Woah, super sour! I'll add some Micle Berry.")
}

text gText_EnterFalkner_ThatWasDelicious {
  format("That was delicious! I'm absolutely satiated. Here's the FalknerBadge, and my favorite Technical Machine.")
}

text gText_EnterFalkner_OhTheMeat {
  format("Oh, the cake!")
}

text gText_EnterFalkner_BadgeGet {
  format("You recieved the FalknerBadge Badge!")
}

text gText_EnterFalkner_WithThatBadge {
  format("With that Badge, traded Pokémon up to Level Psflevel1 will now obey you. And Psfgymattacknum1 is one of my most delicious moves, it allows your Pokémon to Psfgymattackdesc1.")
}

text gText_EnterFalkner_LetsCookTogether {
  format("Let's bake together again soon! Now if you'll excuse me...")
}
