const PLAYER = OBJ_EVENT_ID_PLAYER
const BIANCA = 1
const ARIANA = 2
const GIOVANNI = 3
const ARCHER = 4
const PKMNA = 5
const PKMNB = 6

script WhyAreYouHelpingThem_HideObject_Script {
    setflag(FLAG_TEMP_3)
}

script WhyAreYouHelpingThem_ObjectCleanUp_Script {
        setflag(FLAG_TEMP_4)
    setflag(FLAG_TEMP_3)
        setflag(FLAG_TEMP_2)
        setflag(FLAG_TEMP_1)
}

script WhyAreYouHelpingThem_MoveObject_Script {
    setobjectxy(BIANCA,2,16)
        setobjectxyperm(BIANCA,2,16)
}

script WhyAreYouHelpingThem_MoveObjectBoss_Script {
    setobjectxy(BIANCA,2,16)
        setobjectxyperm(BIANCA,2,16)
        setobjectxy(GIOVANNI,7,3)
        setobjectxyperm(GIOVANNI,7,3)
}

script WhyAreYouHelpingThem_MoveObjectAriana_Script {
    setflag(FLAG_TEMP_2)
        setobjectxy(ARIANA,1,9)
        setobjectxyperm(ARIANA,1,9)
        setobjectxy(BIANCA,2,16)
        setobjectxyperm(BIANCA,2,16)
        turnobject(ARIANA,DIR_NORTH)
}

script WhyAreYouHelpingThem_BlowHoleWall {
    setmetatile(12,1,0x3F2,FALSE)
        special(DrawWholeMapView)
}

script WhyAreYouHelpingThem_ObjectSetUpPostSave_Script {
        setflag(FLAG_TEMP_1)
        setflag(FLAG_TEMP_2)
        setobjectxy(ARCHER,6,3)
        setobjectxyperm(ARCHER,6,3)
        turnobject(ARCHER,DIR_WEST)
        setobjectxy(BIANCA,2,16)
        setobjectxyperm(BIANCA,2,16)
}

script WhyAreYouHelpingThem_ObjectDebriefSetUp_Script {
        setflag(FLAG_TEMP_1)
        setflag(FLAG_TEMP_3)
        setflag(FLAG_TEMP_4)
        setobjectxyperm(4,26,4)
        setobjectxyperm(5,25,6)
        setobjectxyperm(6,27,6)
        setobjectxyperm(7,25,7)
        setobjectxyperm(8,27,7)
}

script WhyAreYouHelpingThem_BackAtCompound_ObjectSetUp_Script {
        setflag(FLAG_TEMP_3)

        setobjectxy(1,6,6)
        setobjectxy(2,6,5)
        setobjectxyperm(1,6,6)
        setobjectxyperm(2,6,5)
        turnobject(2,DIR_SOUTH)

        setobjectmovementtype(2,MOVEMENT_TYPE_FACE_DOWN)
        setobjectmovementtype(1,MOVEMENT_TYPE_FACE_UP)
}

script WhyAreYouHelpingThem_StartRaid {

    lock
        addvar(VAR_ROUTE118_STATE,1)

        playse(SE_FAILURE)
        playse(SE_FAILURE)
        playse(SE_FAILURE)
        playse(SE_FAILURE)
        applymovement(BIANCA,Common_Movement_FaceDown)
        applymovement(7,WhyAreYouHelpingThem_PoliceRunIn)
        applymovement(8,WhyAreYouHelpingThem_PoliceRunIn)
        applymovement(9,WhyAreYouHelpingThem_PoliceRunIn)
        waitmovement(0)
        msgbox(gText_WhyAreYouHelpingThem_StructuralBreach,MSGBOX_AUTOCLOSE)
        waitmovement(0)

        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_DefenseMode)

        setvar(VAR_0x8000, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_WeShouldRedefine)

        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_LetsSplitUp)

        setvar(VAR_0x8000, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_WhaFineIllTry)

        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_SeeYa)
        callnative(ClearMugshot)
        closemessage

        applymovement(BIANCA,WhyAreYouHelpingThem_PoliceRunIn)
        waitmovement(0)
        removeobject(7)
        removeobject(8)
        removeobject(9)
        call(WhyAreYouHelpingThem_MoveObject_Script)
        release
        end
}

script WhyAreYouHelpingThem_WitnessBattle {
    addvar(VAR_ROUTE118_STATE,1)
        lock
        applymovement(GIOVANNI,WhyAreYouHelpingThem_FleeTheBattle)
        waitmovement(0)
        call(WhyAreYouHelpingThem_MoveObjectBoss_Script)

        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_GoAfterIllHold)
        callnative(ClearMugshot)
        closemessage
        release
        end
}


script WhyAreYouHelpingThem_PreBattleBoss_Script {
    lock
        applymovement(GIOVANNI,Common_Movement_FaceLeft)
        applymovement(PLAYER,WhyAreYouHelpingThem_ApproachGiovanni)
        waitmovement(0)
        setvar(VAR_0x8000, MUGSHOT_GIOVANNI)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_ItsAShameThatThe)
        callnative(ClearMugshot)
        closemessage

        applymovement(GIOVANNI,Common_Movement_WalkLeft)
        waitmovement(0)

        setvar(VAR_0x8000, MUGSHOT_GIOVANNI)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_ButBeatingYou)
        callnative(ClearMugshot)
        closemessage

        trainerbattle_continue_after_lose(TRAINER_ARCHIE,gText_WhyAreYouHelpingThem_WaitAMinute)
        goto(WhyAreYouHelpingThem_PostBattleBoss_Dialogue)
}

script WhyAreYouHelpingThem_PostBattleBoss_Dialogue{
        addvar(VAR_ROUTE118_STATE,1)
        addobject(ARIANA)
        call(WhyAreYouHelpingThem_MoveObjectAriana_Script)

        if (defeated(TRAINER_ARCHIE)){

            setvar(VAR_0x8000, MUGSHOT_GIOVANNI)
                callnative(DrawMugshot)
                msgbox(gText_WhyAreYouHelpingThem_YoureIncredible)
                closemessage
                callnative(ClearMugshot)
                applymovement(GIOVANNI,Common_Movement_WalkInPlaceLeft)
                waitmovement(0)
                giveitem(ITEM_MASTER_BALL,1)

                setvar(VAR_0x8000, MUGSHOT_GIOVANNI)
                callnative(DrawMugshot)
                msgbox(gText_WhyAreYouHelpingThem_AndNowWeHave)
        }else {

            setvar(VAR_0x8000, MUGSHOT_HILDA)
                callnative(DrawMugshot)
                msgbox(gText_WhyAreYouHelpingThem_IDidntEvenTouch)
        }

    setvar(VAR_0x8000, MUGSHOT_GIOVANNI)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_ImSorryYouHad)
        callnative(ClearMugshot)
        closemessage

        applymovement(ARIANA,WhyAreYouHelpingThem_WalkPastPlayer)
        applymovement(GIOVANNI,WhyAreYouHelpingThem_HealAndFaceAdmin)
        waitmovement(0)
        call(Common_EventScript_OutOfCenterPartyHeal)

        setvar(VAR_0x8000, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_IHandledTheBrat)
        callnative(ClearMugshot)
        closemessage

        playse(SE_PIN)
        waitse
        applymovement(ARIANA,WhyAreYouHelpingThem_ExclaimAndFacePlayer)
        waitmovement(0)

        setvar(VAR_0x8000, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_DamnYouToo)

        setvar(VAR_0x8000, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_WhatWasAllThatShit)

        setvar(VAR_0x8000, MUGSHOT_GIOVANNI)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_WeveGotToHelp)
        callnative(ClearMugshot)
        closemessage

        applymovement(GIOVANNI,WhyAreYouHelpingThem_BeginEscape)
        applymovement(ARIANA,WhyAreYouHelpingThem_BeginEscape)
        waitmovement(0)
        goto(WhyAreYouHelpingThem_ExplodeWall_Script)
}

script WhyAreYouHelpingThem_ExplodeWall_Script {

    playse(SE_M_EXPLOSION)
        waitse
        setvar(VAR_0x8004,1)
        setvar(VAR_0x8005,2)
        setvar(VAR_0x8006,8)
        setvar(VAR_0x8007,5)
        special(ShakeCamera)
        waitstate
        call(WhyAreYouHelpingThem_BlowHoleWall)
        clearflag(FLAG_TEMP_3)
        playse(SE_EXIT)
        waitse
        addobject(ARCHER)
        applymovement(ARCHER,WhyAreYouHelpingThem_WalkOutOfHole)
        waitmovement(0)

        setvar(VAR_0x8000, MUGSHOT_HUGH)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_ThisPlaceIs)

        setvar(VAR_0x8000, MUGSHOT_GIOVANNI)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_WeCantLeavePeople)

        setvar(VAR_0x8000, MUGSHOT_HUGH)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_WeDontHaveTime)

        setvar(VAR_0x8000, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_IThinkTheyreRight)

        setvar(VAR_0x8000, MUGSHOT_GIOVANNI)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_Sigh)
        callnative(ClearMugshot)
        closemessage
        applymovement(GIOVANNI,WhyAreYouHelpingThem_WalkIntoHoleA)
        applymovement(ARIANA,WhyAreYouHelpingThem_WalkIntoHoleB)
        waitmovement(0)
        playse(SE_EXIT)
        waitse
        removeobject(ARIANA)
        removeobject(GIOVANNI)
        applymovement(ARCHER,WhyAreYouHelpingThem_ApproachPlayer)
        waitmovement(0)

        setvar(VAR_0x8000, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_YoureWorkingWith)
        callnative(ClearMugshot)
        call(Common_EventScript_SaveGame)
        goto(WhyAreYouHelpingThem_StartBattle_Script)
}

script WhyAreYouHelpingThem_InteractBattle_Script {
    lock
        faceplayer
        goto(WhyAreYouHelpingThem_StartBattle_Script)
}

script WhyAreYouHelpingThem_StartBattle_Script{
    lock
        setvar(VAR_0x8000, MUGSHOT_HUGH)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_AlwaysHaveBeen)
        trainerbattle_no_intro(TRAINER_WALLY_VR_2,gText_WhyAreYouHelpingThem_AnotherVictory)
        closemessage
        callnative(ClearMugshot)
        goto(WhyAreYouHelpingThem_PostBattle_Dialogue)
}

script WhyAreYouHelpingThem_PostBattle_Dialogue {

        addvar(VAR_ROUTE118_STATE,1)
    setvar(VAR_0x8000, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_TellsMeYoureAll)

        setvar(VAR_0x8000, MUGSHOT_HUGH)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_WhyWouldIt)

        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_Player)
        callnative(ClearMugshot)
        closemessage

        applymovement(PLAYER,Common_Movement_FaceLeft)
        applymovement(ARCHER,WhyAreYouHelpingThem_WalkIntoHoleC)
        waitmovement(0)
        playse(SE_EXIT)
        waitse
        removeobject(ARCHER)
        setobjectxyperm(BIANCA,1,8)
        addobject(BIANCA)
        applymovement(BIANCA,WhyAreYouHelpingThem_RunToPlayer)
        waitmovement(0)

        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_IGotKnockedOut)

        setvar(VAR_0x8000, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_NoILostToo)
        callnative(ClearMugshot)
        closemessage
        addvar(VAR_NATIVERIVALBETA_STATE,1)
        fadescreen(FADE_TO_BLACK)
        warpsilent(MAP_ALAMEDA,255,26,6)
}


script WhyAreYouHelpingThem_MissionDebrief_Dialogue {
        addvar(VAR_NATIVERIVALBETA_STATE,1)
        addvar(VAR_LITTLEROOT_INTRO_STATE,1)
        lock
        applymovement(PLAYER,Common_Movement_FaceUp)

        msgbox(gText_WhyAreYouHelpingThem_GreatWorkInThere)
        closemessage

        applymovement(6,Common_Movement_WalkUp)
        waitmovement(0)

        msgbox(gText_WhyAreYouHelpingThem_WeSpottedAndTheir)
        closemessage

        applymovement(6,WhyAreYouHelpingThem_BackInFormation)
        waitmovement(0)

        msgbox(gText_WhyAreYouHelpingThem_AShameStillWeveShutDown)
        closemessage

        applymovement(4,Common_Movement_WalkDown)
        waitmovement(0)

        msgbox(gText_WhyAreYouHelpingThem_NotBadInThere)
        closemessage
        release
        end
}

script WhyAreYouHelpingThem_BackAtCompound_Dialogue {
        addvar(VAR_STORYLINE_STATE,1)
        addvar(VAR_LITTLEROOT_INTRO_STATE,1)
        applymovement(PLAYER,WhyAreYouHelpingThem_WalkIntoHome)
        waitmovement(0)

        setvar(VAR_0x8000, MUGSHOT_LANCE)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_WhatADay)

        setvar(VAR_0x8000, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_ItWasADay)

        setvar(VAR_0x8000, MUGSHOT_LANCE)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_GoodNewsWhileYouWere)

        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_OhThatWasFast)

        setvar(VAR_0x8000, MUGSHOT_LANCE)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_DontWorryAboutIt)

        setvar(VAR_0x8000, MUGSHOT_LANCE)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_NowIveGotALot)
        callnative(ClearMugshot)
        closemessage

        applymovement(2,WhyAreYouHelpingThem_LeaveCompoundA)
        waitmovement(0)
        playse(SE_EXIT)
        waitse
        removeobject(2)

        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_WhyAreYouHelpingThem_WeveAllGot)
        callnative(ClearMugshot)
        closemessage


        applymovement(BIANCA,WhyAreYouHelpingThem_LeaveCompoundB)
        waitmovement(0)
        playse(SE_EXIT)
        waitse
        removeobject(1)
        release
        end
}

script WhyAreYouHelpingThem_StartChampionship_Dialogue {
        addvar(VAR_NATIVERIVALBETA_STATE,1) //advance Alameda forward to clean up raid damage
    msgbox(gText_WhyAreYouHelpingThem_AreYouReadyToBegin,MSGBOX_YESNO)

        if (var(VAR_RESULT) == NO){
            closemessage
                return
        } else {
            addvar(VAR_STORYLINE_STATE,1)
                warpsilent(MAP_CHASECENTER_LOCKERS,9,6)
        }
}

text gText_WhyAreYouHelpingThem_StructuralBreach {
    format("STRUCTURAL BREACH DETECTED. BASE IS NOW SHIFTING TO DEFENSE MODE. EVACUATE IMMEDIATELY.")
}

text gText_WhyAreYouHelpingThem_DefenseMode {
    format("“Defense mode” is pretty advanced for some abandoned buildings.")
}

text gText_WhyAreYouHelpingThem_WeShouldRedefine {
    format("We should redefine abandoned…")
}

text gText_WhyAreYouHelpingThem_LetsSplitUp {
    format("Let's split up. I'm faster than you and can cut off the leaders before they escape.")
}

text gText_WhyAreYouHelpingThem_WhaFineIllTry {
    format("wha - fine, I'll try to find the source of the hack and shut it down.")
}

text gText_WhyAreYouHelpingThem_SeeYa {
    format("See ya.")
}

text gText_WhyAreYouHelpingThem_GoAfterIllHold {
    format("Go after Giovanni! I'll hold this one off.")
}

text gText_WhyAreYouHelpingThem_ItsAShameThatThe {
    format("It's a shame that the cops have totally infiltrated our youth. ")
}

text gText_WhyAreYouHelpingThem_ButBeatingYou {
    format("But beating you is going to be the best route out of here.")
}

text gText_WhyAreYouHelpingThem_WaitAMinute {
    format("Wait a minute…")
}

text gText_WhyAreYouHelpingThem_YoureIncredible {
    format("You're incredible. Or a cheater. Can't quite tell. Either way, you can have this.")
}

text gText_WhyAreYouHelpingThem_AndNowWeHave {
    format("And now we have to act like I won.")
}

text gText_WhyAreYouHelpingThem_IDidntEvenTouch {
    format("…I didn't even touch him…")
}

text gText_WhyAreYouHelpingThem_ImSorryYouHad {
    format("I'm sorry you had to experience that. I hope you eventually understand what's at stake here, {PLAYER}. Let me help you.")
}

text gText_WhyAreYouHelpingThem_IHandledTheBrat {
    format("I handled the brat. I'm guessing you beat -")
}

text gText_WhyAreYouHelpingThem_DamnYouToo {
    format("Damn, you too, {PLAYER}?")
}

text gText_WhyAreYouHelpingThem_WhatWasAllThatShit {
    format("What was all that shit you tried to tell me before?! What the hell is TeamRocket doing?")
}

text gText_WhyAreYouHelpingThem_WeveGotToHelp {
    format("We've got to help more comrades escape. Another time.")
}

text gText_WhyAreYouHelpingThem_ThisPlaceIs {
    format("This place is swarming with cops. You two need to get the hell out, right now. This goes straight out.")
}

text gText_WhyAreYouHelpingThem_WeCantLeavePeople {
    format("We can't leave people behind. ")
}

text gText_WhyAreYouHelpingThem_WeDontHaveTime {
    format("We don't have time for this! If you get arrested, this all falls apart! You. Need. To. Go.")
}

text gText_WhyAreYouHelpingThem_IThinkTheyreRight {
    format("I think they're right.")
}

text gText_WhyAreYouHelpingThem_Sigh {
    format("…*sigh*")
}

text gText_WhyAreYouHelpingThem_YoureWorkingWith {
    format("You're working with TeamRocket too?!?")
}

text gText_WhyAreYouHelpingThem_AlwaysHaveBeen {
    format("Always have been.")
}

text gText_WhyAreYouHelpingThem_AnotherVictory {
    format("*sigh* Another victory for the establishment.")
}

text gText_WhyAreYouHelpingThem_TellsMeYoureAll {
    format("Ariana tells me you're all good guys… now all this shit? And you? None of this makes any sense!")
}

text gText_WhyAreYouHelpingThem_WhyWouldIt {
    format("Why would it? You don't see what your bosses are doing to SanFran. You don't see what you're doing to SanFran.")
}

text gText_WhyAreYouHelpingThem_Player {
    format("{PLAYER}!")
}

text gText_WhyAreYouHelpingThem_IGotKnockedOut {
    format("I got knocked out. Did you stop Giovanni?")
}

text gText_WhyAreYouHelpingThem_NoILostToo {
    format("No, I lost too.")
}

text gText_WhyAreYouHelpingThem_GreatWorkInThere {
    format("Great work in there, team. Andre, what's the sitrep regarding TeamRocket leadership?")
}

text gText_WhyAreYouHelpingThem_WeSpottedAndTheir {
    format("We spotted Giovanni and their lieutenant escaping on Pokemon by using the move Fly. We were unable to track them given our resources.")
}

text gText_WhyAreYouHelpingThem_AShameStillWeveShutDown {
    format("A shame. Still, we've shut down their primary base of operations, and we arrested a bunch of these shitheads. This was an absolute win!")
}

text gText_WhyAreYouHelpingThem_NotBadInThere {
    format("Not bad in there. Oak wants you kids back at Southbay Compound.")
}

text gText_WhyAreYouHelpingThem_WhatADay {
    format("What… a day. I know that was a lot of pressure, but from what I hear, you were both fantastic. Thank you.")
}

text gText_WhyAreYouHelpingThem_ItWasADay {
    format("…it was a day.")
}

text gText_WhyAreYouHelpingThem_GoodNewsWhileYouWere {
    format("Good news - while you were training in Muirwoods, construction on Chasecenter finished up. The Group Stage will start in 3 days. Hopefully, that's enough time to rest up and get ready.")
}

text gText_WhyAreYouHelpingThem_OhThatWasFast {
    format("Oh, that was fast. How did you…")
}

text gText_WhyAreYouHelpingThem_DontWorryAboutIt {
    format("Don't worry about it! You two just focus on winning! ")
}

text gText_WhyAreYouHelpingThem_NowIveGotALot {
    format("Now, I've got a lot of work to do between now and then. I'll see you both in a few days!")
}

text gText_WhyAreYouHelpingThem_WeveAllGot {
    format("We've all got some work to do! I'll see you around, {PLAYER}.")
}

text gText_WhyAreYouHelpingThem_AreYouReadyToBegin {
    format("Are you ready to begin the Championship?")
}

movement WhyAreYouHelpingThem_PoliceRunIn {
    walk_left * 8
        step_end
}

movement WhyAreYouHelpingThem_FleeTheBattle {
    walk_up * 4
        step_end
}

movement WhyAreYouHelpingThem_ApproachGiovanni {
    walk_up * 2
        walk_right * 4
        step_end
}

movement WhyAreYouHelpingThem_WalkPastPlayer {
        walk_up * 4
    walk_right * 5
        walk_up
        step_end
}

movement WhyAreYouHelpingThem_BeginEscape {
    walk_right * 3
        step_end
}

movement WhyAreYouHelpingThem_ExclaimAndFacePlayer {
    emote_exclamation_mark
        face_left
        step_end
}

movement WhyAreYouHelpingThem_WalkOutOfHole {
    walk_down 
        walk_left * 3
        face_down
        step_end
}

movement WhyAreYouHelpingThem_WalkIntoHoleA {
    walk_right * 3
        walk_up * 2
        step_end
}

movement WhyAreYouHelpingThem_WalkIntoHoleB {
    walk_right * 3
        walk_up * 3
        step_end
}

movement WhyAreYouHelpingThem_WalkIntoHoleC {
    walk_right * 6
        walk_up * 2
        step_end
}

movement WhyAreYouHelpingThem_ApproachBossAndPlayer {
    walk_left
        walk_up * 8
        step_end
}

movement WhyAreYouHelpingThem_HealAndFaceAdmin {
    walk_in_place_slow_left
        delay_16
        delay_16
        face_down
        step_end
}

movement WhyAreYouHelpingThem_ApproachPlayer {
    walk_down
        walk_left * 3
        step_end
}

movement WhyAreYouHelpingThem_RunToPlayer {
    walk_up * 5
        walk_right * 3
        step_end
}

movement WhyAreYouHelpingThem_BackInFormation {
    lock_facing_direction
        walk_down
        unlock_facing_direction
        step_end
}

movement WhyAreYouHelpingThem_WalkToPlayerFormation {
    walk_right
        walk_down
        step_end
}

movement WhyAreYouHelpingThem_WalkIntoHome {
    walk_up * 3
        step_end
}

movement WhyAreYouHelpingThem_LeaveCompoundA {
    walk_right
        walk_down * 4
        walk_left
        walk_in_place_down
        face_down
        step_end
}

movement WhyAreYouHelpingThem_LeaveCompoundB {
    walk_down * 3
        step_end
}
