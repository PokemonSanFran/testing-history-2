const PLAYER = OBJ_EVENT_ID_PLAYER
const PSFTRANSRIVAL = 1
const PSFBADADMIN = 2
const PSFGOODBOSS = 3
const GOODTEAMA = 4
const GOODTEAMB = 5
const POLICEA = 12
const POLICEB = 13
const PSFBADBOSS = 14

script VSGarbodor_Arrest_ObjectSetup_Script {
    clearflag(FLAG_HIDE_THESTRIKESTRIKESBACK_PSFGOODTEAM)
    clearflag(FLAG_HIDE_THESTRIKESTRIKESBACK_PSFBADADMIN)
    clearflag(FLAG_HIDE_THESTRIKESTRIKESBACK_PSFTRANSRIVAL)

    setobjectxy(GOODTEAMA, 26, 19)
    setobjectxy(GOODTEAMB, 26, 20)
    setobjectxy(PSFTRANSRIVAL, 21, 21)
    setobjectxy(PSFBADADMIN, 21, 22)

    setobjectxyperm(GOODTEAMA, 26, 19)
    setobjectxyperm(GOODTEAMB, 26, 20)
    setobjectxyperm(PSFTRANSRIVAL, 21, 21)
    setobjectxyperm(PSFBADADMIN, 21, 22)
}

script VSGarbodor_Scouting_Dialogue {
    lockall
    applymovement(PLAYER, Common_Movement_FaceRight)
    waitmovement(0)
    setvar(VAR_0x8000, MUGSHOT_BIANCA)
    callnative(DrawMugshot)
    msgbox(gText_VSGarbodor_IDidSomeScouting)
    multichoicedefault(18, 6, MULTI_GARBADOR_CHOOSE, 3, TRUE)
    goto(VSGarbodor_ChooseSide_Script)
}

script VSGarbodor_RandomSide_Script {
    random(2)
    goto(VSGarbodor_ChooseSide_Script)
}

script VSGarbodor_ChooseSide_Script {
    switch (var (VAR_RESULT)) {
    case 0:
        goto(VSGarbodor_ChooseLeft_Script)
    case 1:
        goto(VSGarbodor_ChooseRight_Script)
    case 2:
        goto(VSGarbodor_RandomSide_Script)
    }
}

script VSGarbodor_ChooseLeft_Script {
    setvar(VAR_ROUTE111_STATE, 1)
    setvar(VAR_0x8000, MUGSHOT_BIANCA)
    callnative(DrawMugshot)
    msgbox(gText_VSGarbodor_ThenIllTakeTheRight)
    callnative(ClearMugshot)
    closemessage

    applymovement(PSFTRANSRIVAL, VSGarbodor_WalkFurtherRight)
    delay(60)
    applymovement(PLAYER, VSGarbodor_WalkTowardsLeftSide)
    waitmovement(0)
    removeobject(PSFTRANSRIVAL)
    releaseall
    end
}

script VSGarbodor_ChooseRight_Script {
    setvar(VAR_ROUTE111_STATE, 2)
    setvar(VAR_0x8000, MUGSHOT_BIANCA)
    callnative(DrawMugshot)
    msgbox(gText_VSGarbodor_CoolIWanted)
    callnative(ClearMugshot)
    closemessage

    applymovement(PSFTRANSRIVAL, VSGarbodor_WalkFurtherLeft)
    delay(60)
    applymovement(PLAYER, VSGarbodor_WalkTowardsRightSide)
    waitmovement(0)
    removeobject(PSFTRANSRIVAL)
    releaseall
    end
}

script VSGarbodor_PsfgoodteamGrunt1_Battle_Script {
    trainerbattle_single(TRAINER_GRUNT_AQUA_HIDEOUT_5, gText_VSGarbodor_ScrewThisStadium, gText_VSGarbodor_DamnTheyWereRight, VSGarbodor_PsfgoodteamGrunt1_Battle_Script)
    msgbox(gText_VSGarbodor_WelpIDidWhat, MSGBOX_AUTOCLOSE)
    goto(VSGarbodor_RemoveGrunt)
    end
}

script VSGarbodor_PsfgoodteamGrunt3_Battle_Script {
    trainerbattle_single(TRAINER_GRUNT_AQUA_HIDEOUT_3, gText_VSGarbodor_IsTheName, gText_VSGarbodor_TheyDidntPrepare, VSGarbodor_PsfgoodteamGrunt3_Battle_Script)
    msgbox(gText_VSGarbodor_OhThankGiratina, MSGBOX_AUTOCLOSE)
    goto(VSGarbodor_RemoveGrunt)
    end
}

script VSGarbodor_PsfgoodteamGrunt2_Battle_Script {
    trainerbattle_single(TRAINER_GRUNT_AQUA_HIDEOUT_2, gText_VSGarbodor_RightOnCue, gText_VSGarbodor_HmmIGuess, VSGarbodor_PsfgoodteamGrunt2_Battle_Script)
    msgbox(gText_VSGarbodor_BeingBad, MSGBOX_AUTOCLOSE)
    goto(VSGarbodor_RemoveGrunt)
    end
}

script VSGarbodor_PsfgoodteamGrunt4_Battle_Script {
    trainerbattle_single(TRAINER_GRUNT_AQUA_HIDEOUT_4, gText_VSGarbodor_LookAtYou, gText_VSGarbodor_ThatsRight, VSGarbodor_PsfgoodteamGrunt4_Battle_Script)
    msgbox(gText_VSGarbodor_HaveFunCleaning, MSGBOX_AUTOCLOSE)
    goto(VSGarbodor_RemoveGrunt)
    end
}

script VS_Garbodor_CoreEncounter_Script {
    call(Common_EventScript_SaveGame)
    faceplayer
    setwildbattle(SPECIES_GARBODOR, 40, ITEM_NONE)
    waitse
    playmoncry(SPECIES_GARBODOR, CRY_MODE_ENCOUNTER)
    msgbox(gText_VSGarbodor_Kuh)
    delay(40)
    waitmoncry
    setflag(FLAG_SYS_CTRL_OBJ_DELETE)
    dowildbattle
    clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
    specialvar(VAR_RESULT, GetBattleOutcome)
    switch (var (VAR_RESULT)) {
    case B_OUTCOME_WON:
        goto(VSGarbodor_RemoveGrunt)
    }
    release
    end
}

script VSGarbodor_RemoveGrunt {
    lockall
    closemessage
    fadescreenswapbuffers(FADE_TO_BLACK)
    removeobject(VAR_LAST_TALKED)
    fadescreenswapbuffers(FADE_FROM_BLACK)
    call(VSGarbodor_CheckDefeated_Script)
    releaseall
    end
}

script VSGarbodor_CheckDefeated_Script {
    if (flag(FLAG_DEFEATED_EVIL_TEAM_MT_CHIMNEY) && flag(FLAG_DEFEATED_RIVAL_ROUTE_104) && flag(FLAG_DEFEATED_VOLTORB_1_NEW_MAUVILLE) && flag(FLAG_DEFEATED_VOLTORB_2_NEW_MAUVILLE)) {
        goto(VSGarbodor_MovePsftransrival_Script)
    }
    end
}

script VSGarbodor_PreventCrossingSides_Script {
    lockall
    setvar(VAR_0x8000, MUGSHOT_BIANCA)
    callnative(DrawMugshot)
    msgbox(gText_VSGarbodor_FeelingLonely)
    closemessage
    callnative(ClearMugshot)

    if (var (VAR_ROUTE111_STATE) == 2) {
        applymovement(PLAYER, Common_Movement_WalkRight)
    } else {
        applymovement(PLAYER, Common_Movement_WalkLeft)
    }
    waitmovement(0)
    releaseall
    end
}

script VSGarbodor_MovePsftransrival_Script {
    setvar(VAR_ROUTE112_STATE, 1)
    lockall

    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    clearflag(FLAG_TEMP_1)
    addobject(PSFTRANSRIVAL)

    if (var (VAR_TEMP_0) < 10) {
        applymovement(PLAYER, Common_Movement_FaceRight)
        if (var (VAR_TEMP_1) > 9) {
            setobjectxy(PSFTRANSRIVAL, 11, 12)
            copyobjectxytoperm(PSFTRANSRIVAL)
            applymovement(PSFTRANSRIVAL, PsftransrivalApproachSW)
        } else {
            setobjectxy(PSFTRANSRIVAL, 13, 6)
            copyobjectxytoperm(PSFTRANSRIVAL)
            applymovement(PSFTRANSRIVAL, PsftransrivalApproachNW)
        }
    } else {
        if (var (VAR_TEMP_1) > 9) {
            applymovement(PLAYER, Common_Movement_FaceUp)
            setobjectxy(PSFTRANSRIVAL, 22, 4)
            copyobjectxytoperm(PSFTRANSRIVAL)
            applymovement(PSFTRANSRIVAL, PsftransrivalApproachSE)
        } else {
            applymovement(PLAYER, Common_Movement_FaceLeft)
            setobjectxy(PSFTRANSRIVAL, 10, 6)
            copyobjectxytoperm(PSFTRANSRIVAL)
            applymovement(PSFTRANSRIVAL, PsftransrivalApproachNE)
        }
    }
    waitmovement(0)
    goto(VSGarbodor_DefeatedAll_Dialogue)
}

script VSGarbodor_DefeatedAll_Dialogue {
    setvar(VAR_0x8000, MUGSHOT_BIANCA)
    callnative(DrawMugshot)
    msgbox(gText_VSGarbodor_GodYouWereTaking)
    closemessage
    callnative(ClearMugshot)

    applymovement(PSFTRANSRIVAL, VSGarbodor_LookAround)
    waitmovement(0)
    setvar(VAR_0x8000, MUGSHOT_BIANCA)
    callnative(DrawMugshot)
    msgbox(gText_VSGarbodor_WhateverMineWereEasy)
    closemessage
    callnative(ClearMugshot)
    warpsilent(MAP_PSFROUTE22, 255, 20, 22)
    delay(100)
}

script VSGarbodor_Arrest_Script {
    setvar(VAR_STORYLINE_STATE,20)
    setvar(VAR_ROUTE112_STATE,2)
    setvar(VAR_ROUTE110_STATE,3)

    applymovement(PSFBADBOSS,Common_Movement_FaceUp)
    applymovement(PSFTRANSRIVAL,Common_Movement_FaceUp)
    applymovement(PSFBADADMIN,Common_Movement_FaceUp)
    applymovement(PLAYER,Common_Movement_FaceUp)
    waitmovement(0)

    applymovement(GOODTEAMA, VSGarbodor_PerpWalk)
    applymovement(GOODTEAMB, VSGarbodor_PerpWalk)
    applymovement(POLICEA, VSGarbodor_PerpWalk)
    applymovement(POLICEB, VSGarbodor_PerpWalk)
    waitmovement(0)

    call(VSGarbodor_CleanUpPerpWalk)
    applymovement(PSFBADBOSS, Common_Movement_FaceDown)
    applymovement(PSFTRANSRIVAL, Common_Movement_FaceDown)
    waitmovement(0)
    goto(VSGarbodor_Negotiating_Dialogue)
}

script VSGarbodor_CleanUpPerpWalk {
    removeobject(GOODTEAMA)
    removeobject(GOODTEAMB)
    removeobject(POLICEA)
    removeobject(POLICEB)
    removeobject(PSFGOODBOSS)
}

script VSGarbodor_Arrest_ObjectCleanup_Script {
    removeobject(PSFBADADMIN)
    removeobject(PSFBADBOSS)
    setflag(FLAG_HIDE_THESTRIKESTRIKESBACK_PSFBADADMIN)
}

script VSGarbodor_Negotiating_Dialogue {
    setvar(VAR_0x8000, MUGSHOT_GHETSIS)
    callnative(DrawMugshot)
    msgbox(gText_VSGarbodor_AsWeWereNegotiating)

    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_VSGarbodor_DoneLikeYouGuys)

    setvar(VAR_0x8000, MUGSHOT_GHETSIS)
    callnative(DrawMugshot)
    msgbox(gText_VSGarbodor_ThankYouBoth)
    callnative(ClearMugshot)
    closemessage

    setvar(VAR_0x8000, MUGSHOT_BIANCA)
    callnative(DrawMugshot)
    msgbox(gText_VSGarbodor_YeahImBeat, MSGBOX_YESNO)

    if (var (VAR_RESULT) == NO) {

        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_VSGarbodor_WeirdButOkay)
        callnative(ClearMugshot)
        closemessage
        call(VSGarbodor_FlyWithoutPlayer_Script)
        releaseall
        end
    } else {

        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_VSGarbodor_LetsBounce)
        callnative(ClearMugshot)
        closemessage
        warpsilent(MAP_PSFCITY8_COMPOUND_1F, 7, 3)
        releaseall
        end
    }
}

script VSGarbodor_FlyWithoutPlayer_Script {
        applymovement(PSFTRANSRIVAL, Common_Movement_WalkInPlaceFasterDown)
        waitmovement(0)
        delay(50)
        setfieldeffectargument(0,1)
        dofieldeffect(FLDEFF_NPCFLY_OUT)
        delay(15)
        removeobject(PSFTRANSRIVAL)
        waitfieldeffect(FLDEFF_NPCFLY_OUT)
}

script VSGarbodor_ReturnToTheCompound_ObjectSetUp {
    clearflag(FLAG_HIDE_COMPOUND_PSFTRANSRIVAL)
    addobject(PSFTRANSRIVAL)
    setobjectxy(PSFTRANSRIVAL,7,2)
    setobjectxyperm(PSFTRANSRIVAL,7,2)
    setobjectmovementtype(PSFTRANSRIVAL,MOVEMENT_ACTION_FACE_DOWN)

}

script VSGarbodor_ReturnToTheCompound_Dialogue {
    applymovement(PLAYER,Common_Movement_FaceUp)
    waitmovement(0)
    setvar(VAR_0x8000, MUGSHOT_BIANCA)
    callnative(DrawMugshot)
    msgbox(gText_VSGarbodor_ImGonnaGet)

    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_VSGarbodor_HehWellSee)
    callnative(ClearMugshot)
    closemessage

    applymovement(PSFTRANSRIVAL, VSGarbodor_WalkUpstairs)
    waitmovement(0)
    playse(SE_EXIT)
    waitse
    removeobject(PSFTRANSRIVAL)
    setvar(VAR_STORYLINE_STATE,21)

    releaseall
    end
}

movement VSGarbodor_WalkTowardsLeftSide {
    walk_up * 8
    walk_left * 2
    step_end
}

movement VSGarbodor_WalkFurtherLeft {
    walk_up * 15
    walk_left * 8
    step_end
}

movement VSGarbodor_WalkFurtherRight {
    walk_up * 8
    walk_right * 8
    step_end
}

movement VSGarbodor_WalkTowardsRightSide {
    walk_up * 8
    walk_right * 2
    step_end
}

movement VSGarbodor_LookAround {
    face_up
    delay_16
    face_left
    delay_16
    face_right
    delay_16
    step_end
}
movement PsftransrivalApproachSW {
    walk_left * 6
    step_end
}
movement PsftransrivalApproachNW {
    walk_left * 6
    step_end
}
movement PsftransrivalApproachSE {
    walk_down * 6
    step_end
}
movement PsftransrivalApproachNE {
    walk_right * 7
    step_end
}

movement VSGarbodor_PerpWalk {
    walk_left * 15
    step_end
}

movement VSGarbodor_WalkUpstairs {
    walk_up
    step_end
}

text gText_VSGarbodor_IDidSomeScouting {
    format("I did some scouting while waiting for you. Both sides have two Garbodor and two Trainers guarding ‘em. Do you care which side you take?")
}

text gText_VSGarbodor_ThenIllTakeTheRight {
    format("Then I'll take the right side. See you on the other side. Don't take too long!")
}

text gText_VSGarbodor_CoolIWanted {
    format("Cool, I wanted the left side anyway.  See you on the other side. Don't take too long!")
}

text gText_VSGarbodor_ScrewThisStadium {
    format("Heh, we're Psfgoodteam! Screw this stadium and screw Psfbadguy!")
}

text gText_VSGarbodor_IsTheName {
    format("Psfgoodteam is the name, and shutting this shit down is our game!")
}

text gText_VSGarbodor_DamnTheyWereRight {
    format("Damn, they were right, you're strong…")
}

text gText_VSGarbodor_TheyDidntPrepare {
    format("They didn't prepare us for this!")
}

text gText_VSGarbodor_WelpIDidWhat {
    format("Welp, I did what I could! Good luck with that stinky shit.")
}

text gText_VSGarbodor_OhThankGiratina {
    format("Oh thank Giratina, I can go back to the office now…")
}

text gText_VSGarbodor_Kuh {
    format("Kuh!")
}

text gText_VSGarbodor_RightOnCue {
    format("Right on cue, here come the heroes…")
}

text gText_VSGarbodor_LookAtYou {
    format("Look at you, thinking you're making any difference!")
}

text gText_VSGarbodor_HmmIGuess {
    format("Hmm, I guess I get to be the bad guy today!")
}

text gText_VSGarbodor_ThatsRight {
    format("That's right, dance puppets!")
}

text gText_VSGarbodor_BeingBad {
    format("Being bad was fun for a day!")
}

text gText_VSGarbodor_HaveFunCleaning {
    format("Have fun cleaning up after us, kid.")
}

text gText_VSGarbodor_FeelingLonely {
    format("Feeling lonely over there? I got this side, you handle that side!")
}

text gText_VSGarbodor_GodYouWereTaking {
    format("God, you were taking forever. They don't teach efficiency in Orre, do they?")
}

text gText_VSGarbodor_WhateverMineWereEasy {
    format("Whatever. Mine were easy, they just both used Self-Destruct before I could really get a move in. I already texted Psfbadadmin, he said meet in front of the construction site.")
}

text gText_VSGarbodor_AsWeWereNegotiating {
    format("As we were negotiating, things started to get a little rowdy. The police aren't taking any chances this time, so they've halted escalation… for now. How's the Psfbadguy Task Force?")
}

text gText_VSGarbodor_DoneLikeYouGuys {
    format("Done. Like you guys said, Psfgoodteam up to their shit as usual.")
}

text gText_VSGarbodor_ThankYouBoth {
    format("Thank you both, and I'm sorry you had to get dragged into this. I'll work with our team to figure out what to do about the Championship. I think you two should head back to the Compound for now.")
}

text gText_VSGarbodor_YeahImBeat {
    format("Yeah, I'm beat. Do you want a ride back to the Compound?")
}

text gText_VSGarbodor_WeirdButOkay {
    format("Weird, but okay. I'll see you around.")
}

text gText_VSGarbodor_LetsBounce {
    format("Let's bounce.")
}

text gText_VSGarbodor_ImGonnaGet {
    format("I'm gonna get some sleep. Good shit on finishing your Badges. Can't wait to kick your ass at the Championship… whenever that is.")
}

text gText_VSGarbodor_HehWellSee {
    format("Heh, we'll see. Good night.")
}
