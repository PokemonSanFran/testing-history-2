const PLAYER = OBJ_EVENT_ID_PLAYER
const BIANCA = 1
const ARIANA = 2
const GIOVANNI = 3
const ARCHER = 4
const PKMNA = 5
const PKMNB = 6
const FLAG_HIDE_POLICE = FLAG_TEMP_1
const FLAG_HIDE_GIOVANNI_ARIANA = FLAG_TEMP_2
const FLAG_HIDE_ARCHER = FLAG_TEMP_3
const FLAG_HIDE_BIANCA = FLAG_TEMP_4

script WhyAreYouHelpingThem_HideObject_Script {
    setflag(FLAG_HIDE_ARCHER)
}

script WhyAreYouHelpingThem_ObjectCleanUp_Script {
    setflag(FLAG_HIDE_BIANCA)
    setflag(FLAG_HIDE_ARCHER)
    setflag(FLAG_HIDE_GIOVANNI_ARIANA)
    setflag(FLAG_HIDE_POLICE)
}

script WhyAreYouHelpingThem_MoveObject_Script {
    setobjectxy(BIANCA,2,16)
    setobjectxyperm(BIANCA,2,16)
    setflag(FLAG_HIDE_ARCHER)
    setflag(FLAG_HIDE_POLICE)
}

script WhyAreYouHelpingThem_MoveObjectBoss_Script {
    setobjectxy(BIANCA,2,16)
    setobjectxyperm(BIANCA,2,16)
    setobjectxy(GIOVANNI,7,3)
    setobjectxyperm(GIOVANNI,7,3)
}

script WhyAreYouHelpingThem_MoveObjectAriana_Script {
    setflag(FLAG_HIDE_GIOVANNI_ARIANA)
    setobjectxy(ARIANA,1,9)
    setobjectxyperm(ARIANA,1,9)
    setobjectxy(BIANCA,2,16)
    setobjectxyperm(BIANCA,2,16)
    turnobject(ARIANA,DIR_NORTH)
}

script WhyAreYouHelpingThem_BlowHoleWall {
    setmetatile(12,1,0x3F2,FALSE)
    special(DrawWholeMapView)
}

script WhyAreYouHelpingThem_ObjectSetUpPostSave_Script {
    setflag(FLAG_HIDE_POLICE)
    setflag(FLAG_HIDE_GIOVANNI_ARIANA)
    setobjectxy(ARCHER,6,3)
    setobjectxyperm(ARCHER,6,3)
    turnobject(ARCHER,DIR_WEST)
    setobjectxy(BIANCA,2,16)
    setobjectxyperm(BIANCA,2,16)
    return
}

script WhyAreYouHelpingThem_ObjectDebriefSetUp_Script {
    setflag(FLAG_HIDE_POLICE)
    setflag(FLAG_HIDE_ARCHER)
    setflag(FLAG_HIDE_BIANCA)
    setobjectxyperm(4,26,4)
    setobjectxyperm(5,25,6)
    setobjectxyperm(6,27,6)
    setobjectxyperm(7,25,7)
    setobjectxyperm(8,27,7)
    return
}

script WhyAreYouHelpingThem_BackAtCompound_ObjectSetUp_Script {
    clearflag(FLAG_HIDE_POLICE)
    clearflag(FLAG_HIDE_GIOVANNI_ARIANA)
    setflag(FLAG_HIDE_ARCHER)

    setobjectxy(1,6,6)
    setobjectxy(2,6,5)
    setobjectxyperm(1,6,6)
    setobjectxyperm(2,6,5)
    turnobject(2,DIR_SOUTH)

    setobjectmovementtype(2,MOVEMENT_TYPE_FACE_DOWN)
    setobjectmovementtype(1,MOVEMENT_TYPE_FACE_UP)
    return
}

script WhyAreYouHelpingThem_StartRaid {
    lock
    setvar(VAR_NAVALBASE_STATE,NAVALBASE_RAID_START)
    playse(SE_FAILURE)
    playse(SE_FAILURE)
    playse(SE_FAILURE)
    playse(SE_FAILURE)
    applymovement(BIANCA,Common_Movement_FaceDown)
    applymovement(7,WhyAreYouHelpingThem_PoliceRunIn)
    applymovement(8,WhyAreYouHelpingThem_PoliceRunIn)
    applymovement(9,WhyAreYouHelpingThem_PoliceRunIn)
    waitmovement(0)
    msgbox(gText_WhyAreYouHelpingThem_StructuralBreach,MSGBOX_AUTOCLOSE,SPEAKER_SPEAKER)
    waitmovement(0)

    msgbox(gText_WhyAreYouHelpingThem_DefenseMode,,SPEAKER_BIANCA)

    msgbox(gText_WhyAreYouHelpingThem_WeShouldRedefine,,SPEAKER_ZOEY)

    msgbox(gText_WhyAreYouHelpingThem_LetsSplitUp,,SPEAKER_BIANCA)

    msgbox(gText_WhyAreYouHelpingThem_WhaFineIllTry,,SPEAKER_ZOEY)

    msgbox(gText_WhyAreYouHelpingThem_SeeYa,,SPEAKER_BIANCA)

    closemessage

    applymovement(BIANCA,WhyAreYouHelpingThem_PoliceRunIn)
    waitmovement(0)
    removeobject(7)
    removeobject(8)
    removeobject(9)
    call(WhyAreYouHelpingThem_MoveObject_Script)
    release
    end
}

script WhyAreYouHelpingThem_WitnessBattle {
    setvar(VAR_NAVALBASE_STATE,CHASE_GIOVANNI)
        lock
        applymovement(GIOVANNI,WhyAreYouHelpingThem_FleeTheBattle)
        waitmovement(0)
        call(WhyAreYouHelpingThem_MoveObjectBoss_Script)

        msgbox(gText_WhyAreYouHelpingThem_GoAfterIllHold,,SPEAKER_BIANCA)

        closemessage
        release
        end
}


script WhyAreYouHelpingThem_PreBattleBoss_Script {
    lock
        applymovement(GIOVANNI,Common_Movement_FaceLeft)
        applymovement(PLAYER,WhyAreYouHelpingThem_ApproachGiovanni)
        waitmovement(0)
        msgbox(gText_WhyAreYouHelpingThem_ItsAShameThatThe,,SPEAKER_GIOVANNI)

        closemessage

        applymovement(GIOVANNI,Common_Movement_WalkLeft)
        waitmovement(0)

        msgbox(gText_WhyAreYouHelpingThem_ButBeatingYou,,SPEAKER_GIOVANNI)

        closemessage

        trainerbattle_continue_after_lose(TRAINER_GIOVANNI,gText_WhyAreYouHelpingThem_WaitAMinute)
        goto(WhyAreYouHelpingThem_PostBattleBoss_Dialogue)
}

script WhyAreYouHelpingThem_PostBattleBoss_Dialogue{
        setvar(VAR_NAVALBASE_STATE,SAVE_ARCHER_NAVALBASE)
        addobject(ARIANA)
        call(WhyAreYouHelpingThem_MoveObjectAriana_Script)

        if (defeated(TRAINER_GIOVANNI)){

                msgbox(gText_WhyAreYouHelpingThem_YoureIncredible,,SPEAKER_GIOVANNI)
                closemessage

                applymovement(GIOVANNI,Common_Movement_WalkInPlaceLeft)
                waitmovement(0)
                giveitem(ITEM_MASTER_BALL,1)

                msgbox(gText_WhyAreYouHelpingThem_AndNowWeHave,,SPEAKER_GIOVANNI)
        }else {

                msgbox(gText_WhyAreYouHelpingThem_IDidntEvenTouch,,SPEAKER_ZOEY)
        }

        msgbox(gText_WhyAreYouHelpingThem_ImSorryYouHad,,SPEAKER_GIOVANNI)

        closemessage

        applymovement(ARIANA,WhyAreYouHelpingThem_WalkPastPlayer)
        applymovement(GIOVANNI,WhyAreYouHelpingThem_HealAndFaceAdmin)
        waitmovement(0)
        call(Common_EventScript_OutOfCenterPartyHeal)

        msgbox(gText_WhyAreYouHelpingThem_IHandledTheBrat,,SPEAKER_ARIANA)

        closemessage

        playse(SE_PIN)
        waitse
        applymovement(ARIANA,WhyAreYouHelpingThem_ExclaimAndFacePlayer)
        waitmovement(0)

        msgbox(gText_WhyAreYouHelpingThem_DamnYouToo,,SPEAKER_ARIANA)

        msgbox(gText_WhyAreYouHelpingThem_WhatWasAllThatShit,,SPEAKER_ZOEY)

        msgbox(gText_WhyAreYouHelpingThem_WeveGotToHelp,,SPEAKER_GIOVANNI)

        closemessage

        applymovement(GIOVANNI,WhyAreYouHelpingThem_BeginEscape)
        applymovement(ARIANA,WhyAreYouHelpingThem_BeginEscape)
        waitmovement(0)
        goto(WhyAreYouHelpingThem_ExplodeWall_Script)
}

script WhyAreYouHelpingThem_ExplodeWall_Script {

    playse(SE_M_EXPLOSION)
        waitse
        setvar(VAR_0x8004,1)
        setvar(VAR_0x8005,2)
        setvar(VAR_0x8006,8)
        setvar(VAR_0x8007,5)
        special(ShakeCamera)
        waitstate
        call(WhyAreYouHelpingThem_BlowHoleWall)
        clearflag(FLAG_HIDE_ARCHER)
        playse(SE_EXIT)
        waitse
        addobject(ARCHER)
        applymovement(ARCHER,WhyAreYouHelpingThem_WalkOutOfHole)
        waitmovement(0)

        msgbox(gText_WhyAreYouHelpingThem_ThisPlaceIs,,SPEAKER_ARCHER)

        msgbox(gText_WhyAreYouHelpingThem_WeCantLeavePeople,,SPEAKER_GIOVANNI)

        msgbox(gText_WhyAreYouHelpingThem_WeDontHaveTime,,SPEAKER_ARCHER)

        msgbox(gText_WhyAreYouHelpingThem_IThinkTheyreRight,,SPEAKER_ARIANA)

        msgbox(gText_WhyAreYouHelpingThem_Sigh,,SPEAKER_GIOVANNI)

        closemessage
        applymovement(GIOVANNI,WhyAreYouHelpingThem_WalkIntoHoleA)
        applymovement(ARIANA,WhyAreYouHelpingThem_WalkIntoHoleB)
        waitmovement(0)
        playse(SE_EXIT)
        waitse
        removeobject(ARIANA)
        removeobject(GIOVANNI)
        applymovement(ARCHER,WhyAreYouHelpingThem_ApproachPlayer)
        waitmovement(0)

        msgbox(gText_WhyAreYouHelpingThem_YoureWorkingWith,,SPEAKER_ZOEY)

        startmenu_savescreen
        goto(WhyAreYouHelpingThem_StartBattle_Script)
}

script WhyAreYouHelpingThem_InteractBattle_Script {
    lock
        faceplayer
        goto(WhyAreYouHelpingThem_StartBattle_Script)
}

script WhyAreYouHelpingThem_StartBattle_Script{
    lock
        msgbox(gText_WhyAreYouHelpingThem_AlwaysHaveBeen,,SPEAKER_ARCHER)
        trainerbattle_no_intro(TRAINER_ARCHER_WHYAREYOUHELPINGTHEM,gText_WhyAreYouHelpingThem_AnotherVictory)
        closemessage

        goto(WhyAreYouHelpingThem_PostBattle_Dialogue)
}

script WhyAreYouHelpingThem_PostBattle_Dialogue {

        setvar(VAR_NAVALBASE_STATE,DEFEATED_ARCHER_NAVALBASE)
        msgbox(gText_WhyAreYouHelpingThem_TellsMeYoureAll,,SPEAKER_ZOEY)

        msgbox(gText_WhyAreYouHelpingThem_WhyWouldIt,,SPEAKER_ARCHER)

        msgbox(gText_WhyAreYouHelpingThem_Player,,SPEAKER_BIANCA)

        closemessage

        applymovement(PLAYER,Common_Movement_FaceLeft)
        applymovement(ARCHER,WhyAreYouHelpingThem_WalkIntoHoleC)
        waitmovement(0)
        playse(SE_EXIT)
        waitse
        removeobject(ARCHER)
        setobjectxyperm(BIANCA,1,8)
        addobject(BIANCA)
        applymovement(BIANCA,WhyAreYouHelpingThem_RunToPlayer)
        waitmovement(0)

        msgbox(gText_WhyAreYouHelpingThem_IGotKnockedOut,,SPEAKER_BIANCA)

        msgbox(gText_WhyAreYouHelpingThem_NoILostToo,,SPEAKER_ZOEY)

        closemessage
        setvar(VAR_ALAMEDA_STATE,WHY_ARE_YOU_HELPING_THEM)
        fadescreen(FADE_TO_BLACK)
        warpsilent(MAP_ALAMEDA,255,26,6)
}


script WhyAreYouHelpingThem_MissionDebrief_Dialogue {
        setvar(VAR_ALAMEDA_STATE,SLEPT_AFTER_RAID) //advance Alameda forward to clean up raid damage
        setvar(VAR_STORYLINE_STATE,STORY_COMPLETED_NAVAL_BASE)
        setvar(VAR_PLAYER_HOME_STATE,POST_NAVALBASE_RAID)
        lock
        applymovement(PLAYER,Common_Movement_FaceUp)

        msgbox(gText_WhyAreYouHelpingThem_GreatWorkInThere)
        closemessage

        applymovement(6,Common_Movement_WalkUp)
        waitmovement(0)

        msgbox(gText_WhyAreYouHelpingThem_WeSpottedAndTheir)
        closemessage

        applymovement(6,WhyAreYouHelpingThem_BackInFormation)
        waitmovement(0)

        msgbox(gText_WhyAreYouHelpingThem_AShameStillWeveShutDown)
        closemessage

        applymovement(4,Common_Movement_WalkDown)
        waitmovement(0)

        msgbox(gText_WhyAreYouHelpingThem_NotBadInThere)

        msgbox(gText_WhyAreYouHelpingThem_WannaCome,MSGBOX_YESNO,,SPEAKER_BIANCA)

        if (var(VAR_RESULT) == YES){
            closemessage
            warpsilent(MAP_SOUTHBAY_COMPOUND_1F,0)
        } else {
            closemessage
                release
                end
        }

    }

script WhyAreYouHelpingThem_BackAtCompound_Dialogue {
        lock
        setvar(VAR_PLAYER_HOME_STATE,NEXT_DAY_CHAMPIONSHIP)
        applymovement(PLAYER,WhyAreYouHelpingThem_WalkIntoHome)
        waitmovement(0)

        msgbox(gText_WhyAreYouHelpingThem_WhatADay,,SPEAKER_OAK)

        msgbox(gText_WhyAreYouHelpingThem_ItWasADay,,SPEAKER_ZOEY)

        msgbox(gText_WhyAreYouHelpingThem_GoodNewsWhileYouWere,,SPEAKER_OAK)

        msgbox(gText_WhyAreYouHelpingThem_OhThatWasFast,,SPEAKER_BIANCA)

        msgbox(gText_WhyAreYouHelpingThem_DontWorryAboutIt,,SPEAKER_OAK)

        msgbox(gText_WhyAreYouHelpingThem_NowIveGotALot,,SPEAKER_OAK)

        closemessage

        applymovement(2,WhyAreYouHelpingThem_LeaveCompoundA)
        waitmovement(0)
        playse(SE_EXIT)
        waitse
        removeobject(2)

        msgbox(gText_WhyAreYouHelpingThem_WeveAllGot,,SPEAKER_BIANCA)

        closemessage


        applymovement(BIANCA,WhyAreYouHelpingThem_LeaveCompoundB)
        waitmovement(0)
        playse(SE_EXIT)
        waitse
        removeobject(1)
        release
        end
}

script WhyAreYouHelpingThem_StartChampionship_Dialogue {
    msgbox(gText_WhyAreYouHelpingThem_AreYouReadyToBegin,MSGBOX_YESNO)

        if (var(VAR_RESULT) == NO){
            closemessage
                return
        } else {
            setvar(VAR_STORYLINE_STATE,STORY_START_CHAMPIONSHIP)
                warpsilent(MAP_CHASECENTER_LOCKERS,9,6)
        }
}

text gText_WhyAreYouHelpingThem_StructuralBreach {
    format("STRUCTURAL BREACH DETECTED. BASE IS NOW SHIFTING TO DEFENSE MODE. EVACUATE IMMEDIATELY.")
}

text gText_WhyAreYouHelpingThem_DefenseMode {
    format("“Defense mode” is pretty advanced for some abandoned buildings.")
}

text gText_WhyAreYouHelpingThem_WeShouldRedefine {
    format("We should redefine abandoned…")
}

text gText_WhyAreYouHelpingThem_LetsSplitUp {
    format("Let's split up. I'm faster than you and can cut off the leaders before they escape.")
}

text gText_WhyAreYouHelpingThem_WhaFineIllTry {
    format("wha - fine, I'll try to find the source of the hack and shut it down.")
}

text gText_WhyAreYouHelpingThem_SeeYa {
    format("See ya.")
}

text gText_WhyAreYouHelpingThem_GoAfterIllHold {
    format("Go after Giovanni! I'll hold this one off.")
}

text gText_WhyAreYouHelpingThem_ItsAShameThatThe {
    format("It's a shame that the cops have totally infiltrated our youth. ")
}

text gText_WhyAreYouHelpingThem_ButBeatingYou {
    format("But beating you is going to be the best route out of here.")
}

text gText_WhyAreYouHelpingThem_WaitAMinute {
    format("Wait a minute…")
}

text gText_WhyAreYouHelpingThem_YoureIncredible {
    format("You're incredible. Or a cheater. Can't quite tell. Either way, you can have this.")
}

text gText_WhyAreYouHelpingThem_AndNowWeHave {
    format("And now we have to act like I won.")
}

text gText_WhyAreYouHelpingThem_IDidntEvenTouch {
    format("…I didn't even touch him…")
}

text gText_WhyAreYouHelpingThem_ImSorryYouHad {
    format("I'm sorry you had to experience that. I hope you eventually understand what's at stake here, {PLAYER}. Let me help you.")
}

text gText_WhyAreYouHelpingThem_IHandledTheBrat {
    format("I handled the brat. I'm guessing you beat -")
}

text gText_WhyAreYouHelpingThem_DamnYouToo {
    format("Damn, you too, {PLAYER}?")
}

text gText_WhyAreYouHelpingThem_WhatWasAllThatShit {
    format("What was all that shit you tried to tell me before?! What the hell is TeamRocket doing?")
}

text gText_WhyAreYouHelpingThem_WeveGotToHelp {
    format("We've got to help more comrades escape. Another time.")
}

text gText_WhyAreYouHelpingThem_ThisPlaceIs {
    format("This place is swarming with cops. You two need to get the hell out, right now. This goes straight out.")
}

text gText_WhyAreYouHelpingThem_WeCantLeavePeople {
    format("We can't leave people behind. ")
}

text gText_WhyAreYouHelpingThem_WeDontHaveTime {
    format("We don't have time for this! If you get arrested, this all falls apart! You. Need. To. Go.")
}

text gText_WhyAreYouHelpingThem_IThinkTheyreRight {
    format("I think they're right.")
}

text gText_WhyAreYouHelpingThem_Sigh {
    format("…*sigh*")
}

text gText_WhyAreYouHelpingThem_YoureWorkingWith {
    format("You're working with TeamRocket too?!?")
}

text gText_WhyAreYouHelpingThem_AlwaysHaveBeen {
    format("Always have been.")
}

text gText_WhyAreYouHelpingThem_AnotherVictory {
    format("*sigh* Another victory for the establishment.")
}

text gText_WhyAreYouHelpingThem_TellsMeYoureAll {
    format("Ariana tells me you're all good guys… now all this shit? And you? None of this makes any sense!")
}

text gText_WhyAreYouHelpingThem_WhyWouldIt {
    format("Why would it? You don't see what your bosses are doing to SanFran. You don't see what you're doing to SanFran.")
}

text gText_WhyAreYouHelpingThem_Player {
    format("{PLAYER}!")
}

text gText_WhyAreYouHelpingThem_IGotKnockedOut {
    format("I got knocked out. Did you stop Giovanni?")
}

text gText_WhyAreYouHelpingThem_NoILostToo {
    format("No, I lost too.")
}

text gText_WhyAreYouHelpingThem_GreatWorkInThere {
    format("Great work in there, team. Andre, what's the sitrep regarding TeamRocket leadership?")
}

text gText_WhyAreYouHelpingThem_WeSpottedAndTheir {
    format("We spotted Giovanni and their lieutenant escaping on Pokemon by using the move Fly. We were unable to track them given our resources.")
}

text gText_WhyAreYouHelpingThem_AShameStillWeveShutDown {
    format("A shame. Still, we've shut down their primary base of operations, and we arrested a bunch of these shitheads. This was an absolute win!")
}

text gText_WhyAreYouHelpingThem_NotBadInThere {
    format("Not bad in there. Oak wants you kids back at Southbay Compound.")
}

text gText_WhyAreYouHelpingThem_WhatADay {
    format("What… a day. I know that was a lot of pressure, but from what I hear, you were both fantastic. Thank you.")
}

text gText_WhyAreYouHelpingThem_ItWasADay {
    format("…it was a day.")
}

text gText_WhyAreYouHelpingThem_GoodNewsWhileYouWere {
    format("Good news - while you were training in Muirwoods, construction on Chasecenter finished up. The Group Stage will start in 3 days. Hopefully, that's enough time to rest up and get ready.")
}

text gText_WhyAreYouHelpingThem_OhThatWasFast {
    format("Oh, that was fast. How did you…")
}

text gText_WhyAreYouHelpingThem_DontWorryAboutIt {
    format("Don't worry about it! You two just focus on winning! ")
}

text gText_WhyAreYouHelpingThem_NowIveGotALot {
    format("Now, I've got a lot of work to do between now and then. I'll see you both in a few days!")
}

text gText_WhyAreYouHelpingThem_WeveAllGot {
    format("We've all got some work to do! I'll see you around, {PLAYER}.")
}

text gText_WhyAreYouHelpingThem_AreYouReadyToBegin {
    format("Are you ready to begin the Championship?")
}

text gText_WhyAreYouHelpingThem_WannaCome{
    format("{PLAYER}, I'm gonna head over in a bit. Wanna come?")
}

movement WhyAreYouHelpingThem_PoliceRunIn {
    walk_left * 8
        step_end
}

movement WhyAreYouHelpingThem_FleeTheBattle {
    walk_up * 4
        step_end
}

movement WhyAreYouHelpingThem_ApproachGiovanni {
    walk_up * 2
        walk_right * 4
        step_end
}

movement WhyAreYouHelpingThem_WalkPastPlayer {
        walk_up * 4
    walk_right * 5
        walk_up
        step_end
}

movement WhyAreYouHelpingThem_BeginEscape {
    walk_right * 3
        step_end
}

movement WhyAreYouHelpingThem_ExclaimAndFacePlayer {
    emote_exclamation_mark
        face_left
        step_end
}

movement WhyAreYouHelpingThem_WalkOutOfHole {
    walk_down
        walk_left * 3
        face_down
        step_end
}

movement WhyAreYouHelpingThem_WalkIntoHoleA {
    walk_right * 3
        walk_up * 2
        step_end
}

movement WhyAreYouHelpingThem_WalkIntoHoleB {
    walk_right * 3
        walk_up * 3
        step_end
}

movement WhyAreYouHelpingThem_WalkIntoHoleC {
    walk_right * 6
        walk_up * 2
        step_end
}

movement WhyAreYouHelpingThem_ApproachBossAndPlayer {
    walk_left
        walk_up * 8
        step_end
}

movement WhyAreYouHelpingThem_HealAndFaceAdmin {
    walk_in_place_slow_left
        delay_16
        delay_16
        face_down
        step_end
}

movement WhyAreYouHelpingThem_ApproachPlayer {
    walk_down
        walk_left * 3
        step_end
}

movement WhyAreYouHelpingThem_RunToPlayer {
    walk_up * 5
        walk_right * 3
        step_end
}

movement WhyAreYouHelpingThem_BackInFormation {
    lock_facing_direction
        walk_down
        unlock_facing_direction
        step_end
}

movement WhyAreYouHelpingThem_WalkToPlayerFormation {
    walk_right
        walk_down
        step_end
}

movement WhyAreYouHelpingThem_WalkIntoHome {
    walk_up * 3
        step_end
}

movement WhyAreYouHelpingThem_LeaveCompoundA {
    walk_right
        walk_down * 4
        walk_left
        walk_in_place_down
        face_down
        step_end
}

movement WhyAreYouHelpingThem_LeaveCompoundB {
    walk_down * 3
        step_end
}
