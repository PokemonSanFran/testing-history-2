const PLAYER = OBJ_EVENT_ID_PLAYER
const BIANCA = 1
const SIEBOLD = 2
const FLAG_HIDE_BIANCA = FLAG_TEMP_1 
const FLAG_HIDE_SIEBOLD = FLAG_TEMP_2
const FLAG_HIDE_OAK = FLAG_TEMP_3
const FLAG_HIDE_ROBOTS = FLAG_TEMP_4

script EnterTheMaster_Muirwoods_Entrance_ObjectSetup_Script{
    if (var(VAR_MUIRWOODS_STATE) != BIANCA_WAITING_MUIRWOODS){
        setflag(FLAG_HIDE_BIANCA)
    }
    return
}

script EnterTheMaster_Muirwoods_ObjectSetup_Script{
    if (var(VAR_MUIRWOODS_STATE) != BIANCA_ENTERED_MUIRWOODS){
        setflag(FLAG_HIDE_BIANCA)
    }
    return
}

script EnterTheMaster_Muirwoods_Inner_ObjectSetup_Script{
    switch (var(VAR_MUIRWOODS_STATE)){
        case 0:
        case BIANCA_WAITING_MUIRWOODS:
        case BIANCA_ENTERED_MUIRWOODS:
        case POST_HAVE_YOU_SEEN_THE_NEWS: call(EnterTheMaster_Muirwoods_Inner_HideObjects_Script)
        case MUIRWOODS_MAZE_COMPLETE: 
        case MET_SIEBOLD:call(EnterTheMaster_Inner_Object_SetUp_Script)
        case FINISHED_SIEBOLD_PUZZLES: return
    }
    return
}

script EnterTheMaster_Muirwoods_Inner_HideObjects_Script{
    setflag(FLAG_HIDE_BIANCA)
        setflag(FLAG_HIDE_SIEBOLD)
        setflag(FLAG_HIDE_OAK)
        setflag(FLAG_HIDE_ROBOTS)
        return
}

script EnterTheMaster_Inner_Object_SetUp_Script {
    setflag(FLAG_HIDE_OAK)
        call_if_quest_active(QUEST_SIEBOLDSTRAINING,EnterTheMaster_RepositionBianca_Script)
        call_if_quest_complete(QUEST_SIEBOLDSTRAINING,EnterTheMaster_HideBianca_Script)
        return
}

script EnterTheMaster_RepositionBianca_Script{
    setobjectxy(BIANCA,2,5)
        setobjectxyperm(BIANCA,2,5)
        turnobject(BIANCA,DIR_NORTH)
        return
}

script EnterTheMaster_HideBianca_Script{
    setflag(FLAG_HIDE_BIANCA)
}

script EnterTheMaster_Interact_Script {
    lock
        faceplayer
        goto(EnterTheMaster_Ready_Dialogue)
}

script EnterTheMaster_WalkingTrigger_Script {
    lock
        applymovement(PLAYER,Common_Movement_FaceLeft)
        waitmovement(0)
        goto(EnterTheMaster_Ready_Dialogue)
}

script EnterTheMaster_Ready_Dialogue {

    setvar(VAR_MUIRWOODS_STATE,BIANCA_ENTERED_MUIRWOODS)
        msgbox(gText_EnterTheMaster_MorningReady,,SPEAKER_BIANCA)

        msgbox(gText_EnterTheMaster_AsReadyAs,MSGBOX_DEFAULT,SPEAKER_ZOEY)

        msgbox(gText_EnterTheMaster_DaddySentAnother,,SPEAKER_BIANCA)

        msgbox(gText_EnterTheMaster_ThanksDad,MSGBOX_DEFAULT,SPEAKER_ZOEY)
        closemessage

        applymovement(BIANCA,EnterTheMaster_WalkIntoWoods)
        waitmovement(0)
        playse(SE_EXIT)
        waitse
        removeobject(BIANCA)
        release
        end

        //PSF TODO implement follow me nonsense
}

script EnterTheMaster_InteractFollower_Script {

    lock
        random(4)

        switch(var(VAR_RESULT)){
            case 0: msgbox(gText_EnterTheMaster_HowDoYouThink,MSGBOX_NPC,SPEAKER_BIANCA)
            case 1: msgbox(gText_EnterTheMaster_YouKnow,MSGBOX_NPC,SPEAKER_BIANCA)
            case 2: msgbox(gText_EnterTheMaster_IThoughtGymLeaders,MSGBOX_NPC,SPEAKER_BIANCA)
            case 3: msgbox(gText_EnterTheMaster_DoYouThinkTheyre,MSGBOX_NPC,SPEAKER_BIANCA)
        }

    closemessage
        release
        end
}

script EnterTheMaster_LeaveWoods_Script {
    lock
        faceplayer
        msgbox(gText_EnterTheMaster_TiredAlready,,SPEAKER_BIANCA)
        closemessage
}

script EnterTheMaster_CompleteMaze_Dialogue {

    setvar(VAR_MUIRWOODS_STATE,MUIRWOODS_MAZE_COMPLETE)
        lock
        addobject(BIANCA)
        setobjectxy(BIANCA,24,22) //PSF TODO Remove when followers are implemented
        setobjectxyperm(BIANCA,24,22)

        faceplayer
        msgbox(gText_EnterTheMaster_IDontThinkTheres,,SPEAKER_BIANCA)

        msgbox(gText_EnterTheMaster_YouThinkTheyve,MSGBOX_DEFAULT,SPEAKER_ZOEY)

        msgbox(gText_EnterTheMaster_TheyreGettingPaid,,SPEAKER_BIANCA)
        closemessage

        applymovement(BIANCA,EnterTheMaster_WalkIntoWoods)
        waitmovement(0)
        playse(SE_EXIT)
        waitse
        removeobject(BIANCA)
        release
        end
}

script EnterTheMaster_ClearingIntro_Dialogue {
    setvar(VAR_MUIRWOODS_STATE,MET_SIEBOLD)

        applymovement(PLAYER,EnterTheMaster_ApproachSiebold)
        waitmovement(0)

        msgbox(gText_EnterTheMaster_WelcomeTo,,SPEAKER_SIEBOLD)

        msgbox(gText_EnterTheMaster_ThanksYoure,MSGBOX_DEFAULT,SPEAKER_ZOEY)

        msgbox(gText_EnterTheMaster_AndMyNotebook,,SPEAKER_SIEBOLD)
        closemessage

        applymovement(SIEBOLD,Common_Movement_FaceLeft)
        applymovement(BIANCA,EnterTheMaster_GiveNotebook)
        waitmovement(0)

        msgbox(gText_EnterTheMaster_GettingThemToBattle,,SPEAKER_SIEBOLD)

        msgbox(gText_EnterTheMaster_IsThatWhatYoure,,SPEAKER_BIANCA)

        msgbox(gText_EnterTheMaster_EveryClientAlways,,SPEAKER_SIEBOLD)
        closemessage

        applymovement(BIANCA,EnterTheMaster_StandNextToPlayer)
        waitmovement(0)

        applymovement(SIEBOLD,EnterTheMaster_Pace)
        waitmovement(0)

        msgbox(gText_EnterTheMaster_AreEitherOfYou,,SPEAKER_SIEBOLD)

        msgbox(gText_EnterTheMaster_YeahThatsTheBattleFrontier,,SPEAKER_BIANCA)

        msgbox(gText_EnterTheMaster_HmmTheSmartass,,SPEAKER_SIEBOLD)

        msgbox(gText_EnterTheMaster_OhDoesEachPart,,SPEAKER_BIANCA)

        msgbox(gText_EnterTheMaster_SorryDidntGoTo,MSGBOX_DEFAULT,SPEAKER_ZOEY)

        applymovement(BIANCA,Common_Movement_FaceRight)
        applymovement(PLAYER,Common_Movement_FaceLeft)
        waitmovement(0)

        msgbox(gText_EnterTheMaster_ScottIsTheFounder,,SPEAKER_BIANCA)

        delay(16)
        fadescreen(FADE_TO_BLACK)
        msgbox(gText_EnterTheMaster_OneEternity)
        applymovement(BIANCA,Common_Movement_FaceUp)
        applymovement(PLAYER,Common_Movement_FaceUp)
        waitmovement(0)
        fadescreen(FADE_FROM_BLACK)

        msgbox(gText_EnterTheMaster_AndSoConcludes,,SPEAKER_BIANCA)

        msgbox(gText_EnterTheMaster_SortaKindaNotReally,,SPEAKER_SIEBOLD)
        msgbox(gText_EnterTheMaster_Oh,,SPEAKER_BIANCA)

        msgbox(gText_EnterTheMaster_SoWhatsNext,MSGBOX_DEFAULT,SPEAKER_ZOEY)

        msgbox(gText_EnterTheMaster_IfYouGo,MSGBOX_DEFAULT,SPEAKER_SIEBOLD)
        goto(EnterTheMaster_ExplainRobots_Dialogue)
}

script EnterTheMaster_ExplainRobots_Dialogue{
    msgbox(gText_EnterTheMaster_IfYouTalk,MSGBOX_DEFAULT,SPEAKER_SIEBOLD)
        msgbox(gText_EnterTheMaster_TheFirstStage,,SPEAKER_SIEBOLD)
        closemessage
        goto_if_quest_not_active(QUEST_SIEBOLDSTRAINING,Quest_Sieboldstraining_StartQuest_Script)
        release
        end
}

movement EnterTheMaster_WalkIntoWoods {
    walk_up
        step_end
}

text gText_EnterTheMaster_MorningReady{
    format("Morning. Ready to find this old dude?")
}

text gText_EnterTheMaster_AsReadyAs{
    format("As ready as I'm going to be!")
}

text gText_EnterTheMaster_DaddySentAnother{
    format("Daddy sent another care package, so if we stick together, I'll keep you healed up.")
}

text gText_EnterTheMaster_ThanksDad{
    format("Thanks… dad?")
}

text gText_EnterTheMaster_HowDoYouThink{
    format("How do you think my new Pokémon is faring? I'm curious to see how it integrates with the rest of my team.")
}

text gText_EnterTheMaster_YouKnow{
    format("You know, I'm really sick of TeamRocket's shit. Its their fault we're stuck in this stupid forest.")
}

text gText_EnterTheMaster_IThoughtGymLeaders{
    format("I thought SanFran's Gym Leaders would be tougher. I wonder how their old Elite Four stacks up to Unova's?")
}

text gText_EnterTheMaster_DoYouThinkTheyre{
    format("Do you think they're going to be worth all this hassle?")
}

text gText_EnterTheMaster_TiredAlready{
    format("Tired already? I'll be waiting here.")
}

text gText_EnterTheMaster_IDontThinkTheres{
    format("I don't think there's anywhere else for them to be… they've gotta be in here.")
}

text gText_EnterTheMaster_YouThinkTheyve{
    format("You think they've just been alone in here, waiting for us for Dialga knows how long?")
}

text gText_EnterTheMaster_TheyreGettingPaid{
    format("They're getting paid. ")
}

text gText_EnterTheMaster_WelcomeTo{
    format("Bianca! {PLAYER}! Welcome to Muirwoods. Excellent work so far!")
}

text gText_EnterTheMaster_ThanksYoure{
    format("Thanks. You're Siebold?")
}

text gText_EnterTheMaster_AndMyNotebook{
    format("Yeah! And my notebook?")
}

text gText_EnterTheMaster_GettingThemToBattle{
    format("I hope Pokémon 1 and Pokémon 2 in Robinwilltunnel weren't too much trouble. Getting them to battle without my direct commands was certainly a challenge.")
}

text gText_EnterTheMaster_IsThatWhatYoure{
    format("Is that what you're teaching us? Battling with our eyes closed? Sounds useful.")
}

text gText_EnterTheMaster_EveryClientAlways{
    format("Every client always sends me a smartass… Let's get started.")
}

text gText_EnterTheMaster_AreEitherOfYou{
    format("Are either of you familiar with the Scott's Trainer Theory?")
}

text gText_EnterTheMaster_YeahThatsTheBattleFrontier{
    format("Yeah, that's the Battle Frontier guy, right?")
}

text gText_EnterTheMaster_HmmTheSmartass{
    format("Hmm, the smartass actually knows something!")
}

text gText_EnterTheMaster_OhDoesEachPart{
    format("Oh… does each part of this training map to each of those principles?")
}

text gText_EnterTheMaster_SorryDidntGoTo{
    format("Sorry, didn't go to Battle prep school. Who is Scott and what are these principles?")
}

text gText_EnterTheMaster_ScottIsTheFounder{
    format("Scott is the founder of an amusement park in Hoenn. Its attractions were designed to test what Scott believed were the qualities that all Master trainers require… ")
}

text gText_EnterTheMaster_OneEternity{
    format("One eternity later…")
}
text gText_EnterTheMaster_AndSoConcludes{
    format("…and so concludes my theory on how your silly little tests correspond with each of Scott's 7 principles.")
}

text gText_EnterTheMaster_SortaKindaNotReally{
    format("…sorta, kinda, not really. If you think life is going to map one to one like that for you, you've got a rude awakening ahead.")
}

text gText_EnterTheMaster_Oh{
    format("…Oh.")
}

text gText_EnterTheMaster_SoWhatsNext{
    format("So what's next?")
}

text gText_EnterTheMaster_IfYouGo{
    format("If you go further back into the clearing, there are battle simulation robots there. The one on the right are for {PLAYER} - they're calibrated specifically based on the battle data that Ycombinator gave me for each of you.")
}

text gText_EnterTheMaster_IfYouTalk{
    format("If you talk to them, they'll give you a refresher on how they work, but its pretty simple. The second stage of your training will be defeating the robot that are assigned to you.")
}

text gText_EnterTheMaster_TheFirstStage{
    format("The first stage needed you to work together, but for this one, don't help each other.")
}

text gText_EnterTheMaster_OkayLetsACrack{
    format("Okay, let's take a crack of these!")
}

movement EnterTheMaster_GiveNotebook {
    walk_up
        face_right
        delay_16
        walk_in_place_slow_right
        step_end
}

movement EnterTheMaster_StandNextToPlayer {
    walk_down
        face_up
        step_end
}

movement EnterTheMaster_Pace {
    walk_left * 2
        walk_right *2
        walk_left * 2
        walk_right *2
        face_down
        step_end
}

movement EnterTheMaster_ApproachSiebold {
    walk_up * 1
        step_end
}
