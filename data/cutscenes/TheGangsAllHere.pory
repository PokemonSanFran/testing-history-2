const PLAYER = OBJ_EVENT_ID_PLAYER
const ARNAV = 1
const OLIVER = 2
const OAK = 3
const BIANCA = 4
const ROSE = 5
const MAYOR = 6
const CHIEF = 7
const SABRINA = 8
const KOGA = 9

const FLAG_HIDE_NERDS_COPS = FLAG_TEMP_1
const FLAG_HIDE_OAK = FLAG_TEMP_2
const FLAG_HIDE_BIANCA = FLAG_TEMP_3
const FLAG_HIDE_ROSE = FLAG_TEMP_4
const FLAG_HIDE_SABRINA = FLAG_TEMP_5
const FLAG_HIDE_KOGA = FLAG_TEMP_6

script TheGangsAllHere_ObjectCleanUp_Script {
    call(Salesforcetower_Leagueops_EmptyConferenceRoomOffices)
    return
}

script TheGangsAllHere_MidMission_Object_Script{
    setflag(FLAG_HIDE_NERDS_COPS)
    setflag(FLAG_HIDE_BIANCA)
    setflag(FLAG_HIDE_SABRINA)
    setflag(FLAG_HIDE_KOGA) 
    setobjectxy(OAK,6,46)
    setobjectxy(ROSE,5,56)
    setobjectxyperm(ROSE,5,56)
    return
}

script TheGangsAllHere_ObjectSetup_Script{
    setflag(FLAG_HIDE_NERDS_COPS)
    setflag(FLAG_HIDE_BIANCA)
    setflag(FLAG_HIDE_SABRINA)
    setflag(FLAG_HIDE_KOGA) 
    setobjectxy(ROSE,5,56)
    setobjectxyperm(ROSE,5,56)
    return
}

script TheGangsAllHere_MoveOakToStairs_Script{
        addobject(OAK) //PSF TODO why is oak invisible without this
        setobjectmovementtype(OAK,MOVEMENT_TYPE_FACE_DOWN)
        setobjectxy(OAK,16,9)
        setobjectxyperm(OAK,16,9)
        return
}

script TheGangsAllHere_OakAppearStairs_Script{
    lock
    playse(SE_EXIT)
    waitse

    getplayerxy(VAR_TEMP_0,VAR_TEMP_1)

    if (var(VAR_TEMP_0) == 16){
        applymovement(PLAYER,TheGangsAllHere_StepAsideOak_Movement)
        waitmovement(PLAYER)
        call(TheGangsAllHere_MoveOakToStairs_Script)
        applymovement(OAK,TheGangsAllHere_WalkStairs_FacePlayer_Movement)
        waitmovement(OAK)
    }else{
        call(TheGangsAllHere_MoveOakToStairs_Script)
        playse(SE_PIN)
        applymovement(PLAYER,Common_Movement_FaceLeft)
        applymovement(OAK,TheGangAllHere_WalkStairsToElevator_Movement)
        waitmovement(OAK)
    }

    goto(TheGangsAllHere_WhatBringsYou_Dialogue)
}

script TheGangsAllHere_WhatBringsYou_Dialogue{
    lock
    setvar(VAR_SALESFORCETOWER_CONFERENCE_STATE,START_THE_GANGS_ALL_HERE)
    playbgm(MUS_RG_FOLLOW_ME,FALSE)
    msgbox(gText_TheGangsAllHere_WhatBringsYou,,SPEAKER_OAK)
    msgbox(gText_TheGangsAllHere_ToldMeToVisit,,SPEAKER_ZOEY)
    msgbox(gText_TheGangsAllHere_IsThisWhereYouWork,,SPEAKER_ZOEY)
    msgbox(gText_TheGangsAllHere_TheTowerYes,,SPEAKER_OAK)
    msgbox(gText_TheGangsAllHere_IHaventBeen,,SPEAKER_OAK)
    msgbox(gText_TheGangsAllHere_OfCourseNot,,SPEAKER_ZOEY)
    
    closemessage
    special(HealPlayerParty)
    applymovement(OAK,FollowMe_Movement)
    waitmovement(OAK)
    setobjectxy(OAK,6,46)
    setobjectxyperm(OAK,6,46)
    fadedefaultbgm
    //loadword(0,NewScript)
    //setfollower(OAK,0x100) //PSF TODO hook up FollowMe scripts
    //https://www.pokecommunity.com/showthread.php?t=432529
    releaseall
    end
}

script TheGangsAllHere_OfficeIsntThatWay_Script{
    lockall
    msgbox(gText_TheGangsAllHere_OfficeIsntThatWay,,SPEAKER_OAK)
    
    closemessage
    applymovement(PLAYER,TheGangsAllHere_OfficeIsntThatWay_Movement)
}

script TheGangsAllHere_WalkHallway_Script{
    applymovement(OAK,Common_Movement_WalkDown)
    applymovement(PLAYER,Common_Movement_WalkDown)
    fadescreen(FADE_TO_BLACK)
    delay(30)
    setobjectxy(PLAYER,5,52)
    setobjectxy(OAK,6,52)
    TeleportCamera(5,52)
    fadescreen(FADE_FROM_BLACK)
    return
}

script TheGangsAllHere_ThisIsOffice_Script{
    lock
    faceplayer
    msgbox(gText_TheGangsAllHere_ThisIsOffice,,SPEAKER_OAK)
    closemessage
    
    call(TheGangsAllHere_WalkHallway_Script)
    goto(TheGangsAllHere_ComeOnIn_Dialogue)
}

script TheGangsAllHere_ComeOnIn_Dialogue{
    setvar(VAR_SALESFORCETOWER_CONFERENCE_STATE,POST_THE_GANGS_ALL_HERE) 
    playbgm(MUS_RG_ENCOUNTER_DEOXYS,FALSE)
    playse(SE_PIN)

    applymovement(ROSE,Common_Movement_ExclamationMark)
    waitmovement(ROSE)
    applymovement(ROSE,TheGangsAllHere_RoseWalkToDoor_Movement)

    msgbox(gText_TheGangsAllHere_ComeOnIn,,SPEAKER_ROSE)
    closemessage
    

    msgbox(gText_TheGangsAllHere_AsYouAsked,,SPEAKER_OAK)
    closemessage
    

    applymovement(PLAYER,Common_Movement_FaceRight)
    applymovement(OAK,Common_Movement_FaceLeft)
    waitmovement(PLAYER)

    msgbox(gText_TheGangsAllHere_IThoughtYouSaid,,SPEAKER_ZOEY)

    msgbox(gText_TheGangsAllHere_IDidIsee,,SPEAKER_OAK)

    goto(TheGangsAllHere_NotIntentionally_Dialogue)
    end
}

script TheGangsAllHere_NotIntentionally_Dialogue{
    msgbox(gText_TheGangsAllHere_NotIntentionally,,SPEAKER_ROSE)

    if(var(VAR_SALESFORCETOWER_PRODUCTION_STATE) == POST_ALWAYS_WATCHING_WAZOWSKI){
        msgbox(gText_TheGangsAllHere_PleaseTellMe)

    msgbox(gText_TheGangsAllHere_IHeardWonderfulThings,,SPEAKER_OAK)
    }

    msgbox(gText_TheGangsAllHere_InAnyCase)
    

    applymovement(OAK,Common_Movement_WalkInPlaceDown)
    waitmovement(OAK)

    msgbox(gText_TheGangsAllHere_ImThrilledTheTiming,,SPEAKER_ROSE)
    closemessage
    

    goto(TheGangsAllHere_GiveDowsingMachine_Script)
    end
}

script TheGangsAllHere_GiveDowsingMachine_Script{
    applymovement(PLAYER,Common_Movement_FaceDown)
    applymovement(ROSE,TheGangsAllHere_GivePackageToPlayerChar_Movement)
    waitmovement(ROSE)
    giveitem(ITEM_DOWSING_MACHINE,1)
    goto(TheGangsAllHere_GiveDowsingMachine_Dialogue)
    end
}

script TheGangsAllHere_GiveDowsingMachine_Dialogue{
    msgbox(gText_TheGangsAllHere_TheVSSeekerWillAlert,,SPEAKER_ROSE)
    

    applymovement(OAK,Common_Movement_FacePlayer)
    waitmovement(OAK)
    applymovement(PLAYER,Common_Movement_FaceRight)

    msgbox(gText_TheGangsAllHere_NotToPlaceAnyUndue,,SPEAKER_OAK)

    msgbox(gText_TheGangsAllHere_OhWowThatMeans,,SPEAKER_ZOEY)
    closemessage

    applymovement(PLAYER,Common_Movement_FaceDown)
    msgbox(gText_TheGangsAllHere_WeKnowButNoPressure,,SPEAKER_ROSE)

    goto(TheGangsAllHere_EndScene_Dialogue)
}

script TheGangsAllHere_EndScene_Dialogue{
    msgbox(gText_TheGangsAllHere_TimeForNumber7)
    closemessage
    

    applymovement(OAK,Common_Movement_FaceDown)

    msgbox(gText_TheGangsAllHere_WeShall,,SPEAKER_OAK)
    

    applymovement(OAK,Common_Movement_FacePlayer)
    applymovement(PLAYER,Common_Movement_FaceRight)

    msgbox(gText_TheGangsAllHere_FeelFreeToContinue,,SPEAKER_OAK)

    if(var(VAR_SALESFORCETOWER_CONFERENCE_STATE) != POST_ALWAYS_WATCHING_WAZOWSKI){
        msgbox(gText_TheGangsAllHere_AhAndIf)
    }
    
    closemessage

    applymovement(PLAYER,TheGangsAllHere_WatchPairLeave_Movement)
    applymovement(ROSE,TheGangsAllHere_RoseWalkOut_Movement)
    applymovement(OAK,TheGangsAllHere_OakWalkOut_Movement)
    waitmovement(OAK)
    fadedefaultbgm
    removeobject(ROSE)
    removeobject(OAK)
    addvar(VAR_STORYLINE_STATE,1)
    call(Act1_1_Check_Script)
    release
    end
}

text gText_TheGangsAllHere_WhatBringsYou{
    format("{PLAYER}! What brings you to League HQ?")
}
text gText_TheGangsAllHere_ToldMeToVisit{
    format("Commissioner Rose told me to visit after I beat Whitney!")
}
text gText_TheGangsAllHere_IsThisWhereYouWork{
    format("Is this where you work?")
}
text gText_TheGangsAllHere_TheTowerYes{
    format("My office is on this floor, yeah! This is League Ops, and everybody here is solely dedicated to making sure the Ycombinator Pokemon League is a success.")
}
text gText_TheGangsAllHere_IHaventBeen{
    format("I've an important delivery for the C.E.O., but the workers here in League Ops can be a little... rowdy. Do you mind if I tag along on your tour? I'll keep you in tip-top shape!")
}
text gText_TheGangsAllHere_OfCourseNot{
    format("Of course not!")
}

text gText_TheGangsAllHere_OfficeIsntThatWay{
    format("Hey {PLAYER}, the office isn't that way!")
}

text gText_TheGangsAllHere_ThisIsOffice{
    format("This is Rose's office. After you.")
}

text gText_TheGangsAllHere_ComeOnIn{
    format("{PLAYER}! Oak! Come on in, I just finished an email! Glad you're both here, and excellent timing that you ran into each other.")
}
text gText_TheGangsAllHere_AsYouAsked{
    format("As you asked, I have your package.")
}
text gText_TheGangsAllHere_IThoughtYouSaid{
    format("I thought you said you had a delivery for the C.E.O.?")
}
text gText_TheGangsAllHere_IDidIsee{
    format("I did. I see Rose neglected to reveal his full title...?")
}
text gText_TheGangsAllHere_NotIntentionally{
    format("Not intentionally! I was just so swept up in your battle!")
}

text gText_TheGangsAllHere_PleaseTellMe{
    format("Oak, please tell me you'll go back and watch Sabrina's highlight reel later, it was fantastic.")
}
text gText_TheGangsAllHere_LikeISaidWe{
    format("Like I said, we wear a lot of hats.")
}
text gText_TheGangsAllHere_IHeardWonderfulThings{
    format("I heard wonderful things on the way over here, but I'll be certain to observe later.")
}
text gText_TheGangsAllHere_InAnyCase{
    format("In any case, your pacakge, Rose.")
}
text gText_TheGangsAllHere_ImThrilledTheTiming{
    format("I'm thrilled the timing worked out, because the package actually isn't for me!")
}
text gText_TheGangsAllHere_TheVSSeekerWillAlert{
    format("The VS Seeker will alert Trainers in the area that you want a rematch. It uses steps to charge, so I guess you'll burn some calories alongside your Pok√©mon. I'm excited to see how you use our tools!")
} //TODO Change script to fit whatever item we end up giving out

text gText_TheGangsAllHere_NotToPlaceAnyUndue{
    format("Not to place any undue pressure on you, but we personally reviewed all of the applicants to our little program. You were exceptionally popular among the team, so we're very excited to see you perform well!")
}
text gText_TheGangsAllHere_OhWowThatMeans{
    format("Oh. Wow! That means a lot. I'll absolutely put it to use!")
}
text gText_TheGangsAllHere_WeKnowButNoPressure{
    format("We know. But no pressure. Enjoy your journey!")
}
text gText_TheGangsAllHere_TimeForNumber7{
    format("Time for Starbucks coffee number 7. Shall we, Oak?")
}
text gText_TheGangsAllHere_WeShall{
    format("We shall.")
}
text gText_TheGangsAllHere_FeelFreeToContinue{
    format("Feel free to continue your tour without us, {PLAYER}. Greatly appreciate your help in the battles. Hopefully supporting me wasn't too exhausting!")
}
text gText_TheGangsAllHere_AhAndIf{
    format("Ah, and if you haven't already visited Sabrina, go do so. They're on Marketing, the second floor.")
}
movement TheGangsAllHere_OakWalkToPlayer_Movement{
    face_right
    step_end
}
movement TheGangsAllHere_OfficeIsntThatWay_Movement{
    walk_down
    step_end
}
movement TheGangsAllHere_ThisIsOffice_Movement{
    walk_right * 3
    step_end
}

movement TheGangsAllHere_RoseWalkToDoor_Movement{
    walk_right * 2
    walk_up * 3
    walk_left
    face_up
    step_end
}

movement TheGangsAllHere_GivePackageToPlayerChar_Movement{
    walk_left
    face_up
    walk_in_place_up
    step_end
}    

movement TheGangsAllHere_WatchPairLeave_Movement{
    delay_16 * 2
    face_up
    step_end
}

movement TheGangsAllHere_RoseWalkOut_Movement{
    walk_right
    walk_up * 8
    step_end
}

movement TheGangsAllHere_OakWalkOut_Movement{
    walk_up * 8
    step_end
}
movement FollowMe_Movement{
    walk_left
    walk_down * 20
    step_end
}

movement TheGangsAllHere_StepAsideOak_Movement{
    walk_down
    walk_right
    face_left
    step_end
}
movement TheGangsAllHere_WalkStairs_FacePlayer_Movement{
    walk_down * 2
    emote_exclamation_mark
    face_right
    step_end
}

movement TheGangAllHere_WalkStairsToElevator_Movement{
    walk_down
    walk_right * 2
    step_end
}
