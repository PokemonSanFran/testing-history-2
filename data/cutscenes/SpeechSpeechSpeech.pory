const PLAYER = OBJ_EVENT_ID_PLAYER
const CAMERA = OBJ_EVENT_ID_CAMERA
const ARCHER = 1
const MALVA = 3
const DRASNA = 2
const SIEBOLD = 6
const WIKSTROM = 5
const GIOVANNI = 7
const ARIANA = 8
const GRUNTA = 9
const GRUNTB = 10
const GRUNTC = 11
const GRUNTD = 12

const FLAG_HIDE_ARCHER = FLAG_TEMP_1
const FLAG_HIDE_ELITES = FLAG_TEMP_2
const FLAG_HIDE_GARBODOR = FLAG_TEMP_4
const FLAG_HIDE_SIEBOLD = FLAG_TEMP_5
const FLAG_HIDE_GIOVANNI = FLAG_TEMP_6
const FLAG_HIDE_GRUNTS = FLAG_TEMP_7
const FLAG_HIDE_WIKSTROM = FLAG_TEMP_8
const FLAG_HIDE_ARIANA = FLAG_TEMP_9

script SpeechSpeechSpeech_MoveObject_Script {
    setflag(FLAG_HIDE_GARBODOR)
    setflag(FLAG_HIDE_WIKSTROM)

    setobjectxy(ARCHER,10,8)
    setobjectxyperm(ARCHER,10,8)
    setobjectmovementtype(ARCHER,MOVEMENT_TYPE_FACE_UP)

    setobjectxy(MALVA,7,3)
    setobjectxyperm(MALVA,7,3)

    setobjectxy(DRASNA,8,3)
    setobjectxyperm(DRASNA,8,3)
}

script SpeechSpeechSpeech_MovePlayer_Script {
    setobjectxy(PLAYER,9,9)
        TeleportCamera(9,9)
        applymovement(PLAYER,Common_Movement_FaceUp)
        waitmovement(PLAYER)

        createvobject(OBJ_EVENT_GFX_AQUA_MEMBER_M,13,8,8)
        createvobject(OBJ_EVENT_GFX_AQUA_MEMBER_M,14,11,8)
        createvobject(OBJ_EVENT_GFX_AQUA_MEMBER_M,15,7,9)
        createvobject(OBJ_EVENT_GFX_AQUA_MEMBER_M,16,9,10)
        createvobject(OBJ_EVENT_GFX_AQUA_MEMBER_M,17,10,9)
        createvobject(OBJ_EVENT_GFX_AQUA_MEMBER_M,18,7,10)

        call(SpeechSpeechSpeech_ResetFacing_Script)
}

script SpeechSpeechSpeech_CrowdLookAround_Script {
        turnvobject(13,DIR_WEST)
        delay(16)
        turnvobject(15,DIR_EAST)
        delay(16)
        turnvobject(16,DIR_EAST)
        delay(16)
        turnvobject(18,DIR_EAST)
        delay(16)
}

script SpeechSpeechSpeech_ResetFacing_Script {
        turnvobject(13,DIR_NORTH)
        turnvobject(14,DIR_NORTH)
        turnvobject(15,DIR_NORTH)
        turnvobject(16,DIR_NORTH)
        turnvobject(17,DIR_NORTH)
        turnvobject(18,DIR_NORTH)
}

script SpeechSpeechSpeech_ProjectAssignment_ObjectSetUp_Script {

    setflag(FLAG_HIDE_GARBODOR)
    setflag(FLAG_HIDE_ARCHER)
    setflag(FLAG_HIDE_GIOVANNI)

    setobjectxy(MALVA,11,17)
    setobjectxyperm(MALVA,11,17)
    setobjectmovementtype(MALVA,MOVEMENT_TYPE_FACE_DOWN)
    
    setobjectxy(DRASNA,17,15)
    setobjectxyperm(DRASNA,17,15)
    setobjectmovementtype(DRASNA,MOVEMENT_TYPE_FACE_LEFT)

    setobjectxy(ARIANA,16,15)
    setobjectxyperm(ARIANA,16,15)
    setobjectmovementtype(ARIANA,MOVEMENT_TYPE_FACE_RIGHT)
    
    setobjectxy(SIEBOLD,19,6)
    setobjectxyperm(SIEBOLD,19,6)
    setobjectmovementtype(SIEBOLD,MOVEMENT_TYPE_FACE_DOWN)
    
    setobjectxy(WIKSTROM,4,6)
    setobjectxyperm(WIKSTROM,4,6)
    setobjectmovementtype(WIKSTROM,MOVEMENT_TYPE_FACE_DOWN)

    setobjectmovementtype(GRUNTA,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectnewmovementtype(GRUNTA,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectxy(GRUNTA,11,18)
    setobjectxyperm(GRUNTA,11,18)

    setobjectmovementtype(GRUNTB,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectnewmovementtype(GRUNTB,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectxy(GRUNTB,17,16)
    setobjectxyperm(GRUNTB,17,16)

    setobjectmovementtype(GRUNTC,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectnewmovementtype(GRUNTC,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectxy(GRUNTC,19,7)
    setobjectxyperm(GRUNTC,19,7)

    setobjectmovementtype(GRUNTD,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectnewmovementtype(GRUNTD,MOVEMENT_TYPE_LOOK_AROUND)
    setobjectxy(GRUNTD,4,7)
    setobjectxyperm(GRUNTD,4,7)
}

script SpeechSpeechSpeech_PositionPlayer_Script {
        lock

        applymovement(PLAYER,Common_Movement_WalkRight)
        applymovement(PLAYER,Common_Movement_WalkRight)
        fadescreen(FADE_TO_BLACK)

        call(SpeechSpeechSpeech_MovePlayer_Script)
        fadescreen(FADE_FROM_BLACK)

        goto(SpeechSpeechSpeech_Intro_Dialogue)
}

script SpeechSpeechSpeech_Intro_Dialogue {

        setvar(VAR_MUGSHOT, MUGSHOT_GIOVANNI)
        callnative(DrawMugshot)
        msgbox(gText_SpeechSpeechSpeech_ThankYouAll)
        msgbox(gText_SpeechSpeechSpeech_FirstOfAll)
        callnative(ClearMugshot)
        closemessage

        call(SpeechSpeechSpeech_CrowdLookAround_Script)
        call(SpeechSpeechSpeech_ResetFacing_Script)

        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_SpeechSpeechSpeech_ForAllYouve)
        callnative(ClearMugshot)
        closemessage

        turnvobject(13,DIR_WEST)
        delay(16)
        turnvobject(15,DIR_EAST)
        msgbox(gText_SpeechSpeechSpeech_WaitWhat)
        
        turnvobject(16,DIR_EAST)
        delay(16)
        turnvobject(18,DIR_EAST)
        msgbox(gText_SpeechSpeechSpeech_AreTheyEven)
        closemessage

        call(SpeechSpeechSpeech_ResetFacing_Script)

        setvar(VAR_MUGSHOT, MUGSHOT_GIOVANNI)
        callnative(DrawMugshot)
        msgbox(gText_SpeechSpeechSpeech_HasMyFullConfidence)
        callnative(ClearMugshot)
        closemessage
        goto(SpeechSpeechSpeech_StartTransformation_Script)
}

script SpeechSpeechSpeech_StartTransformation_Script {

        clearflag(FLAG_HIDE_SIEBOLD)
        special(SpawnCameraObject)
        applymovement(CAMERA,SpeechSpeechSpeech_PanCameraUp)
        applymovement(GIOVANNI,SpeechSpeechSpeech_WalkBackstage)
        waitmovement(CAMERA)
        delay(32)
        applymovement(GIOVANNI,SpeechSpeechSpeech_StartSpin)
        waitmovement(GIOVANNI)
        applymovement(WIKSTROM,SpeechSpeechSpeech_FinishSpin)
        waitmovement(GIOVANNI)
        removeobject(GIOVANNI)
        clearflag(FLAG_HIDE_WIKSTROM)
        addobject(WIKSTROM)
        delay(32)

        applymovement(MALVA,SpeechSpeechSpeech_WalkOutElite)
        applymovement(DRASNA,SpeechSpeechSpeech_WalkOutElite)
        applymovement(SIEBOLD,SpeechSpeechSpeech_WalkOutElite)
        applymovement(WIKSTROM,SpeechSpeechSpeech_WalkOutElite)
        applymovement(ARIANA,SpeechSpeechSpeech_StepAside)
        applymovement(CAMERA,SpeechSpeechSpeech_PanCameraDown)
        waitmovement(CAMERA)

        special(RemoveCameraObject)
        goto(SpeechSpeechSpeech_EliteReveal_Dialogue)
}

script SpeechSpeechSpeech_EliteReveal_Dialogue {

        addvar(VAR_STORYLINE_STATE,1)

        setvar(VAR_MUGSHOT, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_SpeechSpeechSpeech_Us)
        callnative(ClearMugshot)
        closemessage

        msgbox(gText_SpeechSpeechSpeech_HolyShit)
        closemessage

        playse(SE_M_EARTHQUAKE)

        setvar(VAR_0x8004,2) //vertical pan
        setvar(VAR_0x8005,2) //horizontal pan
        setvar(VAR_0x8006,25) //number of shakes
        setvar(VAR_0x8007,10) //shake delay
        special(ShakeCamera)

        playse(SE_PIN)
        applymovement(GRUNTA,Common_Movement_ExclamationMark)
        applymovement(GRUNTB,Common_Movement_ExclamationMark)
        applymovement(GRUNTC,Common_Movement_ExclamationMark)
        applymovement(GRUNTD,Common_Movement_ExclamationMark)
        waitmovement(GRUNTD)

        delay(100) //wait until after earthquake
        waitstate

        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_SpeechSpeechSpeech_IsAnybodyHurt)
        callnative(ClearMugshot)
        closemessage

        msgbox(gText_SpeechSpeechSpeech_Yeah)

        msgbox(gText_SpeechSpeechSpeech_HellYes)
        closemessage
        applymovement(GRUNTD,SpeechSpeechSpeech_LookAround)
        waitmovement(GRUNTD)

        setvar(VAR_MUGSHOT, MUGSHOT_ARIANA)
        callnative(DrawMugshot)
        msgbox(gText_SpeechSpeechSpeech_YouAllKnowHowCompanies)
        callnative(ClearMugshot)
        closemessage

        msgbox(gText_SpeechSpeechSpeech_WereFinally)
        closemessage

        applymovement(GRUNTD,SpeechSpeechSpeech_LookAround)
        msgbox(gText_SpeechSpeechSpeech_ThisIsInsane)
        closemessage
        waitmovement(GRUNTD)

        applymovement(DRASNA,Common_Movement_WalkDown)
        waitmovement(DRASNA)
        //Drasna steps forward. Ariana steps aside.

        setvar(VAR_MUGSHOT, MUGSHOT_GRIMSLEY)
        callnative(DrawMugshot)
        msgbox(gText_SpeechSpeechSpeech_WeWantToBegin)
        callnative(ClearMugshot)
        closemessage

        applymovement(WIKSTROM,Common_Movement_WalkDown)
        waitmovement(WIKSTROM)
        //Wikstrom steps forward.

        setvar(VAR_MUGSHOT, MUGSHOT_GRIMSLEY)
        callnative(DrawMugshot)
        msgbox(gText_SpeechSpeechSpeech_4LeadCharge)
        callnative(ClearMugshot)
        closemessage

        applymovement(WIKSTROM,SpeechSpeechSpeech_WalkBackwards)
        applymovement(SIEBOLD,Common_Movement_WalkDown)
        waitmovement(SIEBOLD)
        //Wikstrom steps back. Siebold steps forward.

        setvar(VAR_MUGSHOT, MUGSHOT_GRIMSLEY)
        callnative(DrawMugshot)
        msgbox(gText_SpeechSpeechSpeech_3LeadCharge)
        callnative(ClearMugshot)
        closemessage

        applymovement(SIEBOLD,SpeechSpeechSpeech_WalkBackwards)
        applymovement(MALVA,Common_Movement_WalkDown)
        waitmovement(MALVA)
        //Wikstrom steps back. Malva steps forward.

        setvar(VAR_MUGSHOT, MUGSHOT_GRIMSLEY)
        callnative(DrawMugshot)
        msgbox(gText_SpeechSpeechSpeech_1LeadCharge)
        callnative(ClearMugshot)
        closemessage

        applymovement(MALVA,SpeechSpeechSpeech_WalkBackwards)
        waitmovement(MALVA)


        //Malva steps back.

        setvar(VAR_MUGSHOT, MUGSHOT_SHAUNTAL)
        callnative(DrawMugshot)
        msgbox(gText_SpeechSpeechSpeech_IllBeHandling)
        callnative(ClearMugshot)
        closemessage

        fadescreen(FADE_TO_BLACK)
        setvar(VAR_ALCATRAZ_STATE,POST_SPEECHSPEECH)

        TeleportCamera(9,9)
        callnative(DestroyVirtualObjects)
        call(SpeechSpeechSpeech_ProjectAssignment_ObjectSetUp_Script)

        applymovement(ARCHER,SpeechSpeechSpeech_WalkToPlayer)
        fadescreen(FADE_FROM_BLACK)
        waitmovement(ARCHER)

        //Screen fades to black. Screen fades back in. Elite Four members are spaced around the room, surrounded by groups of TeamRocket members. Archer walks up to the player.
        setvar(VAR_MUGSHOT, MUGSHOT_HUGH)
        callnative(DrawMugshot)
        msgbox(gText_SpeechSpeechSpeech_DamnThisShit)

        setvar(VAR_MUGSHOT, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_SpeechSpeechSpeech_ItFeelsReally)

        setvar(VAR_MUGSHOT, MUGSHOT_HUGH)
        callnative(DrawMugshot)
        msgbox(gText_SpeechSpeechSpeech_Yeah)

        if (var(VAR_STORYLINE_STATE) != STORY_RAVE_AND_SPEECH_COMPLETE){
            msgbox(gText_SpeechSpeechSpeech_YeahAlsoAre)
        }else {
            msgbox(gText_SpeechSpeechSpeech_ByTheWay)
        }

        msgbox(gText_SpeechSpeechSpeech_Peace)
        callnative(ClearMugshot)
        closemessage

        applymovement(ARCHER,SpeechSpeechSpeech_LeaveScreen)
        waitmovement(ARCHER)
        removeobject(ARCHER)
        release
        end

}

text gText_SpeechSpeechSpeech_GoWarehouseRave {
    format("Are you going to the Warehouse Rave? Sounds fun! Wish I had time to go.")
}

text gText_SpeechSpeechSpeech_ThankYouAll {
    format("Thank you all for coming. For all your hard work. All your support. I have a few announcements for you all.")
}

text gText_SpeechSpeechSpeech_FirstOfAll {
    format("First of all, I will be taking a leave of absence. Effectively immediately, Ariana will be leading TeamRocket. She has my full trust and support.")
}

text gText_SpeechSpeechSpeech_ForAllYouve {
    format("Thank you for all you’ve done for us, Giovanni, I hope to live up to your example.")
}

text gText_SpeechSpeechSpeech_WaitWhat {
    format("Wait what?")
}

text gText_SpeechSpeechSpeech_AreTheyEven {
    format("Are they even qualified?")
}

text gText_SpeechSpeechSpeech_HasMyFullConfidence {
    format("Yes, Ariana has my full confidence, as well as the backing of…")
}

text gText_SpeechSpeechSpeech_Us {
    format("… US! The Elite Four of SanFran!")
}
text gText_SpeechSpeechSpeech_HolyShit {
    format("Holy shit ok, good enough for me!")
}

text gText_SpeechSpeechSpeech_IsAnybodyHurt {
    format("Is anybody hurt? …no? Wonderful. Let’s continue. Archer’s restoration project has been going excellently, and we’re going to use that momentum for our next stage. It’s time to take the fight to Ycombinator!")
}

text gText_SpeechSpeechSpeech_Yeah {
    format("Yeah!")
}

text gText_SpeechSpeechSpeech_HellYes {
    format("Hell yes! Woo! Wait, where did Giovanni go?")
}

text gText_SpeechSpeechSpeech_YouAllKnowHowCompanies {
    format("You all know how companies like Amazon and Twitter have ruined our lives and families. But with the combined popularity of the Champion, Elite Four and our manpower, I think we can hurt these people where it counts: their wallets. We can’t drive these parasitic companies out of the region… but all that money has to go somewhere. Let’s use that money to strengthen the region and create a brighter future for us all.")
}

text gText_SpeechSpeechSpeech_WereFinally {
    format("We’re finally doing it…")
}

text gText_SpeechSpeechSpeech_ThisIsInsane {
    format("This is insane yo. But seriously, did they just disappear?")
}

text gText_SpeechSpeechSpeech_WeWantToBegin {
    format("We want to begin by attacking the biggest players in SanFran. Each of us will handle a different offensive plan.")
}

text gText_SpeechSpeechSpeech_4LeadCharge {
    format("Wikstrom will lead the charge against Amazon.")
}

text gText_SpeechSpeechSpeech_3LeadCharge {
    format("Siebold will lead the charge against Uber.")
}

text gText_SpeechSpeechSpeech_1LeadCharge {
    format("Malva will lead the charge against Twitter.")
}

text gText_SpeechSpeechSpeech_IllBeHandling {
    format("And I’ll be handling negotiations with the government, and operations here. Ariana will be giving each of you assignments under one of the Elite Four members, so listen up. ")
}
text gText_SpeechSpeechSpeech_DamnThisShit {
    format("Damn, this shit is so crazy! The air is electric!")
}

text gText_SpeechSpeechSpeech_ItFeelsReally {
    format("It feels really awesome to start making a difference around here.")
}

text gText_SpeechSpeechSpeech_YeahAlsoAre {
    format("Also, are you heading to that party in Oakland now? I'll catch up with you in a second, I just gotta talk to Ariana first.")
}

text gText_SpeechSpeechSpeech_ByTheWay {
    format("By the way, you and I are free to work with whoever we want. Just talk to the Elite Four member you want to team up with. I gotta go, I told Ariana I’d help them set up something. Peace!")
}

text gText_SpeechSpeechSpeech_Peace {
    format("Peace!")
}

movement SpeechSpeechSpeech_PanCameraUp {
    walk_up * 4
    step_end
}

movement SpeechSpeechSpeech_WalkBackstage{
    walk_up * 3
	step_end
}

movement SpeechSpeechSpeech_WalkOutElite{
    walk_down * 2
	step_end
}

movement SpeechSpeechSpeech_PanCameraDown {
    walk_down * 3
    step_end
}

movement SpeechSpeechSpeech_StepAside{
    lock_facing_direction
    walk_right * 2
    unlock_facing_direction
	step_end
}

movement SpeechSpeechSpeech_WalkBackwards{
    lock_facing_direction
    walk_up
    unlock_facing_direction
	step_end
}

movement SpeechSpeechSpeech_WalkToPlayer{
    walk_left
    face_down
	step_end
}

movement SpeechSpeechSpeech_LeaveScreen{
    walk_left * 7
	step_end
}

movement SpeechSpeechSpeech_StartSpin {
    face_left
    face_down
    face_right
    face_up
    face_left
    face_down
    face_right
    face_up
    step_end
}

movement SpeechSpeechSpeech_FinishSpin {
    face_left
    face_down
    face_right
    face_up
    step_end
}
    
movement SpeechSpeechSpeech_LookAround {
    face_left
    delay_16
    face_right
    delay_16
    face_up
    step_end
}
