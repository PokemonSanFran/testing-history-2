const PLAYER = OBJ_EVENT_ID_PLAYER
const BIANCA = 1 //FLAG_TEMP_1
const OAK = 2 //FLAG_TEMP_2

script Swagbag_ObjectSetUp {
    turnobject(OAK, DIR_WEST) 
    setobjectmovementtype(BIANCA,MOVEMENT_TYPE_FACE_RIGHT)
    setobjectxy(BIANCA,6,4)
    setobjectxyperm(BIANCA,6,4)
}
script Swagbag_IncrementState_Script{
    setvar(VAR_PLAYER_HOME_STATE,CHECKED_OUT_BEDROOM)
    return
}

script swagbag_ObjectHide_Script {
    setobjectmovementtype(OAK, MOVEMENT_TYPE_FACE_RIGHT)
    turnobject(OAK,DIR_EAST)
    setobjectxy(OAK,8,6)
    setobjectxyperm(OAK,8,6)
    setflag(FLAG_TEMP_1)
    setflag(FLAG_TEMP_3)
}

script Swagbag_WelcomeToSanFran_Dialogue {
        setvar(VAR_PLAYER_HOME_STATE,POST_SWAGBAG)
        lockall
        playse(SE_PIN)
        playbgm(MUS_RG_FOLLOW_ME,TRUE)
        applymovement(OAK,Common_Movement_ExclamationMark)
        waitmovement(0)
        applymovement(OAK,swagbag_GreetPlayer)
        msgbox(gText_Swagbag_WelcomeToRegion,,SPEAKER_OAK)
        msgbox(gText_Swagbag_GreatThePrivateCar,,SPEAKER_ZOEY)
        msgbox(gText_Swagbag_ExcellentYouveAlready,,SPEAKER_OAK)
        
        closemessage
        applymovement(BIANCA,swagbag_TurnFaceOak_Movement)
        waitmovement(BIANCA)
        goto(Swagbag_LeagueStipend_Dialogue)
        end
}

script Swagbag_LeagueStipend_Dialogue {
        msgbox(gText_Swagbag_SoFirstYoullEach,,SPEAKER_OAK)
        msgbox(gText_Swagbag_BecauseTheBoard)
        closemessage
        addmoney(50000,0)
        setflag(FLAG_SYS_POKENAV_GET)
        setflag(FLAG_SYS_QUEST_MENU_GET)

        goto(Swagbag_PokedexApps_Dialogue)
        end
}

script Swagbag_PokedexApps_Dialogue {
        msgbox(gText_Swagbag_AlsoWeveGoneAhead)
        msgbox(gText_Swagbag_InvasiveMuch,,SPEAKER_BIANCA)
        msgbox(gText_Swagbag_WellAskNextTime,,SPEAKER_OAK)
        msgbox(gText_Swagbag_TeachyTVSoIShould,,SPEAKER_ZOEY)
        msgbox(gText_Swagbag_Correct,,SPEAKER_OAK)
        
        playfanfare(MUS_OBTAIN_ITEM)  
        msgbox(gText_Swagbag_PlayerRecivedMoney)
        waitmessage
        waitfanfare
        goto(Swagbag_WhenCanWeStart_Dialogue)
}

script Swagbag_WhenCanWeStart_Dialogue {
        msgbox(gText_Swagbag_IBelieveThatsEverything,,SPEAKER_OAK)
        msgbox(gText_Swagbag_YeahWhenTheTutorial,,SPEAKER_BIANCA)
        msgbox(gText_Swagbag_TheGymChallengeOfficially,,SPEAKER_OAK)
        msgbox(gText_Swagbag_FineIllExploreTheCity,,SPEAKER_BIANCA)
        closemessage
        
        applymovement(BIANCA,swagbag_LeaveCompound)
        waitmovement(0)
        playse(SE_EXIT)
        removeobject(BIANCA)
        goto(Swagbag_DontLeaveSouthbay_Dialogue)
}

script Swagbag_DontLeaveSouthbay_Dialogue {
        msgbox(gText_Swagbag_IllBeWorkingOnMyLaptop,,SPEAKER_OAK)
        msgbox(gText_Swagbag_GotItThanks,,SPEAKER_ZOEY)
        closemessage
        
        applymovement(OAK,swagbag_WalkToLaptop) 
        waitmovement(0)
        fadedefaultbgm
        startquest(QUEST_PLAYERSADVENTURE)
        releaseall
        end
}

script swagbag_HelpPlayer_Script {//TODO Set up actual question a answer for Oak here
        msgbox(gText_Swagbag_InTheActualGame,MSGBOX_NPC,SPEAKER_OAK)
        
        applymovement(OAK,Common_Movement_FaceRight)
        waitmovement(0)
        return
}

script swagbag_PreventPlayerLeavingSouthbay_Event_Script {
    lock
    msgbox(gText_DontLeaveSouthbay_Text1,,SPEAKER_ZOEY)
    
    closemessage
    applymovement(PLAYER,swagbag_WalkBackwards)
    waitmovement(0)
    releaseall
    end
}

text gText_Swagbag_WelcomeToRegion {
    format("{PLAYER}! Welcome to SanFran! How was your flight?")
}

text gText_Swagbag_GreatThePrivateCar {
    format("Great! The Premium Uber pickup from the airport was a surprise, but everything was super smooth, thanks!")
}

text gText_Swagbag_ExcellentYouveAlready {
    format("Excellent. You've already seen the house, so we can skip the tour... I have some presents!")
}

text gText_Swagbag_SoFirstYoullEach {
    format("So first, you'll each be getting a stipend from the League, which should be hitting your accounts soon. That money is yours, no strings attached, so do whatever you like with it. Just don't spend it all in one place!")
}

text gText_Swagbag_BecauseTheBoard {
    format("*muttering* Because the board absolutely won't let us give you any more...")
}

text gText_Swagbag_AlsoWeveGoneAhead {
    format("Next, we've gone ahead and loaded the newest OS onto your phones last night.")
}

text gText_Swagbag_InvasiveMuch {
    format("Invasive much?")
}

text gText_Swagbag_WellAskNextTime {
    format("Ha ha, we'll ask next time. Another perk: you've also got the cutting edge versions of all the best apps... TeachyTV, FameChecker, QuestLog, and of course, we've reset your Pokedex regions to SanFran.")
}

text gText_Swagbag_TeachyTVSoIShould {
    format("Teachy TV... so I should use that if I have a question or get stuck?")
}

text gText_Swagbag_Correct {
    format("Right! Maps works very well in SanFran, and is another available app on your phone. You can press {START_BUTTON} to access all \lof your apps.")
}

text gText_Swagbag_PlayerRecivedMoney {
    format("{PLAYER} received Â¥50000, and updated phone apps from Oak.")
}

text gText_Swagbag_IBelieveThatsEverything {
    format("I believe that's everything. Questions?")
}

text gText_Swagbag_YeahWhenTheTutorial {
    format("We can't start until tomorrow?")
}

text gText_Swagbag_TheGymChallengeOfficially {
    format("Correct, the Gym Challenge officially starts tomorrow. I suggest you get a good night's sleep, and over breakfast, I'll explain how it all works.")
}

text gText_Swagbag_FineIllExploreTheCity {
    format("...fine. I'll explore the city a bit! Maybe I'll see you around, {PLAYER}.")
}

text gText_Swagbag_IllBeWorkingOnMyLaptop {
    format("{PLAYER}, I'll be working on my laptop here for the next few hours. If you've got any questions, don't be afraid to ask. It's fine if you explore, but don't leave Southbay, okay?")
}

text gText_Swagbag_GotItThanks {
    format("Got it. Thanks for all the help, Oak! I'm still so stoked for the opportunity and the Challenge!")
}

text gText_Swagbag_InTheActualGame {
    format("In the actual game, this will be a list of questions that the player can ask and getr answers to. Tutorial level shit.")
}

text gText_DontLeaveSouthbay_Text1 {
    format("Oak mentioned that I shouldn't leave Southbay yet! Maybe I should get some sleep...")
}

movement swagbag_TurnFaceOak_Movement{
    walk_up * 2
    face_down
    step_end
}

movement swagbag_GreetPlayer {
    face_up
        walk_up
        delay_16
        step_end
}

movement swagbag_LeaveCompound {
        walk_down * 7
        step_end
}

movement swagbag_WalkToLaptop {
        walk_down * 3
        walk_right
        step_end
}

movement swagbag_WalkBackwards {
    lock_facing_direction
    walk_down
    unlock_facing_direction
    step_end
}
