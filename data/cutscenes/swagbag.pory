const PLAYER = OBJ_EVENT_ID_PLAYER
const BIANCA = 1 //FLAG_TEMP_1
const OAK = 2 //FLAG_TEMP_2

script Swagbag_ObjectSetUp {
    turnobject(OAK, DIR_WEST) 
    setobjectmovementtype(BIANCA,MOVEMENT_TYPE_FACE_RIGHT)
    setobjectxy(BIANCA,6,4)
    setobjectxyperm(BIANCA,6,4)
}
script Swagbag_IncrementState_Script{
    setvar(VAR_PLAYER_HOME_STATE,CHECKED_OUT_BEDROOM)
    return
}

script swagbag_ObjectHide_Script {
    setobjectmovementtype(OAK, MOVEMENT_TYPE_FACE_RIGHT)
    turnobject(OAK,DIR_EAST)
    setobjectxy(OAK,8,6)
    setobjectxyperm(OAK,8,6)
    setflag(FLAG_TEMP_1)
    setflag(FLAG_TEMP_3)
}

script Swagbag_WelcomeToSanFran_Dialogue {
        addvar(VAR_LITTLEROOT_INTRO_STATE,1)
        lockall
        playse(SE_PIN)
        playbgm(MUS_RG_FOLLOW_ME,TRUE)
        applymovement(OAK,Common_Movement_ExclamationMark)
        waitmovement(0)
        applymovement(OAK,swagbag_GreetPlayer)
        waitmovement(0)
        setvar(VAR_0x8000, MUGSHOT_LANCE)
        callnative(DrawMugshot)
        msgbox(gText_Swagbag_WelcomeToRegion)
        setvar(VAR_0x8000, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_Swagbag_GreatThePrivateCar)
        setvar(VAR_0x8000, MUGSHOT_LANCE)
        callnative(DrawMugshot)
        msgbox(gText_Swagbag_ExcellentYouveAlready)
        callnative(ClearMugshot)
        closemessage
        applymovement(BIANCA,swagbag_TurnFaceOak_Movement)
        waitmovement(BIANCA)
        goto(Swagbag_LeagueStipend_Dialogue)
        end
}

script Swagbag_LeagueStipend_Dialogue {
    setvar(VAR_0x8000, MUGSHOT_LANCE)
        callnative(DrawMugshot)
        msgbox(gText_Swagbag_SoFirstYoullEach)
        msgbox(gText_Swagbag_BecauseTheBoard)
        closemessage
        addmoney(50000,0)
        setflag(FLAG_SYS_POKENAV_GET)
        setflag(FLAG_SYS_QUEST_MENU_GET)

        givecustommon(SPECIES_SLOWKING_GALARIAN, 100, ITEM_ASSAULT_VEST, ITEM_BEAST_BALL, NATURE_CALM, 2, 252,0,12,124,120,0,31,0,31,31,31,31,MOVE_SURF,MOVE_PSYCHIC,MOVE_FLAMETHROWER,MOVE_SPLASH) 
        givecustommon(SPECIES_KYOGRE,100,ITEM_BLUE_ORB,ITEM_LURE_BALL,NATURE_MODEST,0,0,0,4,252,0,252,31,31,31,31,31,31,MOVE_SURF,MOVE_CALM_MIND,MOVE_SPLASH,MOVE_ICE_BEAM) 
        givecustommon(SPECIES_ZACIAN_CROWNED_SWORD,100,ITEM_GROUNDIUM_Z,ITEM_DUSK_BALL,NATURE_ADAMANT,0,0,252,4,252,0,0,31,31,31,31,31,31,MOVE_PLAY_ROUGH,MOVE_SPLASH,MOVE_DIG,MOVE_CRUNCH)

        goto(Swagbag_PokedexApps_Dialogue)
        end
}

script Swagbag_PokedexApps_Dialogue {
        msgbox(gText_Swagbag_AlsoWeveGoneAhead)
        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_Swagbag_InvasiveMuch)
        setvar(VAR_0x8000, MUGSHOT_LANCE)
        callnative(DrawMugshot)
        msgbox(gText_Swagbag_WellAskNextTime)
        setvar(VAR_0x8000, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_Swagbag_TeachyTVSoIShould)
        setvar(VAR_0x8000, MUGSHOT_LANCE)
        callnative(DrawMugshot)
        msgbox(gText_Swagbag_Correct)
        callnative(ClearMugshot)
        playfanfare(MUS_OBTAIN_ITEM)  
        msgbox(gText_Swagbag_PlayerRecivedMoney)
        waitmessage
        waitfanfare
        goto(Swagbag_WhenCanWeStart_Dialogue)
}

script Swagbag_WhenCanWeStart_Dialogue {
    setvar(VAR_0x8000, MUGSHOT_LANCE)
        callnative(DrawMugshot)
        msgbox(gText_Swagbag_IBelieveThatsEverything)
        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_Swagbag_YeahWhenTheTutorial)
        setvar(VAR_0x8000, MUGSHOT_LANCE)
        callnative(DrawMugshot)
        msgbox(gText_Swagbag_TheGymChallengeOfficially)
        setvar(VAR_0x8000, MUGSHOT_BIANCA)
        callnative(DrawMugshot)
        msgbox(gText_Swagbag_FineIllExploreTheCity)
        closemessage
        callnative(ClearMugshot)
        applymovement(BIANCA,swagbag_LeaveCompound)
        waitmovement(0)
        playse(SE_EXIT)
        removeobject(BIANCA)
        goto(Swagbag_DontLeaveSouthbay_Dialogue)
}

script Swagbag_DontLeaveSouthbay_Dialogue {
        setvar(VAR_0x8000, MUGSHOT_LANCE)
        callnative(DrawMugshot)
        msgbox(gText_Swagbag_IllBeWorkingOnMyLaptop)
        setvar(VAR_0x8000, MUGSHOT_HILDA)
        callnative(DrawMugshot)
        msgbox(gText_Swagbag_GotItThanks)
        closemessage
        callnative(ClearMugshot)
        applymovement(OAK,swagbag_WalkToLaptop) 
        waitmovement(0)
        fadedefaultbgm
        startquest(QUEST_PLAYERSADVENTURE)
        releaseall
        end
}

script swagbag_HelpPlayer_Script {//TODO Set up actual question a answer for Oak here
        msgbox(gText_Swagbag_InTheActualGame,MSGBOX_NPC)
        callnative(ClearMugshot)
        applymovement(OAK,Common_Movement_FaceRight)
        waitmovement(0)
        return
}

script swagbag_PreventPlayerLeavingSouthbay_Event_Script {
    lock
    setvar(VAR_0x8000, MUGSHOT_HILDA)
    callnative(DrawMugshot)
    msgbox(gText_DontLeaveSouthbay_Text1) 
    callnative(ClearMugshot)
    closemessage
    applymovement(PLAYER,swagbag_WalkBackwards)
    waitmovement(0)
    releaseall
    end
}

text gText_Swagbag_WelcomeToRegion {
    format("{PLAYER}! Welcome to SanFran! How was your flight?")
}

text gText_Swagbag_GreatThePrivateCar {
    format("Great! The private car from the airport was a surprise, but everything was super smooth, thanks!")
}

text gText_Swagbag_ExcellentYouveAlready {
    format("Excellent. You've already seen the house, so we can skip the tour... I have some presents!")
}

text gText_Swagbag_SoFirstYoullEach {
    format("So first, you'll each be getting a stipend from the League, which should be hitting your accounts soon. That money is yours, no strings attached, so do whatever you like with it. Just don't spend it all in one place!")
}

text gText_Swagbag_BecauseTheBoard {
    format("*muttering* Because the board absolutely won't let us give you any more...")
}

text gText_Swagbag_AlsoWeveGoneAhead {
    format("Next, we've gone ahead and loaded the newest OS onto your phones last night.")
}

text gText_Swagbag_InvasiveMuch {
    format("Invasive much?")
}

text gText_Swagbag_WellAskNextTime {
    format("Ha ha, we'll ask next time. Another perk: you've also got the cutting edge versions of all the best apps... TeachyTV, FameChecker, QuestLog, and of course, the we've changed your Pokedex regions to SanFran mode.")
}

text gText_Swagbag_TeachyTVSoIShould {
    format("Teachy TV... so I should use that if I have a question or get stuck?")
}

text gText_Swagbag_Correct {
    format("Right! All of the other apps have tutorials, so if you press {START_BUTTON} later and select an app, it'll walk you through all of them.")
}

text gText_Swagbag_PlayerRecivedMoney {
    format("{PLAYER} received Â¥50000, and updated phone apps from Oak.")
}

text gText_Swagbag_IBelieveThatsEverything {
    format("I believe that's everything. Questions?")
}

text gText_Swagbag_YeahWhenTheTutorial {
    format("We can't start until tomorrow?")
}

text gText_Swagbag_TheGymChallengeOfficially {
    format("Correct, the Gym Challenge officially starts tomorrow. I suggest you get a good night's sleep, and over breakfast, I'll explain how it all works.")
}

text gText_Swagbag_FineIllExploreTheCity {
    format("...fine. I'll explore the city a bit! Maybe I'll see you around, {PLAYER}.")
}

text gText_Swagbag_IllBeWorkingOnMyLaptop {
    format("{PLAYER}, I'll be working on my laptop here for the next few hours. If you've got any questions, don't be afraid to ask. It's fine if you explore, but don't leave Southbay, okay?")
}

text gText_Swagbag_GotItThanks {
    format("Got it. Thanks for all the help, Oak! I'm still so stoked for the opportunity and the Challenge!")
}

text gText_Swagbag_InTheActualGame {
    format("In the actual game, this will be a list of questions that the player can ask and getr answers to. Tutorial level shit.")
}

text gText_DontLeaveSouthbay_Text1 {
    format("I don't think I should leave Southbay yet...")
}

movement swagbag_TurnFaceOak_Movement{
    walk_up * 2
    face_down
    step_end
}

movement swagbag_GreetPlayer {
    face_up
        walk_up
        delay_16
        step_end
}

movement swagbag_LeaveCompound {
        walk_down * 7
        step_end
}

movement swagbag_WalkToLaptop {
        walk_down * 3
        walk_right
        step_end
}

movement swagbag_WalkBackwards {
    lock_facing_direction
    walk_down
    unlock_facing_direction
    step_end
}
